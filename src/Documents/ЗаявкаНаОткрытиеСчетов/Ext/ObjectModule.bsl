////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(РаботникиОрганизации, , "Физлицо");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	НаборЛицевыхСчетов = Движения.ЛицевыеСчетаРаботниковОрганизации;
	
	Если ВводНачальныхСведений Тогда
		Для каждого СтрокаСРаботником Из РаботникиОрганизации Цикл
			ЗаписьЛицевогоСчета = НаборЛицевыхСчетов.Добавить();
			ЗаписьЛицевогоСчета.Организация = Организация;
			ЗаписьЛицевогоСчета.Банк = Банк;
			ЗаписьЛицевогоСчета.ФизЛицо = СтрокаСРаботником.ФизЛицо;
			ЗаписьЛицевогоСчета.НомерЛицевогоСчета = СтрокаСРаботником.НомерЛицевогоСчета;
			ЗаписьЛицевогоСчета.ВидВклада = ВидВклада;//О.Т. Аверсон 03.01.2018 № 1400
			
			//Аверсон 12.04.2021 КВВ СВФР-007440 (
			Если ПроставлятьОсновныеРС Тогда
				ЗаписьЛицевогоСчета.ОсновнойРС = СтрокаСРаботником.ОсновнойРС; 
			КонецЕсли;
			//Аверсон 12.04.2021 КВВ СВФР-007440 )
			
			ПроверкаНомераСчета(СтрокаСРаботником.ФизЛицо,СтрокаСРаботником.НомерЛицевогоСчета); //Аверсон 27.08.2020 КВВ СВФР-006474
			
//толик		
			//Если ЗначениеЗаполнено(СтрокаСРаботником.ВидЗачисляемогоСписка) Тогда
			//	ЗаписьЛицевогоСчета.ВидЗачисляемогоСписка = СтрокаСРаботником.ВидЗачисляемогоСписка;
			//Иначе
			//	ОбщегоНазначения.СообщитьОбОшибке("У сотрудника "+Строка(СтрокаСРаботником.ФизЛицо)+" не задан вид зачисляемого счета");
			//	Отказ = Истина;
			//КонецЕсли;
		КонецЦикла;
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
//			
	Иначе
		
		Попытка
			ИсходноеДерево = ЗначениеИзСтрокиВнутр(ТекстПодтверждения);
		Исключение
			Возврат;
		КонецПопытки;
		
		Если ТипЗнч(ИсходноеДерево) <> Тип("ДеревоЗначений") Тогда
			Возврат;
		КонецЕсли;
		
		ПарсерПодтверждения = Обработки.ИмпортЭкспортОперацийПоЛицевымСчетамРаботников.Создать();
		
		МассивОпераций = ПарсерПодтверждения.ПолучитьОперации(ИсходноеДерево, "РезультатОткрытияСчетов");
		
		ЗаполненныеФизЛица = Новый Соответствие;
		
		Для каждого ОперацияПодтверждения из МассивОпераций Цикл
			
			ЗаписьЛицевогоСчета = ЗаполненныеФизЛица[ОперацияПодтверждения.Сотрудник];
			
			Если ЗаписьЛицевогоСчета = Неопределено Тогда
				ЗаписьЛицевогоСчета = НаборЛицевыхСчетов.Добавить();
				ЗаписьЛицевогоСчета.Организация = Организация;
				ЗаписьЛицевогоСчета.ВидВклада = ВидВклада; //О.Т. Аверсон 03.01.2018 № 1400
				ЗаписьЛицевогоСчета.Банк = Банк;
				ЗаписьЛицевогоСчета.ФизЛицо = ОперацияПодтверждения.Сотрудник;
				ЗаполненныеФизЛица.Вставить(ОперацияПодтверждения.Сотрудник, ЗаписьЛицевогоСчета);
			КонецЕсли;
			
			Если ВРЕГ(СтрЗаменить(ОперацияПодтверждения.Результат, " ", "")) = "СЧЕТОТКРЫТ" Тогда
				ЗаписьЛицевогоСчета.НомерЛицевогоСчета = ОперацияПодтверждения.ЛицевойСчет;
				
			Иначе
				ЗаполненныеФизЛица.Вставить(ОперацияПодтверждения.Сотрудник, Неопределено);
				НаборЛицевыхСчетов.Удалить(ЗаписьЛицевогоСчета);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого ЗаписьВРегистр из НаборЛицевыхСчетов Цикл
		Сообщить("Записана информация о лицевом счете " + ЗаписьВРегистр.НомерЛицевогоСчета + " (сотрудник " + ЗаписьВРегистр.ФизЛицо + " )");
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаНомераСчета(Сотрудник,НомерЛицСчета)
	//Аверсон 27.08.2020 КВВ СВФР-006474 (
	З = Новый Запрос;         
	З.Текст = "ВЫБРАТЬ 
	|ЛицСчета.ФизЛицо,
	|ЛицСчета.НомерЛицевогоСчета 
	|ИЗ РегистрСведений.ЛицевыеСчетаРаботниковОрганизации КАК ЛицСчета
	|ГДЕ ЛицСчета.НомерЛицевогоСчета = &НомерЛицСчета
	|И ЛицСчета.ФизЛицо <> &ФЛ";
	З.УстановитьПараметр("НомерЛицСчета",НомерЛицСчета);
	З.УстановитьПараметр("ФЛ",Сотрудник);
	РезЗ = З.Выполнить().Выгрузить();
	Если РезЗ.Количество() > 0 Тогда
		Для Каждого СтрРезЗ ИЗ РезЗ Цикл
			Сообщить("Номер счета сотрудника " + СокрЛП(Сотрудник.Наименование) + " аналогичен номеру счета сотрудника " + СокрЛП(СтрРезЗ.ФизЛицо.Наименование) + " (" +СокрЛП(СтрРезЗ.НомерЛицевогоСчета) +  ")");
		КонецЦикла;
	КонецЕсли;
	//Аверсон 27.08.2020 КВВ СВФР-006474 )
КонецПроцедуры




