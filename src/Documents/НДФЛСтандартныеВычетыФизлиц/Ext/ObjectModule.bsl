Процедура ПерерасчетДокумента() Экспорт
	мЗапрос = Новый Запрос;
	мЗапрос.Текст = 
		"ВЫБРАТЬ
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.ДатаНачалаВычета КАК Дата,
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.КодВычета,
		|	СУММА(1) КАК Действие
		|ИЗ
		|	Документ.НДФЛСтандартныеВычетыФизлиц.СоставСемьи КАК НДФЛСтандартныеВычетыФизлицСоставСемьи
		|ГДЕ
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.Ссылка = &Ссылка
		|	И НДФЛСтандартныеВычетыФизлицСоставСемьи.ПрименятьВычет
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.ДатаНачалаВычета,
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.КодВычета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.ДатаКонцаВычета,
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.КодВычета,
		|	СУММА(-1)
		|ИЗ
		|	Документ.НДФЛСтандартныеВычетыФизлиц.СоставСемьи КАК НДФЛСтандартныеВычетыФизлицСоставСемьи
		|ГДЕ
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.Ссылка = &Ссылка
		|	И НДФЛСтандартныеВычетыФизлицСоставСемьи.ПрименятьВычет
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.ДатаКонцаВычета,
		|	НДФЛСтандартныеВычетыФизлицСоставСемьи.КодВычета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Действие УБЫВ";
	мЗапрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	мВыборка = мЗапрос.Выполнить().Выбрать();
	
	мНаборЗаписейНДФЛСтандартныеВычетыФизлиц = РегистрыСведений.НДФЛСтандартныеВычетыФизлиц.СоздатьНаборЗаписей();
	мНаборЗаписейНДФЛСтандартныеВычетыФизлиц.Отбор.ФизЛицо.Значение = ЭтотОбъект.Физлицо.Ссылка;
    мНаборЗаписейНДФЛСтандартныеВычетыФизлиц.Отбор.ФизЛицо.Использование = Истина;
	ЗаписьНабораП = РегистрыСведений.НДФЛСтандартныеВычетыФизлиц.СоздатьНаборЗаписей().Добавить();
	мДата = '00010101000000';
	Если ЭтотОбъект.ПрименятьЛичныйВычет Тогда
		ЗаписьНабора = мНаборЗаписейНДФЛСтандартныеВычетыФизлиц.Добавить();
		ЗаписьНабора.Период 							= '19000101000000';
		ЗаписьНабора.Физлицо 							= ЭтотОбъект.Физлицо.Ссылка;
		ЗаписьНабора.КодВычетаЛичный 					= Справочники.ВычетыНДФЛ.Код600;
		ЗаписьНабораП.Период = ЗаписьНабора.Период;
		ЗаписьНабораП.КодВычетаЛичный = ЗаписьНабора.КодВычетаЛичный;
	КонецЕсли;
	Пока мВыборка.Следующий() Цикл
		Если мВыборка.Действие < 0 Тогда
			мДатаН = ДобавитьМесяц(НачалоМесяца(мВыборка.Дата), 1);
		Иначе
			мДатаН = НачалоМесяца(мВыборка.Дата);
		КонецЕсли;
		Если мДата <> мДатаН Тогда
			мДата = мДатаН;
			ЗаписьНабора = мНаборЗаписейНДФЛСтандартныеВычетыФизлиц.Добавить();
		КонецЕсли;
		ЗаписьНабора.Физлицо = ЭтотОбъект.Физлицо.Ссылка;
		ЗаписьНабора.КодВычетаЛичный = ЗаписьНабораП.КодВычетаЛичный;
		ЗаписьНабора.КодВычетаНаДетей = ЗаписьНабораП.КодВычетаНаДетей;
		ЗаписьНабора.КоличествоДетей = ЗаписьНабораП.КоличествоДетей;
		ЗаписьНабора.КодВычетаНаДетейЛьгота = ЗаписьНабораП.КодВычетаНаДетейЛьгота;
		ЗаписьНабора.КоличествоДетейЛьгота = ЗаписьНабораП.КоличествоДетейЛьгота;
		ЗаписьНабора.КодВычетаДополнительный = ЗаписьНабораП.КодВычетаДополнительный;
		
		//Аверсон ЕАЕ 10.01.2024 СВФР-012095
		ЗаписьНабора.КодВычетаМолодыеСпециалисты = ЗаписьНабораП.КодВычетаМолодыеСпециалисты;
		//Аверсон СВФР-012095
		
		Если мВыборка.Действие < 0 Тогда
			ЗаписьНабора.Период = ДобавитьМесяц(мВыборка.Дата, 1);
		Иначе
			ЗаписьНабора.Период = мВыборка.Дата;
		КонецЕсли;
		Если мВыборка.КодВычета = Справочники.ВычетыНДФЛ.Код610 Тогда
			ЗаписьНабора.КодВычетаНаДетей = Справочники.ВычетыНДФЛ.Код610;
			ЗаписьНабора.КоличествоДетей = ЗаписьНабора.КоличествоДетей + мВыборка.Действие;
			Если ЗаписьНабора.КоличествоДетей = 0 Тогда
				ЗаписьНабора.КодВычетаНаДетей = Справочники.ВычетыНДФЛ.ПустаяСсылка();
				ЗаписьНабора.КоличествоДетей = 0;
			КонецЕсли;
		ИначеЕсли мВыборка.КодВычета = Справочники.ВычетыНДФЛ.Код611 Тогда
			ЗаписьНабора.КодВычетаНаДетейЛьгота = Справочники.ВычетыНДФЛ.Код611;
			ЗаписьНабора.КоличествоДетейЛьгота = ЗаписьНабора.КоличествоДетейЛьгота + мВыборка.Действие;
			Если ЗаписьНабора.КоличествоДетейЛьгота = 0 Тогда
				ЗаписьНабора.КодВычетаНаДетейЛьгота = Справочники.ВычетыНДФЛ.ПустаяСсылка();
				ЗаписьНабора.КоличествоДетейЛьгота = 0;
			КонецЕсли;
		ИначеЕсли мВыборка.КодВычета = Справочники.ВычетыНДФЛ.Код620 Тогда
			Если мВыборка.Действие = 1 Тогда
				ЗаписьНабора.КодВычетаДополнительный= Справочники.ВычетыНДФЛ.Код620;
			Иначе
				ЗаписьНабора.КодВычетаДополнительный= Справочники.ВычетыНДФЛ.ПустаяСсылка();
			КонецЕсли;
			
		//Аверсон ЕАЕ 10.01.2024 СВФР-012095
		ИначеЕсли мВыборка.КодВычета = Справочники.ВычетыНДФЛ.Код601 Тогда
			Если мВыборка.Действие = 1 Тогда
				ЗаписьНабора.КодВычетаМолодыеСпециалисты = Справочники.ВычетыНДФЛ.Код601;
			Иначе
				ЗаписьНабора.КодВычетаМолодыеСпециалисты = Справочники.ВычетыНДФЛ.ПустаяСсылка();
			КонецЕсли;
		//Аверсон СВФР-012095
		КонецЕсли;
		ЗаписьНабораП.КодВычетаЛичный = ЗаписьНабора.КодВычетаЛичный;
		ЗаписьНабораП.КодВычетаНаДетей = ЗаписьНабора.КодВычетаНаДетей;
		ЗаписьНабораП.КоличествоДетей = ЗаписьНабора.КоличествоДетей;
		ЗаписьНабораП.КодВычетаНаДетейЛьгота = ЗаписьНабора.КодВычетаНаДетейЛьгота;
		ЗаписьНабораП.КоличествоДетейЛьгота = ЗаписьНабора.КоличествоДетейЛьгота;
		ЗаписьНабораП.КодВычетаДополнительный = ЗаписьНабора.КодВычетаДополнительный;
		
		//Аверсон ЕАЕ 10.01.2024 СВФР-012095
		ЗаписьНабораП.КодВычетаМолодыеСпециалисты = ЗаписьНабора.КодВычетаМолодыеСпециалисты;
		//Аверсон СВФР-012095
		
	КонецЦикла;
	мНаборЗаписейНДФЛСтандартныеВычетыФизлиц.Записать();
КонецПроцедуры