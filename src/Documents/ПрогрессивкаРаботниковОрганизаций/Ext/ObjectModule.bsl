////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда

// Выполняет перерасчет по заданному списку физлиц
// Если передан пустой список физлиц - перерасчет не проводится
// Если в качестве списка физлиц передан не массив, а Неопределено - перерасчет по всем физлицам 
// регистратора по которым требуется перерасчет
//
// Параметры
//	Физлица	- список значений - ссылки на физлиц
// Возвращаемое значение
//	Нет
//
Процедура Перерассчитать(Физлица = Неопределено) Экспорт
	
	Если Не ПроведениеРасчетов.НеобходимостьПерерасчета(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	// получим список перерассчитываемых физлиц
	ФизлицаСписок = Новый СписокЗначений;
	Если Физлица = Неопределено Тогда
		// таблица ФизлицаДляПерерасчета
		// Список физлиц по которым необходимо выполнить перерасчет
		// Поля:
		//		ФизЛицо
		// Описание:
		// Получает список неповторяющихся физлиц по которым есть записи
		// перерасчета в одной из таблиц перерасчета - по основным или дополнительным начислениям
		//
		ФизлицаДляПерерасчетаТекст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Перерасчет.ФизЛицо
		|	ИЗ
		|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК Перерасчет
		|	
		|	ГДЕ
		|		Перерасчет.ОбъектПерерасчета = &парамРегистратор";
		
		Запрос = Новый Запрос(ФизлицаДляПерерасчетаТекст);
		Запрос.УстановитьПараметр("парамРегистратор", Ссылка);
		ФизлицаСписок.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизЛицо"));
		
	Иначе
		ФизлицаСписок = Физлица;
		
	КонецЕсли;

	// если не перерассчитываем никого - возврат
	Если ФизлицаСписок.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	// Прочитаем все данные в одной транзакции
	НачатьТранзакцию();
	Прочитать();
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке("").Выбрать();
	Движения.ДополнительныеНачисленияРаботниковОрганизаций.Прочитать();
	Движения.НДФЛСведенияОДоходах.Прочитать();
	ЗафиксироватьТранзакцию();
	
	// позиционируем выборку
	ВыборкаПоШапкеДокумента.Следующий();
	
	////////////////////////////////////////////////////////////////////////
	// Дополнительные начисления и сведения о доходах НДФЛ
	
	// сначала удалим сведения о доходах НДФЛ по перерассчитываемым физлицам
	НДФЛСведенияОДоходах = Движения.НДФЛСведенияОДоходах;
	ПоследнееДвижение = НДФЛСведенияОДоходах.Количество()-1;
	Для Сч = 0 По ПоследнееДвижение Цикл
		// удалим записи по всем физлицам полученного списка
		// обходим в обратном порядке
		Если ФизлицаСписок.НайтиПоЗначению(НДФЛСведенияОДоходах[ПоследнееДвижение - Сч].Физлицо) <> Неопределено Тогда
			НДФЛСведенияОДоходах.Удалить(ПоследнееДвижение - Сч);
		КонецЕсли;
	КонецЦикла;
	
	// перерассчитываем записи начислений
	// при этом передаем не только набор записей регистра расчета, но и набор записей регистра 
	// накопления со сведениями об отработанном времени
	// а также таблицу значений в которую будет заполнять движения для НДФЛСведенияОДоходах
	ДвиженияНДФЛСведенияОДоходах = 	Движения.НДФЛСведенияОДоходах.Выгрузить();// фактически - создаем структуру таблицы значений
	ПроведениеРасчетов.ПереРассчитатьЗаписиРегистраРасчета("ДополнительныеНачисленияРаботниковОрганизаций", Движения.ДополнительныеНачисленияРаботниковОрганизаций, Физлица, ДвиженияНДФЛСведенияОДоходах,, ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, ВыборкаПоШапкеДокумента.ОбособленноеПодразделение, Начисления);
	// запишем набор
	Движения.ДополнительныеНачисленияРаботниковОрганизаций.Записать(Истина, Истина);
	
	// запишем движения по доходам НДФЛ, которые формируются при ПереРассчитатьЗаписиРегистраРасчета
	ПроведениеРасчетов.СвернутьДвиженияНДФЛСведенияОДоходах(ДвиженияНДФЛСведенияОДоходах);
	Движения.НДФЛСведенияОДоходах.Загрузить(ДвиженияНДФЛСведенияОДоходах);
	Движения.НДФЛСведенияОДоходах.Записать();
	
	// записываем документ перед вызовом формирование взаиморасчетов с работниками
	Записать();
	
	////////////////////////////////////////////////////////////////////////
	// взаиморасчеты с работниками

	// ведется ли учет задолженности в разрезе периодов возникновения задолженности
	УчетЗадолженностиПоМесяцам	= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "УчетЗадолженностиПоМесяцам");

	// сначала удалим сведения о взаиморасчетах с работниками
	ВзаиморасчетыСРаботникамиОрганизаций = Движения.ВзаиморасчетыСРаботникамиОрганизаций;
	ВзаиморасчетыСРаботникамиОрганизаций.Записать();
	
	ВыборкаПоНачислениям = СформироватьЗапросПоВзаиморасчетам().Выбрать();

	Пока ВыборкаПоНачислениям.Следующий() Цикл
		// Заполним записи в наборах записей регистров
		ДобавитьСтрокуВДвиженияПоВзаиморасчетам(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, УчетЗадолженностиПоМесяцам);
	КонецЦикла;
	
	Движения.ВзаиморасчетыСРаботникамиОрганизаций.Записать();
	
	////////////////////////////////////////////////////////////////////////
	// Удалим записи перерасчета по которым выполнен перерасчет
		
	НаборЗаписей = РегистрыРасчета.ДополнительныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетДополнительныхНачислений.СоздатьНаборЗаписей();
		
	НаборЗаписей.Отбор.ОбъектПерерасчета.Значение = Ссылка;
	Если Физлица <> Неопределено Тогда
		НаборЗаписей.Прочитать();
		СтрокиКУдалению = Новый Массив;
		Для Каждого СтрокаПерерасчета из НаборЗаписей Цикл
			Если Физлица.НайтиПоЗначению(СтрокаПерерасчета.Физлицо) <> Неопределено Тогда
				СтрокиКУдалению.Добавить(СтрокаПерерасчета);
			КонецЕсли;
		КонецЦикла;
		Для Каждого Строка Из СтрокиКУдалению Цикл
			НаборЗаписей.Удалить(Строка);
		КонецЦикла;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры // Перерассчитать()

// Заполняет документ по перерассчитываемому документу
//
Функция ЗаполнитьПоПерерассчитываемомуДокументу() Экспорт
	
	// выполним заполнение по всем табличным частям документа
	
	// получим список физлиц для перерасчета
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ПерерассчитываемыйДокумент", ПерерассчитываемыйДокумент);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Перерасчет.ФизЛицо
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета = &ПерерассчитываемыйДокумент";
	
	Результат = Запрос.Выполнить().Выгрузить();
	ФизлицаПерерасчета = Результат.ВыгрузитьКолонку("Физлицо");
	
	Если ФизлицаПерерасчета.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Копируем реквизиты шапки
	ВидРасчета		= ПерерассчитываемыйДокумент.ВидРасчета;
	ЧислоМесяцев	= ПерерассчитываемыйДокумент.ЧислоМесяцев;
	ДатаНачалаБП	= ПерерассчитываемыйДокумент.ДатаНачалаБП;
	ДатаОкончанияБП	= ПерерассчитываемыйДокумент.ДатаОкончанияБП;
	
	Запрос.УстановитьПараметр("ФизлицаПерерасчета", ФизлицаПерерасчета);
	
	// выполним запрос к Начисления
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Начисления.Сотрудник,
	|	Начисления.Сотрудник.Физлицо КАК Физлицо,
	|	Начисления.Размер,
	|	Начисления.Результат,
	|	Начисления.Авторасчет
	|ИЗ
	|	Документ.ПрогрессивкаРаботниковОрганизаций.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка = &ПерерассчитываемыйДокумент
	|	И (НЕ Начисления.Сторно)
	|	И Начисления.Сотрудник.Физлицо В(&ФизлицаПерерасчета)
	|	И Начисления.Ссылка.Проведен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		// сторно-строка
		НоваяСтрока = Начисления.Добавить();
		НоваяСтрока.Сотрудник	= Выборка.Сотрудник;
		НоваяСтрока.Физлицо		= Выборка.Физлицо;
		НоваяСтрока.Размер		= Выборка.Размер;
		НоваяСтрока.Результат =	- Выборка.Результат;
		НоваяСтрока.Сторно		= Истина;
		НоваяСтрока.Авторасчет	= Выборка.Авторасчет;
		
		// новая рассчитываемая строка
		НоваяСтрока = Начисления.Добавить();
		НоваяСтрока.Сотрудник	= Выборка.Сотрудник;
		НоваяСтрока.Физлицо		= Выборка.Физлицо;
		НоваяСтрока.Размер		= Выборка.Размер;
		НоваяСтрока.Авторасчет	= Истина;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции // ЗаполнитьПоПерерассчитываемомуДокументу()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//	Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//	НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если Не Проведен Тогда
		Предупреждение("Документ можно распечатать только после его проведения!");
		Возврат Неопределено;
	КонецЕсли;

	// Получить экземпляр документа на печать
	ИмяФормы = "";
	Если      ИмяМакета = "Т11а" Тогда
		ИмяФормы = " (форма Т-11а)";
		ТабДокумент = ПечатьТ11а();
		
	ИначеЕсли ИмяМакета = "Т11" Тогда
		ИмяФормы = " (форма Т-11)";
		ТабДокумент = ПечатьТ11();
		
	КонецЕсли;

	Возврат РаботаСДиалогами.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, РаботаСДиалогами.СформироватьЗаголовокДокумента(ЭтотОбъект,Метаданные().Синоним + ИмяФормы));

КонецФункции // Печать()

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//	Струткура, каждая строка которой соответствует одному из вариантов печати
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура;
	СтруктураМакетов.Вставить("Т11а",	"Форма Т-11а");
	СтруктураМакетов.Вставить("Т11",	"Форма Т-11");
	
	Возврат СтруктураМакетов;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

Процедура РассчитатьПремию(Сотрудник = Неопределено) Экспорт
//толик	
	//// Перечитаем объект и соберем данные для заполнения наборов записей регистров
	//НачатьТранзакцию();
	//Прочитать();
	//ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке("ПоРеквизитамДокумента").Выбрать();
	//ВыборкаПоШапкеДокумента.Следующий();
	//ВыборкаПоДопНачислениям = СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента, Сотрудник).Выбрать();
	//ЗафиксироватьТранзакцию();

	////массивы для хранения индексов строк табличных частей,
	////используются для расчета по одному сотруднику
	//Если Сотрудник = Неопределено Тогда
	//	МассивИндексыСтрокНачисления = Неопределено;
	//	
	//Иначе
	//	МассивИндексыСтрокНачисления = Новый Массив();
	//	
	//КонецЕсли;
	//
	//Отказ = Ложь;
	//
	//// Заголовок для сообщений об ошибках проведения.
	//Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	//ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ,Заголовок);
	//
	//// создадим наборы записей для выполнения движений
	//НаборДопНачисления = РегистрыРасчета.ДополнительныеНачисленияРаботниковОрганизаций.СоздатьНаборЗаписей();
	//НаборДопНачисления.Отбор.Регистратор.Значение = Ссылка;
	//
	//Пока ВыборкаПоДопНачислениям.Следующий() Цикл
	//	ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоДопНачислениям, Отказ,Заголовок);
	//	Если НЕ Отказ Тогда
	//		// Заполним записи в наборах записей регистров
	//		ДобавитьСтрокуВДвиженияПоРегистрамРасчета(ВыборкаПоШапкеДокумента, ВыборкаПоДопНачислениям, НаборДопНачисления);
	//		
	//		Если НЕ (Сотрудник = Неопределено) Тогда
	//			МассивИндексыСтрокНачисления.Добавить(ВыборкаПоДопНачислениям.НомерСтроки-1);
	//		КонецЕсли;
	//		
	//	КонецЕсли;
	//КонецЦикла;

	//Если Отказ Тогда
	//	Возврат;
	//КонецЕсли;
	//	
	//ПроведениеРасчетов.РассчитатьЗаписиРегистраРасчета("ДополнительныеНачисленияРаботниковОрганизаций", НаборДопНачисления,,,ВыборкаПоШапкеДокумента.ГоловнаяОрганизация,,Начисления,,,,,, МассивИндексыСтрокНачисления, Сотрудник);
	//
	//// Удаляем движения
	//НаборДопНачисления.Очистить();
	//НаборДопНачисления.Записать();
//енд

	Для Каждого мСтрока Из Начисления Цикл
		ЧистаяПремия = мСтрока.Начислено * мСтрока.Размер/100;
		мСтрока.Результат = ОбщегоНазначения.ОкруглитьПоВалюте(ЧистаяПремия + (ЧистаяПремия*мСтрока.Увеличение/100) - (ЧистаяПремия*мСтрока.Уменьшение/100),Константы.ВалютаРегламентированногоУчета.Получить());
	КонецЦикла;
	
КонецПроцедуры // РассчитатьПремию()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Формирует запрос по документу и связанным регистрам сведений
//
// Параметры: 
//	Режим	- строка, может принимать значения:
//				"ПоРеквизитамДокумента"
//				"ПоТабличнойЧастиДокумента"
//
// Возвращаемое значение:
//	Результат запроса с данными об организации или о работниках из табличной части
//
Функция СформироватьЗапросДляПечати(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);
	Запрос.УстановитьПараметр("Руководитель",	Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Запрос.УстановитьПараметр("ДатаДокумента",	Дата);

	Если Режим = "ПоРеквизитамДокумента" Тогда

		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПрогрессивкаРаботниковОрганизаций.Дата КАК ДатаДок,
		|	ПрогрессивкаРаботниковОрганизаций.Номер КАК НомерДок,
		|	ВЫРАЗИТЬ(ПрогрессивкаРаботниковОрганизаций.Организация.НаименованиеПолное КАК СТРОКА(300)) КАК НазваниеОрганизации,
		|	ПрогрессивкаРаботниковОрганизаций.Организация.КодПоОКПО КАК КодПоОКПО,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Представление КАК ДолжностьРуководителя,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо,
		|	ПрогрессивкаРаботниковОрганизаций.ВидРасчета.Наименование КАК РасчетСтрокой,
		|	ПрогрессивкаРаботниковОрганизаций.Организация.Префикс
		|ИЗ
		|	Документ.ПрогрессивкаРаботниковОрганизаций КАК ПрогрессивкаРаботниковОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаДокумента, ОтветственноеЛицо = &Руководитель) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизЛицСрезПоследних
		|			ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|		ПО ПрогрессивкаРаботниковОрганизаций.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница
		|ГДЕ
		|	ПрогрессивкаРаботниковОрганизаций.Ссылка = &ДокументСсылка";

	ИначеЕсли Режим = "ПоТабличнойЧастиДокумента" Тогда

		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));

		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество, ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник.Наименование) КАК Работник,
		|	ВЫБОР
		|		КОГДА  РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаДокумента
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|		ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения.Наименование
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации.Наименование
		|	КОНЕЦ КАК ПодразделениеРаботника,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаДокумента
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|		ТОГДА РаботникиОрганизацииСрезПоследних.ДолжностьЗавершения.Наименование
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.Должность.Наименование 
		|	КОНЕЦ КАК Должность,
 		|	ПрогрессивкаРаботниковОрганизацийНачисления.Размер,
		|	ПрогрессивкаРаботниковОрганизацийНачисления.Результат КАК Величина,
		|	ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник.Код КАК ТабельныйНомер
		|ИЗ
		|	Документ.ПрогрессивкаРаботниковОрганизаций.Начисления КАК ПрогрессивкаРаботниковОрганизацийНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
		|		&ДатаДокумента,
		|		Сотрудник В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник
		|			ИЗ
		|				Документ.ПрогрессивкаРаботниковОрганизаций.Начисления КАК ПрогрессивкаРаботниковОрганизацийНачисления
		|			ГДЕ
		|				ПрогрессивкаРаботниковОрганизацийНачисления.Ссылка = &ДокументСсылка)) КАК РаботникиОрганизацииСрезПоследних
		|		ПО ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник = РаботникиОрганизацииСрезПоследних.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
		|		&ДатаДокумента,
		|		Физлицо В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник.Физлицо
		|			ИЗ
		|				Документ.ПрогрессивкаРаботниковОрганизаций.Начисления КАК ПрогрессивкаРаботниковОрганизацийНачисления
		|			ГДЕ
		|				ПрогрессивкаРаботниковОрганизацийНачисления.Ссылка = &ДокументСсылка)) КАК ФИОФизЛиц
		|		ПО ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник.Физлицо = ФИОФизЛиц.ФизЛицо
		|ГДЕ
		|	ПрогрессивкаРаботниковОрганизацийНачисления.Ссылка = &ДокументСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПрогрессивкаРаботниковОрганизацийНачисления.НомерСтроки";

	Иначе
		Возврат Неопределено;
		
	КонецЕсли;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросДляПечати()

#Если Клиент Тогда

// Функция формирует табличный документ с печатной формой "Т-11а",
//
// Возвращаемое значение:
//	Табличный документ - печатная форма
//
Функция ПечатьТ11а()

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ПолеСлева = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПрогрессивкаРаботниковОрганизаций_Т11а";
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;

	// получаем данные для печати
	ВыборкаДляШапкиИПодвала = СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
	ВыборкаРаботники = СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();

	// подсчитываем количество страниц документа - для корректного разбиения на страницы
	ВсегоСтрокДокумента = ВыборкаРаботники.Количество();

	// запоминаем области макета
	Макет = ПолучитьМакет("Т11а");
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка"); // Шапка документа.
	ПовторятьПриПечатиСтроки = Макет.ПолучитьОбласть("ПовторятьПриПечати"); // повторяющаяся шапка страницы
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");// Подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Строка"); // строка работника

	ВыводитьИтоги = Ложь;
	
	// массив с двумя строками - для разбиения на страницы
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакета);
	
	// выводим данные о руководителях организации
	Если ВыборкаДляШапкиИПодвала.Следующий() Тогда 
		ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапкиИПодвала); // Шапка документа.
 		ОбластьМакетаШапка.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
		ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапкиИПодвала); // Для подвала
	КонецЕсли;

	// Начинаем формировать выходной документ
	ТабДокумент.Вывести(ОбластьМакетаШапка); // Шапка документа.

	ВыведеноСтрок = 0;
	ПодИтогСтраницы = 0;
	ИтогДокумента = 0;
	// выводим строки по работникам
	Пока ВыборкаРаботники.Следующий() Цикл

		// Данные по работнику.
		ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
		ПодИтогСтраницы = ПодИтогСтраницы + ВыборкаРаботники.Величина;
		ИтогДокумента = ИтогДокумента + ВыборкаРаботники.Величина;
		
		//уберем из табельного номера префикс
		ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;

		// разбиение на страницы
		ВыведеноСтрок = ВыведеноСтрок + 1;
		
		// Проверим, уместится ли строка на странице или надо открывать новую страницу
		ВывестиПодвалЛиста = Не ТабДокумент.ПроверитьВывод(ВыводимыеОбласти);
		Если Не ВывестиПодвалЛиста и ВыведеноСтрок = ВсегоСтрокДокумента Тогда
			ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
			ВывестиПодвалЛиста = Не ТабДокумент.ПроверитьВывод(ВыводимыеОбласти);
		КонецЕсли;
		Если ВывестиПодвалЛиста Тогда
			ПодИтогСтраницы = 0;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);

	КонецЦикла;

	// если не было ни одного работника - выводим пустой бланк
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакета);
	ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
	Для Сч = 1 По ОбластьМакета.Параметры.Количество() Цикл
		ОбластьМакета.Параметры.Установить(Сч - 1,""); 
	КонецЦикла;
	ОбластьМакета.Параметры.Работник = " " + Символы.ПС + " ";
	Пока ТабДокумент.ПроверитьВывод(ВыводимыеОбласти) Цикл
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	// выводим предварительно подготовленный Подвал документа.
	ТабДокумент.Вывести(ОбластьМакетаПодвал);

	Возврат ТабДокумент;

КонецФункции // ПечатьТ11а()

// Функция формирует табличный документ с печатной формой "Т-11",
//
// Возвращаемое значение:
//	Табличный документ - печатная форма
//
Функция ПечатьТ11()
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПрогрессивкаРаботниковОрганизаций_Т11";
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	// получаем данные для печати
	ВыборкаДляШапки = СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
	ВыборкаРаботники = СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();

	// запоминаем области макета
	Макет = ПолучитьМакет("Т11");
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка"); // Шапка документа
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал"); // Подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("Работник"); // строка работника

	// выводим данные о руководителях организации
	Если ВыборкаДляШапки.Следующий() Тогда
		ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапки); // Шапка документа.
 		ОбластьМакетаШапка.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
		ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапки); // Для подвала.
		ОбластьМакета.Параметры.Заполнить(ВыборкаДляШапки); // название вида расчета
	КонецЕсли;

	Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	СтрокаЧисла = РаботаСДиалогами.СформироватьСуммуПрописью(1,Валюта);
	Если СтрДлина(СтрокаЧисла) = 4 Тогда
		НазваниеЦелыхЕдиниц = " руб. ";	
		НазваниеДробныхЕдиниц = " коп.)";	
	Иначе
		НазваниеЦелыхЕдиниц = " " + Сред(СтрокаЧисла,6,3) + ". ";	
		НазваниеДробныхЕдиниц = " " + Сред(СтрокаЧисла,Найти(СтрокаЧисла,"00") + 3,3) + ".)";
	КонецЕсли;
	// Начинаем формировать выходной документ
	Пока ВыборкаРаботники.Следующий() Цикл

		// Каждый приказ на отдельной странице.
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		// Шапка документа.
		ТабДокумент.Вывести(ОбластьМакетаШапка);
		
		// Данные по работнику.
		ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
		
		//уберем из табельного номера префикс
		ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;
		
		Результат = ВыборкаРаботники.Величина;
		ОбластьМакета.Параметры.ПредставлениеВеличины = РаботаСДиалогами.СформироватьСуммуПрописью(Результат,Валюта);
		Если Результат = 0 Тогда
			ОбластьМакета.Параметры.ПредставлениеВеличиныкр = "(________ руб. ____ коп.)";
		Иначе
			ОбластьМакета.Параметры.ПредставлениеВеличиныкр = "( " + СтрЗаменить(Формат(Результат,"ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧН=' '"),",",НазваниеЦелыхЕдиниц) + НазваниеДробныхЕдиниц;
		КонецЕсли;
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Подвал документа.
		ТабДокумент.Вывести(ОбластьМакетаПодвал);

	КонецЦикла;

	// если не было ни одного работника - выводим пустой бланк
	Если ТабДокумент.ВысотаТаблицы = 0 Тогда
		ТабДокумент.Вывести(ОбластьМакетаШапка);
		ТабДокумент.Вывести(ОбластьМакета);
		ТабДокумент.Вывести(ОбластьМакетаПодвал);
	КонецЕсли;

	Возврат ТабДокумент;

КонецФункции // ПечатьТ11()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	Запрос.УстановитьПараметр("парамПустаяОрганизация", Справочники.Организации.ПустаяСсылка());

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрогрессивкаРаботниковОрганизаций.Дата,
	|	ПрогрессивкаРаботниковОрганизаций.ПериодРегистрации,
	|	ПрогрессивкаРаботниковОрганизаций.Организация,
	|	ВЫБОР
	|		КОГДА ПрогрессивкаРаботниковОрганизаций.Организация.ГоловнаяОрганизация = &парамПустаяОрганизация
	|			ТОГДА ПрогрессивкаРаботниковОрганизаций.Организация
	|		ИНАЧЕ ПрогрессивкаРаботниковОрганизаций.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ПрогрессивкаРаботниковОрганизаций.Организация КАК ОбособленноеПодразделение,
//	|	ПрогрессивкаРаботниковОрганизаций.ВидРасчета,
	|	ПрогрессивкаРаботниковОрганизаций.ЧислоМесяцев,
	|	ПрогрессивкаРаботниковОрганизаций.ДатаНачалаБП КАК БазовыйПериодНачало,
	|	ПрогрессивкаРаботниковОрганизаций.ДатаОкончанияБП КАК БазовыйПериодКонец,
	|	ПрогрессивкаРаботниковОрганизаций.ПерерассчитываемыйДокумент КАК ПерерассчитываемыйДокумент,
	|	ПрогрессивкаРаботниковОрганизаций.ПерерассчитываемыйДокумент.ПериодРегистрации КАК ПериодПерерасчета,
	|	ПрогрессивкаРаботниковОрганизаций.ПерерассчитываемыйДокумент.Организация КАК ОрганизацияПерерасчета,
	|	ПрогрессивкаРаботниковОрганизаций.Ссылка
//	|	ПрогрессивкаРаботниковОрганизаций.ВидРасчета.ВидПремии КАК ВидПремии,
//	|	ПрогрессивкаРаботниковОрганизаций.ВидРасчета.СпособРасчета КАК СпособРасчета,
//	|	ПрогрессивкаРаботниковОрганизаций.ВидРасчета.КодДоходаНДФЛ КАК КодДоходаНДФЛ,
//	|	(НЕ ПрогрессивкаРаботниковОрганизаций.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме) КАК РегистрироватьСуммыКВыплате,
//	|	ПрогрессивкаРаботниковОрганизаций.ВидРасчета.ВидПремии
	|ИЗ
	|	Документ.ПрогрессивкаРаботниковОрганизаций КАК ПрогрессивкаРаботниковОрганизаций
	|ГДЕ
	|	ПрогрессивкаРаботниковОрганизаций.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "Начисления" документа
//
// Параметры: 
//	Режим	- режим проведения
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента, Сотрудник = Неопределено)

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",				Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",		ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("НачалоПериодаРегистрации",	ПериодРегистрации);
	Запрос.УстановитьПараметр("КонецПериодаРегистрации",	КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаРегистрации",			?(Дата > КонецМесяца(ПериодРегистрации),КонецМесяца(ПериодРегистрации),?(Дата < ПериодРегистрации,ПериодРегистрации,Дата)));
	Запрос.УстановитьПараметр("Принят",						Перечисления.ПричиныИзмененияСостояния.ПриемНаРаботу);
	
	Если НЕ (Сотрудник = Неопределено) Тогда
		Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
		
		УсловиеНаСотрудникТекст			= " И СтрокиНачисления.Сотрудник = &Сотрудник ";	
		УсловиеНаСотрудникТекстПремии	= " И ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник = &Сотрудник ";
		
	Иначе
		УсловиеНаСотрудникТекст			= "";
		УсловиеНаСотрудникТекстПремии	= "";
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиНачисления.Сотрудник,
	|	СтрокиНачисления.Сотрудник.Физлицо КАК Физлицо,
	|	СтрокиНачисления.НомерСтроки КАК НомерСтроки,
	|	СтрокиНачисления.Размер,
	//|	ВЫБОР
	//|		КОГДА ДанныеПоРаботникуПослеРегистрации.ПричинаИзмененияСостояния = &Принят
	//|			ТОГДА ДанныеПоРаботникуПослеРегистрации.ПодразделениеОрганизации
	//|		ИНАЧЕ ВЫБОР
	//|				КОГДА  ДанныеПоРаботникуДоРегистрации.ПериодЗавершения <= &ДатаРегистрации
	//|					И ДанныеПоРаботникуДоРегистрации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	//|				ТОГДА ДанныеПоРаботникуДоРегистрации.ПодразделениеОрганизацииЗавершения
	//|				ИНАЧЕ ДанныеПоРаботникуДоРегистрации.ПодразделениеОрганизации
	//|			КОНЕЦ
	//|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	СтрокиНачисления.ВидРасчета,
	|	СтрокиНачисления.ПодразделениеОрганизации,
	|	СтрокиНачисления.Результат,
	|	СтрокиНачисления.Авторасчет,
	|	СтрокиНачисления.Сторно,
	|	ВЫБОР
	|		КОГДА ДанныеПоРаботникуДоРегистрации.ПериодЗавершения <= &ДатаРегистрации
	|						И ДанныеПоРаботникуДоРегистрации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) И ДанныеПоРаботникуДоРегистрации.ПричинаИзмененияСостоянияЗавершения = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|			ТОГДА ВЫБОР
	|					КОГДА ДОБАВИТЬКДАТЕ(ДанныеПоРаботникуДоРегистрации.ПериодЗавершения, ДЕНЬ, -1) < &НачалоПериодаРегистрации
	|						ТОГДА &ДатаРегистрации
	|					ИНАЧЕ ДОБАВИТЬКДАТЕ(ДанныеПоРаботникуДоРегистрации.ПериодЗавершения, ДЕНЬ, -1)
	|				КОНЕЦ
	|		КОГДА ДанныеПоРаботникуДоРегистрации.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|			ТОГДА ВЫБОР
	|					КОГДА ДОБАВИТЬКДАТЕ(ДанныеПоРаботникуДоРегистрации.Период, ДЕНЬ, -1) < &НачалоПериодаРегистрации
	|						ТОГДА &ДатаРегистрации
	|					ИНАЧЕ ДОБАВИТЬКДАТЕ(ДанныеПоРаботникуДоРегистрации.Период, ДЕНЬ, -1)
	|				КОНЕЦ
	|		КОГДА ДанныеПоРаботникуПослеРегистрации.ПричинаИзмененияСостояния = &Принят
	|			ТОГДА 
	|				  ВЫБОР
	|					КОГДА ДанныеПоРаботникуПослеРегистрации.Период > &КонецПериодаРегистрации
	|					ТОГДА &ДатаРегистрации
	|					ИНАЧЕ 
	|						ВЫБОР 
	|							КОГДА ДанныеПоРаботникуПослеРегистрации.ПериодЗавершения <= &ДатаРегистрации
	|									И ДанныеПоРаботникуДоРегистрации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|							ТОГДА ДанныеПоРаботникуПослеРегистрации.ПериодЗавершения
	|							ИНАЧЕ ДанныеПоРаботникуПослеРегистрации.Период
	|						КОНЕЦ	
	|				  КОНЕЦ
	|		ИНАЧЕ &ДатаРегистрации
	|	КОНЕЦ КАК ДатаРегистрации,
	|	ВЫБОР
	|		КОГДА СтрокиНачисления.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации
	|ИЗ
	|	Документ.ПрогрессивкаРаботниковОрганизаций.Начисления КАК СтрокиНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|		&ДатаРегистрации,
	|		Сотрудник В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник
	|			ИЗ
	|				Документ.ПрогрессивкаРаботниковОрганизаций.Начисления КАК ПрогрессивкаРаботниковОрганизацийНачисления
	|			ГДЕ
	|				ПрогрессивкаРаботниковОрганизацийНачисления.Ссылка = &ДокументСсылка" + УсловиеНаСотрудникТекстПремии + ")) КАК ДанныеПоРаботникуДоРегистрации
	|		ПО ДанныеПоРаботникуДоРегистрации.Сотрудник = СтрокиНачисления.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПервых(
	|		&ДатаРегистрации,
	|		Сотрудник В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ПрогрессивкаРаботниковОрганизацийНачисления.Сотрудник
	|			ИЗ
	|				Документ.ПрогрессивкаРаботниковОрганизаций.Начисления КАК ПрогрессивкаРаботниковОрганизацийНачисления
	|			ГДЕ
	|				ПрогрессивкаРаботниковОрганизацийНачисления.Ссылка = &ДокументСсылка" + УсловиеНаСотрудникТекстПремии + ")) КАК ДанныеПоРаботникуПослеРегистрации
	|		ПО ДанныеПоРаботникуПослеРегистрации.Сотрудник = СтрокиНачисления.Сотрудник
	|ГДЕ
	|	СтрокиНачисления.Ссылка = &ДокументСсылка" + УсловиеНаСотрудникТекст + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоНачисления()

// Формирует запрос по таблице "Начисления" документа
//
// Параметры: 
//	Режим	- режим проведения
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоДоходам()

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("Регистратор" , Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Доходы.ФизЛицо,
	|	СУММА(Доходы.Результат) КАК Результат,
	|	Доходы.ПодразделениеОрганизации,
	|	Доходы.ВидРасчета.КодДоходаНДФЛ КАК КодДоходаНДФЛ
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК Доходы
	|ГДЕ
	|	Доходы.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.ФизЛицо,
	|	Доходы.ПодразделениеОрганизации,
	|	Доходы.ВидРасчета.КодДоходаНДФЛ";
				

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоДоходам()

Функция СформироватьЗапросПоВзаиморасчетам()

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("Ссылка" , Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтрокиНачисления.Сотрудник.Физлицо КАК ФизЛицо,
	|	(НЕ СтрокиНачисления.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме) КАК РегистрироватьСуммыКВыплате,
	|	СтрокиНачисления.ВидРасчета.ВидПремии КАК ВидПремии,
	|	СУММА(СтрокиНачисления.Результат) КАК Результат
	|ИЗ
	|	Документ.ПрогрессивкаРаботниковОрганизаций.Начисления КАК СтрокиНачисления
	|ГДЕ
	|	СтрокиНачисления.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СтрокиНачисления.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме,
	|	СтрокиНачисления.ВидРасчета.ВидПремии,
	|	СтрокиНачисления.Сотрудник.Физлицо";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоВзаиморасчетам()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	//  Организация
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация, по которой выполняются начисления!", Отказ, Заголовок);
	КонецЕсли;

	//  ВидРасчета
	//ЕстьВидРасчета = ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ВидРасчета);
	//Если Не ЕстьВидРасчета Тогда
	//	ОбщегоНазначения.СообщитьОбОшибке("Не указан вид премии!", Отказ, Заголовок);
	//КонецЕсли;

	//  Число месяцев
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ЧислоМесяцев) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указано число месяцев за которые начисляется премия!", Отказ, Заголовок);
	КонецЕсли;

	// соответствие периодов документа и перерассчитываемого документа
	Если ВыборкаПоШапкеДокумента.ПериодПерерасчета <> null 
		и ВыборкаПоШапкеДокумента.ПериодРегистрации <= ВыборкаПоШапкеДокумента.ПериодПерерасчета Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Период документа должен быть больше периода перерассчитываемого документа!", Отказ, Заголовок);
	КонецЕсли;
	// соответствие организаций документа и перерассчитываемого документа
	Если ВыборкаПоШапкеДокумента.ОрганизацияПерерасчета <> null 
		и ВыборкаПоШапкеДокумента.ОбособленноеПодразделение <> ВыборкаПоШапкеДокумента.ОрганизацияПерерасчета Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Организация, заданная для документа, должна совпадать с организацией перерассчитываемого документа!", Отказ, Заголовок);
	КонецЕсли;

	//Если ЕстьВидРасчета Тогда
	//	
	//	Если ВыборкаПоШапкеДокумента.ВидПремии = Перечисления.ВидыПремии.НеПремия Тогда
	//		ОбщегоНазначения.СообщитьОбОшибке("В качестве премии указан вид расчета, не описанный как премия!", Отказ, Заголовок);
	//		
	//	Иначе
	//		ЕстьДатаНачалаБП	= ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.БазовыйПериодНачало);
	//		ЕстьДатаОкончанияБП	= ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.БазовыйПериодКонец);
	//		
	//		Если ВыборкаПоШапкеДокумента.СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.Процентом Тогда
	//			
	//			Если Не ЕстьДатаНачалаБП Тогда
	//				ОбщегоНазначения.СообщитьОбОшибке("Не указана дата начала базового периода!", Отказ, Заголовок);
	//			КонецЕсли;
	//			
	//			Если Не ЕстьДатаОкончанияБП Тогда
	//				ОбщегоНазначения.СообщитьОбОшибке("Не указана дата окончания базового периода!", Отказ, Заголовок);
	//			КонецЕсли;
	//			
	//		КонецЕсли;
	//		
	//	КонецЕсли;
	//	
	//	Если ВыборкаПоШапкеДокумента.ВидПремии = Перечисления.ВидыПремии.ГодоваяПремия Тогда
	//		
	//		Если ВыборкаПоШапкеДокумента.СпособРасчета <> Перечисления.СпособыРасчетаОплатыТруда.Процентом Тогда
	//			
	//			Если Не ЕстьДатаНачалаБП Тогда
	//				ОбщегоНазначения.СообщитьОбОшибке("Не указана дата начала базового периода!", Отказ, Заголовок);
	//			КонецЕсли;
	//			
	//			Если Не ЕстьДатаОкончанияБП Тогда
	//				ОбщегоНазначения.СообщитьОбОшибке("Не указана дата окончания базового периода!", Отказ, Заголовок);
	//			КонецЕсли;
	//			
	//		КонецЕсли;
	//		
	//		
	//		Если ЕстьДатаНачалаБП И ЕстьДатаОкончанияБП Тогда
	//			
	//			Если Год(ВыборкаПоШапкеДокумента.БазовыйПериодНачало) <> Год(ВыборкаПоШапкеДокумента.БазовыйПериодКонец) Тогда
	//				
	//				ОбщегоНазначения.СообщитьОбОшибке("Для годовой премии дата начала и окончания базового периода должны быть в пределах одного года!", Отказ, Заголовок)
	//				
	//			КонецЕсли;
	//			
	//		КонецЕсли;
	//		
	//	КонецЕсли;
	//	
	//КонецЕсли;
	//
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "Начисления" документа.
// Если какой-то из реквизтов, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса к ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры:
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса к ТЧ документа, 
//	Отказ						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""Начисления"": ";
	
	// Сотрудник
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указанный сотрудник оформлен на другую организацию!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиНачисления()

// По строке выборок из результатов запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента					- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента				- спозиционированная на определеной строке выборка 
//											  из результата запроса к ТЧ документа, 
//	СтруктураПроведенияПоРегистрамРасчета	- структура, содержащая имена регистров 
//											  расчета по которым надо проводить документ,
//	СтруктураПараметров						- структура параметров проведения,
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамРасчета(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, 
		  НаборЗаписей, СтруктураПараметров = Неопределено)

	Движение = НаборЗаписей.Добавить();
	
	// Свойства
	Движение.ПериодРегистрации			= ВыборкаПоСтрокамДокумента.ДатаРегистрации;
	Движение.ВидРасчета					= ВыборкаПоСтрокамДокумента.ВидРасчета;
	Движение.БазовыйПериодНачало		= ВыборкаПоШапкеДокумента.БазовыйПериодНачало;
	Движение.БазовыйПериодКонец			= КонецДня(ВыборкаПоШапкеДокумента.БазовыйПериодКонец);
	Движение.Сторно						= ВыборкаПоСтрокамДокумента.Сторно;
	
	// Измерения
	Движение.Сотрудник					= ВыборкаПоСтрокамДокумента.Сотрудник;
	Движение.Физлицо					= ВыборкаПоСтрокамДокумента.Физлицо;
	Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	
	// Ресурсы
	Движение.Результат					= ВыборкаПоСтрокамДокумента.Результат;
	
	// Реквизиты
	Движение.Показатель1				= ВыборкаПоСтрокамДокумента.Размер;
	Движение.ЧислоМесяцев				= ВыборкаПоШапкеДокумента.ЧислоМесяцев;
	Движение.Авторасчет					= ВыборкаПоСтрокамДокумента.Авторасчет;
	Движение.ПодразделениеОрганизации	= ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации;
	Движение.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.ОбособленноеПодразделение;
    Движение.ДатаНачалаСобытия 			= ВыборкаПоСтрокамДокумента.ДатаРегистрации;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамРасчета()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//	ВыборкаПоШапкеДокумента						- выборка из результата запроса по шапке документа
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоНДФЛ(ВыборкаПоШапкеДокумента, ВыборкаПоТЧ)
	
	Движение = Движения.НДФЛСведенияОДоходах.Добавить();
	
	// Свойства
	Движение.Период						= КонецМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации);
	
	// Измерения
	Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	Движение.ФизЛицо					= ВыборкаПоТЧ.ФизЛицо;
	Движение.КодДохода					= ВыборкаПоТЧ.КодДоходаНДФЛ;
	Движение.ПериодРегистрации			= НачалоМесяца(ВыборкаПоШапкеДокумента.ПериодРегистрации);
	
	// Ресурсы
	Движение.СуммаДохода				= ВыборкаПоТЧ.Результат; 
	
	// Реквизиты
	Движение.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.ОбособленноеПодразделение;
	Движение.ИсчисленоИзЗарплаты		= Истина;
	Движение.ПодразделениеОрганизации   = ВыборкаПоТЧ.ПодразделениеОрганизации; 
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()

Процедура ДобавитьСтрокуВДвиженияПоВзаиморасчетам(ВыборкаПоШапкеДокумента, ВыборкаПоТЧ, УчетЗадолженностиПоМесяцам)
	
	Движение = Движения.ВзаиморасчетыСРаботникамиОрганизаций.Добавить();
	
	// Свойства
	Движение.Период					= ВыборкаПоШапкеДокумента.ПериодРегистрации;
	Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
	
	// Измерения
	Движение.Организация			= ВыборкаПоШапкеДокумента.Организация;
	Движение.ФизЛицо				= ВыборкаПоТЧ.ФизЛицо;
	Если УчетЗадолженностиПоМесяцам Тогда
		Движение.ПериодВзаиморасчетов	= ВыборкаПоШапкеДокумента.ПериодРегистрации;
	КонецЕсли;
	
	// Ресурсы
	Движение.СуммаВзаиморасчетов	= ВыборкаПоТЧ.Результат;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//структуры, содержащие имена регистров, по которым надо проводить документ
	Перем СтруктураПроведенияПоРегистрамРасчета;
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда
			
			// ведется ли учет задолженности в разрезе периодов возникновения задолженности
			УчетЗадолженностиПоМесяцам	= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "УчетЗадолженностиПоМесяцам");
			
			// получим реквизиты табличной части
			ВыборкаПоНачислениям = СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента).Выбрать();

			Пока ВыборкаПоНачислениям.Следующий() Цикл

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Отказ, Заголовок);

				Если НЕ Отказ Тогда

					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуВДвиженияПоРегистрамРасчета(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Движения.ДополнительныеНачисленияРаботниковОрганизаций);

				КонецЕсли;

			КонецЦикла;

			Если НЕ Отказ Тогда
				
				// перед тем как начать расчет, сформированные движения надо записать - 
				// по ним чуть позже будем получать дополнительные данные для расчетов
				Движения.ДополнительныеНачисленияРаботниковОрганизаций.Записать();
					
				Доходы = СформироватьЗапросПоДоходам().Выбрать();
				Пока Доходы.Следующий() Цикл
					Если ЗначениеЗаполнено(Доходы.КодДоходаНДФЛ) Тогда
						ДобавитьСтрокуВДвиженияПоНДФЛ(ВыборкаПоШапкеДокумента, Доходы);
					КонецЕсли;
				КонецЦикла;
					
				
				Доходы = СформироватьЗапросПоВзаиморасчетам().Выбрать();
				Пока Доходы.Следующий() Цикл
					Если Доходы.РегистрироватьСуммыКВыплате  и Доходы.ВидПремии <> Перечисления.ВидыПремии.ПремияВРезерв Тогда
						ДобавитьСтрокуВДвиженияПоВзаиморасчетам(ВыборкаПоШапкеДокумента, Доходы, УчетЗадолженностиПоМесяцам);
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			Если Не ПерерассчитываемыйДокумент.Пустая() Тогда
				// выполним удаление перерасчетов перерассчитываемого документа
				НаборЗаписейПерерасчета = РегистрыРасчета.ДополнительныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетДополнительныхНачислений.СоздатьНаборЗаписей();
				НаборЗаписейПерерасчета.Отбор.ОбъектПерерасчета.Значение = ПерерассчитываемыйДокумент;
				НаборЗаписейПерерасчета.Записать();
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(Начисления);
	
КонецПроцедуры // ПередЗаписью()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
