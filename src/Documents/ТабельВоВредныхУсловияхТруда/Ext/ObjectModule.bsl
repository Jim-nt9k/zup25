Процедура ПроверкаДублейСтрок(ПроверяемыеРеквизиты,Отказ)
 
	ТаблицаДокумента = РаботникиОрганизации.Выгрузить();
	ТаблицаДокумента.Колонки.Добавить("КвоПроверкаДублей"); 
	ТаблицаДокумента.ЗаполнитьЗначения(1, "КвоПроверкаДублей");
	
	ТаблицаДокумента.Свернуть(ПроверяемыеРеквизиты,"КвоПроверкаДублей");
	
	Для Каждого ТекущаяСтрока Из ТаблицаДокумента Цикл 
		Если ТекущаяСтрока.КвоПроверкаДублей > 1 Тогда
			РеквизитыДляСообщения = "";
			Стр = СокрЛП(ПроверяемыеРеквизиты);
			Пока Найти(Стр,",") > 0 Цикл
				НаимРеквизита = СокрЛП(Лев(Стр,Найти(Стр,",") - 1));
				Стр = Прав(Стр,СтрДлина(Стр) - Найти(Стр,","));
				РеквизитыДляСообщения = РеквизитыДляСообщения+ТекущаяСтрока[НаимРеквизита]+", ";
			КонецЦикла; 
			РеквизитыДляСообщения = РеквизитыДляСообщения+ТекущаяСтрока[СокрЛП(Стр)];
			
			Сообщить("Информация """ + РеквизитыДляСообщения+""" введена несколько раз в табличную часть, проведение невозможно!");
			Отказ = Истина;
		КонецЕсли; 
	КонецЦикла;
 
 КонецПроцедуры



Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	 Если РаботникиОрганизации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаДублейСтрок("Сотрудник,Дата", Отказ);

	Если НЕ Отказ Тогда
		НачатьТранзакцию();
		Движения.РабочееВремяРаботниковОрганизаций.Записывать = Истина;
		Движения.РабочееВремяРаботниковОрганизаций.Очистить();
		Для Каждого ТекСтрока Из РаботникиОрганизации Цикл
			Движение = Движения.РабочееВремяРаботниковОрганизаций.Добавить();
			Движение.Период = ТекСтрока.Дата;
			Движение.ДатаНачала = ТекСтрока.Дата;
			Движение.ДатаОкончания = ТекСтрока.Дата;
			Движение.Организация = Организация;
			Движение.Сотрудник = ТекСтрока.Сотрудник;
			Движение.ВидИспользованияРабочегоВремени = Справочники.КлассификаторИспользованияРабочегоВремени.Вредность;
			Движение.ЧасовВредность = ТекСтрока.Часы;
		КонецЦикла;
		Попытка
			ЗафиксироватьТранзакцию();
		Исключение
			Сообщить("Не удалось провести документ!");
		КонецПопытки;
	КонецЕсли;

КонецПроцедуры


Процедура ПечатьТабеля() экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ТабельВоВредныхУсловияхТруда";
	
	Макет = ПолучитьМакет("Макет");
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка"); // Шапка документа.
	ПовторятьПриПечатиСтроки = Макет.ПолучитьОбласть("ПовторятьПриПечати"); // повторяющаяся шапка страницы
	ОбластьМакетаСтрока = Макет.ПолучитьОбласть("Строка"); // строка работника
	
	// массив с двумя строками - для разбиения на страницы
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакетаСтрока);
	ОбластьМакетаШапка.Параметры.Организация = Ссылка.Организация.НаименованиеПолное;
	Если ЗначениеЗаполнено(Ссылка.ПодразделениеОрганизации) Тогда
		ОбластьМакетаШапка.Параметры.Подразделение = Ссылка.ПодразделениеОрганизации;	
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьМакетаШапка);
	ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТабельВоВредныхУсловияхТрудаРаботникиОрганизации.Сотрудник КАК Сотрудник,
		|	СУММА(ТабельВоВредныхУсловияхТрудаРаботникиОрганизации.Часы) КАК Часы,
		|	ТабельВоВредныхУсловияхТрудаРаботникиОрганизации.Сотрудник.Код КАК ТабНомер,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТабельВоВредныхУсловияхТрудаРаботникиОрганизации.Дата) КАК Дни
		|ИЗ
		|	Документ.ТабельВоВредныхУсловияхТруда.РаботникиОрганизации КАК ТабельВоВредныхУсловияхТрудаРаботникиОрганизации
		|ГДЕ
		|	ТабельВоВредныхУсловияхТрудаРаботникиОрганизации.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ТабельВоВредныхУсловияхТрудаРаботникиОрганизации.Сотрудник,
		|	ТабельВоВредныхУсловияхТрудаРаботникиОрганизации.Сотрудник.Код
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТабельВоВредныхУсловияхТрудаРаботникиОрганизации.Сотрудник.Наименование";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	НомерСтроки = 1;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбластьМакетаСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
		ОбластьМакетаСтрока.Параметры.НомерСТР=НомерСтроки;
		НомерСтроки = НомерСтроки+1;
		Если  НЕ ТабДокумент.ПроверитьВывод(ВыводимыеОбласти) Тогда	
			 ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			 ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
		 КонецЕсли;
		 ТабДокумент.Вывести(ОбластьМакетаСтрока);
	КонецЦикла;
	
	ТабДокумент.Показать();
КонецПроцедуры