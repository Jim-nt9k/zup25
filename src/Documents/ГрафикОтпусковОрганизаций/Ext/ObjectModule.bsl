////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мДлинаСуток;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Заполняет табличную часть документа отпусками по утвержденным отпускам управленческого учета
//
// Параметры
//	ДатаНачала, ДатаОкончания	– даты начала и окончания
//								  просмотра графика отпусков
//
Процедура Автозаполнение(ДатаНачала, ДатаОкончания) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("Организация",			Организация);
	Запрос.УстановитьПараметр("ДатаНачала",				ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",			ДатаОкончания);
	Запрос.УстановитьПараметр("ПустаяДата",				'00010101');
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СобытийныйПланЗанятостиФизлиц.Период КАК ДатаНачала,
	|	СобытийныйПланЗанятостиФизлиц.ПериодЗавершения КАК ДатаОкончания,
	|	РаботникиОрганизацииСрезПоследних.Сотрудник,
	|	РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо КАК Физлицо
	|ИЗ
	|	РегистрСведений.СобытийныйПланЗанятостиФизлиц КАК СобытийныйПланЗанятостиФизлиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|		&ДатаОкончания,
	|		Организация = &ГоловнаяОрганизация
	|			И Сотрудник.Физлицо В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					СобытийныйПланЗанятостиФизлиц.ФизЛицо
	|				ИЗ
	|					РегистрСведений.СобытийныйПланЗанятостиФизлиц КАК СобытийныйПланЗанятостиФизлиц
	|				ГДЕ
	|					СобытийныйПланЗанятостиФизлиц.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|					И СобытийныйПланЗанятостиФизлиц.Состояние = ЗНАЧЕНИЕ(Перечисление.ТипыПериодическихЗадачРаботника.ОтпускЕжегодный))) КАК РаботникиОрганизацииСрезПоследних
	|		ПО (ВЫБОР
	|				КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаОкончания
	|						И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделениеЗавершения
	|				ИНАЧЕ РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделение
	|			КОНЕЦ = &Организация)
	|			И СобытийныйПланЗанятостиФизлиц.ФизЛицо = РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо
	|ГДЕ
	|	СобытийныйПланЗанятостиФизлиц.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И СобытийныйПланЗанятостиФизлиц.Состояние = ЗНАЧЕНИЕ(Перечисление.ТипыПериодическихЗадачРаботника.ОтпускЕжегодный)
	|	И РаботникиОрганизацииСрезПоследних.Сотрудник ЕСТЬ НЕ NULL  ";
	//О.Т. Аверсон 09.02.2016 № 870
	Если ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда 
		Запрос.Текст = Запрос.Текст +"И РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации = &ПодразделениеОрганизации";
		Запрос.УстановитьПараметр("ПодразделениеОрганизации",ПодразделениеОрганизации);    
	КонецЕсли;
	//О.Т. Аверсон 09.02.2016 № 870
	
	РаботникиОрганизации.Загрузить(Запрос.Выполнить().Выгрузить());
	//О.Т. Аверсон 17.12.2018 заявка для стравита
	Если ЗначениеЗаполнено(Год) Тогда
		для Каждого стр из РаботникиОрганизации Цикл 
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СостояниеРаботниковОрганизаций.Сотрудник
			|ИЗ
			|	РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботниковОрганизаций
			|ГДЕ
			|	СостояниеРаботниковОрганизаций.Сотрудник = &Сотрудник
			|	И СостояниеРаботниковОрганизаций.Организация = &Организация
			|	И СостояниеРаботниковОрганизаций.Состояние = &Состояние
			|	И &Период МЕЖДУ СостояниеРаботниковОрганизаций.Период И СостояниеРаботниковОрганизаций.ПериодЗавершения";
			
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("Период", КонецГода(Дата(Год,12,31)));
			Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияРаботникаОрганизации.ОтпускПоУходуЗаРебенком);
			Запрос.УстановитьПараметр("Сотрудник", стр.Сотрудник);
			
			РезультатЗапроса = Запрос.Выполнить().Выгрузить();
			Если РезультатЗапроса.Количество() > 0 Тогда 
				стр.ДатаНачала = Дата(Год,01,01);
				стр.ДатаОкончания = КонецГода(Дата(Год,12,31));
				стр.РабочийГодС = Дата(1,1,1);
				стр.РабочийГодПо = Дата(1,1,1);
				стр.ДнейОтпуска = 0;
				
			КонецЕсли;
			
			
			
		КонецЦикла;
	КонецЕсли;
	//О.Т. Аверсон 17.12.2018
	
	
КонецПроцедуры // Автозаполнение()

#Если Клиент Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
// Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//	НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	// Получить экземпляр документа на печать
	Если Лев(ИмяМакета,2) = "Т7" Тогда
		
		МассивВидовРуководителей = Новый Массив();
		МассивВидовРуководителей.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		МассивВидовРуководителей.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент",	Ссылка);
		Запрос.УстановитьПараметр("ВидЛица",			МассивВидовРуководителей);
		Запрос.УстановитьПараметр("ДатаДокумента",		Дата);
		
//толик фмо руководителя	
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГрафикОтпусковОрганизации.Дата,
		|	ГрафикОтпусковОрганизации.Номер,
		|	ВЫРАЗИТЬ(ГрафикОтпусковОрганизации.Организация.НаименованиеПолное КАК СТРОКА(300)) КАК Организация,
		|	ГрафикОтпусковОрганизации.Организация.КодПоОКПО КАК КодПоОКПО,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Представление КАК ДолжностьРуководителя,
		|	ЕСТЬNULL(ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "". "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "". "" + ФИОФизЛицСрезПоследних.Фамилия, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо,
		|	ГрафикОтпусковОрганизации.Организация.Префикс
		|ИЗ
		|	Документ.ГрафикОтпусковОрганизаций КАК ГрафикОтпусковОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаДокумента, ОтветственноеЛицо В (&ВидЛица)) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизЛицСрезПоследних
		|			ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|		ПО ГрафикОтпусковОрганизации.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница
		|ГДЕ
		|	ГрафикОтпусковОрганизации.Ссылка = &ТекущийДокумент";
		
		РезультатДляШапкиИПодвала = Запрос.Выполнить();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент",		Ссылка);
		Запрос.УстановитьПараметр("ВидЛица",				Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		Запрос.УстановитьПараметр("ДатаДокумента",			Дата);
		Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ОбщегоНазначения.ГоловнаяОрганизация(Организация));
		Запрос.УстановитьПараметр("Праздник",				Перечисления.ВидыДнейПроизводственногоКалендаря.Праздник);
		
//толик добавли примечание
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГрафикОтпусковОрганизацииРаботники.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество, ГрафикОтпусковОрганизацииРаботники.Сотрудник.Наименование) КАК Работник,
		|	ГрафикОтпусковОрганизацииРаботники.ДатаНачала,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Представление КАК ДолжностьРуководителя,
		|	ЕСТЬNULL(ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "". "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "". "" + ФИОФизЛицСрезПоследних.Фамилия, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаДокумента
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации
		|	КОНЕЦ КАК ПодразделениеОрганизации,
		|	ГрафикОтпусковОрганизацииРаботники.Должность КАК Должность,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаДокумента
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения.Наименование
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации.Наименование
		|	КОНЕЦ КАК ПодразделениеОрганизацииНаименование,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаДокумента
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.Должность.Наименование
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.Должность.Наименование
		|	КОНЕЦ КАК ДолжностьНаименование,
		|	ГрафикОтпусковОрганизацииРаботники.ДатаОкончания,
		|	ГрафикОтпусковОрганизацииРаботники.ДнейОтпуска КАК Продолжительность,
		|	ГрафикОтпусковОрганизацииРаботники.Сотрудник.Код КАК ТабельныйНомер,
		|	ГрафикОтпусковОрганизацииРаботники.Примечание,
		|	ГрафикОтпусковОрганизацииРаботники.РабочийГодС,
		|	ГрафикОтпусковОрганизацииРаботники.РабочийГодПо
		|ИЗ
		|	Документ.ГрафикОтпусковОрганизаций.РаботникиОрганизации КАК ГрафикОтпусковОрганизацииРаботники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
		|				&ДатаДокумента,
		|				Сотрудник В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник
		|					ИЗ
		|						Документ.ГрафикОтпусковОрганизаций.РаботникиОрганизации КАК ГрафикОтпусковОрганизацииРаботникиОрганизации
		|					ГДЕ
		|						ГрафикОтпусковОрганизацииРаботникиОрганизации.Ссылка = &ТекущийДокумент)) КАК РаботникиОрганизацииСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаДокумента, ОтветственноеЛицо = &ВидЛица) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизЛицСрезПоследних
		|				ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|			ПО (ВЫБОР
		|					КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаДокумента
		|							И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|						ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения
		|					ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации
		|				КОНЕЦ = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница)
		|		ПО ГрафикОтпусковОрганизацииРаботники.Сотрудник = РаботникиОрганизацииСрезПоследних.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК КалендарьДнейВсего
		|		ПО (КалендарьДнейВсего.ВидДня <> &Праздник)
		|			И (КалендарьДнейВсего.ДатаКалендаря МЕЖДУ ГрафикОтпусковОрганизацииРаботники.ДатаНачала И ГрафикОтпусковОрганизацииРаботники.ДатаОкончания)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
		|				&ДатаДокумента,
		|				Физлицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник.Физлицо
		|					ИЗ
		|						Документ.ГрафикОтпусковОрганизаций.РаботникиОрганизации КАК ГрафикОтпусковОрганизацииРаботникиОрганизации
		|					ГДЕ
		|						ГрафикОтпусковОрганизацииРаботникиОрганизации.Ссылка = &ТекущийДокумент)) КАК ФИОФизЛиц
		|		ПО ГрафикОтпусковОрганизацииРаботники.Сотрудник.Физлицо = ФИОФизЛиц.ФизЛицо
		|ГДЕ
		|	ГрафикОтпусковОрганизацииРаботники.Ссылка = &ТекущийДокумент
		|
		|СГРУППИРОВАТЬ ПО
		|	ГрафикОтпусковОрганизацииРаботники.НомерСтроки,
		|	ГрафикОтпусковОрганизацииРаботники.ДатаНачала,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Представление,
		|	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации,
		|	РаботникиОрганизацииСрезПоследних.Должность,
		|	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения,
		|	РаботникиОрганизацииСрезПоследних.ДолжностьЗавершения,
		|	РаботникиОрганизацииСрезПоследних.ПериодЗавершения,
		|	ГрафикОтпусковОрганизацииРаботники.ДатаОкончания,
		|	ЕСТЬNULL(ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "". "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "". "" + ФИОФизЛицСрезПоследних.Фамилия, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование),
		|	ГрафикОтпусковОрганизацииРаботники.Сотрудник.Код,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаДокумента
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения.Наименование
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации.Наименование
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаДокумента
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.Должность.Наименование
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.Должность.Наименование
		|	КОНЕЦ,
		|	ГрафикОтпусковОрганизацииРаботники.Примечание,
		|	ЕСТЬNULL(ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество, ГрафикОтпусковОрганизацииРаботники.Сотрудник.Наименование),
		|	ГрафикОтпусковОрганизацииРаботники.РабочийГодС,
		|	ГрафикОтпусковОрганизацииРаботники.РабочийГодПо,
		|	ГрафикОтпусковОрганизацииРаботники.ДнейОтпуска,
		|	ГрафикОтпусковОрганизацииРаботники.Должность
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
		РезультатРаботникиОрганизации = Запрос.Выполнить();
		
		Макет = ПолучитьМакет(ИмяМакета);
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ПолеСлева = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПланированиеОтпускаОрганизации_Т7";
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		// Шапка документа.
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		// повторяющаяся шапка страницы
		ПовторятьПриПечатиСтроки = Макет.ПолучитьОбласть("ПовторятьПриПечати");
		// Начало подвала документа
		ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
		
		ВыборкаДляШапкиИПодвала = РезультатДляШапкиИПодвала.Выбрать();
		Если ВыборкаДляШапкиИПодвала.Следующий() Тогда // выводим данные о руководителях организации
			
			ОбластьМакета.Параметры.НазваниеОрганизации = ВыборкаДляШапкиИПодвала.Организация;
			ОбластьМакета.Параметры.КодПоОКПО 			= ВыборкаДляШапкиИПодвала.КодПоОКПО;
			ОбластьМакета.Параметры.НомерДок 			= ВыборкаДляШапкиИПодвала.Номер;
			ОбластьМакета.Параметры.ДатаДок 			= ВыборкаДляШапкиИПодвала.Дата;
			
			ВыборкаДляШапкиИПодвала = РезультатДляШапкиИПодвала.Выбрать();
			Если ВыборкаДляШапкиИПодвала.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель,"ОтветственноеЛицо") Тогда
				ОбластьМакета.Параметры.Заполнить(ВыборкаДляШапкиИПодвала);
			КонецЕсли;
			
			// Для подвала
			ВыборкаДляШапкиИПодвала = РезультатДляШапкиИПодвала.Выбрать();
			Если ВыборкаДляШапкиИПодвала.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы,"ОтветственноеЛицо") Тогда
				ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапкиИПодвала);
			КонецЕсли;
			
		КонецЕсли;
		
		ВыборкаРаботники = РезультатРаботникиОрганизации.Выбрать();
		
		// Начинаем формировать выходной документ (шапка дока)
		ТабДокумент.Вывести(ОбластьМакета);
		
		ВсегоСтрокДокумента = ВыборкаРаботники.Количество();
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаРаботник");
		
		// массив с двумя строками - для разбиения на страницы
		ВыводимыеОбласти = Новый Массив();
		ВыводимыеОбласти.Добавить(ОбластьМакета);
		
		ВыведеноСтрок = 0;
		// выводим строки по работникам
		ВыборкаРаботники = РезультатРаботникиОрганизации.Выбрать();
		Пока ВыборкаРаботники.Следующий() Цикл
		
			// Данные по работнику.
			ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
			ОбластьМакета.Параметры.РабочийГодС = Формат(ВыборкаРаботники.РабочийГодС,"ДЛФ=Д"); //О.Т. Аверсон 19.12.2017 № 1359
			ОбластьМакета.Параметры.РабочийГодПо = Формат(ВыборкаРаботники.РабочийГодПо,"ДЛФ=Д"); //О.Т. Аверсон 19.12.2017 № 1359
			
		//уберем из табельного номера префикс
		ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;
			
			// разбиение на страницы
			ВыведеноСтрок = ВыведеноСтрок + 1;
			
			// Проверим, уместится ли строка на странице или надо открывать новую страницу
			ВывестиПодвалЛиста = Не ТабДокумент.ПроверитьВывод(ВыводимыеОбласти);
			Если Не ВывестиПодвалЛиста и ВыведеноСтрок = ВсегоСтрокДокумента Тогда
				ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
				ВывестиПодвалЛиста = Не ТабДокумент.ПроверитьВывод(ВыводимыеОбласти);
			КонецЕсли;
			Если ВывестиПодвалЛиста Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
			КонецЕсли;
			
			ТабДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;

		// если не было ни одного работника - выводим пустой бланк
		ВыводимыеОбласти = Новый Массив();
		ВыводимыеОбласти.Добавить(ОбластьМакета);
		ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
		Для Сч = 1 По ОбластьМакета.Параметры.Количество() Цикл
			ОбластьМакета.Параметры.Установить(Сч - 1,""); 
		КонецЦикла;
		ОбластьМакета.Параметры.Работник = " " + Символы.ПС + " ";
		Пока ТабДокумент.ПроверитьВывод(ВыводимыеОбласти) Цикл
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
	
		// выводим предварительно подготовленный Подвал документа.
		ТабДокумент.Вывести(ОбластьМакетаПодвал);
		Возврат РаботаСДиалогами.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, РаботаСДиалогами.СформироватьЗаголовокДокумента(ЭтотОбъект,Метаданные().Синоним + " (форма Т-7)"));

	ИначеЕсли ИмяМакета = "Диаграмма" тогда
		
		Если НЕ Проведен Тогда
			Предупреждение("Документ можно распечатать только после его проведения!");
			Возврат Неопределено;
		КонецЕсли;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(ПланированиеОтпускаОрганизацииРаботники.ДатаОкончания) КАК ДатаКон,
		|	МИНИМУМ(ПланированиеОтпускаОрганизацииРаботники.ДатаНачала) КАК ДатаНач
		|ИЗ
		|	Документ.ГрафикОтпусковОрганизаций.РаботникиОрганизации КАК ПланированиеОтпускаОрганизацииРаботники
		|
		|ГДЕ
		|	ПланированиеОтпускаОрганизацииРаботники.Ссылка = &Ссылка");

		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();	 
		
		Если Выборка.Следующий() Тогда
			Отчет = Отчеты.ДиаграммаГанта.Создать();
			Отчет.ВидОтчета = "График отпусков работников организаций";
			Отчет.Периодичность = 2;
			Отчет.ЗаполнитьНачальныеНастройки();
			Отчет.ДатаНач = ?(НЕ ЗначениеЗаполнено(Выборка.ДатаНач),НачалоГода(РабочаяДата),НачалоМесяца(Выборка.ДатаНач));
			Отчет.ДатаКон = ?(НЕ ЗначениеЗаполнено(Выборка.ДатаКон),КонецГода(РабочаяДата),КонецМесяца(Выборка.ДатаКон));
			Возврат Отчет.Печать(Ссылка);
		Иначе
			Предупреждение("Ошибка исполнения запроса к т.ч.");
			Возврат Неопределено
		КонецЕсли;
		
	КонецЕсли;

КонецФункции // Печать()

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//	Струткура, каждая строка которой соответствует одному из вариантов печати
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура;
	
	СтруктураМакетов.Вставить("Т7_от_5_1_2004",	"Форма Т-7");
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры:
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	Запрос.УстановитьПараметр("ПустаяОрганизация" , Справочники.Организации.ПустаяСсылка());

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГрафикОтпусковОрганизаций.Дата,
	|	ГрафикОтпусковОрганизаций.Организация,
	|	ВЫБОР
	|		КОГДА ГрафикОтпусковОрганизаций.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|			ТОГДА ГрафикОтпусковОрганизаций.Организация
	|		ИНАЧЕ ГрафикОтпусковОрганизаций.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ГрафикОтпусковОрганизаций.Ссылка
	|ИЗ
	|	Документ.ГрафикОтпусковОрганизаций КАК ГрафикОтпусковОрганизаций
	|ГДЕ
	|	ГрафикОтпусковОрганизаций.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "РаботникиОрганизации" документа
//
// Параметры:
//	Режим	- режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоРаботникиОрганизации(ВыборкаПоШапкеДокумента)
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);

	// Описание текста запроса:
	// 1. Выборка "ПерваяТаблица": 
	//		Представляет собой вложенный запрос, в котором:  
	//			- в выборке "РаботникиДокумента" выбираются строки документа
	//			- из основной таблицы регистра (выборка "ГрафикОтпусковОрганизации") 
	//			  присоединяются даты движений, непосредственно предшествующих
	//			  датам ДатаОкончания из строк документа
	// 2. Выборка "ГрафикОтпусковОрганизации": 
	//		Из основной таблицы регистра выбираются значения ресурсов на полученные  
	//		в первой выборке даты
	// 3. Выборка "ВтораяТаблица": 
	//		Среди строк документа ищем строки с пересекающимися периодами отпусков 
	//		для одного работника
	//
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПерваяТаблица.НомерСтроки КАК НомерСтроки,
	|	ПерваяТаблица.Сотрудник,
	|	ПерваяТаблица.ДатаНачала,
	|	ПерваяТаблица.ДатаОкончания,
	|	ВЫБОР
	|		КОГДА ПерваяТаблица.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ВЫБОР
	|		КОГДА ГрафикОтпусковОрганизации.Состояние = ЗНАЧЕНИЕ(Перечисление.ТипыПериодическихЗадачРаботника.ОтпускЕжегодный)
	|				ИЛИ ПерваяТаблица.ДатаЗначения >= ПерваяТаблица.ДатаНачала
	|			ТОГДА ""Нельзя""
	|		ИНАЧЕ ""Можно""
	|	КОНЕЦ КАК ПроверяемоеЗначение,
	|	МИНИМУМ(ВтораяТаблица.НомерСтроки) КАК КонфликтнаяСтрока
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ГрафикОтпусковОрганизации.Период) КАК ДатаЗначения,
	|		РаботникиДокумента.Сотрудник КАК Сотрудник,
	|		РаботникиДокумента.ДатаОкончания КАК ДатаОкончания,
	|		РаботникиДокумента.ДатаНачала КАК ДатаНачала,
	|		РаботникиДокумента.Ссылка КАК Ссылка,
	|		РаботникиДокумента.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.ГрафикОтпусковОрганизаций.РаботникиОрганизации КАК РаботникиДокумента
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикОтпусковОрганизаций КАК ГрафикОтпусковОрганизации
	|			ПО РаботникиДокумента.ДатаОкончания > ГрафикОтпусковОрганизации.Период
	|				И РаботникиДокумента.Сотрудник = ГрафикОтпусковОрганизации.Сотрудник
	|	ГДЕ
	|		РаботникиДокумента.Ссылка = &ДокументСсылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РаботникиДокумента.Сотрудник,
	|		РаботникиДокумента.ДатаОкончания,
	|		РаботникиДокумента.ДатаНачала,
	|		РаботникиДокумента.Ссылка,
	|		РаботникиДокумента.НомерСтроки) КАК ПерваяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикОтпусковОрганизаций КАК ГрафикОтпусковОрганизации
	|		ПО ПерваяТаблица.ДатаЗначения = ГрафикОтпусковОрганизации.Период
	|			И ПерваяТаблица.Сотрудник = ГрафикОтпусковОрганизации.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ГрафикОтпусковОрганизаций.РаботникиОрганизации КАК ВтораяТаблица
	|		ПО ПерваяТаблица.Ссылка = ВтораяТаблица.Ссылка
	|			И ПерваяТаблица.НомерСтроки < ВтораяТаблица.НомерСтроки
	|			И (ПерваяТаблица.ДатаНачала <= ВтораяТаблица.ДатаНачала
	|					И ВтораяТаблица.ДатаНачала <= ПерваяТаблица.ДатаОкончания
	|				ИЛИ ПерваяТаблица.ДатаНачала <= ВтораяТаблица.ДатаОкончания
	|					И ВтораяТаблица.ДатаОкончания <= ПерваяТаблица.ДатаОкончания
	|				ИЛИ ВтораяТаблица.ДатаНачала <= ПерваяТаблица.ДатаНачала
	|					И ПерваяТаблица.ДатаОкончания <= ВтораяТаблица.ДатаОкончания)
	|			И ПерваяТаблица.Сотрудник = ВтораяТаблица.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ПерваяТаблица.НомерСтроки,
	|	ГрафикОтпусковОрганизации.Состояние,
	|	ПерваяТаблица.ДатаЗначения,
	|	ПерваяТаблица.ДатаНачала,
	|	ПерваяТаблица.ДатаОкончания,
	|	ПерваяТаблица.Сотрудник";
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоРаботникиОрганизации()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры:
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация, для которой составляется график отпусков!", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "РаботникиОрганизации" документа.
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса по работникам, 
//	Отказ 						- флаг отказа в проведении.
//	Заголовок					- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""Работники организации"": ";
	
	// Сотрудник
	НетСотрудника = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник);
	Если НетСотрудника Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// ДатаНачала
	НетДатыС = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНачала);
	Если НетДатыС Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата начала отпуска!", Отказ, Заголовок);
	КонецЕсли;
	
	// ДатаОкончания
	НетДатыПо = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаОкончания);
	Если НетДатыПо Тогда
//толик
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано количество дней отпуска!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НетСотрудника ИЛИ НетДатыС ИЛИ НетДатыПо Тогда
		Возврат;
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указанный сотрудник оформлен на другую организацию!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.ДатаНачала > ВыборкаПоСтрокамДокумента.ДатаОкончания Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "дата начала отпуска не может превышать дату окончания отпуска!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.ПроверяемоеЗначение = "Нельзя" Тогда
		СтрокаПродолжениеСообщенияОбОшибке = " на указанный период ранее уже был запланирован другой отпуск!";
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.КонфликтнаяСтрока <> Null Тогда
		СтрокаПродолжениеСообщенияОбОшибке = " в строке №" + ВыборкаПоСтрокамДокумента.КонфликтнаяСтрока + " указан пересекающийся период отпуска!";
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиРаботникаОрганизации()

// Создает и заполняет структуру, содержащую имена регистров сведений 
//  по которым надо проводить документ
//
// Параметры: 
//	СтруктураПроведенияПоРегистрамСведений	- структура, содержащая имена регистров сведений 
//											  по которым надо проводить документ
//
// Возвращаемое значение:
//	Нет.
//
Процедура ЗаполнитьСтруктуруПроведенияПоРегистрамСведений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамСведений)

	СтруктураПроведенияПоРегистрамСведений = Новый Структура();
	
	СтруктураПроведенияПоРегистрамСведений.Вставить("ГрафикОтпусковОрганизаций");

КонецПроцедуры // ЗаполнитьСтруктуруПроведенияПоРегистрамСведений()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента					- выборка из результата запроса по шапке документа,
//	СтруктураПроведенияПоРегистрамСведений	- структура, содержащая имена регистров 
//											  сведений по которым надо проводить документ,
//	СтруктураПараметров						- структура параметров проведения,
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации,  
		  СтруктураПроведенияПоРегистрамСведений, СтруктураПараметров = "")

	// Если документ нужно проводить по регистру, то для него есть ключ в структуре
	ИмяРегистра = "ГрафикОтпусковОрганизаций";
	Если СтруктураПроведенияПоРегистрамСведений.Свойство(ИмяРегистра) Тогда

		// отразим начало
		Движение = Движения[ИмяРегистра].Добавить();

		// Свойства
		Движение.Период			= ВыборкаПоРаботникиОрганизации.ДатаНачала;

		// Измерения
		Движение.Сотрудник		= ВыборкаПоРаботникиОрганизации.Сотрудник;
		Движение.Организация	= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;

		// Ресурсы
		Движение.Состояние		= Перечисления.ТипыПериодическихЗадачРаботника.ОтпускЕжегодный;
		
		// Реквизиты
		Движение.ДатаОкончания	= КонецДня(ВыборкаПоРаботникиОрганизации.ДатаОкончания);


		// и окончание отпуска
		Движение = Движения[ИмяРегистра].Добавить();

		// Свойства
		Движение.Период			= ВыборкаПоРаботникиОрганизации.ДатаОкончания + мДлинаСуток - 1;

		// Измерения
		Движение.Сотрудник		= ВыборкаПоРаботникиОрганизации.Сотрудник;
		Движение.Организация	= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		
		// Ресурсы
		Движение.Состояние		= Перечисления.ТипыПериодическихЗадачРаботника.Свободен;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//структура, содержащая имена регистров накопления по которым надо проводить документ
	Перем СтруктураПроведенияПоРегистрамНакопления;

	//структура, содержащая имена регистров сведений по которым надо проводить документ
	Перем СтруктураПроведенияПоРегистрамСведений;
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ = ложь)
		Если НЕ Отказ Тогда

			// Создадим и заполним структуры, содержащие имена регистров, по которым в зависимости от типа учета
			// проводится документ. В дальнейшем будем считать, что если для регистра не создан ключ в структуре,
			// то проводить по нему не надо.
			ЗаполнитьСтруктуруПроведенияПоРегистрамСведений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамСведений);

			// получим реквизиты табличной части
			РезультатЗапросаПоРаботники = СформироватьЗапросПоРаботникиОрганизации(ВыборкаПоШапкеДокумента);
			ВыборкаПоРаботникиОрганизации = РезультатЗапросаПоРаботники.Выбрать();
			
			Пока ВыборкаПоРаботникиОрганизации.Следующий() Цикл

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации, Отказ, Заголовок);
				
				Если НЕ Отказ Тогда
					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации, СтруктураПроведенияПоРегистрамСведений);
				КонецЕсли;

			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаЗаполнения(Основание)
	
	ТипОснования = ТипЗнч(Основание);
	Если ТипОснования = Тип("ДокументСсылка.ПланированиеОтпуска") Тогда
		
		// Заполним реквизиты из стандартного набора.
		ОбщегоНазначения.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		
		Если Основание.Проведен и Основание.Решение = Перечисления.СостоянияОбъектов.Утвержден Тогда
			
			Если НЕ ЗначениеЗаполнено(Организация) Тогда
				Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация")
			КонецЕсли;
			
			Запрос = Новый Запрос;
			
			Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
			Запрос.УстановитьПараметр("Организация",Организация);
			Запрос.УстановитьПараметр("Регистратор",Основание);
			Запрос.УстановитьПараметр("Дата",Дата);
			Запрос.УстановитьПараметр("ПустаяДата",'00010101');

			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	СобытийныйПланЗанятостиФизлиц.Период КАК ДатаНачала,
			|	СобытийныйПланЗанятостиФизлиц.ПериодЗавершения КАК ДатаОкончания,
			|	РаботникиОрганизацииСрезПоследних.Сотрудник,
			|	РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо КАК ФизЛицо
			|ИЗ
			|	РегистрСведений.СобытийныйПланЗанятостиФизлиц КАК СобытийныйПланЗанятостиФизлиц
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
			|		&Дата,
			|		Организация = &ГоловнаяОрганизация
			|			И Сотрудник.Физлицо В
			|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|					СобытийныйПланЗанятостиФизлиц.ФизЛицо
			|				ИЗ
			|					РегистрСведений.СобытийныйПланЗанятостиФизлиц КАК СобытийныйПланЗанятостиФизлиц
			|				ГДЕ
			|					СобытийныйПланЗанятостиФизлиц.Регистратор = &Регистратор)) КАК РаботникиОрганизацииСрезПоследних
			|		ПО РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо = СобытийныйПланЗанятостиФизлиц.ФизЛицо
			|			И (ВЫБОР
			|				КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
			|						И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
			|					ТОГДА РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделениеЗавершения
			|				ИНАЧЕ РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделение
			|			КОНЕЦ = &Организация)
			|ГДЕ
			|	(НЕ РаботникиОрганизацииСрезПоследних.Сотрудник ЕСТЬ NULL )
			|	И СобытийныйПланЗанятостиФизлиц.Состояние = ЗНАЧЕНИЕ(Перечисление.ТипыПериодическихЗадачРаботника.ОтпускЕжегодный)
			|	И СобытийныйПланЗанятостиФизлиц.Регистратор = &Регистратор";
			
			РаботникиОрганизации.Загрузить(Запрос.Выполнить().Выгрузить());
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(РаботникиОрганизации);
	
КонецПроцедуры // ПередЗаписью()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мДлинаСуток = 86400;
