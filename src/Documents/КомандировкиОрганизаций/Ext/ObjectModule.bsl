////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мДлинаСуток;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
	
	// Процедура осуществляет печать документа. Можно направить печать на 
	// экран или принтер, а также распечатать необходмое количество копий.
	//
	// Название макета печати передается в качестве параметра,
	// по переданному названию находим имя макета в соответствии.
	//
	// Параметры:
	//	НазваниеМакета - строка, название макета.
	//
	Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
		
		// Получить экземпляр документа на печать
		ИмяФормы = "";
		Если Лев(ИмяМакета,4) = "Т9а_" Тогда
			ИмяФормы = " (форма Т-9а)";
			ТабДокумент = ПечатьТ9а(ИмяМакета);
			
		ИначеЕсли Лев(ИмяМакета,4) = "Т9_о" Тогда
			ИмяФормы = " (форма Т-9)";
			ТабДокумент = ПечатьТ9(ИмяМакета);
			
		ИначеЕсли Лев(ИмяМакета,4) = "Т10а" Тогда
			ИмяФормы = " (форма Т-10а)";
			ТабДокумент = ПечатьТ10а(ИмяМакета);
			
		ИначеЕсли Лев(ИмяМакета,4) = "Т10_" Тогда
			ИмяФормы = " (форма Т-10)";
			ТабДокумент = ПечатьТ10(ИмяМакета);
			//О.Т. 11.02.2015 № 270
		ИначеЕсли ИмяМакета = "КомандировочноеУдостоверение" ИЛИ ИмяМакета = "КомандировочноеУдостоверение_Вариант2" Тогда //Аверсон 17.01.2020 КВВ СВФР-005034
			ТабДокумент = ПечатьКомандировочноеУдостоверение(ИмяМакета); 
			//О.Т. 11.02.2015 № 270
			
		КонецЕсли;
		
		Возврат РаботаСДиалогами.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, РаботаСДиалогами.СформироватьЗаголовокДокумента(ЭтотОбъект,Метаданные().Синоним + ИмяФормы));
		
	КонецФункции // Печать()
	
#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//	Струткура, каждая строка которой соответствует одному из вариантов печати
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	//О.Т. 11.02.2015 № 270
	//СтруктураМакетов = Новый Структура;
	//СтруктураМакетов.Вставить("Т9а_от_5_1_2004",	"Форма Т-9а");
	//СтруктураМакетов.Вставить("Т9_от_5_1_2004",		"Форма Т-9");
	//СтруктураМакетов.Вставить("Т10_от_5_1_2004",	"Форма Т-10");
	//СтруктураМакетов.Вставить("Т10а_от_5_1_2004",	"Форма Т-10а");
	//СтруктураМакетов.Вставить("Т9а_от_6_4_2001",	"Форма Т-9а (от 06.04.2001)");
	//СтруктураМакетов.Вставить("Т9_от_6_4_2001",		"Форма Т-9 (от 06.04.2001)");
	//СтруктураМакетов.Вставить("Т10_от_6_4_2001",	"Форма Т-10 (от 06.04.2001)");
	СтруктураМакетов = Новый Структура(?(Организация.ИНН="100092167","КомандировочноеУдостоверение_Вариант2","КомандировочноеУдостоверение"),"Командировочное удостоверение");  //Аверсон 17.01.2020 КВВ СВФР-005034
	//О.Т. 11.02.2015 № 270
	
	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Формирует запрос по документу и связанным регистрам сведений
//
// Параметры: 
//	Режим	- строка, может принимать значения:
//				"ПоРеквизитамДокумента"
//				"ПоТабличнойЧастиДокумента"
//
// Возвращаемое значение:
//	Результат запроса с данными об организации или о работниках из табличной части
//
Функция СформироватьЗапросДляПечати(Режим)
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);
	Запрос.УстановитьПараметр("Руководитель",	Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Запрос.УстановитьПараметр("ДатаДокумента",	Дата);
	Запрос.УстановитьПараметр("ПустаяДата",		'00010101');
	
	Если Режим = "ПоРеквизитамДокумента" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КомандировкиОрганизаций.Дата КАК ДатаДок,
		|	КомандировкиОрганизаций.Номер КАК НомерДок,
		|	ВЫРАЗИТЬ(КомандировкиОрганизаций.Организация.НаименованиеПолное КАК СТРОКА(300)) КАК НазваниеОрганизации,
		|	КомандировкиОрганизаций.Организация.КодПоОКПО КАК КодПоОКПО,
		|	КомандировкиОрганизаций.СтранаНазначения,
		|	КомандировкиОрганизаций.ОрганизацияНазначения,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Представление КАК ДолжностьРуководителя,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) <> """"
		|				ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "".""
		|			ИНАЧЕ """"
		|		КОНЕЦ, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо,
		|	КомандировкиОрганизаций.ОснованиеКомандировки,
		|	КомандировкиОрганизаций.Организация.Префикс
		|ИЗ
		|	Документ.КомандировкиОрганизаций КАК КомандировкиОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаДокумента, ОтветственноеЛицо = &Руководитель) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизЛицСрезПоследних
		|			ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|		ПО КомандировкиОрганизаций.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница
		|ГДЕ
		|	КомандировкиОрганизаций.Ссылка = &ДокументСсылка";
		
	ИначеЕсли Режим = "ПоТабличнойЧастиДокумента" Тогда
		
		Запрос.УстановитьПараметр("Организация",	 Организация);
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество, КомандировкиОрганизацииРаботники.Сотрудник.Наименование) КАК Работник,
		|	ЕСТЬNULL(ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид.Наименование + "", серия "" + ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия + "", №"" + ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер, """") КАК РеквизитыПаспорта,
		|	КомандировкиОрганизацииРаботники.НомерСтроки КАК НомерСтроки,
		|	КомандировкиОрганизацииРаботники.ДатаНачала,
		|	КомандировкиОрганизацииРаботники.ДатаОкончания,
		|	КомандировкиОрганизацииРаботники.ВремяНахожденияВПути,
		|	КомандировкиОрганизацииРаботники.Цель,
		|	КомандировкиОрганизацииРаботники.ЦельКомандировки, //Аверсон 17.01.2020 КВВ СВФР-005034
		|	КомандировкиОрганизацииРаботники.МестоНазначения,
		|	ВЫБОР
		|		КОГДА Работники.ПериодЗавершения <= КомандировкиОрганизацииРаботники.ДатаНачала
		|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА Работники.ПодразделениеОрганизацииЗавершения
		|		ИНАЧЕ Работники.ПодразделениеОрганизации
		|	КОНЕЦ КАК ПодразделениеРаботника,
		|	ВЫБОР
		|		КОГДА Работники.ПериодЗавершения <= КомандировкиОрганизацииРаботники.ДатаНачала
		|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА Работники.ДолжностьЗавершения
		|		ИНАЧЕ Работники.Должность
		|	КОНЕЦ КАК Должность,
		|	КомандировкиОрганизацииРаботники.ИсточникФинансирования,
		|	Работники.Сотрудник.Код КАК ТабельныйНомер,
		|	КомандировкиОрганизацииРаботники.Сотрудник,
		|	РАЗНОСТЬДАТ(КомандировкиОрганизацииРаботники.ДатаНачала, КомандировкиОрганизацииРаботники.ДатаОкончания, ДЕНЬ) КАК Срок
		|ИЗ
		|	Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК КомандировкиОрганизацииРаботники
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СписокДат.Сотрудник КАК Сотрудник,
		|			Работники.ПодразделениеОрганизации.Наименование КАК ПодразделениеОрганизации,
		|			Работники.Должность.Наименование КАК Должность,
		|			Работники.ПодразделениеОрганизацииЗавершения.Наименование КАК ПодразделениеОрганизацииЗавершения,
		|			Работники.ДолжностьЗавершения.Наименование КАК ДолжностьЗавершения,
		|			Работники.ПериодЗавершения КАК ПериодЗавершения
		|		ИЗ
		|			(ВЫБРАТЬ
		|				РаботникиВнутри.Сотрудник КАК Сотрудник,
		|				МАКСИМУМ(РаботникиВнутри.Период) КАК ДатаПоследнегоИзменения
		|			ИЗ
		|				РегистрСведений.РаботникиОрганизаций КАК РаботникиВнутри
		|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК КомандировкиОрганизаций
		|					ПО РаботникиВнутри.Период <= КомандировкиОрганизаций.ДатаНачала
		|						И РаботникиВнутри.Сотрудник = КомандировкиОрганизаций.Сотрудник
		|			ГДЕ
		|				КомандировкиОрганизаций.Ссылка = &ДокументСсылка
		|			
		|			СГРУППИРОВАТЬ ПО
		|				РаботникиВнутри.Сотрудник) КАК СписокДат
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК Работники
		|				ПО СписокДат.ДатаПоследнегоИзменения = Работники.Период
		|					И СписокДат.Сотрудник = Работники.Сотрудник) КАК Работники
		|		ПО КомандировкиОрганизацииРаботники.Сотрудник = Работники.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
		|				&ДатаДокумента,
		|				ФизЛицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						КомандировкиОрганизацииРаботники.Сотрудник.Физлицо
		|					ИЗ
		|						Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК КомандировкиОрганизацииРаботники
		|					ГДЕ
		|						КомандировкиОрганизацииРаботники.Ссылка = &ДокументСсылка)) КАК ФИОФизЛиц
		|		ПО КомандировкиОрганизацииРаботники.Сотрудник.Физлицо = ФИОФизЛиц.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(
		|				&ДатаДокумента,
		|				ФизЛицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						КомандировкиОрганизацииРаботники.Сотрудник.Физлицо
		|					ИЗ
		|						Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК КомандировкиОрганизацииРаботники
		|					ГДЕ
		|						КомандировкиОрганизацииРаботники.Ссылка = &ДокументСсылка)) КАК ПаспортныеДанныеФизЛицСрезПоследних
		|		ПО КомандировкиОрганизацииРаботники.Сотрудник.Физлицо = ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо
		|ГДЕ
		|	КомандировкиОрганизацииРаботники.Ссылка = &ДокументСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросДляПечати()

#Если Клиент Тогда
	
	// Функция формирует табличный документ с печатной формой "Т-9а",
	//
	// Возвращаемое значение:
	//	Табличный документ - печатная форма
	//
	Функция ПечатьТ9а(ИмяМакета)
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ПолеСлева = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КомандировкиОрганизации_Т9а";
		
		// получаем данные для печати
		ВыборкаДляШапкиИПодвала = СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
		ВыборкаРаботники		= СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();
		
		// запоминаем области макета
		Макет = ПолучитьМакет(ИмяМакета);
		ОбластьМакетаШапка				= Макет.ПолучитьОбласть("Шапка"); // Шапка документа.
		ОбластьМакетаПодвал				= Макет.ПолучитьОбласть("Подвал");// Подвал документа
		ОбластьМакетаБоковойЗаголовок	= Макет.ПолучитьОбласть("ЗаголовокРаботников"); // "боковик" формы
		ОбластьМакета 					= Макет.ПолучитьОбласть("Работник"); // область работника
		
		// выводим данные о руководителях организации
		Если ВыборкаДляШапкиИПодвала.Следующий() Тогда 
			ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапкиИПодвала); // Шапка документа.
			ОбластьМакетаШапка.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
			ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапкиИПодвала); // Для подвала
			ОбластьМакета.Параметры.Заполнить(ВыборкаДляШапкиИПодвала); // область работника
		КонецЕсли;
		
		// Начинаем формировать выходной документ
		ТабДокумент.Вывести(ОбластьМакетаШапка); // Шапка документа.
		ТабДокумент.Вывести(ОбластьМакетаБоковойЗаголовок); // "боковик" формы
		
		ВыведеноСтрок = 0; ВыведеноСтрокВДокумент = 0;
		// выводим строки по работникам
		Пока ВыборкаРаботники.Следующий() Цикл
			
			// Каждый приказ на отдельной странице.
			Если ВыведеноСтрок = 3 Тогда
				
				// выводим предварительно подготовленный Подвал документа.
				ТабДокумент.Область(20 + ВыведеноСтрокВДокумент,4,20 + ВыведеноСтрокВДокумент,9).Объединить();
				ТабДокумент.Область(21 + ВыведеноСтрокВДокумент,4,21 + ВыведеноСтрокВДокумент,9).Объединить();
				ТабДокумент.Вывести(ОбластьМакетаПодвал);
				ВыведеноСтрокВДокумент = ТабДокумент.ВысотаТаблицы;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				// начинаем новую страницу
				ВыведеноСтрок = 0;
				ТабДокумент.Вывести(ОбластьМакетаШапка); // Шапка документа.
				ТабДокумент.Вывести(ОбластьМакетаБоковойЗаголовок); // "боковик" формы
			КонецЕсли;
			
			// Данные по работнику.
			ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
			
			//уберем из табельного номера префикс
			ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;
			
			ОбластьМакета.Параметры.Продолжительность = ?(ЗначениеЗаполнено(ВыборкаРаботники.ДатаОкончания),Цел((ВыборкаРаботники.ДатаОкончания - ВыборкаРаботники.ДатаНачала + 1) / мДлинаСуток) + 1, "");
			ТабДокумент.Присоединить(ОбластьМакета);
			ВыведеноСтрок = ВыведеноСтрок + 1;
			
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Работник"); // область работника
		Для Сч = ВыведеноСтрок + 1 По 3 Цикл
			ТабДокумент.Присоединить(ОбластьМакета);
		КонецЦикла;
		ТабДокумент.Область(20 + ВыведеноСтрокВДокумент,4,20 + ВыведеноСтрокВДокумент,9).Объединить();
		ТабДокумент.Область(21 + ВыведеноСтрокВДокумент,4,21 + ВыведеноСтрокВДокумент,9).Объединить();
		
		// выводим предварительно подготовленный Подвал документа.
		ТабДокумент.Вывести(ОбластьМакетаПодвал);
		
		Возврат ТабДокумент;
		
	КонецФункции // ПечатьТ9а()
	
	// Функция формирует табличный документ с печатной формой "Т-9",
	//
	// Возвращаемое значение:
	//	Табличный документ - печатная форма
	//
	Функция ПечатьТ9(ИмяМакета)
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_КомандировкиОрганизации_Т9";
		ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
		
		// получаем данные для печати
		ВыборкаДляШапки		= СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
		ВыборкаРаботники	= СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();
		
		// запоминаем области макета
		Макет = ПолучитьМакет(ИмяМакета);
		ОбластьМакетаШапка	= Макет.ПолучитьОбласть("Шапка"); // Шапка документа
		ОбластьМакетаПодвал	= Макет.ПолучитьОбласть("Подвал"); // Подвал документа
		ОбластьМакета		= Макет.ПолучитьОбласть("Работник"); // строка работника
		
		// выводим данные о руководителях организации
		Если ВыборкаДляШапки.Следующий() Тогда
			ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапки); // Шапка документа.
			ОбластьМакетаШапка.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
			ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапки); // Для подвала.
			ОбластьМакета.Параметры.Заполнить(ВыборкаДляШапки); // область работника
			// О.Т. 24.11.2014 291
			если ИмяМакета = "Т9_от_5_1_2004" Тогда 
				ОбластьМакетаШапка.Параметры.НомерДок = НомерПриказа;
				НомерДокДляПечати	= ВыборкаДляШапки.НомерДок;
			иначе
				НомерДокДляПечати	= ВыборкаДляШапки.НомерДок;
			КонецЕсли;
			// О.Т. 24.11.2014 291
			
		КонецЕсли;
		
		// Начинаем формировать выходной документ
		Пока ВыборкаРаботники.Следующий() Цикл
			
			// Каждый приказ на отдельной странице.
			Если ТабДокумент.ВысотаТаблицы > 0 Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			// Шапка документа.
			Если РаботникиОрганизации.Количество() > 1 Тогда
				ОбластьМакетаШапка.Параметры.НомерДок = НомерДокДляПечати + "/" + ВыборкаРаботники.НомерСтроки
			КонецЕсли;
			ТабДокумент.Вывести(ОбластьМакетаШапка);
			
			// Данные по работнику.
			ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
			
			//уберем из табельного номера префикс
			ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;
			
			ОбластьМакета.Параметры.Продолжительность = ?(ЗначениеЗаполнено(ВыборкаРаботники.ДатаОкончания),Цел((ВыборкаРаботники.ДатаОкончания - ВыборкаРаботники.ДатаНачала + 1) / мДлинаСуток) + 1,"");
			ТабДокумент.Вывести(ОбластьМакета);
			
			// Подвал документа.
			ТабДокумент.Вывести(ОбластьМакетаПодвал);
			
		КонецЦикла;
		
		// если не было ни одного работника - выводим пустой бланк
		Если ТабДокумент.ВысотаТаблицы = 0 Тогда
			ТабДокумент.Вывести(ОбластьМакетаШапка);
			ТабДокумент.Вывести(ОбластьМакета);
			ТабДокумент.Вывести(ОбластьМакетаПодвал);
		КонецЕсли;
		
		Возврат ТабДокумент;
		
	КонецФункции // ПечатьТ9()
	
	// Функция формирует табличный документ с печатной формой "Т-10",
	//
	// Возвращаемое значение:
	//  Табличный документ - печатная форма
	//
	Функция ПечатьТ10(ИмяМакета)
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КомандировкиОрганизации_Т10";
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		// получаем данные для печати
		ВыборкаДляШапки   = СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
		ВыборкаРаботники = СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();
		
		// запоминаем области макета
		Макет = ПолучитьМакет(ИмяМакета);
		ОбластьМакетаШапка	= Макет.ПолучитьОбласть("Шапка");			// Шапка документа
		ОбластьМакетаПодвал	= Макет.ПолучитьОбласть("Подвал");			// Подвал документа
		ОбластьМакета		= Макет.ПолучитьОбласть("Работник");		// строка работника
		ОборотШапка			= Макет.ПолучитьОбласть("ШапкаОтметок");	// оборот удостоверения - отметки от прибытии-выбытии
		ОборотОтметки		= Макет.ПолучитьОбласть("Отметки");
		
		// выводим данные о руководителях организации
		Если ВыборкаДляШапки.Следующий() Тогда
			ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапки); // Шапка документа.
			ОбластьМакетаШапка.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
			ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапки); // Для подвала.
			ОбластьМакета.Параметры.Заполнить(ВыборкаДляШапки); // область работника
			НомерДокДляПечати	= ВыборкаДляШапки.НомерДок;
		КонецЕсли;
		
		// Начинаем формировать выходной документ
		Пока ВыборкаРаботники.Следующий() Цикл
			
			// Каждый приказ на отдельной странице.
			Если ТабДокумент.ВысотаТаблицы > 0 Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			// Шапка документа.
			Если РаботникиОрганизации.Количество() > 1 Тогда
				ОбластьМакетаШапка.Параметры.НомерДок = НомерДокДляПечати + "/" + ВыборкаРаботники.НомерСтроки
			КонецЕсли; 
			ТабДокумент.Вывести(ОбластьМакетаШапка);
			
			// Данные по работнику.
			ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
			
			//уберем из табельного номера префикс
			ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;
			
			// в этой форме продолжительность командировки указывается без времени в пути
			ОбластьМакета.Параметры.Продолжительность = ?(ЗначениеЗаполнено(ВыборкаРаботники.ДатаОкончания),Цел((ВыборкаРаботники.ДатаОкончания - ВыборкаРаботники.ДатаНачала + 1) / мДлинаСуток) + 1 - ВыборкаРаботники.ВремяНахожденияВПути,"");
			ТабДокумент.Вывести(ОбластьМакета);
			
			// Подвал документа.
			ТабДокумент.Вывести(ОбластьМакетаПодвал);
			// выводим оборот удостоверения
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ОборотШапка);
			ТабДокумент.Вывести(ОборотОтметки);
			ТабДокумент.Вывести(ОборотОтметки);
			ТабДокумент.Вывести(ОборотОтметки);
			ТабДокумент.Вывести(ОборотОтметки);
			
		КонецЦикла;
		
		// если не было ни одного работника - выводим пустой бланк
		Если ТабДокумент.ВысотаТаблицы = 0 Тогда
			ТабДокумент.Вывести(ОбластьМакетаШапка);
			ТабДокумент.Вывести(ОбластьМакета);
			ТабДокумент.Вывести(ОбластьМакетаПодвал);
			// выводим оборот удостоверения
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ОборотШапка);
			ТабДокумент.Вывести(ОборотОтметки);
			ТабДокумент.Вывести(ОборотОтметки);
			ТабДокумент.Вывести(ОборотОтметки);
			ТабДокумент.Вывести(ОборотОтметки);
		КонецЕсли;
		
		Возврат ТабДокумент;
		
	КонецФункции // ПечатьТ10()
	
	// Функция формирует табличный документ с печатной формой "Т-10а",
	//
	// Возвращаемое значение:
	//	Табличный документ - печатная форма
	//
	Функция ПечатьТ10а(ИмяМакета)
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КомандировкиОрганизации_Т10а";
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		// получаем данные для печати
		ВыборкаДляШапки   = СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
		ВыборкаРаботники = СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();
		
		// запоминаем области макета
		Макет = ПолучитьМакет(ИмяМакета);
		ОбластьМакетаШапка	= Макет.ПолучитьОбласть("Шапка");		// Шапка документа
		ОбластьМакетаПодвал	= Макет.ПолучитьОбласть("Подвал");		// Подвал документа
		ОбластьМакета		= Макет.ПолучитьОбласть("Работник");	// строка работника
		
		// выводим данные о руководителях организации
		Если ВыборкаДляШапки.Следующий() Тогда
			ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапки);	// Шапка документа.
			ОбластьМакетаШапка.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
			ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапки);	// Для подвала.
			ОбластьМакета.Параметры.Заполнить(ВыборкаДляШапки);			// область работника
			НомерДокДляПечати	= ВыборкаДляШапки.НомерДок;
		КонецЕсли;
		
		// Начинаем формировать выходной документ
		Пока ВыборкаРаботники.Следующий() Цикл
			
			// Каждый приказ на отдельной странице.
			Если ТабДокумент.ВысотаТаблицы > 0 Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			// Шапка документа.
			Если РаботникиОрганизации.Количество() > 1 Тогда
				ОбластьМакетаШапка.Параметры.НомерДок = НомерДокДляПечати + "/" + ВыборкаРаботники.НомерСтроки
			КонецЕсли;
			ТабДокумент.Вывести(ОбластьМакетаШапка);
			
			// Данные по работнику.
			ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
			
			//уберем из табельного номера префикс
			ОбластьМакета.Параметры.ТабельныйНомер = ВыборкаРаботники.ТабельныйНомер;
			
			// в этой форме выводим два варианта продолжительности командировки
			ОбластьМакета.Параметры.Продолжительность = ?(ЗначениеЗаполнено(ВыборкаРаботники.ДатаОкончания),Цел((ВыборкаРаботники.ДатаОкончания - ВыборкаРаботники.ДатаНачала + 1) / мДлинаСуток) + 1,"");
			ОбластьМакета.Параметры.ПродолжительностьЧистая = ?(ЗначениеЗаполнено(ВыборкаРаботники.ДатаОкончания),Цел((ВыборкаРаботники.ДатаОкончания - ВыборкаРаботники.ДатаНачала + 1) / мДлинаСуток) + 1 - ВыборкаРаботники.ВремяНахожденияВПути,"");
			ТабДокумент.Вывести(ОбластьМакета);
			
			// Подвал документа.
			ТабДокумент.Вывести(ОбластьМакетаПодвал);
			
		КонецЦикла;
		
		// если не было ни одного работника - выводим пустой бланк
		Если ТабДокумент.ВысотаТаблицы = 0 Тогда
			ТабДокумент.Вывести(ОбластьМакетаШапка);
			ТабДокумент.Вывести(ОбластьМакета);
			ТабДокумент.Вывести(ОбластьМакетаПодвал);
		КонецЕсли;
		
		Возврат ТабДокумент;
		
	КонецФункции // ПечатьТ10а()
	
	//О.Т. 11.02.2015 № 270
	Функция ПечатьКомандировочноеУдостоверение(ИмяМакета)//О.Т. 11.02.2015 № 270
		
		ТабДок = Новый ТабличныйДокумент;
		
		Макет = Документы.КомандировкиОрганизаций.ПолучитьМакет(ИмяМакета);
		
		Область = Макет.ПолучитьОбласть("Шапка");
		// получаем данные для печати
		ВыборкаДляШапки   = СформироватьЗапросДляПечати("ПоРеквизитамДокумента").Выбрать();
		ВыборкаРаботники = СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента").Выбрать();   
		СведенияОтправителя = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, Дата);
		Область.Параметры.ПредставлениеОрганизации = СокрЛП(ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОтправителя, "ПолноеНаименование,"));
		Область.Параметры.ПечУНП= СокрЛП(ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОтправителя,"ИНН,"));
		Область.Параметры.ПечРасчетныйСчет= СокрЛП(ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОтправителя,"НомерСчета,Банк,КодБанка,АдресБанка,"));
		Область.Параметры.ПечАдрес= СокрЛП(ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОтправителя,"ЮридическийАдрес,"));
		Если Организация.ИНН <> "100092167" Тогда  //Аверсон 17.01.2020 КВВ СВФР-005034
			Область.Параметры.Номер    = Номер;
			Область.Параметры.Дата  = Формат(Дата , "ДФ= ""дд ММММ гггг "" ") + "г.";
		КонецЕсли;
		
		пока ВыборкаРаботники.Следующий() Цикл 
			
			// Каждый приказ на отдельной странице.
			Если ТабДок.ВысотаТаблицы > 0 Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			Область.Параметры.Сотрудник = СокрЛП(ВыборкаРаботники.Работник) + ?(Организация.ИНН = "100092167"," (ТН " + СокрЛП(ВыборкаРаботники.Сотрудник.Код) + ")",""); //Аверсон 17.01.2020 КВВ СВФР-005034
			Область.Параметры.Место = СтранаНазначения;
			
			//Аверсон ЕАЕ 26.10.2021 СВФР-008262
			Если ЗначениеЗаполнено(СтранаНазначения) Тогда
				Область.Параметры.Зпт = ", ";
			Иначе
				Область.Параметры.Зпт = "";
			КонецЕсли;
			
			Если НесколькоОрганизаций Тогда
				ОрганизацияНазначенияСтр = "";
				Для Каждого Стр Из ОрганизацииНазначения Цикл
					ОрганизацияНазначенияСтр = ОрганизацияНазначенияСтр + ?(ОрганизацияНазначенияСтр = "", "", ", ") + СокрЛП(Стр.Организация);	
				КонецЦикла;
			Иначе
				ОрганизацияНазначенияСтр = СокрЛП(ОрганизацияНазначения);
			КонецЕсли;
			//Область.Параметры.НаименованиеОрганизации = ОрганизацияНазначения;
			Область.Параметры.НаименованиеОрганизации = ОрганизацияНазначенияСтр;
			//Аверсон СВФР-008262
			
			Область.Параметры.Срок = ВыборкаРаботники.Срок+1;
			
			//Аверсон ЕАЕ 13.06.2025 СВФР-016032
			//Бобруйскводоканал
			Если Организация.ИНН = "790041382" Тогда
				ПериодКомандировки = "Период с " + Формат(ВыборкаРаботники.ДатаНачала, "ДФ=dd.MM.yyyy") + " по " + Формат(ВыборкаРаботники.ДатаОкончания, "ДФ=dd.MM.yyyy") + ".";
			Иначе
				ПериодКомандировки = "";
			КонецЕсли;
			Область.Параметры.ПериодКомандировки = ПериодКомандировки;
			//Аверсон СВФР-016032
			
			Если Организация.ИНН = "100092167" Тогда 
				Область.Параметры.Цель =  ВыборкаРаботники.ЦельКомандировки;  //Аверсон 17.01.2020 КВВ СВФР-005034
				Область.Параметры.НаименованиеОрганизации = "";
				Область.Параметры.Место = ВыборкаРаботники.МестоНазначения;
				Область.Параметры.Основание = "Приказ о командировании № " + СокрЛП(Номер) + " от " + Формат(Дата , "ДФ= ""дд ММММ гггг "" ") + "г."; //Аверсон 17.01.2020 КВВ СВФР-005034
			иначе
				Область.Параметры.Цель = ВыборкаРаботники.Цель;
				Область.Параметры.Основание = ОснованиеКомандировки;
			КонецЕсли;
			
			СведенияПоРаботнику = РегистрыСведений.РаботникиОрганизаций.ПолучитьПоследнее(Дата,Новый Структура("Сотрудник",ВыборкаРаботники.Сотрудник));
			Область.Параметры.СотрудникДолжность = СокрЛП(СведенияПоРаботнику.Должность) + ?(Организация.ИНН = "100092167",?(ЗначениеЗаполнено(СведенияПоРаботнику.РазрядЕТС),", разряд " + СокрЛП(СведенияПоРаботнику.РазрядЕТС),""),"");  //Аверсон 17.01.2020 КВВ СВФР-005034
			
			//Аверсон 17.01.2020 КВВ СВФР-005034 (
			мСотрудникКоманд = ВосстановитьЗначение("мСотрудникКоманд");
			мДолжностьКоманд = ВосстановитьЗначение("мДолжностьКоманд");
			//Аверсон 17.01.2020 КВВ СВФР-005034 )
			
			Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, Дата,);
			Область.Параметры.РуководительДолжность = ?(ЗначениеЗаполнено(мДолжностьКоманд),мДолжностьКоманд,Руководители.РуководительДолжность); //Аверсон 17.01.2020 КВВ СВФР-005034
			ФИОРуководителя = ?(ЗначениеЗаполнено(мСотрудникКоманд),мСотрудникКоманд,Руководители.Руководитель); //Аверсон 17.01.2020 КВВ СВФР-005034
			Пробел = Найти(СокрЛП(ФИОРуководителя), " ");
			Если Не Пробел = 0 Тогда
				Область.Параметры.ФИОРуководителя = Прав(СокрЛП(ФИОРуководителя), 
				СтрДлина(СокрЛП(ФИОРуководителя)) - Пробел)
				+ " "	
				+ Лев(СокрЛП(ФИОРуководителя), Пробел - 1)	;
			КонецЕсли;
			ТабДок.Вывести(Область); // Шапка
		КонецЦикла;
		
		ТабДок.ОтображатьСетку     = Ложь;
		ТабДок.Защита              = Ложь;
		ТабДок.ТолькоПросмотр      = Ложь;
		ТабДок.ОтображатьЗаголовки = Ложь;
		
		ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		ТабДок.ПолеСлева = 20;
		
		ТабДок.ПолеСлева = 15;
		
		ТабДок.АвтоМасштаб = Истина;
		
		//ТабДок.Показать(); // Вывести печатную форму на экран
		
		Возврат ТабДок;	
		
	КонецФункции // ПечатьЗаявкаНаПокупкуВалюты()
	
	
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",		Ссылка);
	Запрос.УстановитьПараметр("ПустаяОрганизация",	Справочники.Организации.ПустаяСсылка());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КомандировкиОрганизаций.Дата,
	|	КомандировкиОрганизаций.Организация,
	|	ВЫБОР
	|		КОГДА КомандировкиОрганизаций.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|			ТОГДА КомандировкиОрганизаций.Организация
	|		ИНАЧЕ КомандировкиОрганизаций.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	КомандировкиОрганизаций.Ссылка
	|ИЗ
	|	Документ.КомандировкиОрганизаций КАК КомандировкиОрганизаций
	|ГДЕ
	|	КомандировкиОрганизаций.Ссылка = &ДокументСсылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "РаботникиОрганизации" документа
//
// Параметры:
//	Режим	- режим проведения
//
// Возвращаемое значение:
//	Результат запроса. В запросе данные документа дополняются значениями
//	проверяемых параметров из связанного с
//
Функция СформироватьЗапросПоРаботникиОрганизации(ВыборкаПоШапкеДокумента, ПроверкаШтатногоРасписания)
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата",				Дата('00010101'));
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	
	// Описание текста запроса:
	// Первая часть запроса  - вид строки запроса "ДанныеДляДвиженийКадров": 
	// 1. Выборка "ТЧРаботникиОрганизации": 
	//		Выбираются строки документа.  
	// 2. Выборка "ДанныеПоРаботникуДоНазначения": 
	//		Для каждой строки ТЧРаботникиОрганизации выполняем срез по регистру РаботникиОрганизации на дату ДатаНачала
	//		для выполнения движений и проверки "Работает ли работник на дату перемещения"
	//		(Использует данные выборки "ДатыПоследнегоДвиженияРаботника")
	// 3. Выборка "ПересекающиесяСтроки": 
	//		Среди остальных строк документа ищем строки, имеющие пересекающийся период действия
	//
	// Вторая часть запроса - вид строки запроса "КонфликтныйДокумент" - поиск конфликтных документов: 
	// 1. Выборка "ТЧРаботникиОрганизации":
	//		Выбираются строки документа 
	// 2. Выборка "Работники":
	//		Для каждой строки ТЧРаботникиОрганизации ищем движения по регистру РаботникиОрганизации за период [ДатаНачала, ДатаОкончания]
	//		по измерению <Сотрудник>
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЧРаботникиОрганизации.НомерСтроки КАК НомерСтроки,
	|	ТЧРаботникиОрганизации.Сотрудник,
	|	ТЧРаботникиОрганизации.Сотрудник.Наименование,
	|	ТЧРаботникиОрганизации.Сотрудник.Физлицо КАК Физлицо,
	|	ТЧРаботникиОрганизации.ДатаНачала,
	|	ТЧРаботникиОрганизации.ДатаОкончания,
	|	ТЧРаботникиОрганизации.ВремяНахожденияВПути,
	|	ТЧРаботникиОрганизации.НапомнитьПоЗавершении,
	|	ТЧРаботникиОрганизации.ОсвобождатьСтавку,
	|	ВЫБОР
	|		КОГДА ТЧРаботникиОрганизации.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ВЫБОР
	|		КОГДА ДанныеПоРаботникуДоНазначения.ПериодЗавершения <= ТЧРаботникиОрганизации.ДатаНачала
	|				И ДанныеПоРаботникуДоНазначения.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДанныеПоРаботникуДоНазначения.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ ДанныеПоРаботникуДоНазначения.ПодразделениеОрганизации
	|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ДанныеПоРаботникуДоНазначения.ПериодЗавершения <= ТЧРаботникиОрганизации.ДатаНачала
	|				И ДанныеПоРаботникуДоНазначения.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДанныеПоРаботникуДоНазначения.ДолжностьЗавершения
	|		ИНАЧЕ ДанныеПоРаботникуДоНазначения.Должность
	|	КОНЕЦ КАК Должность,
	|	ВЫБОР
	|		КОГДА ДанныеПоРаботникуДоНазначения.ПериодЗавершения <= ТЧРаботникиОрганизации.ДатаНачала
	|				И ДанныеПоРаботникуДоНазначения.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДанныеПоРаботникуДоНазначения.ЗанимаемыхСтавокЗавершения
	|		ИНАЧЕ ДанныеПоРаботникуДоНазначения.ЗанимаемыхСтавок
	|	КОНЕЦ КАК ЗанимаемыхСтавок,
	|	ПересекающиесяСтроки.КонфликтнаяСтрокаНомер,
	|	ИмеющиесяСостояния.Состояние КАК КонфликтноеСостояние,
	|	ИмеющиесяСостояния.Регистратор.Представление КАК КонфликтныйДокумент,
	|	ТЧРаботникиОрганизации.ОплатаКомРасходов,
	|	ТЧРаботникиОрганизации.МестоНазначения,
	|	ТЧРаботникиОрганизации.НеНапоминатьОСобытии,
	
	//Аверсон ЕАЕ 11.11.2021 СВФР-008352
	|   ТЧРаботникиОрганизации.ОтменаКомандировки,
	|   ТЧРаботникиОрганизации.КомандировкаОбучение как Учеба
	//Аверсон СВФР-008352
	
	|ИЗ
	|	Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТЧРаботникиОрганизации.НомерСтроки КАК НомерСтроки,
	|			МАКСИМУМ(Работники.Период) КАК ДатаДвижения
	|		ИЗ
	|			Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК Работники
	|				ПО Работники.Период <= ТЧРаботникиОрганизации.ДатаНачала
	|					И ТЧРаботникиОрганизации.Сотрудник = Работники.Сотрудник
	|		ГДЕ
	|			ТЧРаботникиОрганизации.Ссылка = &ДокументСсылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТЧРаботникиОрганизации.НомерСтроки) КАК ДатыПоследнегоДвиженияРаботника
	|		ПО ДатыПоследнегоДвиженияРаботника.НомерСтроки = ТЧРаботникиОрганизации.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК ДанныеПоРаботникуДоНазначения
	|		ПО ДанныеПоРаботникуДоНазначения.Период = ДатыПоследнегоДвиженияРаботника.ДатаДвижения
	|			И ТЧРаботникиОрганизации.Сотрудник = ДанныеПоРаботникуДоНазначения.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТЧРаботникиОрганизации.НомерСтроки КАК НомерСтроки,
	|			МИНИМУМ(ТЧРаботникиОрганизации2.НомерСтроки) КАК КонфликтнаяСтрокаНомер
	|		ИЗ
	|			Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КомандировкиОрганизаций.РаботникиОрганизации КАК ТЧРаботникиОрганизации2
	|				ПО (ТЧРаботникиОрганизации2.Ссылка = &ДокументСсылка)
	|					И ТЧРаботникиОрганизации.НомерСтроки <> ТЧРаботникиОрганизации2.НомерСтроки
	|					И ТЧРаботникиОрганизации.ДатаНачала <= ТЧРаботникиОрганизации2.ДатаНачала
	|					И ТЧРаботникиОрганизации.Сотрудник = ТЧРаботникиОрганизации2.Сотрудник
	|					И (ВЫБОР
	|						КОГДА ТЧРаботникиОрганизации.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|							ТОГДА ИСТИНА
	|						КОГДА ТЧРаботникиОрганизации.НапомнитьПоЗавершении
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ТЧРаботникиОрганизации.ДатаОкончания >= ТЧРаботникиОрганизации2.ДатаНачала
	|					КОНЕЦ)
	|		ГДЕ
	|			ТЧРаботникиОрганизации.Ссылка = &ДокументСсылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТЧРаботникиОрганизации.НомерСтроки) КАК ПересекающиесяСтроки
	|		ПО ТЧРаботникиОрганизации.НомерСтроки = ПересекающиесяСтроки.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботниковОрганизаций КАК ИмеющиесяСостояния
	|		ПО ТЧРаботникиОрганизации.ДатаНачала = ИмеющиесяСостояния.Период
	|			И ТЧРаботникиОрганизации.Ссылка <> ИмеющиесяСостояния.Регистратор
	|			И ТЧРаботникиОрганизации.Сотрудник = ИмеющиесяСостояния.Сотрудник
	|ГДЕ
	|	ТЧРаботникиОрганизации.Ссылка = &ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросПоРаботникиОрганизации()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры:
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)
	
	// Организация
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация, работники которой отправляются в командировку!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "РаботникиОрганизации" документа.
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса по работникам, 
//	Отказ						- флаг отказа в проведении.
//	Заголовок					- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
	""" табл. части ""Работники организации"": ";
	
	// Сотрудник
	НетСотрудника = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник);
	Если НетСотрудника Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// ДатаНачала
	НетДатыНачала = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНачала);
	Если НетДатыНачала Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата начала командировки!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НетСотрудника ИЛИ НетДатыНачала Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаОкончания) Тогда
		Если ВыборкаПоСтрокамДокумента.ДатаНачала > ВыборкаПоСтрокамДокумента.ДатаОкончания Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "дата начала командировки не может превышать дату окончания!", Отказ, Заголовок);
		ИначеЕсли ВыборкаПоСтрокамДокумента.ДатаНачала + мДлинаСуток * ВыборкаПоСтрокамДокумента.ВремяНахожденияВПути > ВыборкаПоСтрокамДокумента.ДатаОкончания	Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "время нахождения в пути не может превышать срок командировки!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указанный сотрудник оформлен на другую организацию!", Отказ, Заголовок);
	КонецЕсли;
	
	// Проверка: ранее работник должен быть принят на работу
	Если ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок = NULL Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоСтрокамДокумента.ДатаНачала, "ДЛФ=DD") + " работник " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " еще не принят на работу!";
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
		
	ИначеЕсли ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок = 0 Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоСтрокамДокумента.ДатаНачала, "ДЛФ=DD") + " работник " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " уже уволен!";
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
		
	КонецЕсли;
	
	// Проверка: противоречие другой строке документа
	Если ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер <> NULL Тогда
		СтрокаСообщениеОбОшибке = "период командировки пересекается с периодом строки " + ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер + "!"; 
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщениеОбОшибке, Отказ, Заголовок);
	КонецЕсли;
	
	//Аверсон 05.04.2021 КВВ СВФР-007334 (
	//Если (Организация.ИНН = "700067572") //для всех проверка на командировки
	Если (ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНачала) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаОкончания)) Тогда
		З = Новый Запрос;
		З.Текст = "
		|ВЫБРАТЬ 
		|ПериодыСостоянийРаботниковОрганизаций.Регистратор,
		|ПериодыСостоянийРаботниковОрганизаций.Период КАК Начало,
		|ДОБАВИТЬКДАТЕ(ПериодыСостоянийРаботниковОрганизаций.ПериодЗавершения,ДЕНЬ,-1) КАК Окончание
		|ИЗ РегистрСведений.СостояниеРаботниковОрганизаций КАК ПериодыСостоянийРаботниковОрганизаций
		|ГДЕ ПериодыСостоянийРаботниковОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Командировка)
		|И ПериодыСостоянийРаботниковОрганизаций.Сотрудник = &Сотрудник
		|И ((&НачалоТЧ МЕЖДУ ПериодыСостоянийРаботниковОрганизаций.Период И ДОБАВИТЬКДАТЕ(ПериодыСостоянийРаботниковОрганизаций.ПериодЗавершения,ДЕНЬ,-1)) 
		|	ИЛИ (&ОкончаниеТЧ МЕЖДУ ПериодыСостоянийРаботниковОрганизаций.Период И ДОБАВИТЬКДАТЕ(ПериодыСостоянийРаботниковОрганизаций.ПериодЗавершения,ДЕНЬ,-1)))
		|И НЕ ПериодыСостоянийРаботниковОрганизаций.Регистратор = &Ссылка";
		З.УстановитьПараметр("Ссылка",Ссылка);
		З.УстановитьПараметр("Сотрудник",ВыборкаПоСтрокамДокумента.Сотрудник);
		З.УстановитьПараметр("НачалоТЧ",ВыборкаПоСтрокамДокумента.ДатаНачала);
		З.УстановитьПараметр("ОкончаниеТЧ",ВыборкаПоСтрокамДокумента.ДатаОкончания);
		РезЗ = З.Выполнить().Выгрузить();
		Если РезЗ.Количество() > 0 Тогда
			Для Каждого СтрЗ Из РезЗ Цикл
				СтрокаСообщениеОбОшибке = "Сотрудник " + СокрЛП(ВыборкаПоСтрокамДокумента.Сотрудник) + " в период " +  Формат(ВыборкаПоСтрокамДокумента.ДатаНачала,"ДЛФ=Д") + " - " + Формат(ВыборкаПоСтрокамДокумента.ДатаОкончания,"ДЛФ=Д") + " уже находится в командировке. Док." + СокрЛП(СтрЗ.Регистратор);
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщениеОбОшибке, Отказ, Заголовок);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	//Аверсон 05.04.2021 КВВ СВФР-007334 )
	
	// Проверка: в регистре уже есть такое движение
	//О.Т. Аверсон 19.06.2018 № 2079 убрана проверка
	Если ВыборкаПоСтрокамДокумента.КонфликтноеСостояние <> NULL Тогда
		//СтрокаСообщениеОбОшибке = "работник уже переведен в состояние """ + ВыборкаПоСтрокамДокумента.КонфликтноеСостояние + """ документом " + ВыборкаПоСтрокамДокумента.КонфликтныйДокумент + "!"; 
		//ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщениеОбОшибке, Отказ, Заголовок);
	КонецЕсли;
	//О.Т. Аверсон 19.06.2018 № 2079 убрана проверка
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиРаботникаОрганизации()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//	ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа,
//	ВыборкаПоРаботникиОрганизации
//	УчетнаяПолитикаПоПерсоналуОрганизации
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации)
	
	//Аверсон ЕАЕ 11.11.2021 СВФР-008352
	Если Не ВыборкаПоРаботникиОрганизации.ОтменаКомандировки Тогда
		//Аверсон СВФР-008352
		
		// движения по дате "с"
		Движение = Движения.СостояниеРаботниковОрганизаций.Добавить();
		
		// Свойства
		Движение.Период							= ВыборкаПоРаботникиОрганизации.ДатаНачала;
		
		// Измерения
		Движение.Сотрудник						= ВыборкаПоРаботникиОрганизации.Сотрудник;
		Движение.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		
		// Ресурсы
		Если ВыборкаПоРаботникиОрганизации.Учеба Тогда       //учеба в ТЧ
			Движение.Состояние						= Перечисления.СостоянияРаботникаОрганизации.ОплатаПоСреднемуЗаработку;  
		Иначе
			Движение.Состояние						= Перечисления.СостоянияРаботникаОрганизации.Командировка;  
		КонецЕсли;
		Если ВыборкаПоРаботникиОрганизации.Учеба Тогда
			Движение.ПериодЗавершения							= ВыборкаПоРаботникиОрганизации.ДатаОкончания + мДлинаСуток;   
			Движение = Движения.СостояниеРаботниковОрганизаций.Добавить();
			Движение.Период							= ВыборкаПоРаботникиОрганизации.ДатаОкончания + мДлинаСуток;
			Движение.Сотрудник						= ВыборкаПоРаботникиОрганизации.Сотрудник;
			Движение.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
			Движение.Состояние						= Перечисления.СостоянияРаботникаОрганизации.Работает;			
		КонецЕсли;    		
		
		Если ЗначениеЗаполнено(ВыборкаПоРаботникиОрганизации.ДатаОкончания) и не ВыборкаПоРаботникиОрганизации.Учеба Тогда
			
			Если НЕ ВыборкаПоРаботникиОрганизации.НапомнитьПоЗавершении Тогда
				// Свойства
				Движение.ПериодЗавершения		= ВыборкаПоРаботникиОрганизации.ДатаОкончания + мДлинаСуток;
				
				// Ресурсы
				Движение.СостояниеЗавершения	= Перечисления.СостоянияРаботникаОрганизации.Работает;
				
			Иначе
				Движение = Движения.НамеченныеСобытияПоПерсоналу.Добавить();
				
				// Свойства
				Движение.Период					= ВыборкаПоРаботникиОрганизации.ДатаОкончания + мДлинаСуток;
				
				// Измерения
				Движение.Сотрудник				= ВыборкаПоРаботникиОрганизации.Сотрудник;
				Движение.Организация			= ВыборкаПоШапкеДокумента.Организация;
				Движение.ДатаИзменения			= ВыборкаПоРаботникиОрганизации.ДатаОкончания + мДлинаСуток;
				Движение.ПланируемоеСобытие		= Перечисления.НамеченныеСобытияПоПерсоналу.Командировка;
				
				// Реквизиты
				Движение.ЗанимаемыхСтавок		= ВыборкаПоРаботникиОрганизации.ЗанимаемыхСтавок;
				Движение.ЗаниматьСтавку			= ВыборкаПоРаботникиОрганизации.ОсвобождатьСтавку;
				Движение.НеНапоминатьОСобытии	= ВыборкаПоРаботникиОрганизации.НеНапоминатьОСобытии;
				
			КонецЕсли;
			
		КонецЕсли;
		
		//Аверсон ЕАЕ 11.11.2021 СВФР-008352
	КонецЕсли;
	//Аверсон СВФР-008352
	
	если Организация.ИНН = "100092167" Тогда
		// движения по дате "с"
		Если ВидКомандирования = 2 Тогда 
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	КомандировкиРаботниковОрганизаций.Сотрудник,
			|	КомандировкиРаботниковОрганизаций.Период,
			|	ВЫБОР
			|		КОГДА КомандировкиРаботниковОрганизаций.ДокументПриОтзыве = ЗНАЧЕНИЕ(Документ.КомандировкиОрганизаций.ПустаяСсылка)
			|			ТОГДА КомандировкиРаботниковОрганизаций.ПериодЗавершения
			|		ИНАЧЕ КомандировкиРаботниковОрганизаций1.ПериодЗавершения
			|	КОНЕЦ КАК ПериодЗавершения,
			|	КомандировкиРаботниковОрганизаций.Регистратор,
			|	КомандировкиРаботниковОрганизаций.МестоНазначения,
			|	КомандировкиРаботниковОрганизаций.ОплатаКомРасходов
			|ИЗ
			|	РегистрСведений.КомандировкиРаботниковОрганизаций КАК КомандировкиРаботниковОрганизаций
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КомандировкиРаботниковОрганизаций КАК КомандировкиРаботниковОрганизаций1
			|		ПО КомандировкиРаботниковОрганизаций.Регистратор = КомандировкиРаботниковОрганизаций1.ДокументПриОтзыве
			|			И КомандировкиРаботниковОрганизаций.Сотрудник = КомандировкиРаботниковОрганизаций1.Сотрудник
			|			И КомандировкиРаботниковОрганизаций.Организация = КомандировкиРаботниковОрганизаций1.Организация
			|			И КомандировкиРаботниковОрганизаций.МестоНазначения = КомандировкиРаботниковОрганизаций1.МестоНазначения
			|ГДЕ
			|	&Дата МЕЖДУ КомандировкиРаботниковОрганизаций.Период И КомандировкиРаботниковОрганизаций.ПериодЗавершения
			|	И ЕСТЬNULL(КомандировкиРаботниковОрганизаций1.ДокументПриОтзыве, 0) = 0
			|	И КомандировкиРаботниковОрганизаций.Сотрудник = &Сотрудник";
			
			Запрос.УстановитьПараметр("Дата", ВыборкаПоРаботникиОрганизации.ДатаНачала);
			Запрос.УстановитьПараметр("Сотрудник", ВыборкаПоРаботникиОрганизации.Сотрудник);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Движение = Движения.КомандировкиРаботниковОрганизаций.Добавить();
				
				// Свойства
				Движение.Период							= ВыборкаДетальныеЗаписи.Период;
				Движение.ПериодЗавершения				= ВыборкаПоРаботникиОрганизации.ДатаНачала; 
				
				// Измерения
				Движение.Сотрудник						= ВыборкаПоРаботникиОрганизации.Сотрудник;
				Движение.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
				
				// Ресурсы
				Движение.Состояние						= Перечисления.СостояниеКомандировокОрганизации.ВКомандировке;
				Движение.ДокументПриОтзыве 				= ВыборкаДетальныеЗаписи.Регистратор; 
				Движение.ОплатаКомРасходов 				= ВыборкаДетальныеЗаписи.ОплатаКомРасходов;
				Движение.МестоНазначения 				= ВыборкаДетальныеЗаписи.МестоНазначения;
				Движение.ДействиеСДокументом 			= "Отозвать";
			КонецЦикла;
			
			
		иначе
			Движение = Движения.КомандировкиРаботниковОрганизаций.Добавить();
			
			// Свойства
			Движение.Период							= ВыборкаПоРаботникиОрганизации.ДатаНачала;
			
			// Измерения
			Движение.Сотрудник						= ВыборкаПоРаботникиОрганизации.Сотрудник;
			Движение.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
			
			// Ресурсы
			Если ВидКомандирования = 3 Тогда 
				Движение.Состояние						= Перечисления.СостояниеКомандировокОрганизации.Продление;
				Движение.ДействиеСДокументом 			= "Продлить";
			Иначе			
				Движение.Состояние						= Перечисления.СостояниеКомандировокОрганизации.ВКомандировке;
				Движение.ДействиеСДокументом 			= "В командировке";
			КонецЕсли;
			
			Движение.ПериодЗавершения		= ВыборкаПоРаботникиОрганизации.ДатаОкончания;  			
			Движение.ОплатаКомРасходов 				= ВыборкаПоРаботникиОрганизации.ОплатаКомРасходов;
			Движение.МестоНазначения 				= ВыборкаПоРаботникиОрганизации.МестоНазначения;
		КонецЕсли;
	КонецЕсли;
	
	//Аверсон ЕАЕ 11.11.2021 СВФР-008352
	Если Не ВыборкаПоРаботникиОрганизации.ОтменаКомандировки Тогда
		//Аверсон СВФР-008352
		
		Движение = Движения.ПериодыСостоянийРаботниковОрганизаций.Добавить();
		
		// Измерения
		Движение.Сотрудник			= ВыборкаПоРаботникиОрганизации.Сотрудник;
		Движение.ДатаНачала			= ВыборкаПоРаботникиОрганизации.ДатаНачала;
		Если ВыборкаПоРаботникиОрганизации.ДатаОкончания <> '0001-01-01' И
			НЕ ВыборкаПоРаботникиОрганизации.НапомнитьПоЗавершении и не ВыборкаПоРаботникиОрганизации.Учеба Тогда
			Движение.ДатаОкончания	= ВыборкаПоРаботникиОрганизации.ДатаОкончания + мДлинаСуток;
		КонецЕсли;
		Если ВыборкаПоРаботникиОрганизации.Учеба Тогда
			Движение = Движения.ПериодыСостоянийРаботниковОрганизаций.Добавить();
			Движение.Сотрудник			= ВыборкаПоРаботникиОрганизации.Сотрудник;
		    Движение.ДатаОкончания	= ВыборкаПоРаботникиОрганизации.ДатаОкончания; 						
		КонецЕсли;
		
		//Аверсон ЕАЕ 11.11.2021 СВФР-008352
	КонецЕсли;
	//Аверсон СВФР-008352
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа
//	ВыборкаПоРаботникиОрганизации
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации)
	
	Движение = Движения.ЗанятыеШтатныеЕдиницыОрганизаций.Добавить();
	
	// Свойства
	Движение.Период						= ВыборкаПоРаботникиОрганизации.ДатаНачала;
	Движение.ВидДвижения				= ВидДвиженияНакопления.Расход;
	
	// Измерения
	Движение.ПодразделениеОрганизации	= ВыборкаПоРаботникиОрганизации.ПодразделениеОрганизации;
	Движение.Должность					= ВыборкаПоРаботникиОрганизации.Должность;
	
	// Ресурсы
	Движение.КоличествоСтавок			= ВыборкаПоРаботникиОрганизации.ЗанимаемыхСтавок; 
	
	Если ЗначениеЗаполнено(ВыборкаПоРаботникиОрганизации.ДатаОкончания) и НЕ ВыборкаПоРаботникиОрганизации.НапомнитьПоЗавершении Тогда
		
		Движение = Движения.ЗанятыеШтатныеЕдиницыОрганизаций.Добавить();
		
		// Свойства
		Движение.Период					= ВыборкаПоРаботникиОрганизации.ДатаОкончания + мДлинаСуток;
		Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
		
		// Измерения
		Движение.ПодразделениеОрганизации = ВыборкаПоРаботникиОрганизации.ПодразделениеОрганизации;
		Движение.Должность				= ВыборкаПоРаботникиОрганизации.Должность;
		
		// Ресурсы
		Движение.КоличествоСтавок		= ВыборкаПоРаботникиОрганизации.ЗанимаемыхСтавок;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);
	
	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();
	
	Если ВыборкаПоШапкеДокумента.Следующий() Тогда
		
		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
		
		// Движения стоит добавлять, если в проведении еще не отказано (отказ = ложь)
		Если НЕ Отказ Тогда
			
			// Получение учетной политики по персоналу организации
			УчетнаяПолитикаПоПерсоналуОрганизации	= глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации");
			ПроверкаШтатногоРасписания				= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(УчетнаяПолитикаПоПерсоналуОрганизации, Организация, "ПроверкаШтатногоРасписания");
			
			РезультатЗапросаПоРаботники		= СформироватьЗапросПоРаботникиОрганизации(ВыборкаПоШапкеДокумента, ПроверкаШтатногоРасписания);
			ВыборкаПоРаботникиОрганизации	= РезультатЗапросаПоРаботники.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоРаботникиОрганизации.Следующий() Цикл
				
				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации, Отказ, Заголовок);
				
				Если НЕ Отказ Тогда
					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации);
					
					Если ВыборкаПоРаботникиОрганизации.ОсвобождатьСтавку Тогда
						ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	//О.Т. Аверсон 25.11.2016 № 51
	Для Каждого ТекСтрокаПереносДней Из ПереносДней Цикл
		Движение = Движения.ПереносДнейКомандировок.Добавить();
		Движение.Период = ТекСтрокаПереносДней.ДатаС;
		Движение.Сотрудник = ТекСтрокаПереносДней.Сотрудник;
		Движение.ФизЛицо = ТекСтрокаПереносДней.ФизЛицо;
		Движение.ДатаНаКакую = ТекСтрокаПереносДней.ДатаНаКакую;
	КонецЦикла;
	//О.Т. Аверсон 25.11.2016 № 51
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(РаботникиОрганизации);
	
	Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда
		ЗаписьРегистрации = ПринадлежностьПоследовательностям.КадровыеПриказыОрганизации.Добавить();
		ЗаписьРегистрации.Период		= Дата;
		ЗаписьРегистрации.Регистратор	= Ссылка;
	КонецЕсли;
	
	Движения.ПериодыСостоянийРаботниковОрганизаций.РежимЗаписиРегистратора = РежимЗаписи;
	
КонецПроцедуры // ПередЗаписью()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мДлинаСуток = 86400;
