////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мДлинаСуток;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

Функция Автозаполнение() Экспорт
	
 	Запрос = Новый Запрос;
	
	СписокВидовРасчета = Новый СписокЗначений;
	Если ЗначениеЗаполнено(ВидРасчета) Тогда
		СписокВидовРасчета.Добавить(ВидРасчета);
	Иначе
		СписокВидовРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПремияИТРИзФОТ);
		СписокВидовРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПремияРабочимИзФОТ);
	КонецЕсли;

	// создаем временную таблицу ВТСписокРаботников с сотрудниками, отобранными по критериям пользователя 
	// 
	// Поля:
	//   Сотрудник
	//   Физлицо
	//   ФИО
	//   ПодразделениеОрганизации
	//   Должность
	//
	
	РаботникиОрганизации.Очистить();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РаботникиОрганизаций.Сотрудник КАК Сотрудник,
	|	РаботникиОрганизаций.Сотрудник.Код КАК СотрудникКод,
	|	РаботникиОрганизаций.Должность,
	|	РаботникиОрганизаций.ПодразделениеОрганизации КАК Подразделение,
	|	РаботникиОрганизаций.Сотрудник.Физлицо,
	|	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета,
	|	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Действие,
	|	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель1
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&КонецМесяца, Организация = &ГоловнаяОрганизация) КАК РаботникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&НачалоМесяца, ) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
	|		ПО РаботникиОрганизаций.Сотрудник = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник
	|ГДЕ
	|	РаботникиОрганизаций.Организация = &ГоловнаяОрганизация
	|	И РаботникиОрганизаций.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|	И РаботникиОрганизаций.ПодразделениеОрганизации В ИЕРАРХИИ(&Подразделение)
	|	И ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета В(&ВидРасчета)
	|	И ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Действие <> &Действие
	
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение,
	|	СотрудникКод";
	
  	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("ОбособленноеПодразделение", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	Запрос.УстановитьПараметр("Подразделение", ПодразделениеОрганизации);
    Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПериодРегистрации));
    Запрос.УстановитьПараметр("Действие", Перечисления.ВидыДействияСНачислением.Прекратить);
    Запрос.УстановитьПараметр("ВидРасчета", СписокВидовРасчета);

	
	Если ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
    	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//И РаботникиОрганизаций.ПодразделениеОрганизации В ИЕРАРХИИ (&Подразделение)","И РаботникиОрганизаций.ПодразделениеОрганизации В ИЕРАРХИИ (&Подразделение)")
	КонецЕсли;

	Запрос.Текст = ТекстЗапроса;
	РезультатВыборка = Запрос.Выполнить().Выбрать();

	Пока РезультатВыборка.Следующий() Цикл
		НоваяСтрока = РаботникиОрганизации.Добавить();
		НоваяСтрока.Сотрудник = РезультатВыборка.Сотрудник;
		НоваяСтрока.Физлицо = РезультатВыборка.Сотрудник.ФизЛицо;
		НоваяСтрока.Подразделение = РезультатВыборка.Подразделение;
		НоваяСтрока.Должность = РезультатВыборка.Должность;
		НоваяСтрока.ВидРасчета = РезультатВыборка.ВидРасчета;
		НоваяСтрока.Процент = РезультатВыборка.Показатель1;
		Если ЗначениеЗаполнено(Процент) Тогда
			НоваяСтрока.ПроцентНовый = Процент;
		КонецЕсли;
		НоваяСтрока.ДатаНачала = НачалоМесяца(ПериодРегистрации);
		НоваяСтрока.ДатаОкончания = КонецМесяца(ПериодРегистрации);
		
	КонецЦикла;
КонецФункции //Автозаполнение()
 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",		Ссылка);
	Запрос.УстановитьПараметр("ПустаяОрганизация",	Справочники.Организации.ПустаяСсылка());

	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаПроцентаПремииПоТекущемуМесяцу.ПериодРегистрации,
	|	КорректировкаПроцентаПремииПоТекущемуМесяцу.Организация,
	|	ВЫБОР
	|		КОГДА КорректировкаПроцентаПремииПоТекущемуМесяцу.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|			ТОГДА КорректировкаПроцентаПремииПоТекущемуМесяцу.Организация
	|		ИНАЧЕ КорректировкаПроцентаПремииПоТекущемуМесяцу.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	КорректировкаПроцентаПремииПоТекущемуМесяцу.Ссылка
	|ИЗ
	|	Документ.КорректировкаПроцентаПремииПоТекущемуМесяцу КАК КорректировкаПроцентаПремииПоТекущемуМесяцу
	|ГДЕ
	|	КорректировкаПроцентаПремииПоТекущемуМесяцу.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "РаботникиОрганизации" документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса. В запросе данные документа дополняются данными о 
//  работниках из регистра сведений РаботникиОрганизации и о начислениях
//  и удержаниях из регистров сведений 
//
Функция СформироватьЗапросПоРаботникиОрганизации(ВыборкаПоШапкеДокумента, Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("Ссылка",				Ссылка);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.Сотрудник КАК Сотрудник,
	|	КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.Подразделение,
	|	КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.Должность,
	|	КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.НомерСтроки,
	|	КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.ВидРасчета,
	|	КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.ПроцентНовый, 
	|	КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.ДатаНачала,
	|	КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.ДатаОкончания
	|ИЗ
	|	Документ.КорректировкаПроцентаПремииПоТекущемуМесяцу.РаботникиОрганизации КАК КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации
	|ГДЕ
	//заявка №1946 (база заявок Кристалла) Дмитрук О.Г.
	//|	НЕ (КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.ПроцентНовый ЕСТЬ NULL)
	//заявка №1946
	|	НЕ (КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.Процент = КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.ПроцентНовый)
	|	И КорректировкаПроцентаПремииПоТекущемуМесяцуРаботникиОрганизации.Ссылка = &Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоРаботникиОрганизации()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "РаботникиОрганизации" документа.
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//  ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//  							  из результата запроса по товарам документа, 
//  Отказ 						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
	""" табл. части ""Работники организации"": ";
	
	// Сотрудник
	НетСотрудника = Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник);
	Если НетСотрудника Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
		
	Если НетСотрудника Тогда
		Возврат; // Дальше не проверяем
	КонецЕсли;
		
КонецПроцедуры // ПроверитьЗаполнениеСтрокиРаботникаОрганизации()

// Создает и заполняет структуру, содержащую имена регистров сведений
// по которым надо проводить документ
//
// Параметры: 
//	СтруктураПроведенияПоРегистрамСведений	- структура, содержащая имена регистров сведений 
//											  по которым надо проводить документ
//
// Возвращаемое значение:
//	Нет.
//
Процедура ЗаполнитьСтруктуруПроведенияПоРегистрамСведений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамСведений)

	СтруктураПроведенияПоРегистрамСведений = Новый Структура();
	СтруктураПроведенияПоРегистрамСведений.Вставить("КорректировкаПроцентаПремии");

КонецПроцедуры // ЗаполнитьСтруктуруПроведенияПоРегистрамСведений

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//  ВыборкаПоШапкеДокумента                - выборка из результата запроса по шапке документа,
//  СтруктураПроведенияПоРегистрамСведений - структура, содержащая имена регистров 
//                                           сведений по которым надо проводить документ,
//  СтруктураПараметров                    - структура параметров проведения,
//
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации, 
	СтруктураПроведенияПоРегистрамСведений)
	
	// Если документ нужно проводить по регистру, то для него есть ключ в структуре
	ИмяРегистра = "КорректировкаПроцентаПремии";
	Если СтруктураПроведенияПоРегистрамСведений.Свойство(ИмяРегистра) Тогда
		
		Движение = Движения[ИмяРегистра].Добавить();
		
		// Свойства
		Движение.Период						= ВыборкаПоШапкеДокумента.ПериодРегистрации;
		
		// Измерения                                                             
		Движение.Сотрудник		    		= ВыборкаПоРаботникиОрганизации.Сотрудник; 
		Движение.Организация	    		= ВыборкаПоШапкеДокумента.Организация; 
		Движение.ПодразделениеОрганизации   = ВыборкаПоРаботникиОрганизации.Подразделение;
		Движение.Должность			    	= ВыборкаПоРаботникиОрганизации.Должность;
		Движение.ВидРасчета			    	= ВыборкаПоРаботникиОрганизации.ВидРасчета;

		//Ресурсы
		Движение.Процент                    = ВыборкаПоРаботникиОрганизации.ПроцентНовый;
		Движение.ДатаНачала                 = ВыборкаПоРаботникиОрганизации.ДатаНачала;
        Движение.ДатаОкончания              = ВыборкаПоРаботникиОрганизации.ДатаОкончания;

	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//структура, содержащая имена регистров накопления по которым надо проводить документ
	Перем СтруктураПроведенияПоРегистрамНакопления;

	//структура, содержащая имена регистров сведений по которым надо проводить документ
	Перем СтруктураПроведенияПоРегистрамСведений;
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	Если ЭтотОбъект.Модифицированность() Тогда
		ЭтотОбъект.Записать();
	КонецЕсли;

	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();
	Если ВыборкаПоШапкеДокумента.Следующий() Тогда
		
		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда

			// Создадим и заполним структуры, содержащие имена регистров, по которым в зависимости от типа учета
			// проводится документ. В дальнейшем будем считать, что если для регистра не создан ключ в структуре,
			// то проводить по нему не надо.
			ЗаполнитьСтруктуруПроведенияПоРегистрамСведений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамСведений);
			
			// получим реквизиты табличной части
			РезультатЗапросаПоРаботники = СформироватьЗапросПоРаботникиОрганизации(ВыборкаПоШапкеДокумента, Режим);
			ВыборкаПоСтрокамДокумента = РезультатЗапросаПоРаботники.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			// обходим строки документа
			Пока ВыборкаПоСтрокамДокумента.Следующий() Цикл
				
				//заявка №1946 (база заявок Кристалла) Дмитрук О.Г.
				//Если ВыборкаПоСтрокамДокумента.ПроцентНовый <> 0 Тогда
					ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента , Отказ, Заголовок);
					
					Если НЕ Отказ Тогда
						ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, СтруктураПроведенияПоРегистрамСведений);
					КонецЕсли;
				//КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	ОбщегоНазначения.ДобавитьПрефиксОрганизации(ЭтотОбъект, Префикс);
	ОбщегоНазначения.ДобавитьПрефиксУзла(Префикс);
	
	// получить новый номер документа по совокупности кадровых документов
	ПроцедурыУправленияПерсоналом.ПриУстановкеНовогоНомераКадровогоДокумента(СтандартнаяОбработка, ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ЕдиныйНумераторКадровыхДокументов"), Номер, Префикс, Дата);
	
КонецПроцедуры // ПриУстановкеНовогоНомера()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(РаботникиОрганизации);
		
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Движения.КорректировкаПроцентаПремии.Очистить();
	Движения.КорректировкаПроцентаПремии.Записать();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мДлинаСуток = 86400;
