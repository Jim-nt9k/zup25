////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция СформироватьЗапросПоОтпускамПриказ(ТекПриказ) Экспорт
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОтпускаОрганизацийРаботникиОрганизации.Сотрудник,
	|	ОтпускаОрганизацийРаботникиОрганизации.ДатаНачала,
	|	ОтпускаОрганизацийРаботникиОрганизации.ДатаОкончания,
	|	ОтпускаОрганизацийРаботникиОрганизации.Ссылка
	|ИЗ
	|	Документ.ОтпускаОрганизаций.РаботникиОрганизации КАК ОтпускаОрганизацийРаботникиОрганизации
	|ГДЕ
	|	ОтпускаОрганизацийРаботникиОрганизации.Сотрудник = &Сотрудник 
	|	И ОтпускаОрганизацийРаботникиОрганизации.Ссылка = &Док";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник.Ссылка);
	Запрос.УстановитьПараметр("Док", ТекПриказ.Ссылка);
	
	Результат = Запрос.Выполнить();
	Возврат Результат;
КонецФункции


Функция СформироватьЗапросПоБЛПриказ(ТекПриказ) Экспорт
	//Аверсон 19.01.2020 КВВ СВФР-005034 (
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НачислениеПоБольничномуЛисту.Сотрудник,
	|	НачислениеПоБольничномуЛисту.ДатаНачала,
	|	НачислениеПоБольничномуЛисту.ДатаОкончания,
	|	НачислениеПоБольничномуЛисту.Ссылка
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту КАК НачислениеПоБольничномуЛисту
	|ГДЕ
	|	НачислениеПоБольничномуЛисту.Сотрудник = &Сотрудник 
	|	И НачислениеПоБольничномуЛисту.Ссылка = &Док";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник.Ссылка);
	Запрос.УстановитьПараметр("Док", ТекПриказ.Ссылка);
	
	Результат = Запрос.Выполнить();
	Возврат Результат;
	//Аверсон 19.01.2020 КВВ СВФР-005034 )
КонецФункции


// Формирует запрос по шапке документа
//
// Параметры:
//	Режим		- режим проведения.
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",		Ссылка);
	Запрос.УстановитьПараметр("ПустаяОрганизация",	Справочники.Организации.ПустаяСсылка());

	Запрос.Текст =
	"ВЫБРАТЬ
	 |	ОтзывИзОтпуска.Дата,
	 |	ОтзывИзОтпуска.Организация,
	 |	ОтзывИзОтпуска.Сотрудник,
	 |	ОтзывИзОтпуска.Приказ,
	 |	ОтзывИзОтпуска.ЗаниматьСтавку,	
	 |	ОтзывИзОтпуска.ДатаНачала,	
	 |  ОтзывИзОтпуска.ДатаОкончания,
	 |	ВЫБОР
	 |		КОГДА  ДанныеПоРаботникуДоНазначения.ПериодЗавершения <= ОтзывИзОтпуска.ДатаНачала
	 |				И ДанныеПоРаботникуДоНазначения.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	 |		ТОГДА ДанныеПоРаботникуДоНазначения.ПодразделениеОрганизацииЗавершения
	 |		ИНАЧЕ ДанныеПоРаботникуДоНазначения.ПодразделениеОрганизации
	 |	КОНЕЦ КАК ПодразделениеОрганизации,
	 |	ВЫБОР
	 |		КОГДА  ДанныеПоРаботникуДоНазначения.ПериодЗавершения <= ОтзывИзОтпуска.ДатаНачала
	 |				И ДанныеПоРаботникуДоНазначения.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	 |		ТОГДА ДанныеПоРаботникуДоНазначения.ДолжностьЗавершения
	 |		ИНАЧЕ ДанныеПоРаботникуДоНазначения.Должность
	 |	КОНЕЦ КАК Должность,
	 |   ВЫБОР
	 |   	 КОГДА  ДанныеПоРаботникуДоНазначения.ПериодЗавершения <= ОтзывИзОтпуска.ДатаНачала
	 |   			 И ДанныеПоРаботникуДоНазначения.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	 |   	 ТОГДА ДанныеПоРаботникуДоНазначения.ЗанимаемыхСтавокЗавершения
	 |   	 ИНАЧЕ ДанныеПоРаботникуДоНазначения.ЗанимаемыхСтавок
	 |	КОНЕЦ КАК ЗанимаемыхСтавок,

	 |	ВЫБОР
	 |		КОГДА ОтзывИзОтпуска.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	 |			ТОГДА ОтзывИзОтпуска.Организация
	 |		ИНАЧЕ ОтзывИзОтпуска.Организация.ГоловнаяОрганизация
	 |	КОНЕЦ КАК ГоловнаяОрганизация,
	 |	ОтзывИзОтпуска.Ссылка
	 |ИЗ
	 |	Документ.ОтзывИзОтпуска КАК ОтзывИзОтпуска
	 |	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК ДанныеПоРаботникуДоНазначения
	 |   	ПО ОтзывИзОтпуска.Сотрудник = ДанныеПоРаботникуДоНазначения.Сотрудник

	 |ГДЕ
	 |	ОтзывИзОтпуска.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация, работники которой возвращаются на работу!", Отказ, Заголовок);
	КонецЕсли;
	
    // Сотрудник
	НетСотрудника = НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Сотрудник);
	Если НетСотрудника Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;

	// Приказ
	НетПриказа = НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Приказ);
	Если НетПриказа Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата окончания отзыва!", Отказ, Заголовок);
	КонецЕсли;

	Если НетСотрудника ИЛИ НетПриказа Тогда
		Возврат; // Дальше не проверяем
	КонецЕсли;

	// Проверка: ранее работник должен быть принят на работу
	Если ВыборкаПоШапкеДокумента.ЗаниматьСтавку = NULL Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоШапкеДокумента.ДатаНачала, "ДЛФ=DD") + " работник " + ВыборкаПоШапкеДокумента.Сотрудник.Наименование + " еще не принят на работу!";
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
			
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Создает и заполняет структуру, содержащую имена регистров сведений
// по которым надо проводить документ
//
// Параметры:
//	СтруктураПроведенияПоРегистрамСведений	- структура, содержащая имена регистров сведений 
//											  по которым надо проводить документ
//
// Возвращаемое значение:
//	Нет.
//
Процедура ЗаполнитьСтруктуруПроведенияПоРегистрамСведений(СтруктураПроведенияПоРегистрамСведений)

	СтруктураПроведенияПоРегистрамСведений = Новый Структура();
	
	СтруктураПроведенияПоРегистрамСведений.Вставить("СостояниеРаботниковОрганизаций");

КонецПроцедуры // ЗаполнитьСтруктуруПроведенияПоРегистрамСведений()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента					- выборка из результата запроса по шапке документа,
//	СтруктураПроведенияПоРегистрамСведений	- структура, содержащая имена регистров 
//											  сведений по которым надо проводить документ,
//  СтруктураПараметров						- структура параметров проведения,
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента,  
		  СтруктураПроведенияПоРегистрамСведений, СтруктураПараметров = "")

	// Если документ нужно проводить по регистру, то для него есть ключ в структуре
	ИмяРегистра = "СостояниеРаботниковОрганизаций";
	Если СтруктураПроведенияПоРегистрамСведений.Свойство(ИмяРегистра) Тогда
		
		Движение = Движения[ИмяРегистра].Добавить();

		// Свойства
		Движение.Период			= ВыборкаПоШапкеДокумента.ДатаНачала;

		// Измерения
		Движение.Сотрудник		= ВыборкаПоШапкеДокумента.Сотрудник;
		Движение.Организация	= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		
		// Ресурсы
		Движение.Состояние		= Перечисления.СостоянияРаботникаОрганизации.Работает;
		Движение.ПериодЗавершения		= ВыборкаПоШапкеДокумента.ДатаОкончания + 86400;	
		
		//Аверсон 14.04.2020 КВВ СВФР-005809 (
		Если ТипОтзыва = Перечисления.ТипыОтзываИзОтпуска.Продление И ОтражатьИзмененияПоПродлению И 
			ЗначениеЗаполнено(ПродлениеС) И ЗначениеЗаполнено(ПродлениеПо) Тогда
			Движение = Движения[ИмяРегистра].Добавить();
			Движение.Период			= ПродлениеС;
			Движение.Сотрудник		= ВыборкаПоШапкеДокумента.Сотрудник;
			Движение.Организация	= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
			Движение.Состояние		= Перечисления.СостоянияРаботникаОрганизации.ОтпускЕжегодный;
			Движение.ПериодЗавершения		= ПродлениеПо + 86400;
			
			Движение = Движения[ИмяРегистра].Добавить();
			Движение.Период			= ПродлениеПо + 86400;
			Движение.Сотрудник		= ВыборкаПоШапкеДокумента.Сотрудник;
			Движение.Организация	= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
			Движение.Состояние		= Перечисления.СостоянияРаботникаОрганизации.Работает;
		КонецЕсли;
		//Аверсон 14.04.2020 КВВ СВФР-005809 )
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений()

// Создает и заполняет структуру, содержащую имена регистров накопления
// документа. В дальнейшем движения заносятся только по тем регистрам накопления, для которых в 
// данной процедуре заданы ключи.
//
// Параметры:
//	СтруктураПроведенияПоРегистрамНакопления - структура, содержащая имена регистров 
//											   накопления по которым надо проводить документ
//
// Возвращаемое значение:
//	Нет.
//
Процедура ЗаполнитьСтруктуруПроведенияПоРегистрамНакопления(СтруктураПроведенияПоРегистрамНакопления)

	СтруктураПроведенияПоРегистрамНакопления = Новый Структура();
	СтруктураПроведенияПоРегистрамНакопления.Вставить("ЗанятыеШтатныеЕдиницыОрганизаций");

КонецПроцедуры // ЗаполнитьСтруктуруПроведенияПоРегистрамНакопления()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента						- выборка из результата запроса по шапке документа
//	СтруктураПроведенияПоРегистрамНакопления	- структура, содержащая имена регистров 
//												  накопления по которым надо проводить документ
//	СтруктураПараметров							- структура параметров проведения.
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, 
		  СтруктураПроведенияПоРегистрамНакопления, СтруктураПараметров = "")

	// Если документ нужно проводить по регистру, то для него есть ключ в структуре
	ИмяРегистра = "ЗанятыеШтатныеЕдиницыОрганизаций";
	Если СтруктураПроведенияПоРегистрамНакопления.Свойство(ИмяРегистра) Тогда

		// займем ставки
		Движение = Движения[ИмяРегистра].Добавить();
		
		// Свойства
		Движение.Период						= ВыборкаПоШапкеДокумента.ДатаНачала;
		Движение.ВидДвижения				= ВидДвиженияНакопления.Приход;
		
		// Измерения
		Движение.ПодразделениеОрганизации	= ВыборкаПоШапкеДокумента.ПодразделениеОрганизации;
		Движение.Должность					= ВыборкаПоШапкеДокумента.Должность;

		// Ресурсы
		Движение.КоличествоСтавок			= ВыборкаПоШапкеДокумента.ЗанимаемыхСтавок; 

		
		Выборка = СформироватьЗапросПоОтпускамПриказ(Приказ).Выбрать();
		Если Выборка.Следующий() Тогда
			Если ВыборкаПоШапкеДокумента.ДатаОкончания < Выборка.ДатаОкончания Тогда
				// освободим ставки
				Движение = Движения[ИмяРегистра].Добавить();
				
				// Свойства
				Движение.Период						= ВыборкаПоШапкеДокумента.ДатаОкончания + 86400;
				Движение.ВидДвижения				= ВидДвиженияНакопления.Расход;
				
				// Измерения
				Движение.ПодразделениеОрганизации	= ВыборкаПоШапкеДокумента.ПодразделениеОрганизации;
				Движение.Должность					= ВыборкаПоШапкеДокумента.Должность;
				
				// Ресурсы
				Движение.КоличествоСтавок			= ВыборкаПоШапкеДокумента.ЗанимаемыхСтавок;
			КонецЕсли;
		КонецЕсли;

		
	КонецЕсли;

КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//структура, содержащая имена регистров накопления по которым надо проводить документ
	Перем СтруктураПроведенияПоРегистрамНакопления;

	//структура, содержащая имена регистров сведений по которым надо проводить документ
	Перем СтруктураПроведенияПоРегистрамСведений;
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	//ОсвобождатьСтавку = ЗаниматьСтавку; //перенесла в процедуру при записи

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
		
		// Движения стоит добавлять, если в проведении еще не отказано (отказ = ложь)
		Если НЕ Отказ Тогда
			
			// Создадим и заполним структуры, содержащие имена регистров, по которым в зависимости от типа учета
			// проводится документ. В дальнейшем будем считать, что если для регистра не создан ключ в структуре,
			// то проводить по нему не надо.
			ЗаполнитьСтруктуруПроведенияПоРегистрамНакопления(СтруктураПроведенияПоРегистрамНакопления);
			ЗаполнитьСтруктуруПроведенияПоРегистрамСведений(СтруктураПроведенияПоРегистрамСведений);
			
			// Заполним записи в наборах записей регистров
			ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамСведений);
			
			Если ВыборкаПоШапкеДокумента.ЗаниматьСтавку Тогда
				ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, СтруктураПроведенияПоРегистрамНакопления);
			КонецЕсли;
			//О.Т. 05.08.2014 № 157
			МаксПериодГод = Дата(1,1,1);
			НомерСтрокиОснование = 0;
			Если ЗначениеЗаполнено(Приказ) и ТипОтзыва = Перечисления.ТипыОтзываИзОтпуска.Перенос Тогда 
				ОтпускаПоСотр = Приказ.Отпуска.НайтиСтроки(Новый Структура("Сотрудник",Сотрудник));
				Для каждого стрОтпуска из ОтпускаПоСотр Цикл 
					если стрОтпуска.РабочийГодС > МаксПериодГод Тогда 
						МаксПериодГод = стрОтпуска.РабочийГодС;
						НомерСтрокиОснование = стрОтпуска.НомерСтроки;
					КонецЕсли;
				КонецЦикла;
				 //Аверсон 12.07.2019 КВВ СВФР-004466 (
				ОтпускОтбор = Приказ.Отпуска.Найти(НомерСтрокиОснование,"НомерСтроки");
				Если ОтпускОтбор <> Неопределено Тогда
					Для каждого стрОтп из Отпуска Цикл 
						Движение = Движения.ОтпускаПоТрудовымДоговорам.Добавить();
						Движение.ВидДвижения    = ВидДвиженияНакопления.Расход;
						Движение.Период         = ?(ЗначениеЗаполнено(стрОтп.РабочийГодС),стрОтп.РабочийГодС,ОтпускОтбор.РабочийГодС);  //Аверсон 22.05.2020 КВВ СВФР-006043 ( стрОтп.РабочийГодС )
						Движение.ДатаОкончания	= ?(ЗначениеЗаполнено(стрОтп.РабочийГодПо),стрОтп.РабочийГодПо,ОтпускОтбор.РабочийГодПо);  //Аверсон 22.05.2020 КВВ СВФР-006043 ( стрОтп.РабочийГодПо )
						Движение.Сотрудник      = Сотрудник;
						Движение.ВидОтпуска     = стрОтп.ВидОтпуска;
						//Ресурсы
						Движение.КоличествоДней = -стрОтп.КоличествоДней;
					КонецЦикла;
				КонецЕсли;
				//Аверсон 12.07.2019 КВВ СВФР-004466 )
				//ОтпускОтбор = Приказ.Отпуска.Найти(НомерСтрокиОснование,"НомерСтроки");
				//Если ОтпускОтбор <> Неопределено Тогда 
				//	Движение = Движения.ОтпускаПоТрудовымДоговорам.Добавить();
				//	//Измерения
				//	Движение.ВидДвижения    = ВидДвиженияНакопления.Расход;
				//	Движение.Период         = ОтпускОтбор.РабочийГодС;
				//	Движение.ДатаОкончания	= ОтпускОтбор.РабочийГодПо;
				//	Движение.Сотрудник      = Сотрудник;
				//	Движение.ВидОтпуска     = ОтпускОтбор.ВидОтпуска;
				//	//Ресурсы
				//	Движение.КоличествоДней = -НеиспольуемаяЧасть;
				//КонецЕсли;
				
			КонецЕсли;
			//О.Т. 05.08.2014 № 157
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ОсвобождатьСтавку = ЗаниматьСтавку;
	
	//Аверсон ЕАЕ 15.10.2025 СВФР-017035
	КраткийСоставДокумента = ЗаполнитьКраткийСоставДокумента();
	//Аверсон СВФР-017035
	
КонецПроцедуры

//Аверсон ЕАЕ 15.10.2025 СВФР-017035
Функция ЗаполнитьКраткийСоставДокумента() Экспорт
	
	ФИОФизЛица = ПроцедурыУправленияПерсоналом.ФамилияИнициалыФизЛица(ПроцедурыУправленияПерсоналом.УдалениеСимволовСкобок(Сотрудник.Наименование));
	КраткийСоставДокумента = ФИОФизЛица;
	
	Возврат КраткийСоставДокумента;
	
КонецФункции // ЗаполнитьКраткийСоставДокумента()
//Аверсон СВФР-017035

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
