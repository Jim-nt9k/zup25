
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//	Струткура, каждая строка которой соответствует одному из вариантов печати
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура;
	СтруктураМакетов.Вставить("Приказ",	   "Приказ");	
	СтруктураМакетов.Вставить("ПриказПоПодразделениям",	   "Приказ по подразделениям");
	
	Возврат СтруктураМакетов;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

// Функция формирует спмсок государственных праздников в месяце начисления.
//
// Параметры
//
// Возвращаемое значение:
//   СписокПраздников - СписокЗначений   – список государственных праздников.
//
Функция СформироватьСписокПраздниковИВыходных() Экспорт 

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДатаНачала",		НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецМесяца(ПериодРегистрации));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегламентированныйПроизводственныйКалендарь.ДатаКалендаря,
	|	РегламентированныйПроизводственныйКалендарь.ВидДня
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|ГДЕ
	|	РегламентированныйПроизводственныйКалендарь.ВидДня В (ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Праздник), ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Суббота), ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Воскресенье))
	|	И РегламентированныйПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	СписокПраздников = Новый СписокЗначений;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокПраздников.Добавить(Выборка.ДатаКалендаря, Формат(Выборка.ДатаКалендаря, "ДФ='d ММММ'") + " - " + СокрЛП(Выборка.ВидДня));
	КонецЦикла;
	
	Возврат СписокПраздников
	
КонецФункции // СформироватьСписокПраздников()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	Запрос.УстановитьПараметр("ПустаяОрганизация" , Справочники.Организации.ПустаяСсылка());

	Запрос.Текст =
	"ВЫБРАТЬ
	|	РаботаВВыходныеИПраздники.Дата,
	|	РаботаВВыходныеИПраздники.Организация,
	|	ВЫБОР
	|		КОГДА РаботаВВыходныеИПраздники.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|			ТОГДА РаботаВВыходныеИПраздники.Организация
	|		ИНАЧЕ РаботаВВыходныеИПраздники.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	РаботаВВыходныеИПраздники.ПериодРегистрации
	|ИЗ
	|	Документ.ПриказНаРаботуВПраздничныеИВыходныеДни КАК РаботаВВыходныеИПраздники
	|ГДЕ
	|	РаботаВВыходныеИПраздники.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	//  Организация
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация, по которой выполняются приказ!", Отказ, Заголовок);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПериодРегистрации) Тогда 
		ОбщегоНазначения.СообщитьОбОшибке("Не указан, период в котором осуществляется работа!", Отказ, Заголовок);
	КонецЕсли;
	
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Формирует запрос по таблице "Начисления" документа
//
// Параметры: 
//	Режим	- режим проведения
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоСостояниям(ВыборкаПоШапкеДокумента, Сотрудник = Неопределено)

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник КАК Сотрудник,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Физлицо,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха КАК ДатаСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.ДополнительныеВыходныеДниБезОплаты) КАК ПричинаОтсутствия,
	|	ВЫБОР
	|		КОГДА ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха = ДАТАВРЕМЯ(1, 1, 1)
	|				И НЕ ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Доплачивать
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаНетДоплатыИВыходного,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И РаботникиОрганизаций.ПериодЗавершения <= ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДатаВыхода
	|			ТОГДА РаботникиОрганизаций.ЗанимаемыхСтавокЗавершения
	|		ИНАЧЕ РаботникиОрганизаций.ЗанимаемыхСтавок
	|	КОНЕЦ КАК ЗанимаемыхСтавок,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник.Наименование,
	
	//Аверсон ЕАЕ 05.08.2025 СВФР-016362
	|   ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ВремяС,
	|   ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ВремяПо
	//Аверсон СВФР-016362
	
	|ИЗ
	|	Документ.ПриказНаРаботуВПраздничныеИВыходныеДни.РаботникиОрганизации КАК ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период
	|		ИЗ
	|			Документ.ПриказНаРаботуВПраздничныеИВыходныеДни.РаботникиОрганизации КАК ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|				ПО ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник = РаботникиОрганизаций.Сотрудник
	|					И ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха >= РаботникиОрганизаций.Период
	|		ГДЕ
	|			ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Ссылка = &ДокументСсылка
	|			И ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха <> ДАТАВРЕМЯ(1, 1, 1)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник) КАК ДатыПоследнегоДвиженияРаботникаНаДеньОтдыха
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|			ПО ДатыПоследнегоДвиженияРаботникаНаДеньОтдыха.Сотрудник = РаботникиОрганизаций.Сотрудник
	|				И ДатыПоследнегоДвиженияРаботникаНаДеньОтдыха.Период = РаботникиОрганизаций.Период
	|		ПО ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник = ДатыПоследнегоДвиженияРаботникаНаДеньОтдыха.Сотрудник
	|ГДЕ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Ссылка = &ДокументСсылка
	|	И ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха <> ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Физлицо,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДатаВыхода,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.РаботаВПраздничныеИВыходныеДни),
	|	ВЫБОР
	|		КОГДА ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха = ДАТАВРЕМЯ(1, 1, 1)
	|				И НЕ ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Доплачивать
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И РаботникиОрганизаций.ПериодЗавершения <= ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДатаВыхода
	|			ТОГДА РаботникиОрганизаций.ЗанимаемыхСтавокЗавершения
	|		ИНАЧЕ РаботникиОрганизаций.ЗанимаемыхСтавок
	|	КОНЕЦ,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник.Наименование,
	
	//Аверсон ЕАЕ 05.08.2025 СВФР-016362
	|   ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ВремяС,
	|   ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ВремяПо
	//Аверсон СВФР-016362
	
	|ИЗ
	|	Документ.ПриказНаРаботуВПраздничныеИВыходныеДни.РаботникиОрганизации КАК ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период
	|		ИЗ
	|			Документ.ПриказНаРаботуВПраздничныеИВыходныеДни.РаботникиОрганизации КАК ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|				ПО ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник = РаботникиОрганизаций.Сотрудник
	|					И ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДатаВыхода >= РаботникиОрганизаций.Период
	|		ГДЕ
	|			ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Ссылка = &ДокументСсылка
	|			И ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДатаВыхода <> ДАТАВРЕМЯ(1, 1, 1)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник) КАК ДатыПоследнегоДвиженияРаботникаНаДеньОтдыха
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|			ПО ДатыПоследнегоДвиженияРаботникаНаДеньОтдыха.Сотрудник = РаботникиОрганизаций.Сотрудник
	|				И ДатыПоследнегоДвиженияРаботникаНаДеньОтдыха.Период = РаботникиОрганизаций.Период
	|		ПО ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник = ДатыПоследнегоДвиженияРаботникаНаДеньОтдыха.Сотрудник
	|ГДЕ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Ссылка = &ДокументСсылка
	|	И ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДатаВыхода <> ДАТАВРЕМЯ(1, 1, 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоНачисления()

// Проверяет правильность заполнения реквизитов в строке ТЧ "Начисления" документа.
// Если какой-то из реквизтов, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса к ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры:
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса к ТЧ документа, 
//	Отказ						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиСостояния(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""Работники организаций"": ";
	
	// Сотрудник
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указанный сотрудник оформлен на другую организацию!", Отказ, Заголовок);
	КонецЕсли;
	
	// Проверка: ранее работник должен быть принят на работу
	Если ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок = NULL Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоСтрокамДокумента.ДатаСобытия, "ДЛФ=DD") + " работник " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " еще не принят на работу!";
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
			
	ИначеЕсли ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок = 0 Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоСтрокамДокумента.ДатаСобытия, "ДЛФ=DD") + " работник " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " уже уволен!";
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
			
	КонецЕсли;
	
	//Если ХьюменСистем.обПустоеЗначение(ВыборкаПоСтрокамДокумента.ДатаСобытия) Тогда
	//	ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата события!", Отказ, Заголовок);
	//КонецЕсли;
	//
	//Если ВыборкаПоСтрокамДокумента.ОшибкаНетДоплатыИВыходного Тогда 
	//	ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "должна быть выбрана или доплата или выходной в другой день!", Отказ, Заголовок);
	//КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиНачисления()

Функция СформироватьЗапросДляПроверки()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник КАК Сотрудник,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха <> ДАТАВРЕМЯ(1, 1, 1)
	|				И НАЧАЛОПЕРИОДА(ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДатаВыхода, МЕСЯЦ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПредупреждениеОВыходномВРазныхПериодах,
	|	ВЫБОР
	|		КОГДА ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха <> ДАТАВРЕМЯ(1, 1, 1)
	|				И ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Доплачивать
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПредупреждениеОДвойнойВыгоде
	|ИЗ
	|	Документ.ПриказНаРаботуВПраздничныеИВыходныеДни.РаботникиОрганизации КАК ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации
	|ГДЕ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Ссылка = &ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.НомерСтроки";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПроверитьИПредупредитьОДвойныхВыгодах(ВыборкаПоСтрокамДокумента)
	
	ВыводитьПредупреждение = Ложь;
	СтрокаНачалаСообщения = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) + """ табл. части ""Работники организаций"": ";
									
	Если ВыборкаПоСтрокамДокумента.ПредупреждениеОВыходномВРазныхПериодах Тогда 
		СтрокаСообщения = СтрокаНачалаСообщения + "День отдыха находится за пределами текущего месяца, не будет отображен в табеле, если день отдыха находится в другом периоде";
	КонецЕсли;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Сообщить(СтрокаСообщения);
	#КонецЕсли
	
	Если ВыборкаПоСтрокамДокумента.ПредупреждениеОДвойнойВыгоде Тогда 
		ВыводитьПредупреждение = Истина;
	КонецЕсли;
	
	Возврат ВыводитьПредупреждение;
	
КонецФункции

Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоТЧ)
	
	Если НЕ УчетВремениСвэй.обПустоеЗначение(ВыборкаПоТЧ.ДатаСобытия) Тогда 
		
		Движение = Движения.СостояниеРаботниковОрганизаций.Добавить();
		
		Движение.Период		= ВыборкаПоТЧ.ДатаСобытия + 10;
		Движение.ПериодЗавершения = КонецДня(ВыборкаПоТЧ.ДатаСобытия) + 1;
		// Измерения
		Движение.Сотрудник		= ВыборкаПоТЧ.Сотрудник;
		Движение.Организация	= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		
		// Ресурсы
		Движение.Состояние		= ВыборкаПоТЧ.ПричинаОтсутствия;
		Движение.СостояниеЗавершения	= УчетВремениСвэй.ПолучитьСостояниеСотрудникаНаДату(Организация, ВыборкаПоТЧ.Сотрудник, КонецДня(ВыборкаПоТЧ.ДатаСобытия) + 60*60*24-1);//Перечисления.СостоянияРаботникаОрганизации.Работает;
		
		//Аверсон 05.11.2021 КВВ СВФР-008344 (
		ЗПроверка = Новый Запрос;
		ЗПроверка.Текст = "ВЫБРАТЬ *
		|ИЗ РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРО
		|ГДЕ СостояниеРО.Сотрудник = &Сотрудник
		|И СостояниеРО.Регистратор <> &Регистратор
		|И СостояниеРО.Состояние = &Состояние
		|И НАЧАЛОПЕРИОДА(СостояниеРО.Период,ДЕНЬ) = НАЧАЛОПЕРИОДА(&Период,ДЕНЬ)";
		ЗПроверка.УстановитьПараметр("Сотрудник",Движение.Сотрудник);
		ЗПроверка.УстановитьПараметр("Регистратор",Ссылка);
		ЗПроверка.УстановитьПараметр("Состояние",Движение.Состояние);
		ЗПроверка.УстановитьПараметр("Период",Движение.Период);
		РезЗПроверка = ЗПроверка.Выполнить().Выгрузить();
		Для Каждого СтрРезЗПроверка Из РезЗПроверка Цикл
			
			//Аверсон ЕАЕ 05.08.2025 СВФР-016362
			Если Организация.ИНН = "790041382" Тогда
				Для Каждого Стр Из СтрРезЗПроверка.Регистратор.РаботникиОрганизации Цикл
					Если Стр.Сотрудник <> ВыборкаПоТЧ.Сотрудник Тогда
						Продолжить;
					КонецЕсли;
					Если НачалоДня(Стр.ДатаВыхода) <> НачалоДня(ВыборкаПоТЧ.ДатаСобытия) Тогда
						Продолжить;
					КонецЕсли;
					Если НЕ (ВыборкаПоТЧ.ВремяПо < Стр.ВремяС ИЛИ ВыборкаПоТЧ.ВремяС > Стр.ВремяПо) Тогда
						Сообщить("По сотруднику " + ПроцедурыУправленияПерсоналом.ФамилияИнициалыФизЛица(Движение.Сотрудник.Физлицо) + " на дату " + 
								Формат(Движение.Период, "ДЛФ=Д") + " уже проведен документ " + СтрРезЗПроверка.Регистратор + " (время с " + Формат(Стр.ВремяС, "ДФ=HH:mm:ss") + " по " + Формат(Стр.ВремяПо, "ДФ=HH:mm:ss") + ")");	
					КонецЕсли;
				КонецЦикла;
			Иначе
			//Аверсон СВФР-016362
			
				Сообщить("По сотруднику " + ПроцедурыУправленияПерсоналом.ФамилияИнициалыФизЛица(Движение.Сотрудник.Физлицо) + " на дату " + Формат(Движение.Период, "ДЛФ=Д") + " уже проведен документ " + СтрРезЗПроверка.Регистратор);
			КонецЕсли;	
		КонецЦикла;
		//Аверсон 05.11.2021 КВВ СВФР-008344 )
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()

#Если ТолстыйКлиентОбычноеПриложение Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//	Название макета печати передается в качестве параметра,
//	по переданному названию находим имя макета в соответствии.
//
// Параметры:
//	НазваниеМакета	- строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	Если ИмяМакета = "Приказ" Тогда		
		СпрХранилище = Справочники.ХранилищеДополнительнойИнформации;
		ИскомыйПриказ = СпрХранилище.НайтиПоРеквизиту("Объект",Ссылка);
		Если УчетВремениСвэй.обПустоеЗначение(ИскомыйПриказ) Тогда
			ТабДокумент = ПечатьПриказ(ИмяМакета);
		Иначе
			ФайлПриказа = ИскомыйПриказ.Хранилище.Получить();
			РаботаСФайлами.ОткрытьФайлИзХранилища(ИскомыйПриказ.Наименование,ИскомыйПриказ.Хранилище,"",истина);
			//ОжидатьЗакрытияФайлаИОбновитьПрикрепленныйФайл(КаталогВременныхФайлов()+ИскомыйПриказ.Наименование, ИскомыйПриказ);
		КонецЕсли;
	ИначеЕсли ИмяМакета = "ПриказПоПодразделениям" Тогда		
		ТабДокумент = ПечатьПриказ(ИмяМакета);
	КонецЕсли;   	
	
	ИмяФормы = "";
	Возврат РаботаСДиалогами.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, РаботаСДиалогами.СформироватьЗаголовокДокумента(ЭтотОбъект,Метаданные().Синоним + ИмяФормы));

КонецФункции // Печать()

// Функция формирует табличный документ с печатной формой "Т-6а",
//
// Возвращаемое значение:
//	Табличный документ - печатная форма
//
Функция ПечатьПриказ(ИмяМакета = "")

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	//ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтпускаОрганизации_Т6а";
	
	Запрос =  Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапроса();    	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Документ", Ссылка);

	Выборка = Запрос.Выполнить().Выбрать();
	
	// подсчитываем количество страниц документа - для корректного разбиения на страницы
	ВсегоСтрокДокумента = Выборка.Количество();
	
	// запоминаем области макета
	Макет = ПолучитьМакет("Приказ");
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка"); // Шапка документа.
	ПовторятьПриПечатиСтроки = Макет.ПолучитьОбласть("ПовторятьПриПечати"); // повторяющаяся шапка страницы
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");// Подвал документа
	ОбластьМакетаСтрока = Макет.ПолучитьОбласть("Строка"); // строка работника
	
	// массив с двумя строками - для разбиения на страницы
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакетаСтрока);
	
	ОтветственныеЛица = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, Дата);
	Руководитель =  ОтветственныеЛица.Руководитель;
	ОбластьМакетаПодвал.Параметры.ДолжностьРуководителя = ТРег(ОтветственныеЛица.РуководительДолжность);
//	ФИО = ОбщегоНазначения.ФамилияИнициалыФизЛица(Руководитель);
	ОбластьМакетаПодвал.Параметры.Руководитель = Руководитель; 
	
	ОбластьМакетаШапка.Параметры.Организация = СокрЛП(Организация.НаименованиеПолное);
	ОбластьМакетаШапка.Параметры.Дата = Формат(Дата,"ДЛФ=Д");
	Если СокрЛП(НомерКадровогоПриказа) <> "" Тогда 
		ОбластьМакетаШапка.Параметры.Номер = НомерКадровогоПриказа;
	Иначе 		
		ОбластьМакетаШапка.Параметры.Номер = Номер;
	КонецЕсли;
	
//	ОбластьМакетаШапка.Параметры.Город = Обработки.ПечатьКадровыхПриказов.Создать().ПолучитГородОрганизации(Организация);		
	
	Попытка
		ДатаПраздника = Формат(РаботникиОрганизации[0].ДатаВыхода,"ДЛФ=Д");
	Исключение
		ДатаПраздника = Дата;		
	КонецПопытки;
	
	ТекстПриказываю = Символы.Таб + "1. Привлечь к работе в праздничный(выходной) день " + Формат(ДатаПраздника,"ДЛФ=Д") + 
	" следующих сотрудников " + Организация + " с их согласия:";
	ОбластьМакетаШапка.Параметры.ТекстПриказа = Ссылка.СодержаниеПриказа;
	ОбластьМакетаШапка.Параметры.ТекстПриказываю = ТекстПриказываю;	
	
	// Начинаем формировать выходной документ
	ТабДокумент.Вывести(ОбластьМакетаШапка); // Шапка документа.
	ВыведеноСтрок = 0;
	ПредПодразделение  = "";
	Если ВсегоСтрокДокумента <> 0 Тогда
		// выводим строки по работникам
		Пока Выборка.Следующий() Цикл
			Если ИмяМакета = "ПриказПоПодразделениям" Тогда
				НаименованиеПодразделения = ?( ЗначениеЗаполнено( Выборка.ПодразделениеОрганизации ), 
												СокрЛП(Выборка.ПодразделениеОрганизации.Наименование), "" );
				Если ПредПодразделение <> НаименованиеПодразделения И НаименованиеПодразделения <> "" Тогда 
					ОбластьМакетаПодразделение = Макет.ПолучитьОбласть("Подразделение"); 
					ОбластьМакетаПодразделение.Параметры.Подразделение = НаименованиеПодразделения;
					ТабДокумент.Вывести(ОбластьМакетаПодразделение);
					ПредПодразделение = НаименованиеПодразделения;
				КонецЕсли;			
			КонецЕсли;
			ОбластьМакетаСтрока.Параметры.Заполнить(Выборка);
			// разбиение на страницы
			ВыведеноСтрок = ВыведеноСтрок + 1;
			
			// Проверим, уместится ли строка на странице или надо открывать новую страницу
			ВывестиПодвалЛиста = Не ТабДокумент.ПроверитьВывод(ВыводимыеОбласти);
			Если Не ВывестиПодвалЛиста и ВыведеноСтрок = ВсегоСтрокДокумента Тогда
				ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
				ВывестиПодвалЛиста = Не ТабДокумент.ПроверитьВывод(ВыводимыеОбласти);
			КонецЕсли;
			Если ВывестиПодвалЛиста Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
			КонецЕсли;
			ОбластьМакетаСтрока.Параметры.Номер = ВыведеноСтрок;
			//ОбластьМакетаСтрока.Параметры.Должность = СокрЛП(Выборка.Должность.Наименование);
			ОбластьМакетаСтрока.Параметры.Должность = ?( ЗначениеЗаполнено(Выборка.Должность), СокрЛП(Выборка.Должность.Наименование), "");
			ТабДокумент.Вывести(ОбластьМакетаСтрока);
		КонецЦикла;
	Иначе
		// если не было ни одного работника - выводим пустой бланк
		ВыводимыеОбласти = Новый Массив();
		ВыводимыеОбласти.Добавить(ОбластьМакетаСтрока);
		ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
		Для Сч = 1 По ОбластьМакетаСтрока.Параметры.Количество() Цикл
			ОбластьМакетаСтрока.Параметры.Установить(Сч - 1,""); 
		КонецЦикла;
		ОбластьМакетаСтрока.Параметры.Сотрудник = " " + Символы.ПС + " ";
		Пока ТабДокумент.ПроверитьВывод(ВыводимыеОбласти) Цикл
			ТабДокумент.Вывести(ОбластьМакетаСтрока);
		КонецЦикла;
	КонецЕсли;
	// выводим предварительно подготовленный Подвал документа.
	ТабДокумент.Вывести(ОбластьМакетаПодвал);
	
	Возврат ТабДокумент;
	
КонецФункции 

Функция ПолучитьТекстЗапроса()
	Текст = 
	"ВЫБРАТЬ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДатаВыхода,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Физлицо,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ДеньОтдыха,
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.ОтработаноЧасов
	|ПОМЕСТИТЬ ВТСотрудникиДокумента
	|ИЗ
	|	Документ.ПриказНаРаботуВПраздничныеИВыходныеДни.РаботникиОрганизации КАК ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации
	|ГДЕ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Ссылка = &Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСотрудникиДокумента.Сотрудник,
	|	ВТСотрудникиДокумента.ДатаВыхода,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				И РаботникиОрганизаций.ПериодЗавершения <= ВТСотрудникиДокумента.ДатаВыхода
	|			ТОГДА РаботникиОрганизаций.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ РаботникиОрганизаций.ПодразделениеОрганизации
	|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				И РаботникиОрганизаций.ПериодЗавершения <= ВТСотрудникиДокумента.ДатаВыхода
	|			ТОГДА РаботникиОрганизаций.ДолжностьЗавершения
	|		ИНАЧЕ РаботникиОрганизаций.Должность
	|	КОНЕЦ КАК Должность,
	//|	ВЫБОР
	//|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	//|				И РаботникиОрганизаций.ПериодЗавершения <= ВТСотрудникиДокумента.ДатаВыхода
	//|			ТОГДА РаботникиОрганизаций.ДолжностьПодразделенияЗавершения
	//|		ИНАЧЕ РаботникиОрганизаций.ДолжностьПодразделения
	//|	КОНЕЦ КАК ДолжностьПодразделения,
	//|	ВЫБОР
	//|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	//|				И РаботникиОрганизаций.ПериодЗавершения <= ВТСотрудникиДокумента.ДатаВыхода
	//|			ТОГДА РаботникиОрганизаций.РазрядПоЕТСЗавершения
	//|		ИНАЧЕ РаботникиОрганизаций.РазрядПоЕТС
	//|	КОНЕЦ КАК РазрядПоЕТС,
	//|	ВЫБОР
	//|		КОГДА ВЫБОР
	//|				КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	//|						И РаботникиОрганизаций.ПериодЗавершения <= ВТСотрудникиДокумента.ДатаВыхода
	//|					ТОГДА РаботникиОрганизаций.ДолжностьЗавершения.КатегорияСтатистическогоУчета.КатегорияДляСтатистическогоУчета
	//|				ИНАЧЕ РаботникиОрганизаций.Должность.КатегорияСтатистическогоУчета.КатегорияДляСтатистическогоУчета
	//|			КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.КатегорииДолжностейДляСтатистическогоУчета.Рабочие)
	//|			ТОГДА ИСТИНА
	//|		ИНАЧЕ ЛОЖЬ
	//|	КОНЕЦ КАК ЭтоРазряд,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И РаботникиОрганизаций.ПериодЗавершения <= ВТСотрудникиДокумента.ДатаВыхода
	|			ТОГДА РаботникиОрганизаций.ЗанимаемыхСтавокЗавершения
	|		ИНАЧЕ РаботникиОрганизаций.ЗанимаемыхСтавок
	|	КОНЕЦ КАК ЗанимаемыхСтавок
	|ПОМЕСТИТЬ ВТРаботникиСДолжностями
	|ИЗ
	|	ВТСотрудникиДокумента КАК ВТСотрудникиДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТСотрудникиДокумента.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период
	|		ИЗ
	|			ВТСотрудникиДокумента КАК ВТСотрудникиДокумента
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|				ПО ВТСотрудникиДокумента.Сотрудник = РаботникиОрганизаций.Сотрудник
	|					И ВТСотрудникиДокумента.ДатаВыхода >= РаботникиОрганизаций.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВТСотрудникиДокумента.Сотрудник) КАК МаксимальныеДатыВРегистреРаботники
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|			ПО МаксимальныеДатыВРегистреРаботники.Сотрудник = РаботникиОрганизаций.Сотрудник
	|				И МаксимальныеДатыВРегистреРаботники.Период = РаботникиОрганизаций.Период
	|		ПО ВТСотрудникиДокумента.Сотрудник = МаксимальныеДатыВРегистреРаботники.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРаботникиСДолжностями.Сотрудник,
	|	ВТРаботникиСДолжностями.ДатаВыхода,
	|	ВТРаботникиСДолжностями.ПодразделениеОрганизации,
	|	ВТРаботникиСДолжностями.Должность,
	//|	ВТРаботникиСДолжностями.ДолжностьПодразделения,
	//|	ВТРаботникиСДолжностями.ЭтоРазряд,
	//|	ВТРаботникиСДолжностями.РазрядПоЕТС,
	//|	ВЫБОР
	//|		КОГДА ВТРаботникиСДолжностями.Должность.КатегорияСтатистическогоУчета.КатегорияДляСтатистическогоУчета = ЗНАЧЕНИЕ(Перечисление.КатегорииДолжностейДляСтатистическогоУчета.Рабочие)
	//|			ТОГДА ВТРаботникиСДолжностями.РазрядПоЕТС
	//|		ИНАЧЕ ВЫБОР
	//|				КОГДА ШтатноеРасписаниеОрганизаций.КатегорияШР ЕСТЬ НЕ NULL 
	//|					ТОГДА ШтатноеРасписаниеОрганизаций.КатегорияШР.Наименование
	//|				ИНАЧЕ """"""""
	//|			КОНЕЦ
	//|	КОНЕЦ КАК РазрядИЛИКатегория,
	|	ВТРаботникиСДолжностями.ЗанимаемыхСтавок
	|ПОМЕСТИТЬ ВТРаботникиСКатегориями
	|ИЗ
	|	ВТРаботникиСДолжностями КАК ВТРаботникиСДолжностями
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТРаботникиСДолжностями.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(ШтатноеРасписаниеОрганизаций.Период) КАК Период,
	|			ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|			ШтатноеРасписаниеОрганизаций.Должность КАК Должность
	//|			ШтатноеРасписаниеОрганизаций.ДолжностьПодразделения КАК ДолжностьПодразделения
	|		ИЗ
	|			ВТРаботникиСДолжностями КАК ВТРаботникиСДолжностями
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписаниеОрганизаций
	|				ПО ВТРаботникиСДолжностями.ПодразделениеОрганизации = ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации
	|					И ВТРаботникиСДолжностями.Должность = ШтатноеРасписаниеОрганизаций.Должность
	//|					И ВТРаботникиСДолжностями.ДолжностьПодразделения = ШтатноеРасписаниеОрганизаций.ДолжностьПодразделения
	|					И ВТРаботникиСДолжностями.ДатаВыхода >= ШтатноеРасписаниеОрганизаций.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВТРаботникиСДолжностями.Сотрудник,
	|			ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации,
	|			ШтатноеРасписаниеОрганизаций.Должность) КАК МаксимальныеДатыШтатного
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписаниеОрганизаций
	|			ПО МаксимальныеДатыШтатного.ПодразделениеОрганизации = ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации
	|				И МаксимальныеДатыШтатного.Должность = ШтатноеРасписаниеОрганизаций.Должность
	//|				И МаксимальныеДатыШтатного.ДолжностьПодразделения = ШтатноеРасписаниеОрганизаций.ДолжностьПодразделения
	|				И МаксимальныеДатыШтатного.Период = ШтатноеРасписаниеОрганизаций.Период
	|		ПО ВТРаботникиСДолжностями.Сотрудник = МаксимальныеДатыШтатного.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРаботникиСКатегориями.Сотрудник,
	|	ВТРаботникиСКатегориями.ДатаВыхода,
	|	ВТРаботникиСКатегориями.ПодразделениеОрганизации,
	|	ВТРаботникиСКатегориями.Должность,
	//|	ВТРаботникиСКатегориями.ДолжностьПодразделения,
	//|	ВТРаботникиСКатегориями.ЭтоРазряд,
	//|	ВТРаботникиСКатегориями.РазрядПоЕТС,
	//|	ВТРаботникиСКатегориями.РазрядИЛИКатегория,
	|	ВЫБОР
	|		КОГДА ФИОФизЛиц.ФизЛицо ЕСТЬ NULL 
	|				ИЛИ ФИОФизЛиц.Фамилия = """"
	|			ТОГДА ВТРаботникиСКатегориями.Сотрудник.Физлицо.Наименование
	|		ИНАЧЕ ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество
	|	КОНЕЦ КАК ФИО,
	|	ВТРаботникиСКатегориями.Сотрудник.Физлицо КАК Физлицо,
	|	ВТРаботникиСКатегориями.Сотрудник.Физлицо.Пол КАК Пол,
	|	ВТРаботникиСКатегориями.ЗанимаемыхСтавок
	|ИЗ
	|	ВТРаботникиСКатегориями КАК ВТРаботникиСКатегориями
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТРаботникиСКатегориями.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(ФИОФизЛиц.Период) КАК Период
	|		ИЗ
	|			ВТРаботникиСКатегориями КАК ВТРаботникиСКатегориями
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|				ПО ВТРаботникиСКатегориями.Сотрудник.Физлицо = ФИОФизЛиц.ФизЛицо
	|					И ВТРаботникиСКатегориями.ДатаВыхода >= ФИОФизЛиц.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВТРаботникиСКатегориями.Сотрудник) КАК МаксимальныеДатыФИО
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|			ПО МаксимальныеДатыФИО.Сотрудник.Физлицо = ФИОФизЛиц.ФизЛицо
	|				И МаксимальныеДатыФИО.Период = ФИОФизЛиц.Период
	|		ПО ВТРаботникиСКатегориями.Сотрудник = МаксимальныеДатыФИО.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТРаботникиСКатегориями.ПодразделениеОрганизации.Наименование,
	|	ФИО";	
	Возврат Текст;
КонецФункции 

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда
		
		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
		
		//проверка на константу
		//ТзПоПраздничным			    = СформироватьЗапросПоРаботеВПраздники().Выгрузить();
		//ТзПоПраздничным.Свернуть("Сотрудник","КоличествоДней");
		//НормаВыходныхДней = ?(ПРОФИТ_ХьюменСистем.обПустоеЗначение(ПРОФИТ_ХьюменСистем.обПолучитьЗначениеКонстантыОбщие(Перечисления.Константы.НормаВыходныхДней, КонецДня(Дата))),0,ПРОФИТ_ХьюменСистем.обПолучитьЗначениеКонстантыОбщие(Перечисления.Константы.НормаВыходныхДней));
		//Если НормаВыходныхДней = 0 Тогда
		//		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено значение константы ""Норма выходных дней"" ");
		//КонецЕсли;	
		
		//Для Каждого СтрокаТЗ Из ТзПоПраздничным Цикл 
		//	КолДнейПоСотрудникуВДокументе = РаботникиОрганизации.НайтиСтроки(Новый Структура("Сотрудник",СтрокаТЗ.Сотрудник)).Количество();
		//	Если СтрокаТЗ.КоличествоДней + КолДнейПоСотрудникуВДокументе > НормаВыходныхДней Тогда 
		//		ОбщегоНазначения.СообщитьОбОшибке("Количество отработанных праздничных дней с начала года " + (СтрокаТЗ.КоличествоДней + КолДнейПоСотрудникуВДокументе) +" превышает установленную константу ""Норма выходных дней"" = " + НормаВыходныхДней +"! Сотрудник " + СокрЛП(СтрокаТЗ.Сотрудник), Отказ, Заголовок);			
		//	КонецЕсли;
		//КонецЦикла; 
		
		ТекстСотрудников = "";
		ВыборкаПроверки = СформироватьЗапросДляПроверки().Выбрать();
		
		Пока ВыборкаПроверки.Следующий() Цикл 
			// выведем необходимые сообщения
			Если ПроверитьИПредупредитьОДвойныхВыгодах(ВыборкаПроверки) Тогда 
				ФИО = УчетВремениСвэй.ПредставлениеРаботника(ВыборкаПроверки.Сотрудник.Физлицо, 3, );
				ТекстСотрудников = ТекстСотрудников + ?(ПустаяСтрока(ТекстСотрудников),"",", ") + СокрЛП(ФИО);
			КонецЕсли;
		КонецЦикла;
		Если Не ПустаяСтрока(ТекстСотрудников) Тогда 
			#Если ТолстыйКлиентОбычноеПриложение Тогда
				Если Вопрос("Вы действительно назначаете доплату и день отдыха " + ТекстСотрудников + "! Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда 
					Отказ = Истина;
				КонецЕсли;
			#КонецЕсли
		КонецЕсли;
		
		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда
			
			// получим реквизиты табличной части
			ВыборкаПоСостояниям = СформироватьЗапросПоСостояниям(ВыборкаПоШапкеДокумента).Выбрать();
			
			Пока ВыборкаПоСостояниям.Следующий() Цикл
				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиСостояния(ВыборкаПоШапкеДокумента, ВыборкаПоСостояниям, Отказ, Заголовок);
				
				Если НЕ Отказ Тогда
					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоСостояниям);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;

	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	УчетВремениСвэй.УстановитьПоследнегоОтветственногоПоОбъекту(ЭтотОбъект);
	Если РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда	
		КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(РаботникиОрганизации);
	КонецЕсли;
	
	Движения.СостояниеРаботниковОрганизаций.Записывать = Истина;

КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УчетВремениСвэй.УстановитьПоследнееЗначениеСчетчика(ЭтотОбъект);

КонецПроцедуры // ПриЗаписи()

Функция СформироватьЗапросПоСостояниям_2()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоГода(Дата));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Дата));

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник
	|ПОМЕСТИТЬ ВТСотрудникиДокумента
	|ИЗ
	|	Документ.ПриказНаРаботуВПраздничныеИВыходныеДни.РаботникиОрганизации КАК ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации
	|ГДЕ
	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Ссылка = &ДокументСсылка";
	Запрос.Выполнить();
	
	УчетВремениСвэй.ОбработатьСостоянияРаботниковОрганизаций(Запрос, "ВЫБРАТЬ ВТСотрудникиДокумента.Сотрудник КАК Сотрудник	ИЗ ВТСотрудникиДокумента");
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВТСостояниеСотрудников.Состояние,
	|	ВТСостояниеСотрудников.Сотрудник,
	|	ВТСостояниеСотрудников.ПериодНачало,
	|	ВТСостояниеСотрудников.ПериодКонец,
	|	РАЗНОСТЬДАТ(ВТСостояниеСотрудников.ПериодНачало, ВТСостояниеСотрудников.ПериодКонец, ДЕНЬ) + 1 КАК КоличествоДней
	|ИЗ
	|	ВТСостояниеСотрудников КАК ВТСостояниеСотрудников
	|ГДЕ
	|	ВТСостояниеСотрудников.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.РаботаВПраздничныеИВыходныеДни))";
	
	Возврат Запрос.Выполнить();
	
КонецФункции


//Функция СформироватьЗапросПоРаботеВПраздники()
//	
//	Запрос = Новый Запрос;
//	//Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
//	// Установим параметры запроса
//	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);
//	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Дата));
//	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
//	Запрос.УстановитьПараметр("Организация", Организация);

//	Запрос.Текст = 
//	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
//	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник
//	|ПОМЕСТИТЬ ВТСотрудникиДокумента
//	|ИЗ
//	|	Документ.ПриказНаРаботуВПраздничныеИВыходныеДни.РаботникиОрганизации КАК ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации
//	|ГДЕ
//	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Ссылка = &ДокументСсылка
//	|
//	|СГРУППИРОВАТЬ ПО
//	|	ПриказНаРаботуВПраздничныеИВыходныеДниРаботникиОрганизации.Сотрудник
//	|;
//	|
//	|////////////////////////////////////////////////////////////////////////////////
//	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
//	|	СостояниеРаботниковОрганизаций.Сотрудник,
//	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СостояниеРаботниковОрганизаций.Период) КАК КоличествоДней
//	|ПОМЕСТИТЬ ВТРаботвВПразники
//	|ИЗ
//	|	РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботниковОрганизаций
//	|ГДЕ
//	|	СостояниеРаботниковОрганизаций.Организация = &Организация
//	|	И СостояниеРаботниковОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
//	|	И СостояниеРаботниковОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.РаботаВПраздничныеИВыходныеДни)
//	|
//	|СГРУППИРОВАТЬ ПО
//	|	СостояниеРаботниковОрганизаций.Сотрудник
//	|;
//	|
//	|////////////////////////////////////////////////////////////////////////////////
//	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
//	|	ВТРаботвВПразники.Сотрудник,
//	|	ЕСТЬNULL(ВТРаботвВПразники.КоличествоДней, 0) КАК КоличествоДней
//	|ИЗ
//	|	ВТСотрудникиДокумента КАК ВТСотрудникиДокумента
//	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРаботвВПразники КАК ВТРаботвВПразники
//	|		ПО ВТСотрудникиДокумента.Сотрудник = ВТРаботвВПразники.Сотрудник";
//	
//	Возврат Запрос.Выполнить();
//	
//	
//КонецФункции

