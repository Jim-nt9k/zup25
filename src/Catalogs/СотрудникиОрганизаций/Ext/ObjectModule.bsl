
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

// Формирует запрос по документу
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросДляПечатиТрудовогоДоговора()
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("СправочникСсылка", Ссылка);
	Запрос.УстановитьПараметр("Руководитель",	Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Запрос.УстановитьПараметр("ДатаДокумента",	 ДатаДоговора);
	Запрос.УстановитьПараметр("ВидАдресаРегистрации" , Справочники.ВидыКонтактнойИнформации.ЮрАдресФизЛица);
	Запрос.УстановитьПараметр("ВидТелефонаДомашний" , Справочники.ВидыКонтактнойИнформации.ТелефонФизЛица);
	Запрос.УстановитьПараметр("ВидАдресаОрганизации" , Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	Запрос.УстановитьПараметр("ВидТелефонаОрганизации" , Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	Запрос.УстановитьПараметр("ОсновноеМестоРаботы",	Перечисления.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы);
	
	//СВЭЙ 15.03.2024 КВВ СВФР-012643 (в тексте запроса ниже было изменено везде "ТрудовойДоговор" на "ТрудДог". Был конфликт имен)
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК ДолжностьРуководителя,
	|	ТрудДог.НомерДоговора КАК НомерДок,
	|	ТрудДог.ДатаДоговора КАК ДатаДок,
	|	ВЫРАЗИТЬ(ТрудДог.Организация.НаименованиеПолное КАК СТРОКА(300)) КАК ПолноеНазваниеОрганизации,
	|	ЕСТЬNULL(ФИООтветственныхЛиц.Фамилия + "" "" + ФИООтветственныхЛиц.Имя + "" "" + ФИООтветственныхЛиц.Отчество, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
	|	ЕСТЬNULL(ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество, ТрудДог.Физлицо.Наименование) КАК ФИОРаботника,
	|	ТрудДог.ДатаОкончания КАК ДатаУвольнения,
	|	ТрудДог.ДатаНачала КАК ДатаПриема,
	|	ТрудДог.Должность.Наименование КАК Должность,
	|	ТрудДог.ЗанимаемыхСтавок,
	|	ТрудДог.ИспытательныйСрок,
	|	ТрудДог.ПодразделениеОрганизации.Наименование КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТрудДог.ВидЗанятости = &ОсновноеМестоРаботы
	|			ТОГДА ""основной работе""
	|		ИНАЧЕ ""совместительству""
	|	КОНЕЦ КАК ВидЗанятости,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументДатаВыдачи,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан1.Наименование КАК ДокументКемВыдан, //Островская Татьяна 26.03.2014 № 17191
	|	АдресРегистрации.Представление КАК АдресРегистрации,
	|	ТелефонДомашний.Представление КАК Телефоны,
	|	КонтактнаяИнформация.Представление КАК АдресОрганизации,
	|	ТелефонОрганизации.Представление КАК ТелефоныОрганизации,
	|	ТрудДог.Организация.Наименование КАК НазваниеОрганизации,
	|	ТрудДог.Организация.ИНН + ""/"" + ТрудДог.Организация.КПП КАК ИНН,
	|	ТрудДог.ГрафикРаботы.ВидГрафика КАК ВидГрафика,
	|	ТрудДог.ГрафикРаботы.ДлительностьРабочейНедели КАК ДлительностьРабочейНедели,
	|	ТрудДог.ВидРасчета,
	|	ТрудДог.ТарифнаяСтавка,
	|	ТрудДог.ВалютаТарифнойСтавки.Наименование КАК ВалютаТарифнойСтавки,
	|	ТрудДог.ПерсональныеНадбавки.(
	|		Ссылка,
	|		НомерСтроки,
	|		Надбавка КАК Надбавка,
	|		Показатель1
	|	)
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК ТрудДог
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаДокумента, ОтветственноеЛицо = &Руководитель) КАК ОтветственныеЛицаОрганизацийСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИООтветственныхЛиц
	|			ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИООтветственныхЛиц.ФизЛицо
	|		ПО ТрудДог.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизЛиц
	|		ПО ТрудДог.Физлицо = ФИОФизЛиц.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ПаспортныеДанныеФизЛицСрезПоследних
	|		ПО ТрудДог.Физлицо = ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресРегистрации
	|		ПО ТрудДог.Физлицо = АдресРегистрации.Объект
	|			И (АдресРегистрации.Вид = &ВидАдресаРегистрации)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК ТелефонДомашний
	|		ПО ТрудДог.Физлицо = ТелефонДомашний.Объект
	|			И (АдресРегистрации.Вид = &ВидТелефонаДомашний)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ПО ТрудДог.Организация = КонтактнаяИнформация.Объект
	|			И (КонтактнаяИнформация.Вид = &ВидАдресаОрганизации)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК ТелефонОрганизации
	|		ПО ТрудДог.Организация = ТелефонОрганизации.Объект
	|			И (ТелефонОрганизации.Вид = &ВидТелефонаОрганизации)
	|ГДЕ
	|	ТрудДог.Ссылка = &СправочникСсылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросДляПечати()

///////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Функция формирует уникальный номер трудового договора
// уникальность в пределах года
// 
Функция ПолучитьНомерТрудовогоДоговора() Экспорт
	
	Если Организация.Пустая() Тогда
		Возврат "0000000001";
	КонецЕсли;
	
	Если ВидДоговора <> Перечисления.ВидыДоговоровСФизЛицами.ТрудовойДоговор и ВидДоговора <> Перечисления.ВидыДоговоровСФизЛицами.Контракт Тогда
		Возврат НомерДоговора;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("парамОрганизация",Организация);
	Запрос.УстановитьПараметр("парамНачалоГода" ,НачалоГода(НачалоДня(ДатаДоговора)));
	Запрос.УстановитьПараметр("парамКонецГода"  ,КонецГода(КонецДня(ДатаДоговора)));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МАКСИМУМ(СотрудникиОрганизаций.НомерДоговора) КАК НомерДок
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|ГДЕ
	|	СотрудникиОрганизаций.Организация = &парамОрганизация
	|	И СотрудникиОрганизаций.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.ТрудовойДоговор)
	|	И СотрудникиОрганизаций.ДатаДоговора МЕЖДУ &парамНачалоГода И &парамКонецГода";
	
	Запрос.Текст = ТекстЗапроса;
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Организация.Префикс + "0000001";
	Иначе
		
		СтрокаРезультата = Запрос.Выполнить().Выгрузить()[0];
		Если НЕ ЗначениеЗаполнено(СтрокаРезультата.НомерДок) Тогда
			Возврат Организация.Префикс + "0000001";
		Иначе
			Возврат ПроцедурыУправленияПерсоналом.ПолучитьСледующийНомер(СокрП(СтрокаРезультата.НомерДок));
		КонецЕсли;
		
	КонецЕсли;
	
	
КонецФункции // ПолучитьНомерТрудовогоДоговора()

Процедура ПроверитьНомерТрудовогоДоговора(НачальнаяДатаДокумента) Экспорт
	
	//определяем разность старой и новой даты договора
	РазностьДат = НачалоГода(НачальнаяДатаДокумента) - НачалоГода(ДатаДоговора);
	
	Если РазностьДат <> 0 Тогда
		НомерДоговора = ПолучитьНомерТрудовогоДоговора();
	КонецЕсли;
	
КонецПроцедуры // РаботаСДиалогами.ПроверитьНомерДокумента() 

// Функция формирует очередной табельный номер сотрудника
// уникальность в пределах организации и вида договора
// Возвращаемое значение:
//   Строка   – табельный номер
//
Функция ПолучитьОчереднойТабельныйНомер() Экспорт
	
	Если Не ЗначениеЗаполнено(ВидДоговора) Тогда
		Возврат "";
	КонецЕсли;
	
	Префикс = "";
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("парамОрганизация",Организация);
	парамВидДоговора = Новый Массив;
	Если ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.ТрудовойДоговор Тогда
		парамВидДоговора.Добавить(Перечисления.ВидыДоговоровСФизЛицами.ТрудовойДоговор);
	ИначеЕсли ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.Контракт Тогда
		парамВидДоговора.Добавить(Перечисления.ВидыДоговоровСФизЛицами.Контракт);	
	ИначеЕсли ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.ДоговорУправленческий Тогда
		парамВидДоговора.Добавить(Перечисления.ВидыДоговоровСФизЛицами.ДоговорУправленческий);
	Иначе
		Префикс = "д";
		парамВидДоговора.Добавить(Перечисления.ВидыДоговоровСФизЛицами.Авторский);
		парамВидДоговора.Добавить(Перечисления.ВидыДоговоровСФизЛицами.Подряда);
	КонецЕсли;
	Запрос.УстановитьПараметр("парамВидДоговора",парамВидДоговора);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Код КАК Код
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|ГДЕ
	|	СотрудникиОрганизаций.Организация = &парамОрганизация
	|	И СотрудникиОрганизаций.ВидДоговора В(&парамВидДоговора)
	|	И СотрудникиОрганизаций.Ссылка <> &Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	РезультатаЗапроса = Запрос.Выполнить();
	ТЗ = РезультатаЗапроса.Выгрузить();
	ТЗ.Колонки.Добавить("КодЧисло", Новый ОписаниеТипов("Число"));
	Для Каждого НоваяСтр из ТЗ Цикл
		НоваяСтр.КодЧисло = Формат(Число(НоваяСтр.Код),"ЧГ=0");
	КонецЦикла;
	ТЗ.Сортировать("КодЧисло Возр");
	ТЗ.ВыбратьСтроку();
	Если РезультатаЗапроса.Пустой() Тогда
		Возврат ?(ЗначениеЗаполнено(Префикс), Префикс + "000000001", "0000000001");
	Иначе
		СтрокаРезультата = РезультатаЗапроса.Выгрузить()[0];
		Если НЕ ЗначениеЗаполнено(СтрокаРезультата.Код) Тогда
			Возврат ?(ЗначениеЗаполнено(Префикс), Префикс + "000000001", "0000000001");
		Иначе
			Возврат ПроцедурыУправленияПерсоналом.ПолучитьСледующийНомер(СокрП(СтрокаРезультата.Код));
		КонецЕсли;
	КонецЕсли;
	
КонецФункции // ПолучитьОчереднойТабельныйНомер()


#Если Клиент Тогда
	
	// Процедура осуществляет печать документа. Можно направить печать на 
	// экран или принтер, а также распечатать необходмое количество копий.
	//
	//  Название макета печати передается в качестве параметра,
	// по переданному названию находим имя макета в соответствии.
	//
	// Параметры:
	//  НазваниеМакета - строка, название макета.
	//
	Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
		
		// Получить экземпляр документа на печать
		Если ИмяМакета = "Печать" Тогда
			
			ТабДокумент = Новый ТабличныйДокумент;
			ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ТрудовойДоговор_Печать";
			
			// получаем данные для печати
			Выборка = СформироватьЗапросДляПечатиТрудовогоДоговора().Выбрать();
			
			// получаем макет
			Макет = ПолучитьМакет("Макет");
			
			// выводим данные 
			Если Выборка.Следующий() Тогда
				Макет.Параметры.Заполнить(Выборка);
				Макет.Параметры.ИспытательныйСрокСтрокой = ?(НЕ ЗначениеЗаполнено(Выборка.ИспытательныйСрок),"             месяцев", "" + Выборка.ИспытательныйСрок + " месяца(ев)" );
				Макет.Параметры.РежимРаботы = "" + Выборка.ВидГрафика + "; " + Выборка.ДлительностьРабочейНедели + " - часовая рабочая неделя";
				Макет.Параметры.ФормаОплаты = "Форма оплаты: " + Выборка.ВидРасчета + "; Оклад (тариф) = " + Выборка.ТарифнаяСтавка + " ("+Выборка.ВалютаТарифнойСтавки+")";  
				Макет.Параметры.ПолноеНазваниеОрганизации = СокрЛП(Макет.Параметры.ПолноеНазваниеОрганизации);
				Макет.Параметры.ДокументКемВыдан = СокрЛП(Макет.Параметры.ДокументКемВыдан);
				
				ВыборкаПерсональныхНадбавок = Выборка.ПерсональныеНадбавки.Выбрать();
				Если ВыборкаПерсональныхНадбавок.Количество()>0 Тогда
					СтрокаНадбавки = "Персональные надбавки: ";
					Пока ВыборкаПерсональныхНадбавок.Следующий() Цикл
						СтрокаНадбавки = СтрокаНадбавки + ВыборкаПерсональныхНадбавок.Надбавка + "- " + ВыборкаПерсональныхНадбавок.Показатель1 + "; ";
					КонецЦикла; 
					Макет.Параметры.Надбавки = СтрокаНадбавки;
				КонецЕсли; 
			КонецЕсли;
			
			// выводим готовый документ
			ТабДокумент.Вывести(Макет);
			
			Возврат РаботаСДиалогами.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "Трудовой договор");
			//толик		
		ИначеЕсли ИмяМакета = "Начисления" Тогда
			мОтчет = Отчеты.ОтчетПоНачислениямСотрудников.Создать().ПолучитьФорму();
			мОтчет.КонДата = ТекущаяДата();
			мОтчет.Сотрудники.Добавить(ЭтотОбъект.Ссылка);
			мОтчет.Временно = Истина;
			мОтчет.Открыть();
		КонецЕсли;
		//енд
		
	КонецФункции // Печать
	
#КонецЕсли

// Возвращает доступные варианты печати 
//
// Вовращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтркутураПечатныхФорм = Новый Структура;
	СтркутураПечатныхФорм.Вставить("Печать","Трудовой договор");
	//толик
	Если РольДоступна("ОТиЗ") или РольДоступна("ПолныеПрава") или РольДоступна("ПолныеПраваБезЭкономики") Тогда //О.Т. Аверсон 17.03.2017 № 335
		СтркутураПечатныхФорм.Вставить("Начисления","Начисления и удержания");
	КонецЕсли;
	Возврат СтркутураПечатныхФорм;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "Копирование" объекта
Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда
		Физлицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		Наименование = "";
		Если ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.ТрудовойДоговор или ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.Контракт Тогда
			НомерДоговора = ПолучитьНомерТрудовогоДоговора();	
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

// Процедура - обработчик события "Заполнение" объекта
Процедура ОбработкаЗаполнения(Основание)
	
	ТипОснования = ТипЗнч(Основание);
	
	Если ТипОснования = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Физлицо = Основание;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПередЗаписью" объекта
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа И Физлицо.Пустая() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Для сотрудника не задано физическое лицо!", Отказ);
	КонецЕсли;
	
	Если Не ЭтоГруппа И ОбособленноеПодразделение.Пустая() Тогда
		ОбособленноеПодразделение = Организация
	КонецЕсли;
	
	//	Толик несоответстчие имени
	Если Не ЭтотОбъект.ЭтоНовый() и  НЕ ЭтотОбъект.ЭтоГруппа Тогда // №14834 07.02.2012 , Дыма 
		мСотрудник = РегистрыСведений.ФИОФизЛиц.ПолучитьПоследнее(ТекущаяДата(),
		Новый Структура ("ФизЛицо", Физлицо));
		мФИО = мСотрудник.Фамилия+" "+мСотрудник.Имя+?(ЗначениеЗаполнено(мСотрудник.Отчество)," "+мСотрудник.Отчество,"");//О.Т. Аверсон 18.04.2017 № 527
		Если СтрЧислоВхождений(Наименование, мФИО)=0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Несоответствие имени сотрудника и физического лица !", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	//Аверсон 26.04.2021 КВВ СВФР-007501 (
	Если ЗначениеЗаполнено(Организация) И СокрЛП(Организация.ИНН) = "700067572" //Бобруйск
		И (ЗначениеЗаполнено(ЭтотОбъект.ТипРаботника) И ЭтотОбъект.ТипРаботника = Перечисления.ТипыРаботников.Практикант) Тогда 
		СсылкаБезНалогов = Справочники.ВидыНалогообложения.НайтиПоНаименованию("Без налогов");
		Если ЗначениеЗаполнено(СсылкаБезНалогов) Тогда
			Если ЭтотОбъект.ВидНалогообложенияОсновной <> СсылкаБезНалогов Тогда
				ЭтотОбъект.ВидНалогообложенияОсновной = СсылкаБезНалогов;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//Аверсон 26.04.2021 КВВ СВФР-007501 )
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
	//Аверсон 27.05.2022 КВВ СВФР-009095 (
	Если ДополнительнаяПенсия.Количество() > 0 И (РольДоступна("ПолныеПрава") или РольДоступна("ЗП")) Тогда
		//Аверсон 29.09.2022 КВВ СВФР-009506 (
		Если ВидНалогообложенияОсновной = Справочники.ВидыНалогообложения.ПустаяСсылка() Тогда 
			ВидНалогообложения = Организация.ОсновнаяСтавкаНалогообложения;
		иначе
			ВидНалогообложения = ВидНалогообложенияОсновной;
		КонецЕсли;
		
		ТЗГражд = Новый ТаблицаЗначений;
		ТЗГражд.Колонки.Добавить("Период");
		ТЗГражд.Колонки.Добавить("ИнаяСтавка");
		ТЗГражд.Колонки.Добавить("Страна");
		ТЗГражд.Колонки.Добавить("НеИмеетПравоНаПенсию");
		ТЗГражд.Колонки.Добавить("НеЯвляетсяНалоговымРезидентомРФ");
		
		ЗГражд = Новый Запрос;
		ЗГражд.Текст = "ВЫБРАТЬ * 
		|ИЗ РегистрСведений.ГражданствоФизЛиц КАК ГФЛ 
		|ГДЕ ГФЛ.ФизЛицо = &ФЛ";
		ЗГражд.УстановитьПараметр("ФЛ",Ссылка.Физлицо);
		РезЗГражд = ЗГражд.Выполнить().Выгрузить();
		Если РезЗГражд.Количество() > 0 Тогда
			Для Каждого СтрРезЗГражд ИЗ РезЗГражд Цикл
				СтрТЗГражд = ТЗГражд.Добавить();
				СтрТЗГражд.Период = СтрРезЗГражд.Период;
				СтрТЗГражд.ИнаяСтавка = СтрРезЗГражд.ИнаяСтавка;
				СтрТЗГражд.Страна = СтрРезЗГражд.Страна;
				СтрТЗГражд.НеИмеетПравоНаПенсию = СтрРезЗГражд.НеИмеетПравоНаПенсию;
				СтрТЗГражд.НеЯвляетсяНалоговымРезидентомРФ = СтрРезЗГражд.НеЯвляетсяНалоговымРезидентомРФ;
			КонецЦикла;
		КонецЕсли;
		//Аверсон 29.09.2022 КВВ СВФР-009506 )
		
		НаборЗаписейДопПенсия = РегистрыСведений.ДополнительнаяПенсияРаботодательРаботник.СоздатьНаборЗаписей();
		
		НаборЗаписейДопПенсия.Отбор.Сотрудник.Установить(Ссылка);
		Для Каждого Стр ИЗ ДополнительнаяПенсия Цикл
			
			//Аверсон 27.09.2022 КВВ СВФР-009506 (
			ЗИнв = Новый Запрос;
			ЗИНв.Текст = "ВЫБРАТЬ
			|Инв.ГруппаИнвалидности
			|ИЗ РегистрСведений.СведенияОбИнвалидностиФизлиц.СрезПоследних(&Период) КАК Инв
			|ГДЕ Инв.ФизЛицо = &ФЛ";
			ЗИнв.УстановитьПараметр("Период",Стр.ДатаИзменения);
			ЗИнв.УстановитьПараметр("ФЛ",Ссылка.Физлицо);
			РезЗИнв = ЗИнв.Выполнить().Выгрузить();
			Если РезЗИнв.Количество() > 0 Тогда
				Если СокрЛП(РезЗИнв[0].ГруппаИнвалидности)="II" ИЛИ СокрЛП(РезЗИнв[0].ГруппаИнвалидности)="I" Тогда
					Сообщить("Дополнительная пенсия не начисляется инвалидам I и II группы!" + Символы.ПС + "Внесите изменения в табличную часть доп.пенсии и повторите попытку записи элемента справочника.");
					Отказ = Истина;
					Прервать;
				КонецЕсли;
			КонецЕсли;
			//Аверсон 27.09.2022 КВВ СВФР-009506 )
			
			НоваяЗапись = НаборЗаписейДопПенсия.Добавить(); 
			НоваяЗапись.Сотрудник = Ссылка; 
			НоваяЗапись.ФизЛицо = Ссылка.Физлицо; //Аверсон 03.10.2022 КВВ СВФР-009604
			НоваяЗапись.Период = Стр.ДатаИзменения; 
			НоваяЗапись.ПроцентРаботника = Стр.ПроцентРаботника;
			НоваяЗапись.ПроцентРаботодателя = Стр.ПроцентРаботодателя;
			
			//Аверсон 29.09.2022 КВВ СВФР-009506 (
			СтавкаФСЗН = РегистрыСведений.СтавкиОтчисленийСФОТиЗП.ПолучитьПоследнее(НоваяЗапись.Период, 
			Новый Структура("Организация,ВидОтчисления,ВидНалогообложения", Организация,Перечисления.ВидыОтчисленийСФОТиЗП.ФСЗН,ВидНалогообложения)).Размер;
			
			ИнаяСтавкаНАДОВГражданство = ?(НоваяЗапись.ПроцентРаботодателя=0,0,СтавкаФСЗН - НоваяЗапись.ПроцентРаботодателя);
			РегСвГрФЛ = РегистрыСведений.ГражданствоФизЛиц.ПолучитьПоследнее(НоваяЗапись.Период,Новый Структура("ФизЛицо",НоваяЗапись.Сотрудник.Физлицо));
			
			ЗГражд = Новый Запрос;
			ЗГражд.Текст = "ВЫБРАТЬ * 
			|ИЗ РегистрСведений.ГражданствоФизЛиц КАК ГФЛ 
			|ГДЕ ГФЛ.ФизЛицо = &ФЛ
			|И НАЧАЛОПЕРИОДА(ГФЛ.Период,ДЕНЬ) = &Период";
			ЗГражд.УстановитьПараметр("ФЛ",Ссылка.Физлицо);
			ЗГражд.УстановитьПараметр("Период",НачалоДня(Стр.ДатаИзменения));
			РезЗГраж = ЗГражд.Выполнить().Выгрузить();
			Если РезЗГраж.Количество() = 0 Тогда
				СтрТЗГражд = ТЗГражд.Добавить();
				СтрТЗГражд.Период = Стр.ДатаИзменения;
				СтрТЗГражд.ИнаяСтавка = ИнаяСтавкаНАДОВГражданство;
				СтрТЗГражд.Страна = РегСвГрФЛ.Страна;
				СтрТЗГражд.НеИмеетПравоНаПенсию = РегСвГрФЛ.НеИмеетПравоНаПенсию;
				СтрТЗГражд.НеЯвляетсяНалоговымРезидентомРФ = РегСвГрФЛ.НеЯвляетсяНалоговымРезидентомРФ;
			Иначе
				ИнаяСтавкаИзЗапроса = РезЗГраж[0].ИнаяСтавка;
				Если ИнаяСтавкаИзЗапроса <> ИнаяСтавкаНАДОВГражданство Тогда
					_Страна_ = РезЗГраж[0].Страна;
					_НеИмеетПравоНаПенсию_ = РезЗГраж[0].НеИмеетПравоНаПенсию;
					_НеЯвляетсяНалоговымРезидентомРФ_ = РезЗГраж[0].НеЯвляетсяНалоговымРезидентомРФ;
					
					Отбор = Новый Структура;
					Отбор.Вставить("Период", Стр.ДатаИзменения);
					Строки = ТЗГражд.НайтиСтроки(Отбор);
					Для Каждого Стрр Из Строки Цикл
						Стрр.Период = Стр.ДатаИзменения;
						Стрр.ИнаяСтавка = ИнаяСтавкаНАДОВГражданство;
						Стрр.Страна = _Страна_;
						Стрр.НеИмеетПравоНаПенсию = _НеИмеетПравоНаПенсию_;
						Стрр.НеЯвляетсяНалоговымРезидентомРФ = _НеЯвляетсяНалоговымРезидентомРФ_;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			//Аверсон 29.09.2022 КВВ СВФР-009506 )
		КонецЦикла;
		
		НаборЗаписейДопПенсия.Записать(Истина);
		
		//Аверсон 29.09.2022 КВВ СВФР-009506 (
		Если ТЗГражд.Количество() > 0  Тогда			
			НаборЗаписейГ = РегистрыСведений.ГражданствоФизЛиц.СоздатьНаборЗаписей();
			НаборЗаписейГ.Отбор.ФизЛицо.Установить(Ссылка.Физлицо);
			НаборЗаписейГ.Записать();
			
			НаборЗаписейГраж = РегистрыСведений.ГражданствоФизЛиц.СоздатьНаборЗаписей();
			НаборЗаписейГраж.Отбор.ФизЛицо.Установить(Ссылка.Физлицо);
			Для Каждого СтрТЗГражд ИЗ ТЗГражд Цикл
				НоваяЗаписьГраж = НаборЗаписейГраж.Добавить(); 
				НоваяЗаписьГраж.ФизЛицо = Ссылка.Физлицо; 
				НоваяЗаписьГраж.Период = СтрТЗГражд.Период;
				НоваяЗаписьГраж.ИнаяСтавка = СтрТЗГражд.ИнаяСтавка;
				НоваяЗаписьГраж.Страна = СтрТЗГражд.Страна;
				НоваяЗаписьГраж.НеИмеетПравоНаПенсию = СтрТЗГражд.НеИмеетПравоНаПенсию;
				НоваяЗаписьГраж.НеЯвляетсяНалоговымРезидентомРФ = СтрТЗГражд.НеЯвляетсяНалоговымРезидентомРФ;
			КонецЦикла;
			НаборЗаписейГраж.Записать(Истина);
		КонецЕсли;
		//Аверсон 29.09.2022 КВВ СВФР-009506 )
		
	КонецЕсли;
	//Аверсон 27.05.2022 КВВ СВФР-009095 )
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
