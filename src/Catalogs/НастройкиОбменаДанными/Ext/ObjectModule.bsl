
Перем мРегламентноеЗадание Экспорт;

//Функция определяет есть ли для данной настройки Автоматический обмен данными
Функция ЭтоНастройкаАвтоматическогоОбменаДанными() Экспорт
	
	Возврат КаждыйЗапускПрограммы 
		ИЛИ КаждоеЗавершениеРаботыСПрограммой
		ИЛИ Не ПустаяСтрока(КаталогПроверкиДоступности);
	
КонецФункции
	
//Функция по настройке определяет нужно ли для нее периодически определять необходимость обмена данными
Функция ОпределитьНеобходимостьПериодическогоОпределенияВыполненияОбменаДанными() Экспорт
	
	Возврат Не ПустаяСтрока(КаталогПроверкиДоступности);
	
КонецФункции

// процедура производит работу с параметрами обмена данными
Процедура ОбработатьНаборЗаписейНастроек()
	
	Если ЗначениеЗаполнено(Ссылка) 
		И ((ПроизводитьОтправкуСообщений <> Ссылка.ПроизводитьОтправкуСообщений)
		ИЛИ (ПроизводитьПриемСообщений <> Ссылка.ПроизводитьОтправкуСообщений)) Тогда
		
		// если изменились флажки выгрузки и загрузки данных, то надо очищать информацию по последнему действию
		НаборЗаписейПараметров = РегистрыСведений.ПараметрыОбменаДанными.СоздатьНаборЗаписей();
		
		НаборЗаписейПараметров.Отбор.НастройкаОбменаДанными.Значение = Ссылка;	
		НаборЗаписейПараметров.Отбор.НастройкаОбменаДанными.Использование = Истина;
		НаборЗаписейПараметров.Отбор.НастройкаОбменаДанными.ВидСравнения = ВидСравнения.Равно;
		
		НаборЗаписейПараметров.Прочитать();
		
		Если НаборЗаписейПараметров.Количество() = 1 Тогда
			
			ЗаписьПараметров = НаборЗаписейПараметров[0];
			
			Если ПроизводитьПриемСообщений <> Ссылка.ПроизводитьПриемСообщений Тогда
				
				ЗаписьПараметров.ДатаПоследнейЗагрузки = Неопределено;	
				ЗаписьПараметров.РезультатПоследнейЗагрузки = Ложь;
				
			КонецЕсли;
			
			Если ПроизводитьОтправкуСообщений <> Ссылка.ПроизводитьОтправкуСообщений Тогда
				
				ЗаписьПараметров.ДатаПоследнейВыгрузки = Неопределено;	
				ЗаписьПараметров.РезультатПоследнейВыгрузки = Ложь;
				
			КонецЕсли;
			
			Попытка
				НаборЗаписейПараметров.Записать();
			Исключение
				#Если Клиент Тогда
					Сообщить("Ошибка при записи изменений в настройку обмена """ + Ссылка + """ : " + ОписаниеОшибки(), СтатусСообщения.Важное);
				#КонецЕсли
			КонецПопытки;
		
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

// при записи
Процедура ПриЗаписи(Отказ) 
	
	// надо проверить наличие одной записи в регистре сведений
	НаборЗаписейПараметров = РегистрыСведений.ПараметрыОбменаДанными.СоздатьНаборЗаписей();
		
	НаборЗаписейПараметров.Отбор.НастройкаОбменаДанными.Значение = Ссылка;	
	НаборЗаписейПараметров.Отбор.НастройкаОбменаДанными.Использование = Истина;
	НаборЗаписейПараметров.Отбор.НастройкаОбменаДанными.ВидСравнения = ВидСравнения.Равно;
		
	НаборЗаписейПараметров.Прочитать();
		
	Если НаборЗаписейПараметров.Количество() = 1 Тогда
		// запись есть, ничего делать не надо
		Возврат;
	КонецЕсли;
	
	// добавляем только одну запись и все
	ЗаписьДатПараметров = НаборЗаписейПараметров.Добавить();
	ЗаписьДатПараметров.НастройкаОбменаДанными = Ссылка;
		
	Попытка
		НаборЗаписейПараметров.Записать();
	Исключение
		#Если Клиент Тогда
			Сообщить("Ошибка при записи изменений в настройку обмена """ + Ссылка + """ : " + ОписаниеОшибки(), СтатусСообщения.Важное);
		#КонецЕсли
	КонецПопытки;	
		
КонецПроцедуры

Функция НайтиРегламентноеЗаданиеПоНастройке() Экспорт
	
	Попытка
		
		Если НЕ ПустаяСтрока(РегламентноеЗадание) Тогда
			
			УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор(РегламентноеЗадание);
			ТекущееРегламентноеЗадание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(УникальныйИдентификаторЗадания);
			
		Иначе
			
			ТекущееРегламентноеЗадание = Неопределено;
			
		КонецЕсли;
		
	Исключение
		
		ТекущееРегламентноеЗадание = Неопределено;
		
    КонецПопытки;
	
	Возврат ТекущееРегламентноеЗадание;
	
КонецФункции


// Перед записью
Процедура ПередЗаписью(Отказ)
			
	Если НЕ ЗначениеЗаполнено(ТипНастройки) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Тип настройки""", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(УзелИнформационнойБазы) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Узел""", Отказ);
	КонецЕсли;
	
	Если ЭтоНастройкаАвтоматическогоОбменаДанными() Тогда
		
		Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Пользователь"". Автоматический обмен по такой настройке невозможен.", Отказ);	
		КонецЕсли;
				
	КонецЕсли;
	
	
	// работа с регламентными заданиями
	Если мРегламентноеЗадание = Неопределено Тогда
		
		мРегламентноеЗадание = НайтиРегламентноеЗаданиеПоНастройке();
			
	КонецЕсли;
	
	Если НЕ ИспользоватьРегламентныеЗадания Тогда
		
		Если мРегламентноеЗадание <> Неопределено Тогда
			мРегламентноеЗадание.Использование = Ложь;			
		КонецЕсли;
			
	Иначе
		
		Если мРегламентноеЗадание = Неопределено Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не выбрано регламентное задание для настройки обмена.", Отказ);
		Иначе
			мРегламентноеЗадание.Использование = Истина;	
		КонецЕсли;
			
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если мРегламентноеЗадание = Неопределено Тогда
		
		РегламентноеЗадание = "";
		
	Иначе	
		
		РегламентноеЗадание = Строка(мРегламентноеЗадание.УникальныйИдентификатор);
		
		мРегламентноеЗадание.Наименование = Наименование;
		// генерируем уникальный ключ, чтобы в один момент времени 2 регламентных задания не выполнялись
		Если ПустаяСтрока(мРегламентноеЗадание.Ключ) Тогда
			мРегламентноеЗадание.Ключ = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		Массив = Новый Массив();
		Массив.Добавить(Код);
		
		мРегламентноеЗадание.Параметры = Массив;
		мРегламентноеЗадание.Записать();	
					
	КонецЕсли;
			
	ОбработатьНаборЗаписейНастроек();
	
	// в зависимости от типа настройки очищаем настройки остальных типов
	Если ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезФайловыйРесурс Тогда
		
		КаталогОбменаИнформацией = СокрЛП(КаталогОбменаИнформацией);
		
		FTPАдресОбмена = "";
		ПользовательFTPСоединения = "";
		ПарольFTPСоединения = "";
		
		УчетнаяЗаписьПриемаОтправкиСообщений = Справочники.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка(); 
		ПочтовыйАдресПолучателя = "";
		АвтоматическиЗакачиватьПочтуПриОбмене = Ложь;
		
	ИначеЕсли ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезFTPРесурс Тогда
		
		Если ПустаяСтрока(ПользовательFTPСоединения) Тогда
			ПарольFTPСоединения = "";	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПортFTPСоединения) Тогда
			ПортFTPСоединения = 21;	
		КонецЕсли;
		
		КаталогОбменаИнформацией = "";
		
		УчетнаяЗаписьПриемаОтправкиСообщений = Справочники.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка();
		ПочтовыйАдресПолучателя = "";
		АвтоматическиЗакачиватьПочтуПриОбмене = Ложь;
		
	ИначеЕсли ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезПочту Тогда
		
		ПочтовыйАдресПолучателя = СокрЛП(ПочтовыйАдресПолучателя);
		
		КаталогОбменаИнформацией = "";
		
		FTPАдресОбмена = "";
		ПользовательFTPСоединения = "";
		ПарольFTPСоединения = "";
				
	КонецЕсли;
		
	КаталогПроверкиДоступности = СокрЛП(КаталогПроверкиДоступности);
			
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	РегламентноеЗадание = "";
	
КонецПроцедуры
