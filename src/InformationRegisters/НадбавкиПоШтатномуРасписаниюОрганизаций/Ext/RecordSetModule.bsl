Процедура ЗаписьПерерасчетов()
	
	Запрос = Новый Запрос;
	
	УсловиеТекст = "";
	Для Каждого ЭлементОтбора из ЭтотОбъект.Отбор Цикл
		Если ЭлементОтбора.Использование Тогда
			Если не ПустаяСтрока(УсловиеТекст) Тогда
				УсловиеТекст = УсловиеТекст + " И ";
			КонецЕсли;
			УсловиеТекст = УсловиеТекст + "Надбавки." + ЭлементОтбора.Имя + " = &" + ЭлементОтбора.Имя;
			Запрос.УстановитьПараметр(ЭлементОтбора.Имя, ЭлементОтбора.Значение);
		КонецЕсли;
		
	КонецЦикла;
	//ПСА 20.05.2021 СВФР-007600 +++
	//Если не ПустаяСтрока(УсловиеТекст) Тогда
	//	УсловиеТекст = "ГДЕ " + УсловиеТекст;
	//КонецЕсли;
	//+++ПСА 20.05.2021 СВФР-007600

	ЗапросТекст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыбранныеЗаписи.Регистратор КАК Регистратор,
	|	ВыбранныеЗаписи.ФизЛицо,
	|	ВыбранныеЗаписи.Сотрудник,
	|	ВыбранныеЗаписи.Организация
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Расчеты.Регистратор КАК Регистратор,
	|		Расчеты.ФизЛицо КАК ФизЛицо,
	|		Расчеты.Сотрудник КАК Сотрудник,
	|		Расчеты.Организация КАК Организация
	|	ИЗ
	|		РегистрСведений.НадбавкиПоШтатномуРасписаниюОрганизаций КАК Надбавки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				РаботникиОрганизации.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|				РаботникиОрганизации.Должность КАК Должность,
	|				РаботникиОрганизации.Сотрудник КАК Сотрудник
	|			ИЗ
	|				РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				Работники.ПодразделениеОрганизацииЗавершения,
	|				Работники.ДолжностьЗавершения,
	|				Работники.Сотрудник
	|			ИЗ
	|				РегистрСведений.РаботникиОрганизаций КАК Работники
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК ПериодыПерекрытия
	|					ПО (ПериодыПерекрытия.Период <= Работники.ПериодЗавершения)
	|						И (ПериодыПерекрытия.Период > Работники.Период)
	|						И (ПериодыПерекрытия.Сотрудник = Работники.Сотрудник)
	|			ГДЕ
	|				Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И ПериодыПерекрытия.Период ЕСТЬ NULL) КАК РаботникиОрганизации
	|			ПО (РаботникиОрганизации.ПодразделениеОрганизации = Надбавки.ПодразделениеОрганизации)
	|				И (РаботникиОрганизации.Должность = Надбавки.Должность)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК Расчеты
	|			ПО (Расчеты.Сотрудник = РаботникиОрганизации.Сотрудник)
	|				И (Расчеты.ПериодДействияНачало >= Надбавки.Период)
	|	//ПСА 20.05.2021 СВФР-007600 +++			
	|	ГДЕ &УсловиеТекста
	|	) КАК ВыбранныеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчеты
	|		ПО (Перерасчеты.ОбъектПерерасчета = ВыбранныеЗаписи.Регистратор)
	|			И (Перерасчеты.ФизЛицо = ВыбранныеЗаписи.ФизЛицо)
	|			И (Перерасчеты.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ПустаяСсылка))
	|ГДЕ
	|	Перерасчеты.ОбъектПерерасчета ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор";
	//ПСА 20.05.2021 СВФР-007600 +++
	Если ЗначениеЗаполнено(УсловиеТекст) Тогда
		ЗапросТекст =  СтрЗаменить(ЗапросТекст,"&УсловиеТекста", УсловиеТекст);
	Иначе
		ЗапросТекст =  СтрЗаменить(ЗапросТекст,"&УсловиеТекста", "Истина");
	КонецЕсли;	
	//+++ПСА 20.05.2021 СВФР-007600 

	Запрос.Текст = ЗапросТекст;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПроведениеРасчетов.ДописатьПерерасчетыОсновныхНачислений(Выборка);
	
КонецПроцедуры

