Перем мПодразделенияДокументы Экспорт;  // соответствие подразделений и документов перерасчета
Перем мРаботникиДокументы Экспорт; // соответствие работников и документов перерасчета

// Заполняет список физлиц по которым необходимо пересчитать зарплату по заданной организации
// Параметры: 
//	Организация - ссылка на организацию
// Возвращаемое значение:
//	Нет
//
Процедура ЗаполнитьДанныеПоОрганизации() Экспорт
	
	мПодразделенияДокументы.Очистить();
	мРаботникиДокументы.Очистить();
	
	СписокПодразделений.Очистить();
	СписокРаботников.Очистить();
	
	// пока не выбрана организация ничего не делаем
	Если Организация.Пустая() Тогда
		СписокДокументов.Очистить();
		Возврат;
	КонецЕсли;
	
	//запомним какие документы отмечены в списке документов
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Пометка", Истина);
	ТаблицаДокументов = СписокДокументов.Выгрузить();
	МассивСтрок = ТаблицаДокументов.НайтиСтроки(СтруктураПоиска);
	СписокПомеченныхДокументов = ТаблицаДокументов.Скопировать(МассивСтрок);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",	Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("ТекущийПериод",	ТекущийПериод);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПерерасчетОсновныхНачислений.ОбъектПерерасчета КАК Документ,
	|	ПерерасчетОсновныхНачислений.ОбъектПерерасчета.ПериодРегистрации КАК ПериодРегистрации
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК ПерерасчетОсновныхНачислений
	|ГДЕ
	|	ПерерасчетОсновныхНачислений.ОбъектПерерасчета.Организация = &Организация
	|	И ПерерасчетОсновныхНачислений.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПерерасчетДополнительныхНачислений.ОбъектПерерасчета,
	|	ПерерасчетДополнительныхНачислений.ОбъектПерерасчета.ПериодРегистрации
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК ПерерасчетДополнительныхНачислений
	|ГДЕ
	|	ПерерасчетДополнительныхНачислений.ОбъектПерерасчета.Организация = &Организация
	|	И ПерерасчетДополнительныхНачислений.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПерерасчетСреднегоЗаработка.ОбъектПерерасчета,
	|	ПерерасчетСреднегоЗаработка.ОбъектПерерасчета.ПериодРегистрации
	|ИЗ
	|	РегистрРасчета.РасчетСреднегоЗаработка.ПерерасчетСреднегоЗаработка КАК ПерерасчетСреднегоЗаработка
	|ГДЕ
	|	ПерерасчетСреднегоЗаработка.ОбъектПерерасчета.Организация = &Организация
	|	И ПерерасчетСреднегоЗаработка.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗаполнениеПлановыхНачислений.ОбъектЗаполнения,
	|	ЗаполнениеПлановыхНачислений.ПериодРегистрации
	|ИЗ
	|	РегистрСведений.ЗаполнениеПлановыхНачислений КАК ЗаполнениеПлановыхНачислений
	|ГДЕ
	|	ЗаполнениеПлановыхНачислений.ПериодРегистрации < &ТекущийПериод
	|	И ЗаполнениеПлановыхНачислений.ОбособленноеПодразделение = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодРегистрации";
	
	СписокДокументов.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если СписокПомеченныхДокументов.Количество() > 0 Тогда
		
		МассивДокументов = СписокПомеченныхДокументов.ВыгрузитьКолонку("Документ");
		Для каждого ДокументСсылка Из МассивДокументов Цикл
			СтрокаСписка = СписокДокументов.Найти(ДокументСсылка, "Документ");
			Если Не СтрокаСписка = Неопределено Тогда
				СтрокаСписка.Пометка = Истина;
			КонецЕсли;	
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

// перерассчитать помеченные документы,
// в порядке от самого старого до самого нового.
Процедура Перерассчитать() Экспорт
	
	ИндексСтрокиТаблицы = СписокДокументов.Количество();
	Пока ИндексСтрокиТаблицы > 0 Цикл
		ИндексСтрокиТаблицы = ИндексСтрокиТаблицы - 1;
		
		СтрокаТаблицы = СписокДокументов[ИндексСтрокиТаблицы];
		Если СтрокаТаблицы.Пометка Тогда
			ДокументОбъект = СтрокаТаблицы.Документ.ПолучитьОбъект();
			Попытка
				ДокументОбъект.Перерассчитать();
			Исключение
				Сообщить("Документ " + Строка(ДокументОбъект) + " не может быть перерассчитан!
				|" + ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	ЗаполнитьДанныеПоОрганизации();
	
КонецПроцедуры

// удалить сведения о перерасчете помеченных документов
Процедура УдалитьСведенияОПерерасчете() Экспорт
	
	ПерерасчетыПоДокументам = РегистрыРасчета.ОсновныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетОсновныхНачислений.СоздатьНаборЗаписей();
	ПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.Использование = Истина;
	ПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.ВидСравнения = ВидСравнения.Равно;
	
	ДопПерерасчетыПоДокументам = РегистрыРасчета.ДополнительныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетДополнительныхНачислений.СоздатьНаборЗаписей();
	ДопПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.Использование = Истина;
	ДопПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.ВидСравнения = ВидСравнения.Равно;
	
	СредПерерасчетыПоДокументам = РегистрыРасчета.РасчетСреднегоЗаработка.Перерасчеты.ПерерасчетСреднегоЗаработка.СоздатьНаборЗаписей();
	СредПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.Использование = Истина;
	СредПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.ВидСравнения = ВидСравнения.Равно;
	
	НаборПерезаполнения = РегистрыСведений.ЗаполнениеПлановыхНачислений.СоздатьНаборЗаписей();
	НаборПерезаполнения.Отбор.ОбъектЗаполнения.Использование = Истина;
	НаборПерезаполнения.Отбор.ОбъектЗаполнения.ВидСравнения = ВидСравнения.Равно;
	
	Для Каждого СтрокаТаблицы Из СписокДокументов Цикл
		Если СтрокаТаблицы.Пометка Тогда
			ТипРегистратора = ТипЗнч(СтрокаТаблицы.Документ);
			Если ПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.ТипЗначения.СодержитТип(ТипРегистратора) тогда
				ПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.Значение = СтрокаТаблицы.Документ;
				ПерерасчетыПоДокументам.Записать(Истина);
			КонецЕсли;
			Если ДопПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.ТипЗначения.СодержитТип(ТипРегистратора) тогда
				ДопПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.Значение = СтрокаТаблицы.Документ;
				ДопПерерасчетыПоДокументам.Записать(Истина);
			КонецЕсли;
			Если СредПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.ТипЗначения.СодержитТип(ТипРегистратора) тогда
				СредПерерасчетыПоДокументам.Отбор.ОбъектПерерасчета.Значение = СтрокаТаблицы.Документ;
				СредПерерасчетыПоДокументам.Записать(Истина);
			КонецЕсли;
			Если НаборПерезаполнения.Отбор.ОбъектЗаполнения.ТипЗначения.СодержитТип(ТипРегистратора) Тогда
				НаборПерезаполнения.Отбор.ОбъектЗаполнения.Значение = СтрокаТаблицы.Документ;
				НаборПерезаполнения.Записать(Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьДанныеПоОрганизации();
	
КонецПроцедуры

// заполняет табличную часть СписокПодразделений и соответсвие мПодразделенияДокументы 
// предназначенные для выполнения команды "отметить по подразделениеям"
Процедура ЗаполнитьСписокПодразделений() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТекущийПериод", ТекущийПериод);
	Запрос.УстановитьПараметр("Организация", Организация);

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Перерасчет.ФизЛицо КАК ФизЛицо,
	|	Перерасчет.ОбъектПерерасчета КАК Документ
	|ПОМЕСТИТЬ ВТСписокФизлиц
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Перерасчет.ФизЛицо,
	|	Перерасчет.ОбъектПерерасчета КАК Документ
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Перерасчет.Физлицо,
	|	Перерасчет.ОбъектПерерасчета КАК Документ
	|ИЗ
	|	РегистрРасчета.РасчетСреднегоЗаработка.ПерерасчетСреднегоЗаработка КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаполнениеПлановыхНачислений.Сотрудник.Физлицо,
	|	ЗаполнениеПлановыхНачислений.ОбъектЗаполнения КАК Документ
	|ИЗ
	|	РегистрСведений.ЗаполнениеПлановыхНачислений КАК ЗаполнениеПлановыхНачислений
	|ГДЕ
	|	ЗаполнениеПлановыхНачислений.ПериодРегистрации < &ТекущийПериод
	|	И ЗаполнениеПлановыхНачислений.ОбособленноеПодразделение = &Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Перерасчеты.Документ КАК Документ,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &ТекущийПериод
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ПодразделениеОрганизацииЗавершения.Наименование
	|		ИНАЧЕ Работники.ПодразделениеОрганизации.Наименование
	|	КОНЕЦ КАК ПодразделениеНаименование,
	|	ВЫБОР
	|		КОГДА Работники.ПериодЗавершения <= &ТекущийПериод
	|				И Работники.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА Работники.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ Работники.ПодразделениеОрганизации
	|	КОНЕЦ КАК Подразделение
	|ИЗ
	|	ВТСписокФизлиц КАК Перерасчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ТекущийПериод,
	|				Организация = &Организация
	|					И Сотрудник.Физлицо В
	|						(ВЫБРАТЬ ФизЛицо ИЗ ВТСписокФизлиц)) КАК Работники
	|		ПО Перерасчеты.ФизЛицо = Работники.Сотрудник.Физлицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодразделениеНаименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокументыМассив = мПодразделенияДокументы.Получить(Выборка.Подразделение);
		Если ДокументыМассив = НеОпределено Тогда
			ДокументыМассив = Новый Массив;
			ДокументыМассив.Добавить(Выборка.Документ);
			мПодразделенияДокументы[Выборка.Подразделение] = ДокументыМассив;
			Строка = СписокПодразделений.Добавить();
			Строка.Подразделение = Выборка.Подразделение;
			Строка.ПодразделениеНаименование = Выборка.ПодразделениеНаименование;
		Иначе
			ДокументыМассив.Добавить(Выборка.Документ);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// заполняет табличную часть СписокРаботников и соответсвие мРаботникиДокументы 
// предназначенные для выполнения команды "отметить по подразделениям"
Процедура ЗаполнитьСписокРаботников() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТекущийПериод", ТекущийПериод);
	Запрос.УстановитьПараметр("Организация", Организация);

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Перерасчет.ФизЛицо КАК ФизЛицо,
	|	Перерасчет.ФизЛицо.Наименование КАК ФизЛицоНаименование,
	|	Перерасчет.ОбъектПерерасчета КАК Документ
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Перерасчет.ФизЛицо,
	|	Перерасчет.ФизЛицо.Наименование,
	|	Перерасчет.ОбъектПерерасчета
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Перерасчет.Физлицо,
	|	Перерасчет.Физлицо.Наименование,
	|	Перерасчет.ОбъектПерерасчета
	|ИЗ
	|	РегистрРасчета.РасчетСреднегоЗаработка.ПерерасчетСреднегоЗаработка КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаполнениеПлановыхНачислений.Сотрудник.Физлицо,
	|	ЗаполнениеПлановыхНачислений.Сотрудник.Физлицо.Наименование,
	|	ЗаполнениеПлановыхНачислений.ОбъектЗаполнения
	|ИЗ
	|	РегистрСведений.ЗаполнениеПлановыхНачислений КАК ЗаполнениеПлановыхНачислений
	|ГДЕ
	|	ЗаполнениеПлановыхНачислений.ПериодРегистрации < &ТекущийПериод
	|	И ЗаполнениеПлановыхНачислений.ОбособленноеПодразделение = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизЛицоНаименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокументыМассив = мРаботникиДокументы.Получить(Выборка.Физлицо);
		Если ДокументыМассив = НеОпределено Тогда
			ДокументыМассив = Новый Массив;
			ДокументыМассив.Добавить(Выборка.Документ);
			мРаботникиДокументы[Выборка.Физлицо] = ДокументыМассив;
			Строка = СписокРаботников.Добавить();
			Строка.Работник = Выборка.Физлицо;
			Строка.РаботникНаименование = Выборка.ФизЛицоНаименование;
		Иначе
			ДокументыМассив.Добавить(Выборка.Документ);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

мПодразделенияДокументы = Новый Соответствие;
мРаботникиДокументы = Новый Соответствие;
