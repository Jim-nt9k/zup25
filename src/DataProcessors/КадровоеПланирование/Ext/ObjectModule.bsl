////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ОбновитьРабочиеМестаУпр(Подразделение, Период = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Подразделение",	Подразделение);
	
	Если Период <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаАктуальности",	Период);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровоеПланирование.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА КадровоеПланирование.Подразделение = &Подразделение
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьИерархия,
		|	КадровоеПланирование.Должность,
		|	КадровоеПланирование.Должность.Наименование КАК ДолжностьНаименование,
		|	КадровоеПланирование.Занято,
		|	КадровоеПланирование.Запланировано,
		|	КадровоеПланирование.Запланировано - КадровоеПланирование.Занято КАК Вакантно
		|ИЗ
		|	(ВЫБРАТЬ
		|		КадровоеПланирование.Подразделение КАК Подразделение,
		|		КадровоеПланирование.Должность КАК Должность,
		|		МАКСИМУМ(КадровоеПланирование.Занято) КАК Занято,
		|		МАКСИМУМ(КадровоеПланирование.Запланировано) КАК Запланировано
		|	ИЗ
		|		(ВЫБРАТЬ
		|			КадровыйПланСрезПоследних.Подразделение КАК Подразделение,
		|			КадровыйПланСрезПоследних.Должность КАК Должность,
		|			СУММА(0) КАК Занято,
		|			СУММА(КадровыйПланСрезПоследних.Количество) КАК Запланировано
		|		ИЗ
		|			РегистрСведений.КадровыйПлан.СрезПоследних(
		|					&ДатаАктуальности,
		|					ПодразделениеОрганизации = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)";
		Если Подразделение <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|						И Подразделение В ИЕРАРХИИ (&Подразделение)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|						И Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)) КАК КадровыйПланСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			КадровыйПланСрезПоследних.Подразделение,
		|			КадровыйПланСрезПоследних.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ЗанятыеРабочиеМестаОстатки.Подразделение,
		|			ЗанятыеРабочиеМестаОстатки.Должность.Должность,
		|			СУММА(ЗанятыеРабочиеМестаОстатки.КоличествоОстаток),
		|			СУММА(0)
		|		ИЗ
		|			РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|					&ДатаАктуальности,
		|					Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)";
		Если Подразделение <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|						И Подразделение В ИЕРАРХИИ (&Подразделение)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|																	) КАК ЗанятыеРабочиеМестаОстатки
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ЗанятыеРабочиеМестаОстатки.Подразделение,
		|			ЗанятыеРабочиеМестаОстатки.Должность.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			Вакансии.Подразделение,
		|			Вакансии.Должность,
		|			СУММА(0),
		|			СУММА(1)
		|		ИЗ
		|			Справочник.Вакансии КАК Вакансии
		|		ГДЕ
		|			(НЕ Вакансии.Закрыта)";
		Если Подразделение <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|			И Вакансии.Подразделение В ИЕРАРХИИ(&Подразделение)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|			И Вакансии.Подразделение ССЫЛКА Справочник.Подразделения
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Вакансии.Подразделение,
		|			Вакансии.Должность) КАК КадровоеПланирование
		|	
		|	СГРУППИРОВАТЬ ПО
		|		КадровоеПланирование.Подразделение,
		|		КадровоеПланирование.Должность) КАК КадровоеПланирование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДолжностьНаименование,
		|	Подразделение ИЕРАРХИЯ";
		
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровыйПлан.Регистратор.Дата КАК Период,
		|	КадровыйПлан.Подразделение КАК Подразделение,
		|	МАКСИМУМ(ВЫБОР
		|		КОГДА КадровыйПлан.Подразделение = &Подразделение
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ) КАК ЕстьИерархия,
		|	КадровыйПлан.Должность КАК Должность,
		|	КадровыйПлан.Должность.Наименование КАК ДолжностьНаименование,
		|	СУММА(КадровыйПлан.Количество) КАК Запланировано
		|ИЗ
		|	РегистрСведений.КадровыйПлан КАК КадровыйПлан
		|ГДЕ
		|	КадровыйПлан.ПодразделениеОрганизации = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|	И КадровыйПлан.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)";
		Если Подразделение <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И КадровыйПлан.Подразделение В ИЕРАРХИИ (&Подразделение)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|
		|СГРУППИРОВАТЬ ПО
		|	КадровыйПлан.Регистратор.Дата,
		|	КадровыйПлан.Подразделение,
		|	КадровыйПлан.Должность,
		|	КадровыйПлан.Должность.Наименование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДолжностьНаименование,
		|	Период";
		
	КонецЕсли;
	
	РабочиеМеста.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ОбновитьРабочиеМестаРегл(Организация, ПодразделениеОрганизации, Период = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",				Организация);
	Запрос.УстановитьПараметр("ПодразделениеОрганизации",	ПодразделениеОрганизации);
	
	Если Период <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаАктуальности",	Период);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровоеПланирование.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА КадровоеПланирование.Подразделение = &ПодразделениеОрганизации
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьИерархия,
		|	КадровоеПланирование.Должность,
		|	КадровоеПланирование.Должность.Наименование КАК ДолжностьНаименование,
		|	КадровоеПланирование.Запланировано,
		|	КадровоеПланирование.Занято,
		|	КадровоеПланирование.Запланировано - КадровоеПланирование.Занято КАК Вакантно
		|ИЗ
		|	(ВЫБРАТЬ
		|		КадровоеПланирование.Подразделение КАК Подразделение,
		|		КадровоеПланирование.Должность КАК Должность,
		|		МАКСИМУМ(КадровоеПланирование.Запланировано) КАК Запланировано,
		|		МАКСИМУМ(КадровоеПланирование.Занято) КАК Занято
		|	ИЗ
		|		(ВЫБРАТЬ
		|			КадровыйПланСрезПоследних.ПодразделениеОрганизации КАК Подразделение,
		|			КадровыйПланСрезПоследних.Должность КАК Должность,
		|			СУММА(КадровыйПланСрезПоследних.Количество) КАК Запланировано,
		|			СУММА(0) КАК Занято
		|		ИЗ
		|			РегистрСведений.КадровыйПлан.СрезПоследних(
		|					&ДатаАктуальности,
		|					Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|						И ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|						И ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|																						) КАК КадровыйПланСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			КадровыйПланСрезПоследних.ПодразделениеОрганизации,
		|			КадровыйПланСрезПоследних.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.ПодразделениеОрганизации,
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.Должность.Должность,
		|			СУММА(0),
		|			СУММА(ЗанятыеШтатныеЕдиницыОрганизацийОстатки.КоличествоСтавокОстаток)
		|		ИЗ
		|			РегистрНакопления.ЗанятыеШтатныеЕдиницыОрганизаций.Остатки(
		|					&ДатаАктуальности,
		|					ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|						И ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|						И Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК ЗанятыеШтатныеЕдиницыОрганизацийОстатки
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.ПодразделениеОрганизации,
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.Должность.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			Вакансии.Подразделение,
		|			Вакансии.Должность,
		|			СУММА(1),
		|			СУММА(0)
		|		ИЗ
		|			Справочник.Вакансии КАК Вакансии
		|		ГДЕ
		|			(НЕ Вакансии.Закрыта)
		|			И Вакансии.Подразделение ССЫЛКА Справочник.ПодразделенияОрганизаций";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|			И Вакансии.Подразделение В ИЕРАРХИИ(&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|			И Вакансии.Подразделение.Владелец = &Организация
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Вакансии.Подразделение,
		|			Вакансии.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации,
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.Должность.Должность,
		|			СУММА(ШтатноеРасписаниеОрганизацийСрезПоследних.КоличествоСтавок),
		|			СУММА(0)
		|		ИЗ
		|			РегистрСведений.ШтатноеРасписаниеОрганизаций.СрезПоследних(
		|					&ДатаАктуальности,
		|					ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|						И ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|						И Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|						И (НЕ (ПодразделениеОрганизации, Должность.Должность) В
		|							(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|								КадровыйПлан.ПодразделениеОрганизации,
		|								КадровыйПлан.Должность
		|							ИЗ
		|								РегистрСведений.КадровыйПлан.СрезПоследних(&ДатаАктуальности, Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|									И ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|									И ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|									) КАК КадровыйПлан
		|							ГДЕ
		|								КадровыйПлан.Количество <> 0))) КАК ШтатноеРасписаниеОрганизацийСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации,
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.Должность.Должность) КАК КадровоеПланирование
		|	
		|	СГРУППИРОВАТЬ ПО
		|		КадровоеПланирование.Подразделение,
		|		КадровоеПланирование.Должность) КАК КадровоеПланирование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДолжностьНаименование,
		|	Подразделение";
		
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровыйПлан.Регистратор.Дата КАК Период,
		|	КадровыйПлан.ПодразделениеОрганизации КАК Подразделение,
		|	МАКСИМУМ(ВЫБОР
		|		КОГДА КадровыйПлан.ПодразделениеОрганизации = &ПодразделениеОрганизации
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ) КАК ЕстьИерархия,
		|	КадровыйПлан.Должность КАК Должность,
		|	КадровыйПлан.Должность.Наименование КАК ДолжностьНаименование,
		|	СУММА(КадровыйПлан.Количество) КАК Запланировано
		|ИЗ
		|	РегистрСведений.КадровыйПлан КАК КадровыйПлан
		|ГДЕ
		|	КадровыйПлан.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|	И КадровыйПлан.ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И КадровыйПлан.ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|
		|СГРУППИРОВАТЬ ПО
		|	КадровыйПлан.Регистратор.Дата,
		|	КадровыйПлан.ПодразделениеОрганизации,
		|	КадровыйПлан.Должность,
		|	КадровыйПлан.Должность.Наименование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДолжностьНаименование,
		|	Период";
		
	КонецЕсли;
	
	РабочиеМеста.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
