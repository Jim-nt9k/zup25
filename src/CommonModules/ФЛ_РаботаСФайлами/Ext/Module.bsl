//Островская Татьяна 20.11.2013 № 16927

Функция ТБ_УстановитьБлокировку(обФайл, пПуть)
	Вернуть = Ложь;
	Если глФЛ_СписокРедактируемыхФайлов.Найти(обФайл, "ФайлОбъект") = Неопределено Тогда
		НоваяБлокировка = глФЛ_СписокРедактируемыхФайлов.Добавить();
		НоваяБлокировка.ФайлОбъект = обФайл;
		НоваяБлокировка.ФайлСсылка = обФайл.Ссылка;
		НоваяБлокировка.Путь = пПуть;
		Вернуть = Истина;
	КонецЕсли;
	Возврат Вернуть;
КонецФункции



Функция ТБ_СнятьВсеБлокировки() Экспорт
	Для каждого Блокировка Из глФЛ_СписокРедактируемыхФайлов Цикл
		Блокировка.ФайлОбъект = Неопределено;
	КонецЦикла;
	глФЛ_СписокРедактируемыхФайлов.Очистить();
КонецФункции

Функция ТБ_СнятьБлокировкуСФайла(ФайлСсылка)
	Вернуть = Ложь;
	СтрокаБлокировки = глФЛ_СписокРедактируемыхФайлов.Найти(ФайлСсылка, "ФайлСсылка");
	Если СтрокаБлокировки <> Неопределено Тогда
		СтрокаБлокировки.ФайлОбъект = Неопределено;
		глФЛ_СписокРедактируемыхФайлов.Удалить(СтрокаБлокировки);
		Вернуть = Истина;
	КонецЕсли;
	Возврат Вернуть;
КонецФункции



Функция ТБ_СписокРедактируемыхФайлов() Экспорт
	Возврат глФЛ_СписокРедактируемыхФайлов.Скопировать(, "ФайлСсылка");
КонецФункции

Функция ТБ_ПутьЗаблокирован(пПуть)
	Вернуть = Истина;
	Если глФЛ_СписокРедактируемыхФайлов.Найти(пПуть, "Путь") = Неопределено Тогда
		Вернуть = Ложь;
	КонецЕсли;
	Возврат Вернуть;
КонецФункции

Функция ТБ_ПолучитьПутьРедактированияФайла(ФайлСсылка)
	Вернуть = Неопределено;
	СтрокаБлокировки = глФЛ_СписокРедактируемыхФайлов.Найти(ФайлСсылка, "ФайлСсылка");
	Если СтрокаБлокировки <> Неопределено Тогда
		Вернуть = СтрокаБлокировки.Путь;
	КонецЕсли;
	Возврат Вернуть;
КонецФункции




Функция ПолучитьПутьВременногоКаталогаХранилищаФайлов()
	Перем Путь;
	Путь = КаталогВременныхФайлов() + "FileStorage_" + СокрЛП(ИмяПользователя()) + "\";
	Каталог = Новый Файл(Путь);
	Если Не Каталог.Существует() Тогда
		СоздатьКаталог(Путь);
	КонецЕсли;
	Возврат Путь;
КонецФункции
Функция ПолучитьПутьКаталогаДляФайла(пФайл, пВерсия)
	Перем РабочийКаталог;
	РабочийКаталог = ПолучитьПутьВременногоКаталогаХранилищаФайлов();
	Путь = РабочийКаталог + пФайл.Код + "_" + пВерсия.Код + "\";
	Каталог = Новый Файл(Путь);
	Если Не Каталог.Существует() Тогда
		СоздатьКаталог(Путь);
	КонецЕсли;
	Возврат Путь;
КонецФункции

Функция ОткрытьВерсиюФайла(пВерсияФайла, ТолькоПросмотр) Экспорт
	ФайлВБД = пВерсияФайла.Владелец;
	КаталогФайла = ПолучитьПутьКаталогаДляФайла(ФайлВБД, пВерсияФайла);
	ИмяФайла = ФайлВБД.ИмяФайла;
	ПолныйПуть = КаталогФайла + ИмяФайла;

	Если ТБ_ПутьЗаблокирован(ПолныйПуть) Тогда
		Предупреждение("Файл уже открыт для редактирования!");
		Возврат Ложь;
	КонецЕсли;
	ДвоичныеДанные = пВерсияФайла.Данные.Получить();

	Попытка
		ФайлНаДиске = Новый Файл(ПолныйПуть);
		Если ФайлНаДиске.Существует() Тогда
			ФайлНаДиске.УстановитьТолькоЧтение(Ложь);
		КонецЕсли;
		УдалитьФайлы(КаталогФайла, "*.*");
		ДвоичныеДанные.Записать(ПолныйПуть);
		ВремяИзмененияФайла = ФайлНаДиске.ПолучитьВремяИзменения();
	Исключение
		Предупреждение("Ошибка открытия файла! Возможно файл уже открыт.");
		Возврат Ложь;
	КонецПопытки;
	Если Не ТолькоПросмотр Тогда
		Если ЗахватитьФайлДляРедактирования(ФайлВБД, ВремяИзмененияФайла, ПолныйПуть) Тогда
		ИначеЕсли Не (ФайлВБД.Редактируется = ПараметрыСеанса.ТекущийПользователь) Тогда
			Если Вопрос("В данный момент файл редактируется: " + ФайлВБД.Редактируется + "
			|Дата начала редактирования: " + ФайлВБД.НачалоРедактирования + "
			|Открыть файл в режиме просмотра?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				ТолькоПросмотр = Истина;
			Иначе
				Возврат Ложь;
			КонецЕсли;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	Если ТолькоПросмотр Тогда

		ФайлНаДиске = Новый Файл(ПолныйПуть);
		ФайлНаДиске.УстановитьТолькоЧтение(ТолькоПросмотр);
	КонецЕсли;
	Попытка
		ЗапуститьПриложение(ПолныйПуть);
	Исключение
		Предупреждение("Ошибка запуска приложения при открытии файла!");
		Сообщить(ИнформацияОбОшибке().Описание);
		Возврат Ложь;
	КонецПопытки;
	Возврат Истина;
КонецФункции

Функция УдалитьВерсиюФайла(пВерсияФайла) Экспорт
	ФайлВБД = пВерсияФайла.Владелец;
	Если ФайлВБД.АктуальнаяВерсия = пВерсияФайла И (пВерсияФайла.Автор = ПараметрыСеанса.ТекущийПользователь Или РольДоступна("ПолныеПрава") Или РольДоступна("ПолныеПраваБезЭкономики")) Тогда //О.Т. Аверсон 17.03.2017 № 335

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ФЛ_Версии.Ссылка КАК ПрежняяВерсия
		|ИЗ
		|	Справочник.ФЛ_Версии КАК ФЛ_Версии
		|ГДЕ
		|	ФЛ_Версии.ПометкаУдаления = ЛОЖЬ
		|	И ФЛ_Версии.Ссылка <> &Ссылка
		|	И ФЛ_Версии.Владелец = &Владелец
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФЛ_Версии.ДатаДобавления УБЫВ";

		Запрос.УстановитьПараметр("Владелец", ФайлВБД);
		Запрос.УстановитьПараметр("Ссылка", пВерсияФайла);
		Выборка = Запрос.Выполнить().Выбрать();
		ПрежняяВерсия = Неопределено;
		Если Выборка.Следующий() Тогда

			ПрежняяВерсия = Выборка.ПрежняяВерсия;
			обФайл = ФайлВБД.ПолучитьОбъект();
			обФайл.АктуальнаяВерсия = ПрежняяВерсия;
			обУдаляемаяВерсия = пВерсияФайла.ПолучитьОбъект();
			обУдаляемаяВерсия.ПометкаУдаления = Истина;
		Иначе

			обФайл = ФайлВБД.ПолучитьОбъект();
			обФайл.ПометкаУдаления = Истина;
			обУдаляемаяВерсия = пВерсияФайла.ПолучитьОбъект();
			обУдаляемаяВерсия.ПометкаУдаления = Истина;
		КонецЕсли;
		НачатьТранзакцию();
		обФайл.Записать();
		обУдаляемаяВерсия.Записать();
		ЗафиксироватьТранзакцию();
	Иначе
		Предупреждение("Вы можете удалить только последнюю версию, если Вы ее автор!");
	КонецЕсли;
КонецФункции

Функция СохранитьВерсиюФайлаВ1С(пФайл) Экспорт
	Перем ПолныйПуть;
	ПолныйПуть = ТБ_ПолучитьПутьРедактированияФайла(пФайл);
	Если ПолныйПуть = Неопределено Тогда
		Предупреждение("Редактируемый файл не найден!");
		Возврат Ложь;
	КонецЕсли;

	ФайлНаДиске = Новый Файл(ПолныйПуть);
	Если Не ФайлНаДиске.Существует() Тогда
		Предупреждение("Редактируемый файл не найден!");
		Возврат Ложь;
	КонецЕсли;

	Если ФайлНаДиске.ПолучитьВремяИзменения() = пФайл.НачалоРедактирования Тогда
		Если Вопрос("Время изменения файла не изменилось, продолжить редактирование?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

	ДанныеФайла = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ПолныйПуть), Новый СжатиеДанных(9));
	Если Не ЗначениеЗаполнено(ДанныеФайла) Тогда
		Предупреждение("Данные файла не считаны!");
		Возврат Ложь;
	КонецЕсли;

	НоваяВерсия = Справочники.ФЛ_Версии.СоздатьЭлемент();
	НоваяВерсия.Владелец = пФайл;
	НоваяВерсия.Данные = ДанныеФайла;
	НоваяВерсия.Автор = ПараметрыСеанса.ТекущийПользователь;

	НачатьТранзакцию();
	НоваяВерсия.Записать();
	Если Не РазблокироватьФайл(пФайл, НоваяВерсия) Тогда
		ОтменитьТранзакцию();
		Предупреждение("Новая версия не записана!");
		Возврат Ложь;
	КонецЕсли;
	ЗафиксироватьТранзакцию();
	Возврат Истина;
КонецФункции

Функция СохранитьВерсиюФайлаНаДиск(пВерсияФайла) Экспорт
	Вернуть = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.ПолноеИмяФайла = пВерсияФайла.Владелец.ИмяФайла;
	Если Диалог.Выбрать() Тогда
		Попытка
			ДвоичныеДанные = пВерсияФайла.Данные.Получить();
			ДвоичныеДанные.Записать(Диалог.ПолноеИмяФайла);
			Вернуть = Истина;
		Исключение
			Предупреждение("Файл не записан!");
		КонецПопытки;
	КонецЕсли;
	Возврат Вернуть;
КонецФункции


Функция ДобавитьФайл(ПутьФайла, ОбъектПривязкиСсылка) Экспорт
	ФайлНаДиске = Новый Файл(ПутьФайла);
	Если Не ФайлНаДиске.Существует() Тогда
		Предупреждение("Загружаемый файл не найден: " + ПутьФайла);
		Возврат Ложь;
	КонецЕсли;

	МаксДопустимыйРазмерФайла = Константы.ОграничениеНаРазмерФайла.Получить();
	Если МаксДопустимыйРазмерФайла <> 0 И ФайлНаДиске.Размер() / 1024 / 1024 > МаксДопустимыйРазмерФайла Тогда

		Предупреждение("Размер файла превышает максимально допустимый (" + Строка(МаксДопустимыйРазмерФайла) + " Mb): " + ПутьФайла);
		Возврат Ложь;
	КонецЕсли;

	ДанныеФайла = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ПутьФайла), Новый СжатиеДанных(9));
	Если Не ЗначениеЗаполнено(ДанныеФайла) Тогда
		Предупреждение("Данные файла не считаны: " + ПутьФайла);
		Возврат Ложь;
	КонецЕсли;
	НачатьТранзакцию();

	НовыйФайл = Справочники.ФЛ_Файлы.СоздатьЭлемент();
	НовыйФайл.ИмяФайла = ФайлНаДиске.Имя;
	НовыйФайл.ИсходныйПуть = ФайлНаДиске.Путь;
	НовыйФайл.Записать();

	ФайлСсылка = НовыйФайл.Ссылка;


	НоваяВерсия = Справочники.ФЛ_Версии.СоздатьЭлемент();
	НоваяВерсия.Владелец = ФайлСсылка;
	НоваяВерсия.Данные = ДанныеФайла;
	НоваяВерсия.Автор = ПараметрыСеанса.ТекущийПользователь;
	НоваяВерсия.Записать();


	НовыйФайл = ФайлСсылка.ПолучитьОбъект();
	НовыйФайл.АктуальнаяВерсия = НоваяВерсия.Ссылка;
	НовыйФайл.Записать();


	СоздатьПривязку(ОбъектПривязкиСсылка, НовыйФайл.Ссылка);

	ЗафиксироватьТранзакцию();
	Возврат НовыйФайл.Ссылка;
КонецФункции


Функция ДобавитьФайлыВ1С(ОбъектПривязкиСсылка) Экспорт
	Вернуть = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файлы для сохранения в 1С";
	Диалог.Фильтр = "Все файлы (*.*)|*.*";
	Диалог.ПроверятьСуществованиеФайла = Истина;
	Диалог.МножественныйВыбор = Истина;
	Если Диалог.Выбрать() Тогда
		Для каждого ПутьФайла Из Диалог.ВыбранныеФайлы Цикл
			Попытка
				ДобавитьФайл(ПутьФайла, ОбъектПривязкиСсылка);
			Исключение
				Предупреждение("Файл не загружен:" + ПутьФайла);
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
КонецФункции



Функция ПереоткрытьРедактируемыйФайл(пФайл) Экспорт
	Перем ПутьКФайлу;
	ПутьКФайлу = ТБ_ПолучитьПутьРедактированияФайла(пФайл);

	Вернуть = Ложь;

	Если ПутьКФайлу <> Неопределено Тогда


		Попытка
			ЗапуститьПриложение(ПутьКФайлу);
			Вернуть = Истина;
		Исключение


			Предупреждение("Ошибка запуска приложения при открытии файла!");
			Сообщить(ИнформацияОбОшибке().Описание);

		КонецПопытки;

	КонецЕсли;

	Возврат Вернуть;

КонецФункции







Функция ЗахватитьФайлДляРедактирования(пФайл, пВремяИзменения, пПуть)
	Вернуть = Ложь;

	обФайл = пФайл.ПолучитьОбъект();


	Попытка
		обФайл.Заблокировать();


		Если ТБ_УстановитьБлокировку(обФайл, пПуть) Тогда

			обФайл.Редактируется = ПараметрыСеанса.ТекущийПользователь;
			обФайл.НачалоРедактирования = пВремяИзменения;
			обФайл.Записать();

			Вернуть = Истина;

		КонецЕсли;
	Исключение


		Вернуть = Ложь;

	КонецПопытки;


	Возврат Вернуть;

КонецФункции


Функция РазблокироватьФайл(пФайл, НоваяВерсия = Неопределено) Экспорт
	БезОшибок = Истина;

	НачатьТранзакцию();

	обФайл = пФайл.ПолучитьОбъект();
	Если Не (НоваяВерсия = Неопределено) Тогда
		обФайл.АктуальнаяВерсия = НоваяВерсия.Ссылка;
	КонецЕсли;
	обФайл.Редактируется = Неопределено;
	обФайл.НачалоРедактирования = Неопределено;

	Попытка
		обФайл.Записать();
	Исключение
		БезОшибок = Ложь;
	КонецПопытки;

	Если Не ТБ_СнятьБлокировкуСФайла(пФайл) Тогда
		БезОшибок = Ложь;
	КонецЕсли;

	Если БезОшибок Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;


	Возврат БезОшибок;

КонецФункции


Функция НазначитьТипФайлу(пФайл) Экспорт
	НовыйТип = пФайл.ТипФайла;

	Если ВвестиЗначение(НовыйТип, "Выберите новый тип для файла", Тип("СправочникСсылка.ФЛ_ТипыФайлов")) Тогда

		обФайл = пФайл.ПолучитьОбъект();
		обФайл.ТипФайла = НовыйТип;
		обФайл.Записать();

	КонецЕсли;

КонецФункции

Функция УдалитьТипУФайла(пФайл) Экспорт
	Перем обФайл;
	обФайл = пФайл.ПолучитьОбъект();
	обФайл.ТипФайла = Справочники.ФЛ_ТипыФайлов.ПустаяСсылка();
	обФайл.Записать();

КонецФункции





Функция СоздатьПривязку(ОбъектСсылка, ФайлСсылка) Экспорт
	Запись = РегистрыСведений.ФЛ_СсылкиНаФайлы.СоздатьМенеджерЗаписи();
	Запись.Объект = ОбъектСсылка;
	Запись.Файл = ФайлСсылка;
	Запись.Записать();
	Возврат Истина;
КонецФункции

Функция ПеренестиПривязку(пФайл, пСтарыйОбъект, пНовыйОбъект) Экспорт
	Вернуть = Ложь;
	Выборка = РегистрыСведений.ФЛ_СсылкиНаФайлы.Выбрать(Новый Структура("Файл", пФайл));
	Пока Выборка.Следующий() Цикл
		Если Выборка.Объект = пСтарыйОбъект Тогда
			НачатьТранзакцию();
			Запись = Выборка.ПолучитьМенеджерЗаписи();
			Запись.Удалить();
			Запись.Файл = пФайл;
			Запись.Объект = пНовыйОбъект;
			Запись.Записать();
			ЗафиксироватьТранзакцию();
			Вернуть = Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат Вернуть;
КонецФункции





Функция ОткрытьФормуРаботыСФайлами() Экспорт
	Перем Форма;
	Форма = Обработки.ФЛ_РаботаСФайлами.ПолучитьФорму("Форма");

	Если Не Форма.Открыта() Тогда
		Форма.Открыть();
	Иначе
		Форма.Закрыть();
	КонецЕсли;

КонецФункции









Функция ФЛ_ПолучитьРасширениеФайла(ИмяФайла) Экспорт
	ПозицияПоследнейТочки = 0;
	РасширениеФайла = ИмяФайла;
	Пока 1 = 1 Цикл
		ПозицияПоследнейТочки = Найти(РасширениеФайла, ".");
		Если ПозицияПоследнейТочки = 0 Тогда
			Прервать;
		Иначе
			РасширениеФайла = Сред(РасширениеФайла, ПозицияПоследнейТочки + 1);
		КонецЕсли;
	КонецЦикла;
	Возврат ?(РасширениеФайла = ИмяФайла, "", РасширениеФайла);
КонецФункции










Функция ФЛ_ПолучитьПиктограммуФайла(РасширениеФайла) Экспорт
	РасширениеФайла = ВРег(РасширениеФайла);

	Если Найти(",1CD,CF,CFU,DT,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_1С;
	ИначеЕсли "MXL" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_MXL;
	ИначеЕсли "TXT" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_TXT;
	ИначеЕсли "EPF" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_EPF;
	ИначеЕсли Найти(",BMP,DIB,RLE,JPG,JPEG,TIF,GIF,PNG,ICO,WMF,EMF,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_BMP;
	ИначеЕсли Найти(",HTM,HTML,MHT,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_HTML;
	ИначеЕсли Найти(",DOC,RTF,DOCX,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_Word;
	ИначеЕсли Найти(",XLS,XLSX,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_Excel;
	ИначеЕсли "PPT" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_PowerPoint;
	ИначеЕсли "VSD" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_VSD;
	ИначеЕсли "MPP" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_MPP;
	ИначеЕсли "MDB" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_MDB;
	ИначеЕсли "XML" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_XML;
	ИначеЕсли "MSG" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_MSG;
	ИначеЕсли Найти(",RAR,ZIP,ARJ,CAB,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_WinRar;
	ИначеЕсли Найти(",EXE,COM,", "," + РасширениеФайла + ",") > 0 Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_EXE;
	ИначеЕсли "BAT" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_BAT;
	ИначеЕсли "PDF" = РасширениеФайла Тогда
		Возврат БиблиотекаКартинок.ПиктограммаФайла_PDF;
	Иначе
		Возврат БиблиотекаКартинок.ПиктограммаФайла_НеОпределен;
	КонецЕсли;

КонецФункции


Функция ФЛ_ПолучитьЦветПоСправочнику(пПарам) Экспорт
	Если ТипЗнч(пПарам) = Тип("СправочникСсылка.Цвета") И ЗначениеЗаполнено(пПарам) Тогда
		Возврат Новый Цвет(пПарам.Красный, пПарам.Зеленый, пПарам.Синий);
	Иначе
		Возврат Новый Цвет;
	КонецЕсли;

КонецФункции

Процедура ОповеститьОбАктивизацииСтроки(Элемент) Экспорт
	Если Элемент.ТекущаяСтрока <> Неопределено Тогда
		Оповестить("ФЛ_РаботаСФайлами_УстановитьНовыйКонтекст", Элемент.ТекущаяСтрока.Ссылка);
	КонецЕсли;
КонецПроцедуры

