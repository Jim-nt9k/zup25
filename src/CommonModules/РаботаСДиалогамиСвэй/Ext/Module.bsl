
//(начало) добавлено Кухарчуком Д.Я. 17.08.18, Задача СВФР-002063

// Заменяет одни термины, которые используются в коде, другими - уместными в интерфейсе.
// Позволяет иметь один тот же код при разных названиях объектов.
//
// Параметры
//  СтрокаИзИнтерфейса - строка - заголовок колонки, надпись и т.п.
//
// Возвращаемое значение:
//  Строка - новый заголовок колонки, надпись и т.п. 
//
Функция ПреобразоватьСтрокуИнтерфейса(СтрокаИзИнтерфейса) Экспорт 

	КоллекцияЗначенийЗаголовков = глЗначениеПеременной("глКоллекцияЗначенийЗаголовков");
	
	НоваяСтрока = КоллекцияЗначенийЗаголовков[СтрокаИзИнтерфейса];
	
	Возврат ?(НоваяСтрока = Неопределено, СтрокаИзИнтерфейса, НоваяСтрока)

КонецФункции // ПреобразоватьСтрокуИнтерфейса()

Процедура обВывестиСписокДокументов(Данные, ДанныеЖурнала=Неопределено) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ОтчетПечати = Отчеты.ПечатьСпискаДокументов.Создать();
		ОтчетПечати.Данные = Данные;
		ФормаОтчета = ОтчетПечати.ПолучитьФорму();
		ФормаОтчета.Открыть();
	#КонецЕсли
	
КонецПроцедуры

Процедура ОбработкаИзмененияБыстрогоОтбораПоОрганизации(Форма, Элемент, ИмяСпискаДокументов) Экспорт
	
	Если Элемент.Значение = Неопределено Тогда
		Элемент.Значение = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	Форма[ИмяСпискаДокументов].Отбор.Организация.Использование = ЗначениеЗаполнено(Элемент.Значение);
	
КонецПроцедуры


// Меняет цвет фона и цвет текста строки, если у документа есть движения по регистрам
Процедура СписокДокументовПриВыводеСтроки(ОформлениеСтроки, ДанныеСтроки) Экспорт 
	
	ЕстьДвижения = Ложь;
	Если Не ДанныеСтроки.Проведен Тогда 
		Движения = ДанныеСтроки.Ссылка.Метаданные().Движения;
		Если Движения.Количество()>0 Тогда 
			
			МассивРасшифровки = Новый Массив;
			МассивРасшифровки.Добавить(Новый Структура("Представление, Расшифровка", ДанныеСтроки.Ссылка, ДанныеСтроки.Ссылка));
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДанныеСтроки.Ссылка);
			Для Каждого Движение Из Движения Цикл 
				
				ИмяРегистра = "";
				ТекстЗапроса = 
				"ВЫБРАТЬ 
				|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Регистр.Регистратор) КАК Регистратор
				|ИЗ
				|";
				Если Метаданные.РегистрыСведений.Содержит(Движение) Тогда 
					ТекстЗапроса = ТекстЗапроса + 
					"	РегистрСведений." + Движение.Имя + " КАК Регистр
					|";
					ИмяРегистра = "сведений";
				ИначеЕсли Метаданные.РегистрыНакопления.Содержит(Движение) Тогда 
					ТекстЗапроса = ТекстЗапроса + 
					"	РегистрНакопления." + Движение.Имя + " КАК Регистр
					|";
					ИмяРегистра = "накопления";
				ИначеЕсли Метаданные.РегистрыРасчета.Содержит(Движение) Тогда 
					ТекстЗапроса = ТекстЗапроса + 
					"	РегистрРасчета." + Движение.Имя + " КАК Регистр
					|";
					ИмяРегистра = "расчета";
				КонецЕсли;
				ТекстЗапроса = ТекстЗапроса + 
				"ГДЕ
				|	Регистр.Регистратор = &Ссылка";
				
				Запрос.Текст = ТекстЗапроса;
				Выборка = Запрос.Выполнить().Выбрать();
				
				Если Выборка.Следующий() Тогда 
					ЕстьДвижения = Выборка.Регистратор>0; // для отладки = 0, но в реале нужно > 0
					Если ЕстьДвижения Тогда 
						//ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщенийПоЦентру");
						//ОбработкаКомментариев.УдалитьСообщения();
						//Родительская = ОбработкаКомментариев.ДобавитьСообщение("Ошибки документа по движениям в регистрах!", Перечисления.ВидыСообщений.Раздел);
						//ОбработкаКомментариев.ДобавитьСообщение("У не проведенного документа имеются движения по регистру " + ИмяРегистра + " " + """" + Движение + """" + " : " , Перечисления.ВидыСообщений.Ошибка, МассивРасшифровки, Родительская);
						//ОбработкаКомментариев.ПоказатьСообщения();
						Прервать;
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	ОформлениеСтроки.ЦветФона = ?(ЕстьДвижения, WebЦвета.ТеплоРозовый, ЦветаСтиля.ЦветФонаПоля);
	ОформлениеСтроки.ЦветТекста = ?(ЕстьДвижения, ЦветаСтиля.ЦветГруппыДерева, ЦветаСтиля.ЦветТекстаПоля);

КонецПроцедуры


Процедура СписокДокументовПередНачаломДобавления(Форма, Отказ, Копирование, Организация, ВидДокумента) Экспорт
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	СоздатьПоВидуДокументаСтрокой(ВидДокумента, Организация, Форма.ЭлементыФормы.ДокументСписок);
	
КонецПроцедуры


Процедура СоздатьПоВидуДокументаСтрокой(ВидДокумента, Организация = Неопределено, ЭлементСписок = Неопределено) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы[ВидДокумента];
	
	Если Не ПравоДоступа("ИнтерактивноеДобавление", МетаданныеДокумента) Тогда
		Предупреждение("Нарушение прав доступа!");
		Возврат;
	КонецЕсли;
	
	ФормаНовогоДокумента = Документы[ВидДокумента].ПолучитьФормуНовогоДокумента(, ЭлементСписок);
	Если Организация <> Неопределено И МетаданныеДокумента.Реквизиты.Найти("Организация") <> Неопределено Тогда
		Если ТипЗнч(ФормаНовогоДокумента) = Тип("Форма") Тогда
			ФормаНовогоДокумента.Организация = Организация;
		Иначе // Управляемая форма
			ФормаНовогоДокумента.Объект.Организация = Организация;
		КонецЕсли;
	КонецЕсли;
	
	ФормаНовогоДокумента.ЗакрыватьПриЗакрытииВладельца = Ложь;
	ФормаНовогоДокумента.Открыть();
	
КонецПроцедуры


// Процедура - общая часть обработок оповещения форм списков документов и журналов документов
//
// Параметры
//  Форма 							- форма списка документов или журнала документов
//  ИмяСпискаДокументов				- строка, имя реквизита содержащего список документов
//
Процедура ОбработкаОповещенияСпискаДокументов(ИмяСобытия, Параметр, Источник, Форма, ИмяСпискаДокументов, ОтборПоГоловнойОрганизации = Ложь) Экспорт
		
	Если ИмяСобытия = "ЗаписьПользователя" И Параметр = глЗначениеПеременной("глТекущийПользователь") Тогда
		УстановитьБыстрыйОтборПоОрганизации(Форма, ИмяСпискаДокументов, ОтборПоГоловнойОрганизации);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКнопкуПечатьРеестра(КоманднаяПанель) Экспорт
	
	ИмяКнопки = "ПечатьРеестра";
	
	Если КоманднаяПанель.Кнопки.Найти(ИмяКнопки) = Неопределено Тогда
		
		// Добавление кнопки
		Если КоманднаяПанель.ВыравниваниеКнопок = ВыравниваниеКнопокКоманднойПанели.Право Тогда
			
			КоманднаяПанель.Кнопки.Вставить(0,, ТипКнопкиКоманднойПанели.Разделитель);
			КнопкаСоветы = КоманднаяПанель.Кнопки.Вставить(0, ИмяКнопки, ТипКнопкиКоманднойПанели.Действие, "Печать реестра", Новый Действие("ДействияФормыПечатьРеестра"));
			
		Иначе
			
			КоманднаяПанель.Кнопки.Добавить(, ТипКнопкиКоманднойПанели.Разделитель);
			КнопкаСоветы = КоманднаяПанель.Кнопки.Добавить(ИмяКнопки, ТипКнопкиКоманднойПанели.Действие, "Печать реестра", Новый Действие("ДействияФормыПечатьРеестра"));
			
		КонецЕсли;
		
		КнопкаСоветы.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
		КнопкаСоветы.Картинка    = БиблиотекаКартинок.ПечатьРеестра;
		КнопкаСоветы.Подсказка   = "Печать реестра документов";
		
	КонецЕсли;

КонецПроцедуры // УстановитьПодменюСоветы()


Процедура УстановитьБыстрыйОтборПоОрганизации(Форма, ИмяСпискаДокументов, ОтборПоГоловнойОрганизации) 
	
	УстанавливатьОтбор = Истина;
	
	// проверим, не открывается ли список из документа
	ПараметрТекущаяСтрока = Форма.ПараметрТекущаяСтрока;
	Если ЗначениеЗаполнено(ПараметрТекущаяСтрока) Тогда
		ЗначениеОтбора = ПолучитьЗначениеРеквизита(ПараметрТекущаяСтрока, "Организация");
	Иначе
		ЗначениеОтбора = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
		// если форма списка открыта из другой формы, важно не менять установленный формой владельцем отбор
		УстанавливатьОтбор = Форма.ВладелецФормы = Неопределено;
	КонецЕсли;
	
	ГоловнаяОрганизация = ПолучитьЗначениеРеквизита(ЗначениеОтбора, "ГоловнаяОрганизация");
	Если ОтборПоГоловнойОрганизации 
		И ЗначениеЗаполнено(ЗначениеОтбора) 
		И ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
		ЗначениеОтбора = ГоловнаяОрганизация;
	КонецЕсли;
	
	Если УстанавливатьОтбор Тогда
		Форма[ИмяСпискаДокументов].Отбор.Организация.Установить(ЗначениеОтбора, ЗначениеЗаполнено(ЗначениеОтбора));	
	КонецЕсли;
		
КонецПроцедуры

Процедура СформироватьКнопкиДействийСписка(ДокументСписок, ЭтаФорма) Экспорт 

	#Если ТолстыйКлиентОбычноеПриложение Тогда
		
		// Поиск командной панели формы
		Если ЭтаФорма.ЭлементыФормы.Найти("ДействияФормы") = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ДействияФормы = ЭтаФорма.ЭлементыФормы.ДействияФормы;
		Если ДействияФормы = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// Установка начального отображения списка в конце для списка документов
		Если ЭтаФорма.ЭлементыФормы.Найти("ДокументСписок") <> Неопределено Тогда
			Если ЭтаФорма.ЭлементыФормы.ДокументСписок.НачальноеОтображениеСписка = Неопределено или
				ЭтаФорма.ЭлементыФормы.ДокументСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Авто Тогда 
				ЭтаФорма.ЭлементыФормы.ДокументСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
			КонецЕсли;			
		КонецЕсли;
		
		// Установка начального отображения списка в конце для журнала документов
		Если ЭтаФорма.ЭлементыФормы.Найти("ЖурналДокументовСписок") <> Неопределено Тогда
			Если ЭтаФорма.ЭлементыФормы.ЖурналДокументовСписок.НачальноеОтображениеСписка = Неопределено или
				ЭтаФорма.ЭлементыФормы.ЖурналДокументовСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Авто Тогда 
				ЭтаФорма.ЭлементыФормы.ЖурналДокументовСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
			КонецЕсли;			
		КонецЕсли;		
				
		// Определение обработчика кнопок
		ДействиеКнопки = Новый Действие("ОбработатьНажатиеСлужебнойКнопки");
		
		// Подготовка к обработке объектов
		МетаданныеСписка = Метаданные.НайтиПоТипу(ТипЗнч(ДокументСписок));
		Если Метаданные.ЖурналыДокументов.Содержит(МетаданныеСписка) Тогда
			ГруппаСписка = "Журнал документов";
		ИначеЕсли Метаданные.Документы.Содержит(МетаданныеСписка) Тогда
			ГруппаСписка = "Документ";
		Иначе
			Возврат;
		КонецЕсли;
		
		// Добавление информации пользователя
		РазделительИнформация = ДействияФормы.Кнопки.Добавить("РазделительИнформация", ТипКнопкиКоманднойПанели.Разделитель);
		кнИнформация = ДействияФормы.Кнопки.Добавить("кнИнформация", ТипКнопкиКоманднойПанели.Действие, "Информация", ДействиеКнопки);
		кнИнформация.Картинка = БиблиотекаКартинок.Информация;
		кнИнформация.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
		кнИнформация.Подсказка = "Доп.реквизиты документа";
		кнИнформация.Пояснение = "Доп.реквизиты документа";
		
	#КонецЕсли
	
КонецПроцедуры


// Процедура определяет необходимость быстрого отбора по организации, 
//  устанавливает его, если такая необходимость есть, или скрывает, если ее нет
//
// Параметры
//  Форма 							- форма списка документов или журнала документов
//  ИмяСпискаДокументов				- строка, имя реквизита содержащего список документов
//
Процедура НастроитьОтборПоОрганизации(Форма, ИмяСпискаДокументов, ОтборПоГоловнойОрганизации = Ложь) Экспорт
	
	ДоступностьОрганизаций = ДоступностьОрганизаций();
	ОтборДоступен = ?(ОтборПоГоловнойОрганизации, ДоступностьОрганизаций.ДоступноНесколькоГоловныхОрганизаций, ДоступностьОрганизаций.ДоступноНесколькоОрганизаций);
	
	ЭлементыФормы = Форма.ЭлементыФормы;
	ЭлементыФормы[ИмяСпискаДокументов].Колонки.Организация.Видимость = ОтборДоступен;
	
	// если элемент управления отбором размещен на панели - управляем сверткой
	// если нет - видимостью
	Если ЭлементыФормы.Найти("ПанельОтборПоОрганизации") = Неопределено Тогда
		ЭлементыФормы.ОрганизацияФлаг.Видимость = ОтборДоступен;
		ЭлементыФормы.Организация.Видимость 	= ОтборДоступен;
	Иначе
		ЭлементыФормы.ПанельОтборПоОрганизации.Свертка = ?(ОтборДоступен, РежимСверткиЭлементаУправления.Нет, РежимСверткиЭлементаУправления.Верх);
	КонецЕсли;
	
	Если ОтборДоступен Тогда
		УстановитьБыстрыйОтборПоОрганизации(Форма, ИмяСпискаДокументов, ОтборПоГоловнойОрганизации);
	КонецЕсли;
	
КонецПроцедуры


// Функция определяет доступность организаций
//
// Параметры
//  нет
//
// Возвращаемое значение:
//	ДоступностьОрганизаций - 	структура, определяет доступность нескольких организаций,
//  							нескольких головных организаций
//
Функция ДоступностьОрганизаций() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Организации.Ссылка,
	|	Организации.ГоловнаяОрганизация
	|ПОМЕСТИТЬ РазрешенныеОрганизации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	КОЛИЧЕСТВО(РазрешенныеОрганизации.Ссылка) КАК КоличествоОрганизаций
	|ПОМЕСТИТЬ ДоступныеОрганизации
	|ИЗ
	|	РазрешенныеОрганизации КАК РазрешенныеОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	КОЛИЧЕСТВО(РазрешенныеОрганизации.Ссылка) КАК КоличествоГоловныхОрганизаций
	|ПОМЕСТИТЬ ДоступныеГоловныеОрганизации
	|ИЗ
	|	РазрешенныеОрганизации КАК РазрешенныеОрганизации
	|ГДЕ
	|	РазрешенныеОрганизации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ДоступныеОрганизации.КоличествоОрганизаций > 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоступноНесколькоОрганизаций,
	|	ВЫБОР
	|		КОГДА ДоступныеГоловныеОрганизации.КоличествоГоловныхОрганизаций > 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоступноНесколькоГоловныхОрганизаций
	|ИЗ
	|	ДоступныеОрганизации КАК ДоступныеОрганизации,
	|	ДоступныеГоловныеОрганизации КАК ДоступныеГоловныеОрганизации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	ДоступностьОрганизаций = Новый Структура("ДоступноНесколькоОрганизаций,ДоступноНесколькоГоловныхОрганизаций");
	ЗаполнитьЗначенияСвойств(ДоступностьОрганизаций, Выборка);
	
	Возврат ДоступностьОрганизаций;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ДЛЯ ВЫВОДА СООБЩЕНИЙ

// Создает структуру, с параметрами выводимого сообщения 
// для помещения в массив сообщений-структур
Функция СоздатьСообщениеСтруктуру(ТекстСообщения, ВидСообщения, РасшифровкаСообщения = Неопределено, РаскрыватьСообщения = Истина) Экспорт 
	СтруктураСообщения = Новый Структура;
	СтруктураСообщения.Вставить("ТекстСообщения", ТекстСообщения);
	СтруктураСообщения.Вставить("ВидСообщения", ВидСообщения);
	СтруктураСообщения.Вставить("РасшифровкаСообщения", РасшифровкаСообщения);
	СтруктураСообщения.Вставить("РаскрыватьСообщения", РаскрыватьСообщения);
	Возврат СтруктураСообщения
КонецФункции // СоздатьСообщениеСтруктуру()

// Процедура выводить сообщения в специальное окно сообщений
// тексты сообщений берутся из переменной МассивСообщений
//
Процедура ВывестиСообщенияОбОшибках(МассивСообщений, Заголовок="") Экспорт
	
	Если МассивСообщений.Количество() > 0 Тогда
		ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщенийПоЦентру");
		ОбработкаКомментариев.УдалитьСообщения();
		ОбработкаКомментариев.ДобавитьСообщение(Заголовок, Перечисления.ВидыСообщений.Раздел);
		Для каждого ЭлементМассива Из МассивСообщений Цикл
			Если ЭлементМассива.РасшифровкаСообщения <> Неопределено И ЭлементМассива.РасшифровкаСообщения.Количество() > 1 Тогда
				РодительскаяСтрока = ОбработкаКомментариев.ДобавитьСообщение(ЭлементМассива.ТекстСообщения, ЭлементМассива.ВидСообщения, , , ЭлементМассива.РаскрыватьСообщения);
				Для Каждого ЭлементРасшифровки Из ЭлементМассива.РасшифровкаСообщения Цикл
					МассивРасшифровки = Новый Массив;
					МассивРасшифровки.Добавить(Новый Структура("Представление, Расшифровка", ЭлементРасшифровки.Представление, ЭлементРасшифровки.Расшифровка));
					ОбработкаКомментариев.ДобавитьСообщение("", Перечисления.ВидыСообщений.Информация, МассивРасшифровки, РодительскаяСтрока);
					
				КонецЦикла;
			Иначе
				ОбработкаКомментариев.ДобавитьСообщение(ЭлементМассива.ТекстСообщения, ЭлементМассива.ВидСообщения, ЭлементМассива.РасшифровкаСообщения);
			КонецЕсли
			
		КонецЦикла;
		ОбработкаКомментариев.ПоказатьСообщения();
	КонецЕсли;	
	
КонецПроцедуры // ВывестиСообщенияОбОшибках()


// Процедура предназначена для управления видимостью элементов формы,
// вызывается в обработчиках событий "ПриОткрытии" форм, при изменении реквизита Организация
//
// Параметры:
//  ЭлементыФормы  - массив с коллекцией ЭУ формы, видимость которых необходимо определить
//  УчетнаяПолитикаПоПерсоналу - структура, содержит данные учетной политики    
//    предприятия или конкретной организации
//
Процедура УстановитьВидимостьЭУПоУчетнойПолитикеПоПерсоналу(ЭлементыФормы,УчетнаяПолитикаПоПерсоналу,Организация = Неопределено) Экспорт
	
	ИспользуютсяНачисленияВВалюте			= Ложь;
	ПоказыватьТабельныеНомераВДокументах	= Ложь;
	ПоддерживатьНесколькоСхемМотивации		= Ложь;
	
	Если Организация = Неопределено Тогда
		Если УчетнаяПолитикаПоПерсоналу <> Неопределено Тогда
			Если Не УчетнаяПолитикаПоПерсоналу.Свойство("ИспользуютсяНачисленияВВалюте",ИспользуютсяНачисленияВВалюте) Тогда
				ИспользуютсяНачисленияВВалюте = Ложь;
			КонецЕсли;
			Если Не УчетнаяПолитикаПоПерсоналу.Свойство("ПоказыватьТабельныеНомераВДокументах",ПоказыватьТабельныеНомераВДокументах) Тогда
				ПоказыватьТабельныеНомераВДокументах = Ложь;
			КонецЕсли;
			Если Не УчетнаяПолитикаПоПерсоналу.Свойство("ПоддерживатьНесколькоСхемМотивации",ПоддерживатьНесколькоСхемМотивации) Тогда
				ПоддерживатьНесколькоСхемМотивации = Ложь;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ИспользуютсяНачисленияВВалюте			= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(УчетнаяПолитикаПоПерсоналу, Организация, "ИспользуютсяНачисленияВВалюте");
		ПоказыватьТабельныеНомераВДокументах	= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(УчетнаяПолитикаПоПерсоналу, Организация, "ПоказыватьТабельныеНомераВДокументах");
	КонецЕсли;
	
	ТипКолонкаТабличногоПоля = Тип("КолонкаТабличногоПоля");
	ТипПолеВвода = Тип("ПолеВвода");
	ТипНадпись = Тип("Надпись");
	ТипПанель  = Тип("Панель");
	
	Для каждого Элемент из ЭлементыФормы Цикл
		Если ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "ТабельныйНомерСтрока" Тогда
			Элемент.Видимость			= ПоказыватьТабельныеНомераВДокументах;
			Элемент.ИзменятьВидимость	= Ложь;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и (Элемент.Имя = "Валюта" Или Элемент.Имя = "ТекущаяВалюта") Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и (Элемент.Имя = "Валюта1" Или Элемент.Имя = "ТекущаяВалюта1") Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и (Элемент.Имя = "Валюта2" Или Элемент.Имя = "ТекущаяВалюта2")Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и (Элемент.Имя = "Валюта3" Или Элемент.Имя = "ТекущаяВалюта3")Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;	
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и (Элемент.Имя = "Валюта4" Или Элемент.Имя = "ТекущаяВалюта4")Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и (Элемент.Имя = "Валюта5" Или Элемент.Имя = "ТекущаяВалюта5")Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и (Элемент.Имя = "Валюта6" Или Элемент.Имя = "ТекущаяВалюта6")Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "ВалютаЗавершения" Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "Валюта1Завершения" Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "Валюта2Завершения" Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "Валюта3Завершения" Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;	
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "Валюта4Завершения" Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "Валюта5Завершения" Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "Валюта6Завершения" Тогда
			Элемент.Видимость			= ИспользуютсяНачисленияВВалюте;
			Элемент.ИзменятьВидимость	= ИспользуютсяНачисленияВВалюте;	
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "ВидСхемыМотивации" Тогда
			Элемент.Видимость			= Элемент.Видимость И ПоддерживатьНесколькоСхемМотивации;
			Элемент.ИзменятьВидимость	= ПоддерживатьНесколькоСхемМотивации;
		ИначеЕсли ТипЗнч(Элемент) = ТипКолонкаТабличногоПоля и Элемент.Имя = "ПоВременнойСхемеМотивации" Тогда
			Элемент.Видимость			= Элемент.Видимость И ПоддерживатьНесколькоСхемМотивации;
			Элемент.ИзменятьВидимость	= ПоддерживатьНесколькоСхемМотивации;
		ИначеЕсли ТипЗнч(Элемент) = ТипПолеВвода и Элемент.Имя = "ВалютаДокумента" Тогда
			Элемент.Видимость = ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипНадпись и Элемент.Имя = "НадписьВалютаДокумента" Тогда
			Элемент.Видимость = ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипПолеВвода и Элемент.Имя = "ВалютаДоговораГПХ" Тогда
			Элемент.Видимость = ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипНадпись и Элемент.Имя = "НадписьВалютаДоговораГПХ" Тогда
			Элемент.Видимость = ИспользуютсяНачисленияВВалюте;	
		ИначеЕсли ТипЗнч(Элемент) = ТипПолеВвода и Элемент.Имя = "ВалютаТарифнойСтавки" Тогда
			Элемент.Видимость = ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипНадпись и Элемент.Имя = "НадписьВалютаТарифнойСтавки" Тогда
			Элемент.Видимость = ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипПолеВвода и Элемент.Имя = "ВалютаТарифнойСтавки1" Тогда
			Элемент.Видимость = ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипНадпись и Элемент.Имя = "НадписьВалютаТарифнойСтавки1" Тогда
			Элемент.Видимость = ИспользуютсяНачисленияВВалюте;
		ИначеЕсли ТипЗнч(Элемент) = ТипПолеВвода и Элемент.Имя = "ВидСхемыМотивации" Тогда
			Элемент.Видимость = ПоддерживатьНесколькоСхемМотивации;
		ИначеЕсли ТипЗнч(Элемент) = ТипНадпись и Элемент.Имя = "НадписьВидСхемыМотивации" Тогда
			Элемент.Видимость = ПоддерживатьНесколькоСхемМотивации;
		ИначеЕсли ТипЗнч(Элемент) = ТипНадпись и Элемент.Имя = "ПоВременнойСхемеМотивации" Тогда
			Элемент.Видимость = ПоддерживатьНесколькоСхемМотивации;
		ИначеЕсли ТипЗнч(Элемент) = ТипПанель и Элемент.Имя = "ПанельСхемыМотивации" Тогда
			СверткаПанелиМотивации = ?(ПоддерживатьНесколькоСхемМотивации, РежимСверткиЭлементаУправления.Нет, РежимСверткиЭлементаУправления.Верх);	
			Элемент.Свертка = СверткаПанелиМотивации;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // УстановитьВидимостьЭУПоУчетнойПолитикеПоПерсоналу()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА АВТОНУМЕРАЦИИ

// Процедура изменения доступности "ручного" изменения кода/номера объекта
//
// Параметры
//  МетаданныеОбъекта  - метаданные объекта.
//  ФормаОбъекта      - форма объекта.
//  ПодменюДействия - меню "Действия" командной панели формы. В этот меню должен присутствовать пункт "Редактировать код/номер"
//  ПолеВводаНомера - поле вводе, связанное с кодом/номером объекта
//	ПолеВводаНомераДубль - необязательный параметр, необходим в случаях, когда код/номер одновременно связан с двумя элементами формы
//	КодНомерСтрокой - необязательный параметр, позволяет задать переназначить имя кода/номера при выводе запроса на подтверждение
//
Процедура ИзменениеВозможностиРедактированияНомера(МетаданныеОбъекта, ФормаОбъекта, ПодменюДействия, ПолеВводаНомера, ПолеВводаНомераДубль = Неопределено, ТекстВопроса = "") Экспорт
	
	Кнопка = ПодменюДействия.Кнопки.РедактироватьКодНомер;
	Если НЕ Кнопка.Пометка Тогда
		
		Если ПустаяСтрока(ТекстВопроса) Тогда			
			Если ВРЕГ(ПолеВводаНомера.Данные) = ВРЕГ("Код") Тогда
				ТекстВопроса = "Код присваивается автоматически при записи элемента, самостоятельное его редактирование может привести к нарушению в нумерации в системе. Вы действительно хотите установить код вручную?"
			Иначе
				ТекстВопроса = "Номер документу присваивается автоматически при записи, самостоятельное его редактирование может привести к нарушению в нумерации в системе. Вы действительно хотите установить номер вручную?"
			КонецЕсли;
		КонецЕсли;		
		
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;				
	КонецЕсли;
	Кнопка.Пометка = Не Кнопка.Пометка;
	ПолеВводаНомера.ТолькоПросмотр = НЕ Кнопка.Пометка;
	ПолеВводаНомера.ПропускатьПриВводе = ПолеВводаНомера.ТолькоПросмотр;	
	ОбновитьПодсказкуКодНомерОбъекта(МетаданныеОбъекта, ПодменюДействия, ПолеВводаНомера);
	
	Если ПолеВводаНомераДубль <> Неопределено Тогда
		ПолеВводаНомераДубль.ТолькоПросмотр = ПолеВводаНомера.ТолькоПросмотр;
		ПолеВводаНомераДубль.ПропускатьПриВводе = ПолеВводаНомераДубль.ТолькоПросмотр;
		ОбновитьПодсказкуКодНомерОбъекта(МетаданныеОбъекта, ПодменюДействия, ПолеВводаНомераДубль);
	КонецЕсли;
	
	
КонецПроцедуры // ИзменениеВозможностиРедактированияНомера()


//	Возвращает стратегию нумерациии для объекта, сохраненную в соотв. регистре сведений
//
// Параметры
//	МетаданныеОбъекта - метаданные объекта
//
// Возвращаемое значение 
//  Стратегия нумерациии для объекта
//
Функция ПолучитьСтратегиюАвтонумерацииДляОбъекта(МетаданныеОбъекта)
	
	Перем ТипОбъекта;
	
	Если Метаданные.Справочники.Содержит(МетаданныеОбъекта) Тогда
		ТипОбъекта = "Справочники";
	ИначеЕсли Метаданные.Документы.Содержит(МетаданныеОбъекта) Тогда
		ТипОбъекта = "Документы";
	ИначеЕсли Метаданные.ПланыВидовХарактеристик.Содержит(МетаданныеОбъекта) Тогда
		ТипОбъекта = "ПланыВидовХарактеристик";
	Иначе
		Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.НеДоступно;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СтратегияРедактированияНомераОбъекта КАК СтратегияРедактированияНомераОбъекта
	|ИЗ 
	|	РегистрСведений.СтратегииРедактированияНомеровОбъектов
	|ГДЕ
	|	ТипОбъекта = &ТипОбъекта
	|	И ВидОбъекта = &ВидОбъекта";
	
	Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	Запрос.УстановитьПараметр("ВидОбъекта", МетаданныеОбъекта.Имя);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтратегияРедактированияНомераОбъекта;
	Иначе
		Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.НеДоступно;
	КонецЕсли;	
	
КонецФункции // ПолучитьСтратегиюАвтонумерацииДляОбъекта()

//	Процедура установки флага ТолькоПросмотр для поля ввода кода/номера в зависимости от стратегии автонумерации объекта
//
// Параметры
//  МетаданныеОбъекта  - метаданные объекта.
//  ФормаОбъекта      - форма объекта.
//  ПодменюДействия - меню "Действия" командной панели формы. В этот меню должен присутствовать пункт "Редактировать код/номер"
//  ПолеВводаНомера - поле вводе, связанное с кодом/номером объекта
//
Процедура УстановитьДоступностьПоляВводаНомера(МетаданныеОбъекта, ФормаОбъекта, ПодменюДействия, ПолеВводаНомера) Экспорт
	
	Если ФормаОбъекта.Автонумерация = АвтонумерацияВФорме.Авто Тогда
		Возврат;
	КонецЕсли;	
	
	СтратегияРедактирования = ПолучитьСтратегиюРедактированияНомераОбъекта(МетаданныеОбъекта);
	
	Если СтратегияРедактирования = Перечисления.СтратегияРедактированияНомеровОбъектов.Доступно Тогда
		Если ПодменюДействия.Кнопки.Найти("РедактироватьКодНомер") <> Неопределено Тогда
			ПодменюДействия.Кнопки.Удалить(ПодменюДействия.Кнопки.РедактироватьКодНомер);
		КонецЕсли;
		ПолеВводаНомера.ТолькоПросмотр = Ложь;		
	КонецЕсли;
	
	ПолеВводаНомера.ПропускатьПриВводе = ПолеВводаНомера.ТолькоПросмотр;		
	УстановитьПодсказкуПоляВводаКодаНомера(ПолеВводаНомера, ПодменюДействия, СтратегияРедактирования);	
		
КонецПроцедуры // УстановитьДоступностьПоляВводаНомера()


//	Возвращает стратегию нумерациии для объекта
//
// Параметры
//	МетаданныеОбъекта - метаданные объекта
//
// Возвращаемое значение 
//  Стратегия нумерациии для объекта
//
Функция ПолучитьСтратегиюРедактированияНомераОбъекта(МетаданныеОбъекта) Экспорт
	КэшСтратегииАвтонумерации = глЗначениеПеременной("КэшСтратегииАвтонумерации");
	СтратегияРедактирования = КэшСтратегииАвтонумерации[МетаданныеОбъекта];
	Если СтратегияРедактирования = Неопределено Тогда
		СтратегияРедактирования = ПолучитьСтратегиюАвтонумерацииДляОбъекта(МетаданныеОбъекта);
		КэшСтратегииАвтонумерации.Вставить(МетаданныеОбъекта, СтратегияРедактирования);
	КонецЕсли;		
	Возврат СтратегияРедактирования;
КонецФункции // ПолучитьСтратегиюРедактированияНомераОбъекта()


//	Процедура установки текста подсказки для поля ввода кода/номера в зависимости от стратегии редактирования
//	и введенного значения
//
// Параметры
//  ПолеВводаНомера - поле вводе, связанное с кодом/номером объекта
//  ПодменюДействия - меню "Действия" командной панели формы. В этот меню должен присутствовать пункт "Редактировать код/номер"
//	СтратегияРедактирования - стратегия автонумерации объекта
//
Процедура УстановитьПодсказкуПоляВводаКодаНомера(ПолеВводаНомера, ПодменюДействия, СтратегияРедактирования)
		
	КодНомерСтрокой = ПолеВводаНомера.Данные;		
	
	Если ТипЗнч(ПолеВводаНомера) = Тип("ПолеВвода") Тогда
		ПолеВвода = ПолеВводаНомера;
	Иначе
		ПолеВвода = ПолеВводаНомера.ЭлементУправления;
	КонецЕсли;	
	
	Если ПустаяСтрока(ПолеВвода.Значение) Тогда
		ПолеВвода.Подсказка =  КодНомерСтрокой + " будет присвоен при записи объекта.";
	Иначе
		ПолеВвода.Подсказка =  "";
	КонецЕсли;
	
	Если СтратегияРедактирования = Перечисления.СтратегияРедактированияНомеровОбъектов.НеДоступно Тогда
		Если НЕ ПодменюДействия.Кнопки.Найти("РедактироватьКодНомер") = Неопределено Тогда
			Если НЕ ПодменюДействия.Кнопки.РедактироватьКодНомер.Пометка Тогда
				ПолеВвода.Подсказка =  ПолеВвода.Подсказка + ?(ПустаяСтрока(ПолеВвода.Подсказка), "", " ") 
											 + "Для возможности редактирования " + ?(КодНомерСтрокой = "Номер", "номера", "кода") + " используйте подменю ""Действия"".";
			КонецЕсли;
		КонецЕсли;								 
	КонецЕсли;
	
КонецПроцедуры // УстановитьПодсказкуПоляВводаКодаНомера()


//	Процедура обновления текста подсказки для поля ввода кода/номера
//
// Параметры
//	МетаданныеОбъекта - метаданные объекта
//  ПолеВводаНомера - поле вводе, связанное с кодом/номером объекта
//  ПодменюДействия - меню "Действия" командной панели формы. В этот меню должен присутствовать пункт "Редактировать код/номер"
//
Процедура ОбновитьПодсказкуКодНомерОбъекта(МетаданныеОбъекта, ПодменюДействия, ПолеВводаНомера) Экспорт
	УстановитьПодсказкуПоляВводаКодаНомера(ПолеВводаНомера, ПодменюДействия, ПолучитьСтратегиюРедактированияНомераОбъекта(МетаданныеОбъекта));
КонецПроцедуры // ОбновитьПодсказкуКодНомерОбъекта()


//	Процедура очистки введенного кода/номера объекта
// Параметры
//  ЭтотОбъект  - объект.
//	КодНомер - имя обрабатываемого реквизита (Код или Номер)
//  ПодменюДействия - меню "Действия" командной панели формы. В этот меню должен присутствовать пункт "Редактировать код/номер"
//  ПолеВводаНомера - поле вводе, связанное с кодом/номером объекта
//
Процедура СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, КодНомер, ПодменюДействия, ПолеВводаНомера) Экспорт
	ЭтотОбъект[КодНомер] = "";
	ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ПодменюДействия, ПолеВводаНомера);
КонецПроцедуры // СброситьУстановленныйКодНомерОбъекта()


// Выводит надпись и соответствующую ей картинку
//
// Параметры
//  ЭУНадпись, ЭУКартинка - элементы управления формы, соответственно надпись и картинка
//  ТекстСообщения - строка - выводимое сообщение
//  ВажностьСообщения - строка - определяет расцветку текста и картинку
//		допустимые значения: "СообщениеОПроблемах", "ВажнаяИнформация" и "Информация"
//
Процедура ПоказатьИнформациюОДокументе(ЭУНадпись = Неопределено, ЭУКартинка = Неопределено, ТекстСообщения = "", ВажностьСообщения = "") Экспорт 
	
	Если ЭУНадпись = Неопределено Или ЭУКартинка = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Если ВажностьСообщения = "ВажнаяИнформация" Тогда
		ЭУКартинка.Картинка = БиблиотекаКартинок.СообщениеВажнаяИнформация;
		ЭУНадпись.ЦветТекста = ЦветаСтиля.ТекстСообщенияВажнойИнформации;
	ИначеЕсли ВажностьСообщения = "СообщениеОПроблемах" Тогда
		ЭУКартинка.Картинка = БиблиотекаКартинок.СообщениеОПроблемах;
		ЭУНадпись.ЦветТекста = ЦветаСтиля.ТекстСообщенияОПроблемах;
	ИначеЕсли ВажностьСообщения = "Ошибка" Тогда
		ЭУКартинка.Картинка = БиблиотекаКартинок.СообщениеОшибка;
		ЭУНадпись.ЦветТекста = ЦветаСтиля.ТекстСообщенияОПроблемах;
	Иначе
		ЭУКартинка.Картинка	= БиблиотекаКартинок.СообщениеИнформация;
		ЭУНадпись.ЦветТекста = ЦветаСтиля.ТекстСообщенияИнформации;
	КонецЕсли;
	ЭУНадпись.Заголовок = ТекстСообщения;
	
КонецПроцедуры // ПоказатьИнформациюОДокументе()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПРЕДСТАВЛЕНИЯ ИНФОРМАЦИИ В ФОРМЕ

// По описателям состояния расчетного документа (флажкам) формирует текст инфо-строки и важность сообщения
//
// Параметры
//	НеЗаполнен,ЗаполненСОшибками,Рассчитан,Проведен,ЭтоИсправление,ДокументИсправлен,Отсторнирован - булево (все)
//		описывают достижение документом того или иного состояния
//
// Возвращаемое значение:
//	Структура из двух строковых значений - ТекстСообщения и ВажностьСообщения
//
Функция ПолучитьОписаниеРасчетногоДокумента(Заполнен = Ложь, ЗаполненСОшибками = Ложь, Рассчитан = Ложь, Проведен = Ложь, ЭтоИсправление = Ложь, ДокументИсправлен = Ложь, Отсторнирован = Ложь, ИмеетПродолжение = Ложь, ЯвляетсяПродолжением = Ложь) Экспорт 
	
	ТекстСообщения = "";
	ВажностьСообщения = "";
	
	Если Не Заполнен Тогда
		ВажностьСообщения = "СообщениеОПроблемах";
		ТекстСообщения = "Документ не заполнен";
	ИначеЕсли ЭтоИсправление Тогда
		ТекстСообщения = "Документ является исправлением документа прошлого периода; ";
		Если ДокументИсправлен Тогда
			ВажностьСообщения = "СообщениеОПроблемах";
			Если Не Рассчитан Тогда
				ТекстСообщения = ТекстСообщения + "не был рассчитан, но ";
			КонецЕсли;
			ТекстСообщения = ТекстСообщения + "был исправлен; редактирование документа запрещено"
		ИначеЕсли ЗаполненСОшибками Тогда
			ВажностьСообщения = "СообщениеОПроблемах";
			ТекстСообщения = ТекстСообщения + "поля документа заполнены не полностью или неверно";
		ИначеЕсли Не Рассчитан Тогда
			ВажностьСообщения = "ВажнаяИнформация";
			ТекстСообщения = ТекстСообщения + "документ не рассчитан";
		ИначеЕсли Рассчитан И Не Проведен Тогда
			ВажностьСообщения = "Информация";
			ТекстСообщения = ТекстСообщения + "рассчитан, но не проведен";
		ИначеЕсли Рассчитан И Проведен Тогда
			ВажностьСообщения = "Информация";
			ТекстСообщения = ТекстСообщения + "рассчитан и проведен";
		КонецЕсли;
	ИначеЕсли ДокументИсправлен Тогда
		ВажностьСообщения = "СообщениеОПроблемах";
		ТекстСообщения = "Документ ";
		Если Не Рассчитан Тогда
			ТекстСообщения = ТекстСообщения + "не был рассчитан, но ";
		КонецЕсли;
		ТекстСообщения = ТекстСообщения + "был исправлен; редактирование документа запрещено"
	ИначеЕсли Отсторнирован Тогда
		ВажностьСообщения = "СообщениеОПроблемах";
		ТекстСообщения = "Документ был отменен полностью (отсторнирован); редактирование документа запрещено";
	ИначеЕсли ЗаполненСОшибками Тогда
		ВажностьСообщения = "СообщениеОПроблемах";
		ТекстСообщения = "Поля документа заполнены не полностью или неверно";
	ИначеЕсли Не Рассчитан Тогда
		ВажностьСообщения = "ВажнаяИнформация";
		ТекстСообщения = "Документ не рассчитан";
	ИначеЕсли Рассчитан И Не Проведен Тогда
		ВажностьСообщения = "Информация";
		ТекстСообщения = "Документ рассчитан, но не проведен";
	ИначеЕсли Рассчитан И Проведен Тогда
		ВажностьСообщения = "Информация";
		ТекстСообщения = "Документ рассчитан и проведен";
	КонецЕсли;
	
	ТекстПродолжений = "";
	Если ЯвляетсяПродолжением Тогда
		ТекстПродолжений = "Этот больничный является б/л-продолжением. ";
	КонецЕсли;
	Если ИмеетПродолжение Тогда
		ТекстПродолжений = ТекстПродолжений + "Этот больничный имеет продолжение. ";
	КонецЕсли;
	
	Возврат Новый Структура("ТекстСообщения,ВажностьСообщения", ТекстПродолжений + ТекстСообщения, ВажностьСообщения)
	
КонецФункции



// ПРОЧИЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//
// Возвращает выборку со значениями реквизитов переданной ссылки
//
Функция ПолучитьЗначенияРеквизитов(Ссылка, ИменаРеквизитов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	" + ИменаРеквизитов + "
	|ИЗ
	|	" + Ссылка.Метаданные().ПолноеИмя() + " КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Результат = Новый Структура(ИменаРеквизитов);
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	Возврат Результат;
	
КонецФункции


// Получить значение одного реквизита по ссылке.
// Например, для элемента справочника нужно получить наименование,
// в этом случае в качестве ИмяРеквизита нужно передать "Наименование".
//
Функция ПолучитьЗначениеРеквизита(Ссылка, ИмяРеквизита) Экспорт
	
	Результат = ПолучитьЗначенияРеквизитов(Ссылка, ИмяРеквизита);
	
	Возврат Результат[ИмяРеквизита];
	
КонецФункции

//(конец ) добавлено Кухарчуком Д.Я. 17.08.18, Задача СВФР-002063


//+ СВЭЙ 15.03.19  КДЯ  СВФР-004088
// ОкруглитьКоэффициентИндексации
// Округлить Коэффициент Индексации
// ОкрКоэфИнд
// ОкрКИ
Функция ОкруглитьКоэффициентИндексации( Знач КоэффициентИндексации, ОкруглитьКоэффициентИндексации = Истина ) Экспорт
	Перем ОкругленныйКоэффициентИндексации, ПорядокОкругления;
	//Аверсон 15.06.2020 КВВ СВФР-006146 (
	Орг = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	ПределКоэфИндексации = Орг.ПределКоэффициентаИндексации;
	//Аверсон 15.06.2020 КВВ СВФР-006146 )
	
	Если НЕ ОкруглитьКоэффициентИндексации Тогда
		//Аверсон 15.06.2020 КВВ СВФР-006146 (
		//КоэффициентИндексации = ?( КоэффициентИндексации > 2, 1, КоэффициентИндексации );  //СВЭЙ КДЯ 03.01.20 СВФР-005167
		Если ЗначениеЗаполнено(ПределКоэфИндексации) Тогда 
			КоэффициентИндексации = ?( КоэффициентИндексации > ПределКоэфИндексации, 1, КоэффициентИндексации ); 
		КонецЕсли;
		//Аверсон 15.06.2020 КВВ СВФР-006146 )
		Возврат КоэффициентИндексации;
	КонецЕсли; 
	ПорядокОкругления = Константы.АВ_ПорядокОкругленияКоэффициентаИндексации.Получить();
	Если ПорядокОкругления = 0 Тогда
		ПорядокОкругления = 4;
	КонецЕсли;
	ОкругленныйКоэффициентИндексации = Окр( КоэффициентИндексации, ПорядокОкругления, РежимОкругления.Окр15как20 );
	
	//Аверсон 15.06.2020 КВВ СВФР-006146 (
	//ОкругленныйКоэффициентИндексации = ?( ОкругленныйКоэффициентИндексации > 2, 1, ОкругленныйКоэффициентИндексации );  //СВЭЙ КДЯ 03.01.20 СВФР-005167
	Если ЗначениеЗаполнено(ПределКоэфИндексации) Тогда 
		ОкругленныйКоэффициентИндексации = ?( ОкругленныйКоэффициентИндексации > ПределКоэфИндексации, 1, ОкругленныйКоэффициентИндексации ); 
	КонецЕсли;
	//Аверсон 15.06.2020 КВВ СВФР-006146 )
	
	Возврат ОкругленныйКоэффициентИндексации;
КонецФункции // ОкруглитьКоэффициентИндексации
//- СВЭЙ 15.03.19  КДЯ  СВФР-004088

