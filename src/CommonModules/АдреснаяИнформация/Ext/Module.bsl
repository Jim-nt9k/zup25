//Островская Татьяна 20.11.2013 № 16927

Функция обСуществуетРеквизит(прОбъект, прИмяРекв, Знач прОбласть = "Шапка") Экспорт
	Перем лкСтруктура, лкЭлем;
	лкМетаОбъект = обМетаданные(прОбъект);
	лкВозврат = Ложь;


	Если прОбласть = "*" Или прОбласть = "#" Тогда
		прОбласть = ?(прОбласть = "*", "Шапка", "");
		Для каждого лкЭлем Из лкМетаОбъект.ТабличныеЧасти Цикл
			прОбласть = прОбласть + ?(прОбласть = "", "", ",") + лкЭлем.Имя;
		КонецЦикла;
	КонецЕсли;


	лкСтруктура = обСтрокаВСтруктуру(прОбласть);


	Для каждого лкЭлем Из лкСтруктура Цикл
		лкМетаОбласть = ?(лкЭлем.Значение = "Шапка", лкМетаОбъект, лкМетаОбъект.ТабличныеЧасти[лкЭлем.Значение]);
		Если лкМетаОбласть.Реквизиты.Найти(прИмяРекв) <> Неопределено Тогда
			лкВозврат = Истина; Прервать;
		КонецЕсли;
	КонецЦикла;


	Возврат лкВозврат;

КонецФункции

Функция обМетаданные(прОбъект) Экспорт
	Перем лкВозврат;
	лкВозврат = Метаданные.НайтиПоТипу(ТипЗнч(прОбъект));


	Возврат лкВозврат;

КонецФункции

Функция обСтрокаВСтруктуру(Знач прСтрока, прРазделитель = ",", прКлючи = Истина) Экспорт
	лкВозврат = Новый Структура;


	Пока Не ПустаяСтрока(прСтрока) Цикл


		лкПоз = Найти(прСтрока, прРазделитель);
		Если лкПоз > 0 Тогда
			лкЗначение = Лев(прСтрока, лкПоз - 1); прСтрока = Сред(прСтрока, лкПоз + 1);
		Иначе
			лкЗначение = прСтрока; прСтрока = "";
		КонецЕсли;


		лкКлюч = ?(прКлючи, "Ключ" + Строка(лкВозврат.Количество() + 1), лкЗначение);
		лкВозврат.Вставить(лкКлюч, лкЗначение);

	КонецЦикла;


	Возврат лкВозврат;

КонецФункции




