Процедура ОбработатьОбъект(источник, Отказ)    Экспорт
    
    ТекущееСобытие = "Запись";
	Если ВыполнитьВыгрузку(источник) = Ложь Тогда
		Отказ = истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОбъектПриУдалении(источник, Отказ)    Экспорт
    
	ТекущееСобытие = "Удаление";
	Если ВыполнитьВыгрузку(источник) = Ложь Тогда
		Отказ = истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ВыполнитьВыгрузку(источник)
    Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(УправлениеПользователями.ОпределитьТекущегоПользователя(),"ОсновнаяОрганизация");
    Попытка
        Если Организация.ИНН <> "100092167" Тогда
            Возврат Истина;
        КонецЕсли;
    Исключение
        Возврат Истина;
    КонецПопытки;

	КаталогВыгрузки = СокрЛП(Константы.КаталогВыгрузкиДляПодпискиНаСобытия.Получить());
	Если НЕ ЗначениеЗаполнено(КаталогВыгрузки) Тогда
		КаталогВыгрузки = КаталогВременныхФайлов();
		Константы.КаталогВыгрузкиДляПодпискиНаСобытия = КаталогВременныхФайлов();
		Сообщить("Установлен каталог для выгрузки файдов: " + КаталогВременныхФайлов());
	КонецЕсли;
	
	ВыбранныйВидСправочника = источник.Метаданные().Имя;
	Если (ВыбранныйВидСправочника = "СотрудникиОрганизаций") или 
		(ВыбранныйВидСправочника = "ФизическиеЛица") Тогда
		ИмяФайлаБазы = "\Sotrud.dbf";
		
	ИначеЕсли ВыбранныйВидСправочника = "ДолжностиОрганизаций" Тогда
		ИмяФайлаБазы = "\Dolgn.dbf";
		
	ИначеЕсли ВыбранныйВидСправочника = "ПодразделенияОрганизаций" Тогда
		ИмяФайлаБазы = "\PDR.dbf";
		
	КонецЕсли;
	
	БазаВыгрузки = Новый xBase; 
	БазаВыгрузки.Кодировка = КодировкаXBase.ANSI;
	
	ПолноеИмяФайла = КаталогВыгрузки + ИмяФайлаБазы;
	ФайлБазы = Новый Файл(ПолноеИмяФайла);
	Если НЕ ФайлБазы.Существует() Тогда
		Если СоздатьНовыйФайлДлявыгрузки(ВыбранныйВидСправочника,БазаВыгрузки,ПолноеИмяФайла) = Ложь Тогда
			Сообщить("В данный момент операция не может быть выполнена! Повторите попытку позднее");
			Возврат ложь;
		Конецесли;
		
	Иначе
		Попытка
			БазаВыгрузки.ОткрытьФайл(ПолноеИмяФайла);
		Исключение
			Сообщить("В данный момент операция не может быть выполнена! Повторите попытку позднее");
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
    
    Если Не БазаВыгрузки.Открыта() Тогда
        Сообщить("Не смог открыть файл для записи");
        Возврат Ложь;
    КонецЕсли;
    
	Если (ВыбранныйВидСправочника = "СотрудникиОрганизаций") или 
		(ВыбранныйВидСправочника = "ФизическиеЛица") Тогда
		Возврат ВыгрузитьСотрудников(источник,БазаВыгрузки,ВыбранныйВидСправочника);
		 
	ИначеЕсли (ВыбранныйВидСправочника = "ДолжностиОрганизаций")
		или (ВыбранныйВидСправочника = "ПодразделенияОрганизаций") Тогда
		
		Возврат ВыгрузитьПрочие(Источник,БазаВыгрузки,ВыбранныйВидСправочника);
    КонецЕсли;
    
	БазаВыгрузки.ЗакрытьФайл();

	Возврат Истина;
КонецФункции

Функция СоздатьНовыйФайлДлявыгрузки(ВыбранныйВидСправочника,БазаВыгрузки,ПолноеИмяФайла)
	
	БазаВыгрузки.поля.Добавить("GUID", "S", 40);         // GUID элемента справочника
	Если (ВыбранныйВидСправочника = "СотрудникиОрганизаций") или 
		(ВыбранныйВидСправочника = "ФизическиеЛица") Тогда
		// GUID выгружается элемента "Сотрудник"
		БазаВыгрузки.поля.Добавить("FIO", "S",100);      //наименование (ФИО)
		БазаВыгрузки.поля.Добавить("DataRogd", "S",10);  // дата рождения
		БазаВыгрузки.поля.Добавить("Pol", "S",1);        // пол (М/Ж)
		// данные документа
		БазаВыгрузки.поля.Добавить("VD_GUID", "S", 40);  // вид документа GUID
		БазаВыгрузки.поля.Добавить("VD_Naim", "S",100);  // наименование вида документа
		БазаВыгрузки.поля.Добавить("DSeria", "S", 14);   // серия документа
		БазаВыгрузки.поля.Добавить("DNomer", "S", 14);   // номер документа
		БазаВыгрузки.поля.Добавить("DataVyd", "S",10);   // дата выдачи
		БазаВыгрузки.поля.Добавить("SrokDeis", "S",10);  // срок действия - дата
		БазаВыгрузки.поля.Добавить("OVD_GUID", "S", 40); // GUID отдела внутренних дел
        БазаВыгрузки.поля.Добавить("OVD_NAIM", "S",100); // наименование Органа, выдавшего паспорт. 
		БазаВыгрузки.поля.Добавить("LichNom", "S", 14);  // личный номер
		БазаВыгрузки.поля.Добавить("DataReg", "S",10);   // дата регистрации по месту жительства
		БазаВыгрузки.поля.Добавить("DataDeis", "S",10);  // запись о паспортных данных действует с 
		// адрес
		БазаВыгрузки.поля.Добавить("ADR_GUID", "S", 40); // вид алреса GUID
		БазаВыгрузки.поля.Добавить("ADR_Kod", "S", 20);  // вид адреса код
		БазаВыгрузки.поля.Добавить("ADR_Nain", "S", 100);// вид адреса наименование
		БазаВыгрузки.поля.Добавить("ADR_RB", "N", 1,0);  // адрес РБ (Да+Нет)
		//БазаВыгрузки.поля.Добавить("ADR_nRB", "N", 1,0); // адрес за пределами РБ (Да+Нет)
		БазаВыгрузки.поля.Добавить("SOATO", "S", 10);    // СОАТО
		БазаВыгрузки.поля.Добавить("KodIMNS", "S", 10);  // Код(Kod) ИМНС
		БазаВыгрузки.поля.Добавить("Oblast", "S", 50);   // область
		БазаВыгрузки.поля.Добавить("Raion", "S", 50);    // район
		БазаВыгрузки.поля.Добавить("S_Sovet", "S", 50);  // сельсовет
		БазаВыгрузки.поля.Добавить("Gorod", "S", 50);    // населенный пункт
		БазаВыгрузки.поля.Добавить("Ulitsa", "S", 50);   // улица
		БазаВыгрузки.поля.Добавить("NomDom", "S", 6);    // номер дома
		БазаВыгрузки.поля.Добавить("Korpus", "S", 5);    // корпус
		БазаВыгрузки.поля.Добавить("Kvart", "S", 5);     // номер квартиры
		БазаВыгрузки.поля.Добавить("Index", "S", 6);     // почтовый индекс
		БазаВыгрузки.поля.Добавить("Komment", "S", 150); // комментарий
		// гражданство
		БазаВыгрузки.поля.Добавить("STR_GUID", "S", 40); // GUID страны гражданства
		БазаВыгрузки.поля.Добавить("STR_Kod", "S", 14);  // страна код
		БазаВыгрузки.поля.Добавить("BEZ_Gr", "N", 1, 0); // лицо без гражданства да(1)/нет(0) 
		
		БазаВыгрузки.поля.Добавить("Pensia", "N", 1, 0); // не имеет право на пенсию да(1)/нет(0) 
		БазаВыгрузки.поля.Добавить("Rezident", "N", 1, 0);//не является налоговым резидентом РБ да(1)/нет(0)
		
		//сотрудник
		БазаВыгрузки.поля.Добавить("TipRab", "N", 3, 0); // тип работника
		БазаВыгрузки.поля.Добавить("VidRab", "N", 3, 0); //вид занятости
		БазаВыгрузки.поля.Добавить("TabNomer", "N", 10, 0);//табельный номер

	Иначе                                                 
		БазаВыгрузки.поля.Добавить("Kod", "S", 10);      // код элемента
		БазаВыгрузки.поля.Добавить("Naim", "S", 100);    // наименование элемента
		
		Если ВыбранныйВидСправочника = "ПодразделенияОрганизаций" Тогда
			БазаВыгрузки.поля.Добавить("GUIDRod", "S", 40);// GUID родительской группы
            БазаВыгрузки.поля.Добавить("GROUP", "N", 1, 0);		 // признак группы элементов
		КонецЕсли;
	КонецЕсли;
    
	БазаВыгрузки.поля.Добавить("Del", "N", 2);			 // пометка на удаление
	БазаВыгрузки.СоздатьФайл(ПолноеИмяФайла);
	
	Возврат БазаВыгрузки.открыта();
КонецФункции

// ВернутьСписокАдресов
//
// Параметры
//  Физлицо  - ссылка на физическое лицо
//
// Возвращаемое значение:
//   Результат выборки   - таблица значений

//
Функция ВернутьСписокАдресов(Физлицо)

    ЗапросАдрес = Новый Запрос;
    ЗапросАдрес.Текст = 
        "ВЫБРАТЬ
        |   КонтактнаяИнформация.Объект,
        |	КонтактнаяИнформация.Тип,
		|	КонтактнаяИнформация.Вид,
		|	КонтактнаяИнформация.Поле1,
		|	КонтактнаяИнформация.Поле2,
		|	КонтактнаяИнформация.Поле3,
		|	КонтактнаяИнформация.Поле4,
		|	КонтактнаяИнформация.Поле5,
		|	КонтактнаяИнформация.Поле6,
		|	КонтактнаяИнформация.Поле7,
		|	КонтактнаяИнформация.Поле8,
		|	КонтактнаяИнформация.Поле9,
		|	КонтактнаяИнформация.Поле10,
		|	КонтактнаяИнформация.Комментарий,
		|	КонтактнаяИнформация.Поле11,
		|	КонтактнаяИнформация.Поле12,
		|	КонтактнаяИнформация.Поле13,
        |	КонтактнаяИнформация.Представление
        |ИЗ
        |   РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
        |ГДЕ
        |   КонтактнаяИнформация.Объект = &ФизЛицо
        |	И
        |	КонтактнаяИнформация.Тип = &Тип";

    ЗапросАдрес.УстановитьПараметр("ФизЛицо", Физлицо.Ссылка);
    ЗапросАдрес.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Адрес);
    
    РезультатАдрес = ЗапросАдрес.Выполнить();

    Возврат РезультатАдрес.Выгрузить();
    
КонецФункции // ВернутьСписокАдресов()
 
Функция ВыгрузитьСотрудников(Источник,БазаВыгрузки,ВыбранныйВидСправочника)
    
    СтрокаАдреса =  Неопределено;

    Если ВыбранныйВидСправочника = "СотрудникиОрганизаций" Тогда
        Физлицо = Источник.ФизЛицо.Ссылка;
        СписокАдресов = ВернутьСписокАдресов(Физлицо);
        Если СписокАдресов.Количество() = 0 Тогда
            ВыгрузитьСотрудника(Источник,БазаВыгрузки,СтрокаАдреса);
        Иначе
            Для каждого СтрокаАдреса Из СписокАдресов Цикл
                ВыгрузитьСотрудника(Источник,БазаВыгрузки,СтрокаАдреса);
            КонецЦикла; 
        КонецЕсли; 
        
    Иначе
        Физлицо = Источник;
        СписокАдресов = ВернутьСписокАдресов(Физлицо);
        Запрос = Новый Запрос;
        Запрос.Текст = 
        "ВЫБРАТЬ
        |	СотрудникиОрганизаций.Ссылка
        |ИЗ
        |	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
        |ГДЕ
        |	СотрудникиОрганизаций.Физлицо.Ссылка = &Физлицо";
        Запрос.УстановитьПараметр("Физлицо", Физлицо.Ссылка);
        
        Результат = Запрос.Выполнить();
        
        Выборка = Результат.Выбрать();
        
        Пока Выборка.Следующий() Цикл
            Если СписокАдресов.Количество() = 0 Тогда
                ВыгрузитьСотрудника(Выборка.Ссылка,БазаВыгрузки,СтрокаАдреса);
            Иначе
                Для каждого СтрокаАдреса Из СписокАдресов Цикл
                    ВыгрузитьСотрудника(Выборка.Ссылка,БазаВыгрузки,СтрокаАдреса);
                КонецЦикла; 
            КонецЕсли; 
        КонецЦикла;
    КонецЕсли;
    
    Возврат Истина;
КонецФункции

Функция ВыгрузитьСотрудника(Сотрудник,БазаВыгрузки,СтрокаАдреса)
	
	Пометка = 1;
	Если Сотрудник.ПометкаУдаления Тогда
		Пометка = -1;
	КонецЕсли;
	
	ФизЛицо = Сотрудник.Физлицо.Ссылка;
	БазаВыгрузки.Добавить();
	БазаВыгрузки.УстановитьЗначениеПоля("GUID", Сотрудник.Ссылка.УникальныйИдентификатор());
	БазаВыгрузки.УстановитьЗначениеПоля("FIO",  СокрЛП(ФизЛицо.Наименование));
    БазаВыгрузки.УстановитьЗначениеПоля("DataRogd",Формат(ФизЛицо.ДатаРождения, "ДФ=dd.MM.yyyy"));
    БазаВыгрузки.УстановитьЗначениеПоля("Pol",Лев(ФизЛицо.Пол,1));
    
    // паспортные данные
    ЗапросПаспорт = Новый Запрос;
    ЗапросПаспорт.Текст = 
        "ВЫБРАТЬ
        |   ПаспортныеДанныеФизЛицСрезПоследних.Период,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДокументДатаВыдачи,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДокументКодПодразделения,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДатаРегистрацииПоМестуЖительства,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ЛичныйНомер,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДатаСрокДействия,
        |   ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан1
        |ИЗ
        |   РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних КАК ПаспортныеДанныеФизЛицСрезПоследних
        |ГДЕ
        |   ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо = &ФизЛицо";

    ЗапросПаспорт.УстановитьПараметр("ФизЛицо", ФизЛицо);
    
    РезультатПаспорт = ЗапросПаспорт.Выполнить().Выбрать();
    VD_GUID = "";
    VD_Naim = "";
    DSeria  = "";
    DNomer  = "";
    DataVyd = "";
    SrokDeis = "";
    OVD_GUID = "";
    LichNom = "";
    DataReg = "";
    DataDeis = "";
    Если РезультатПаспорт.Следующий() Тогда
        VD_GUID = РезультатПаспорт.ДокументВид.УникальныйИдентификатор();
        VD_Naim = РезультатПаспорт.ДокументВид.Наименование;
        DSeria  = РезультатПаспорт.ДокументСерия;
        DNomer  = РезультатПаспорт.ДокументНомер;
        DataVyd = Формат(РезультатПаспорт.ДокументДатаВыдачи, "ДФ=dd.MM.yyyy");
        SrokDeis = Формат(РезультатПаспорт.ДатаСрокДействия, "ДФ=dd.MM.yyyy");
        OVD_GUID = РезультатПаспорт.ДокументКемВыдан1.УникальныйИдентификатор();
        OVD_NAIM = РезультатПаспорт.ДокументКемВыдан1.Наименование;
        LichNom = РезультатПаспорт.ЛичныйНомер;
        DataReg = Формат(РезультатПаспорт.ДатаРегистрацииПоМестуЖительства, "ДФ=dd.MM.yyyy");
        DataDeis = Формат(РезультатПаспорт.Период, "ДФ=dd.MM.yyyy");
    КонецЕсли; 
    БазаВыгрузки.УстановитьЗначениеПоля("VD_GUID",VD_GUID);
    БазаВыгрузки.УстановитьЗначениеПоля("VD_Naim",VD_Naim);
    БазаВыгрузки.УстановитьЗначениеПоля("DSeria",DSeria);
    БазаВыгрузки.УстановитьЗначениеПоля("DNomer",DNomer);
    БазаВыгрузки.УстановитьЗначениеПоля("DataVyd",DataVyd);
    БазаВыгрузки.УстановитьЗначениеПоля("SrokDeis",SrokDeis);
    БазаВыгрузки.УстановитьЗначениеПоля("OVD_GUID",OVD_GUID);
    БазаВыгрузки.УстановитьЗначениеПоля("OVD_NAIM",OVD_NAIM);
    БазаВыгрузки.УстановитьЗначениеПоля("LichNom",LichNom);
    БазаВыгрузки.УстановитьЗначениеПоля("DataReg",DataReg);
    БазаВыгрузки.УстановитьЗначениеПоля("DataDeis",DataDeis);
    
    // адресная часть
    Если СтрокаАдреса = Неопределено Тогда
        
    Иначе
        
        ПроизвольныйАдрес = (УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдреса(СтрокаАдреса) <> СтрокаАдреса.Представление);
        
        ВидАдреса = СтрокаАдреса.Вид;        
        БазаВыгрузки.УстановитьЗначениеПоля("ADR_GUID",ВидАдреса.Ссылка.УникальныйИдентификатор());
        БазаВыгрузки.УстановитьЗначениеПоля("ADR_Kod",СокрЛП(ВидАдреса.Код));
        БазаВыгрузки.УстановитьЗначениеПоля("ADR_Nain",СокрЛП(ВидАдреса.Наименование));

        Если ПроизвольныйАдрес = Истина Тогда
            АдресРБ = 0;
        Иначе
            АдресРБ = 1;
        КонецЕсли;
        
        БазаВыгрузки.УстановитьЗначениеПоля("ADR_RB",АдресРБ);
        БазаВыгрузки.УстановитьЗначениеПоля("SOATO",СокрЛП(СтрокаАдреса.Поле13));
        БазаВыгрузки.УстановитьЗначениеПоля("KodIMNS",СокрЛП(СтрокаАдреса.Поле12));
        БазаВыгрузки.УстановитьЗначениеПоля("Oblast",СокрЛП(СтрокаАдреса.Поле2));
        БазаВыгрузки.УстановитьЗначениеПоля("Raion",СокрЛП(СтрокаАдреса.Поле3));
        БазаВыгрузки.УстановитьЗначениеПоля("S_Sovet",СокрЛП(СтрокаАдреса.Поле5));
        БазаВыгрузки.УстановитьЗначениеПоля("Gorod",СокрЛП(СтрокаАдреса.Поле4));
        БазаВыгрузки.УстановитьЗначениеПоля("Ulitsa",СокрЛП(СтрокаАдреса.Поле6));
        БазаВыгрузки.УстановитьЗначениеПоля("NomDom",СокрЛП(СтрокаАдреса.Поле7));
        БазаВыгрузки.УстановитьЗначениеПоля("Korpus",СокрЛП(СтрокаАдреса.Поле8));
        БазаВыгрузки.УстановитьЗначениеПоля("Kvart",СокрЛП(СтрокаАдреса.Поле9));
        БазаВыгрузки.УстановитьЗначениеПоля("Index",СокрЛП(СтрокаАдреса.Поле1));
        БазаВыгрузки.УстановитьЗначениеПоля("Komment",СокрЛП(СтрокаАдреса.Представление));
    КонецЕсли; 
    
	// гражданство
    ЗапросГражданства = Новый Запрос;
    ЗапросГражданства.Текст =
    "ВЫБРАТЬ
    |   ГражданствоФизЛицСрезПоследних.ФизЛицо,
    |   ГражданствоФизЛицСрезПоследних.Страна,
    |   ГражданствоФизЛицСрезПоследних.НеИмеетПравоНаПенсию,
    |   ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ
    |ИЗ
    |   РегистрСведений.ГражданствоФизЛиц.СрезПоследних КАК ГражданствоФизЛицСрезПоследних
    |ГДЕ
    |   ГражданствоФизЛицСрезПоследних.ФизЛицо = &ФизЛицо";
    ЗапросГражданства.УстановитьПараметр("Физлицо", ФизЛицо);
    Результат = ЗапросГражданства.Выполнить().Выбрать();
        
    STR_GUID = "";
    STR_Kod  = "";
    BEZ_Gr   = ложь;
    Pensia   = 0;
    Rezident = 0;
    Если Результат.Следующий() Тогда
        STR_GUID = Результат.Страна.Ссылка.УникальныйИдентификатор();
        STR_Kod  = Результат.Страна.Ссылка.Код;
        BEZ_Gr   = Результат.Страна = Справочники.КлассификаторСтранМира.ЛицоБезГражданства;
        Pensia   = ?(Результат.НеИмеетПравоНаПенсию = ложь,0,1);
        Rezident = ?(Результат.НеЯвляетсяНалоговымРезидентомРФ = ложь, 0,1);
    КонецЕсли;
    Базавыгрузки.УстановитьЗначениеПоля("STR_GUID",STR_GUID);
    Базавыгрузки.УстановитьЗначениеПоля("STR_Kod",STR_Kod);
    Базавыгрузки.УстановитьЗначениеПоля("BEZ_Gr",?(BEZ_Gr = ложь,0,1));
    Базавыгрузки.УстановитьЗначениеПоля("Pensia",Pensia);
    Базавыгрузки.УстановитьЗначениеПоля("Rezident",Rezident);
    
	Тектип = Сотрудник.ТипРаботника;
	Если Тектип = Перечисления.ТипыРаботников.Временный Тогда
		ТипРаботника = 2;
	ИначеЕсли Тектип = Перечисления.ТипыРаботников.Совместитель Тогда
		ТипРаботника = 3;
	ИначеЕсли Тектип = Перечисления.ТипыРаботников.ДоговорПодряда Тогда
		ТипРаботника = 4;
	ИначеЕсли Тектип = Перечисления.ТипыРаботников.Практикант Тогда
		ТипРаботника = 5;
	ИначеЕсли Тектип = Перечисления.ТипыРаботников.НесписочныйСостав Тогда
		ТипРаботника = 6;
	Иначе
		ТипРаботника = 1;
    КонецЕсли;
    ВидЗанятости = 0;
    Если  Сотрудник.ВидЗанятости = Перечисления.ВидыЗанятостиВОрганизации.Совместительство Тогда
        ВидЗанятости = 2;
    ИначеЕсли  Сотрудник.ВидЗанятости = Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство Тогда
        ВидЗанятости = 3;
    ИначеЕсли  Сотрудник.ВидЗанятости = Перечисления.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы Тогда
        ВидЗанятости = 1;
    КонецЕсли;
    
	БазаВыгрузки.УстановитьЗначениеПоля("TipRab", ТипРаботника);
    БазаВыгрузки.УстановитьЗначениеПоля("VidRab", ВидЗанятости);
	БазаВыгрузки.УстановитьЗначениеПоля("TabNomer",Сотрудник.Код);
	Базавыгрузки.УстановитьЗначениеПоля("Del",Пометка);
	БазаВыгрузки.Записать();
    
    Возврат Истина;	
КонецФункции

Функция ВыгрузитьПрочие(Источник,БазаВыгрузки,ВыбранныйВидСправочника)
	
	Пометка = 1;
	Если Источник.ПометкаУдаления Тогда
		Пометка = -1;
	КонецЕсли;
	
	БазаВыгрузки.Добавить();
	БазаВыгрузки.УстановитьЗначениеПоля("GUID", Источник.Ссылка.УникальныйИдентификатор());
	БазаВыгрузки.УстановитьЗначениеПоля("Kod",  СокрЛП(Источник.Код));
	БазаВыгрузки.УстановитьЗначениеПоля("Naim",  СокрЛП(Источник.Наименование));
	Если ВыбранныйВидСправочника = "ПодразделенияОрганизаций" Тогда
		БазаВыгрузки.УстановитьЗначениеПоля("GUIDRod", Источник.Родитель.УникальныйИдентификатор());
        Базавыгрузки.УстановитьЗначениеПоля("GROUP",ИмеетВложения(Источник));
    КонецЕсли;
	Базавыгрузки.УстановитьЗначениеПоля("Del",Пометка);
	БазаВыгрузки.Записать();
	
	Возврат Истина;
КонецФункции

// Проверяет наличие вложенных элементов
// Параметры
//  <Источник>  - проверяемый объект справочника
//
// Возвращаемое значение:
// 1   - имеет вложенные элементы, 0 - не имеет
//
Функция ИмеетВложения(Источник)

    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |   ПодразделенияОрганизаций.Родитель.Ссылка
        |ИЗ
        |   Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
        |ГДЕ
        |   ПодразделенияОрганизаций.Родитель.Ссылка = &Ссылка";

    Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);

    Результат = Запрос.Выполнить().Пустой();

    Если Результат = Истина Тогда
    	Возврат 0;
    Иначе
        Возврат 1;
    КонецЕсли; 

КонецФункции // ИмеетВложения()
 
	