
#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Функция возвращают строку-представление двух чисел
//
Функция ПолучитьСтрокуИзДвухЧисел(Число1,Число2)
	Если Число1=Число2 Тогда
		Возврат ФормированиеПечатныхФорм.ФорматСумм(Число1,,,,ДатаАктуальности);
	Иначе
		Возврат ""+ФормированиеПечатныхФорм.ФорматСумм(Число1,,,,ДатаАктуальности)+" - "+ФормированиеПечатныхФорм.ФорматСумм(Число2,,,,ДатаАктуальности);
	КонецЕсли;
КонецФункции	 

// Выводит строки по подразделениям очередного уровня
Процедура ВывестиСтрокиПодразделения(ВыборкаСтрокШтРасписания, ВыведеноСтрокВОтчет, КоличествоСтрокОтчета, ДокументРезультат, ОбластьСтрока, ОбластьПодвалЛиста, ОбластьШапкаЛиста, 
	    ВыводимаяСтрока,  КоличествоСтавокЛиста, ФондЛиста , ВыводимыеОбласти, Макет)
		
	КурсВалютыОтчета = ОбщегоНазначения.ПолучитьКурсВалюты(Константы.ВалютаРегламентированногоУчета.Получить(),ДатаАктуальности).Курс;
	
	ТаблицаПовышений = Новый ТаблицаЗначений;
	ТаблицаПовышений.Колонки.Добавить("ВидКоэф");
	ТаблицаПовышений.Колонки.Добавить("РазмерКоэф");
	ТаблицаПовышений.Колонки.Добавить("ВидПовышения");
	ТаблицаПовышений.Колонки.Добавить("РазмерПовышения");
	ТаблицаПовышений.Колонки.Добавить("ВидНадбавкиПредставление");
	ТаблицаПовышений.Колонки.Добавить("ПроцентНадбавки");
	ТаблицаПовышений.Колонки.Добавить("РезультатНадбавки");
	
	ЗапросКоэф = Новый Запрос;
	
	ТекстЗапросаКоэф =
	"ВЫБРАТЬ
	|	ПлановоеШтатноеРасписаниеОрганизацийКоэфПовышенияТО.Коэффициент КАК ВидКоэф,
	|	ПлановоеШтатноеРасписаниеОрганизацийКоэфПовышенияТО.Размер КАК РазмерКоэф
	|ИЗ
	|	Документ.ПлановоеШтатноеРасписаниеОрганизаций.КоэфПовышенияТО КАК ПлановоеШтатноеРасписаниеОрганизацийКоэфПовышенияТО
	|ГДЕ
	|	ПлановоеШтатноеРасписаниеОрганизацийКоэфПовышенияТО.КодСтроки = &КодСтроки
	|	И ПлановоеШтатноеРасписаниеОрганизацийКоэфПовышенияТО.Ссылка = &Документ";	
	
	
	ЗапросКоэф.УстановитьПараметр("Документ",	ДокументОснование);
		
	ЗапросКоэф.Текст = ТекстЗапросаКоэф;
	
	ЗапросПовышения = Новый Запрос;
	
	ТекстЗапросаПовышения =
	"ВЫБРАТЬ
	|	ПлановоеШтатноеРасписаниеОрганизацийПовышенияПоИнструкции.Размер КАК РазмерПовышения,
	|	ПлановоеШтатноеРасписаниеОрганизацийПовышенияПоИнструкции.Коэффициент.Представление КАК ВидПовышения
	|ИЗ
	|	Документ.ПлановоеШтатноеРасписаниеОрганизаций.ПовышенияПоИнструкции КАК ПлановоеШтатноеРасписаниеОрганизацийПовышенияПоИнструкции
	|ГДЕ
	|	ПлановоеШтатноеРасписаниеОрганизацийПовышенияПоИнструкции.КодСтроки = &КодСтроки
	|	И ПлановоеШтатноеРасписаниеОрганизацийПовышенияПоИнструкции.Ссылка = &Документ";
	
	
	
	ЗапросПовышения.УстановитьПараметр("Документ",	ДокументОснование);
	
	ЗапросПовышения.Текст = ТекстЗапросаПовышения;
	
	ЗапросНадбавки = Новый Запрос;
	
	ТекстЗапросаНадбавки =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПлановоеШтатноеРасписаниеОрганизацийНадбавки.ВидНадбавки.СпособРасчета = &НадбавкаПроцентом
	|			ТОГДА ПлановоеШтатноеРасписаниеОрганизацийНадбавки.Показатель1
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПлановоеШтатноеРасписаниеОрганизацийНадбавки.ВидНадбавки.СпособРасчета = &НадбавкаПроцентомОтТС
	|					ТОГДА ПлановоеШтатноеРасписаниеОрганизацийНадбавки.Показатель2
	|			КОНЕЦ
	|	КОНЕЦ КАК ПроцентНадбавки,
	|	ВЫБОР
	|		КОГДА ПлановоеШтатноеРасписаниеОрганизацийНадбавки.ВидНадбавки.СпособРасчета <> &НадбавкаПроцентом
	|			ТОГДА ПлановоеШтатноеРасписаниеОрганизацийНадбавки.Показатель1
	|	КОНЕЦ КАК РезультатНадбавки,
	|	ПлановоеШтатноеРасписаниеОрганизацийНадбавки.ВидНадбавки.Представление КАК ВидНадбавкиПредставление
	|ИЗ
	|	Документ.ПлановоеШтатноеРасписаниеОрганизаций.Надбавки КАК ПлановоеШтатноеРасписаниеОрганизацийНадбавки
	|ГДЕ
	|	ПлановоеШтатноеРасписаниеОрганизацийНадбавки.Ссылка = &Документ
	|	И ПлановоеШтатноеРасписаниеОрганизацийНадбавки.КодСтроки = &КодСтроки";
	
	
	ЗапросНадбавки.УстановитьПараметр("Документ",	ДокументОснование);

	ЗапросНадбавки.УстановитьПараметр("НадбавкаПроцентом",	Перечисления.СпособыРасчетаОплатыТруда.Процентом);
	ЗапросНадбавки.УстановитьПараметр("НадбавкаПроцентомОтТС",	Перечисления.СпособыРасчетаОплатыТруда.ПроцентОтТарифнойСтавки);
	ЗапросНадбавки.Текст = ТекстЗапросаНадбавки;
		
		
		
	//КоличествоНадбавок = СписокНадбавок.Количество();
		
	Пока ВыборкаСтрокШтРасписания.СледующийПоЗначениюПоля("ПодразделениеОрганизации") Цикл 
		
		ВыборкаПодразделенийСледующегоУровня = ВыборкаСтрокШтРасписания.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		КоличествоСтавок = 0;
				
		//РБ
		Фонд = 0;
		ТарифнаяСтавка = 0;
		Ставка =0;
		
		Пока ВыборкаСтрокШтРасписания.Следующий() Цикл 
			
			ТаблицаПовышений.Очистить();
			// Готовим строку отчета для последующего вывода
			ВыводимаяСтрока.Очистить();
			ВыведеноНадбавок = 0;
			//ЗапросКоэф.УстановитьПараметр("Подразделение",ВыборкаСтрокШтРасписания.ПодразделениеОрганизации);
			//ЗапросКоэф.УстановитьПараметр("Должность",ВыборкаСтрокШтРасписания.Должность); 
			//ЗапросКоэф.УстановитьПараметр("ВидДеятельности",ВыборкаСтрокШтРасписания.ВидДеятельностиИзмерение); 
			//ЗапросКоэф.УстановитьПараметр("УсловияТруда",ВыборкаСтрокШтРасписания.УсловияТрудаИзмерение); 
			ЗапросКоэф.УстановитьПараметр("КодСтроки",ВыборкаСтрокШтРасписания.КодСтроки); 
			//
			ВременнаяТаблица = ЗапросКоэф.Выполнить().Выгрузить();
			Для Н=1 По ВременнаяТаблица.Количество() Цикл
				ТаблицаПовышений.Добавить();
			КонецЦикла;
			
			ТаблицаПовышений.ЗагрузитьКолонку(ВременнаяТаблица.ВыгрузитьКолонку("ВидКоэф"),"ВидКоэф");
			ТаблицаПовышений.ЗагрузитьКолонку(ВременнаяТаблица.ВыгрузитьКолонку("РазмерКоэф"),"РазмерКоэф");

			//ЗапросПовышения.УстановитьПараметр("Подразделение",ВыборкаСтрокШтРасписания.ПодразделениеОрганизации);
			//ЗапросПовышения.УстановитьПараметр("Должность",ВыборкаСтрокШтРасписания.Должность); 
			//ЗапросПовышения.УстановитьПараметр("ВидДеятельности",ВыборкаСтрокШтРасписания.ВидДеятельностиИзмерение); 
			//ЗапросПовышения.УстановитьПараметр("УсловияТруда",ВыборкаСтрокШтРасписания.УсловияТрудаИзмерение);
			ЗапросПовышения.УстановитьПараметр("КодСтроки",ВыборкаСтрокШтРасписания.КодСтроки);
			
			ВременнаяТаблица = ЗапросПовышения.Выполнить().Выгрузить();
			Если ДатаАктуальности < НачалоДня(Дата("20090901")) Тогда	
				Если ЗначениеЗаполнено(ВыборкаСтрокШтРасписания.ПроцентПовышения) тогда
					ПроцентПоКонтрактуСтрока  = ВременнаяТаблица.Добавить();
					ПроцентПоКонтрактуСтрока.ВидПовышения = "Процент по контракту";
					ПроцентПоКонтрактуСтрока.РазмерПовышения = ВыборкаСтрокШтРасписания.ПроцентПовышения;
               	КонецЕсли;
			КонецЕсли;
				
			Для Н=1 По ВременнаяТаблица.Количество() - ТаблицаПовышений.Количество() Цикл
				ТаблицаПовышений.Добавить();
			КонецЦикла;
			
			ТаблицаПовышений.ЗагрузитьКолонку(ВременнаяТаблица.ВыгрузитьКолонку("ВидПовышения"),"ВидПовышения");
			ТаблицаПовышений.ЗагрузитьКолонку(ВременнаяТаблица.ВыгрузитьКолонку("РазмерПовышения"),"РазмерПовышения");
			
			//ЗапросНадбавки.УстановитьПараметр("Подразделение",ВыборкаСтрокШтРасписания.ПодразделениеОрганизации);
			//ЗапросНадбавки.УстановитьПараметр("Должность",ВыборкаСтрокШтРасписания.Должность); 
			//ЗапросНадбавки.УстановитьПараметр("ВидДеятельности",ВыборкаСтрокШтРасписания.ВидДеятельностиИзмерение); 
			//ЗапросНадбавки.УстановитьПараметр("УсловияТруда",ВыборкаСтрокШтРасписания.УсловияТрудаИзмерение);
			ЗапросНадбавки.УстановитьПараметр("КодСтроки",ВыборкаСтрокШтРасписания.КодСтроки);
			
			ТаблицаНадбавок = ЗапросНадбавки.Выполнить().Выгрузить();
			КурсТарифнойСтавки = ОбщегоНазначения.ПолучитьКурсВалюты(ВыборкаСтрокШтРасписания.ВалютаТарифнойСтавки,ДатаАктуальности).Курс;
			ФондНадбавок = 0;
			Для Каждого Строка Из ТаблицаНадбавок Цикл
				Если ЗначениеЗаполнено(Строка.ПроцентНадбавки) тогда
					Строка.РезультатНадбавки = ОбщегоНазначения.ОкруглитьПоВалюте(Строка.ПроцентНадбавки*ВыборкаСтрокШтРасписания.РасчетнаяТарифнаяСтавка/100*КурсТарифнойСтавки/КурсВалютыОтчета,Константы.ВалютаРегламентированногоУчета.Получить());
				Иначе
					Строка.РезультатНадбавки =  Строка.РезультатНадбавки*КурсТарифнойСтавки/КурсВалютыОтчета;
				КонецЕсли;
				ФондНадбавок = ФондНадбавок + Строка.РезультатНадбавки;
			КонецЦикла;
			
			Для Н=1 По ТаблицаНадбавок.Количество() - ТаблицаПовышений.Количество() Цикл
				ТаблицаПовышений.Добавить();
			КонецЦикла;
			
			ТаблицаПовышений.ЗагрузитьКолонку(ТаблицаНадбавок.ВыгрузитьКолонку("ВидНадбавкиПредставление"),"ВидНадбавкиПредставление");
			ТаблицаПовышений.ЗагрузитьКолонку(ТаблицаНадбавок.ВыгрузитьКолонку("ПроцентНадбавки"),"ПроцентНадбавки");
			ТаблицаПовышений.ЗагрузитьКолонку(ТаблицаНадбавок.ВыгрузитьКолонку("РезультатНадбавки"),"РезультатНадбавки");

	 		КоличествоСтавок = ВыборкаСтрокШтРасписания.КоличествоСтавок;
			Фонд = ?(ВыборкаСтрокШтРасписания.РасчетнаяТарифнаяСтавка <> NULL,(ВыборкаСтрокШтРасписания.РасчетнаяТарифнаяСтавка+ФондНадбавок)*ВыборкаСтрокШтРасписания.КоличествоСтавок,0);
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтрокШтРасписания);
			ОбластьСтрока.Параметры.РазмерКоэф = "";
			ОбластьСтрока.Параметры.РазмерПовышения = "";
			ОбластьСтрока.Параметры.ВидКоэф = "";
			ОбластьСтрока.Параметры.ВидПовышения = "";
			ОбластьСтрока.Параметры.МесячныйФонд = Фонд;
			СтрокНадбавок = 0;
			Если ТаблицаПовышений.Количество() > 0 тогда
				
				Для Каждого СтрокаНадбавок Из ТаблицаПовышений Цикл
					СтрокНадбавок = СтрокНадбавок + 1;
					Если СтрокНадбавок = 1 тогда 
						ОбластьСтрока.Параметры.Заполнить(СтрокаНадбавок);
					Иначе
						Для Сч = 1 По ОбластьСтрока.Параметры.Количество() Цикл
							ОбластьСтрока.Параметры.Установить(Сч - 1,""); 
						КонецЦикла;
                      	ОбластьСтрока.Параметры.Заполнить(СтрокаНадбавок);
					КонецЕсли;
					Если СтрокНадбавок = ТаблицаПовышений.Количество() тогда
						ОбластьСтрока.Области.СтрокаПодчеркиваение.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1); 
					Иначе
						ОбластьСтрока.Области.СтрокаПодчеркиваение.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии,0);
                    КонецЕсли;
					ВыводимаяСтрока.Вывести(ОбластьСтрока);
					ВыведеноСтрокВОтчет = ВыведеноСтрокВОтчет + 1; 
				КонецЦикла;
			Иначе
				ВыводимаяСтрока.Вывести(ОбластьСтрока);
				ВыведеноСтрокВОтчет = ВыведеноСтрокВОтчет + 1;
			КонецЕсли;
						
			// Проверим, уместится ли строка на странице или надо открывать новую страницу
			ВывестиПодвалЛиста = Не ДокументРезультат.ПроверитьВывод(ВыводимыеОбласти);
			//Если Не ВывестиПодвалЛиста и ВыведеноСтрокВОтчет = КоличествоСтрокОтчета Тогда
			//	// Для последней строки отчета дополнительно проверим, 
			//	// уместится ли на этой же странице подвал всего отчета
			//	ВыводимыеОбласти.Добавить(Макет.ПолучитьОбласть("Подвал"));
			//	ВывестиПодвалЛиста = Не ДокументРезультат.ПроверитьВывод(ВыводимыеОбласти);
			//КонецЕсли;	
			//
			// Если нужно, выводим подвал листа
			Если ВывестиПодвалЛиста Тогда
				ОбластьПодвалЛиста.Параметры.КоличествоСтавок = КоличествоСтавокЛиста;
							
				ОбластьПодвалЛиста.Параметры.МесячныйФонд = ФондЛиста;
				ДокументРезультат.Вывести(ОбластьПодвалЛиста);
				
				//Начнем новую страницу
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				КоличествоСтавокЛиста = 0;
				ФондЛиста = 0;
				
				//Выводим шапку нового листа
				ДокументРезультат.Вывести(ОбластьШапкаЛиста);
			КонецЕсли;
			
			//Теперь выводим строку
			ДокументРезультат.Вывести(ВыводимаяСтрока);
			
			//Увеличиваем счетчики по листу
			КоличествоСтавокЛиста = КоличествоСтавокЛиста + КоличествоСтавок;
			ФондЛиста = ФондЛиста + Фонд;
		КонецЦикла;//цикл по должностям
		
		ВывестиСтрокиПодразделения(ВыборкаПодразделенийСледующегоУровня, ВыведеноСтрокВОтчет, КоличествоСтрокОтчета, ДокументРезультат, ОбластьСтрока, ОбластьПодвалЛиста, ОбластьШапкаЛиста, 
			 ВыводимаяСтрока,КоличествоСтавокЛиста,ФондЛиста, ВыводимыеОбласти, Макет)
		
	КонецЦикла;//цикл по подразделениям текущего уровня 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//  Дописать - булево.  Истина - табличный документ дописывается формируемым отчетом, 
//                      Ложь - заполняется формируемым отчетом (с предварительной очистой 
//                      табличного документа)
//
Процедура СформироватьОтчет(ДокументРезультат, Дописать = Ложь) Экспорт

	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Расписание.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Расписание.Должность КАК Должность,
	|	Расписание.ВидТарифнойСтавки КАК ВидТарифнойСтавки,
	|	Расписание.РазрядЕТС КАК РазрядЕТС,
	|	Расписание.КоэффициентЕТС КАК КоэфЕТС,
	|	Расписание.ПроцентПоКонтракту КАК ПроцентПовышения,
	|	Расписание.ТарифнаяСтавка КАК ТарифнаяСтавка,
	|	Расписание.ВидДеятельностиИзмерение КАК ВидДеятельностиИзмерение,
	|	Расписание.УсловияТрудаИзмерение КАК УсловияТрудаИзмерение,
	|	Расписание.ПовышенныйКоэффициентЕТС,
	|	Расписание.ИтоговоеПовышениеПоИнструкции,
	|	Расписание.Должность.Представление КАК ДолжностьПредставление,
	|	Расписание.ПодразделениеОрганизации.Представление КАК ПодразделениеОрганизацииПредставление,
	|	Расписание.ЗанимаемыхСтавок КАК КоличествоСтавок,
	|	Расписание.МесячнаяТарифнаяСтавка КАК РасчетнаяТарифнаяСтавка,
	|	Расписание.РазмерСтавки1Разряда КАК РазмерСтавкиПервогоРазряда,
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА Надбавки.ВидНадбавки.СпособРасчета = &НадбавкаПроцентом
	|					ТОГДА Расписание.МесячнаяТарифнаяСтавка * Надбавки.Показатель1 / 100
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Надбавки.ВидНадбавки.СпособРасчета = &НадбавкаПроцентомОтТС
	|							ТОГДА Расписание.МесячнаяТарифнаяСтавка * Надбавки.Показатель2 / 100
	|						ИНАЧЕ Надбавки.Показатель1
	|					КОНЕЦ
	|			КОНЕЦ КАК ЧИСЛО(15, 0))) КАК СуммаНадбавки,
	|	&Валюта КАК ВалютаТарифнойСтавки,
	|	Расписание.Сотрудник,
	|	Расписание.КодСтроки
	|ИЗ
	|	Документ.ПлановоеШтатноеРасписаниеОрганизаций.ШтатныеЕдиницы КАК Расписание
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлановоеШтатноеРасписаниеОрганизаций.Надбавки КАК Надбавки
	|		ПО Расписание.Ссылка = Надбавки.Ссылка
	|			И Расписание.КодСтроки = Надбавки.КодСтроки
	|ГДЕ
	|	Расписание.ПодразделениеОрганизации.Владелец = &Организация
	|	И Расписание.Ссылка = &Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	Расписание.ПодразделениеОрганизации,
	|	Расписание.Должность,
	|	Расписание.ВидТарифнойСтавки,
	|	Расписание.РазрядЕТС,
	|	Расписание.КоэффициентЕТС,
	|	Расписание.ПроцентПоКонтракту,
	|	Расписание.ТарифнаяСтавка,
	|	Расписание.ВидДеятельностиИзмерение,
	|	Расписание.УсловияТрудаИзмерение,
	|	Расписание.ПовышенныйКоэффициентЕТС,
	|	Расписание.ИтоговоеПовышениеПоИнструкции,
	|	Расписание.Должность.Представление,
	|	Расписание.ПодразделениеОрганизации.Представление,
	|	Расписание.ЗанимаемыхСтавок,
	|	Расписание.МесячнаяТарифнаяСтавка,
	|	Расписание.РазмерСтавки1Разряда,
	|	Расписание.Сотрудник,
	|	Расписание.КодСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодразделениеОрганизации ИЕРАРХИЯ,
	|	КоэфЕТС УБЫВ";
 
	Запрос.УстановитьПараметр("ДатаАктуальности",	ДатаАктуальности);
	Запрос.УстановитьПараметр("Организация",		Организация);   
	Запрос.УстановитьПараметр("Подразделение",      Подразделение);

	Запрос.УстановитьПараметр("НадбавкаПроцентом",	Перечисления.СпособыРасчетаОплатыТруда.Процентом);
	Запрос.УстановитьПараметр("НадбавкаПроцентомОтТС",	Перечисления.СпособыРасчетаОплатыТруда.ПроцентОтТарифнойСтавки);
	Запрос.УстановитьПараметр("Документ",			ДокументОснование);
//	КурсВалютыОтчета = ОбщегоНазначения.ПолучитьКурсВалюты(Константы.ВалютаРегламентированногоУчета.Получить(),ДатаАктуальности).Курс;
	Запрос.УстановитьПараметр("Валюта",	Константы.ВалютаРегламентированногоУчета.Получить());

	Если Не Подразделение.Пустая() Тогда
    	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПодразделениеОрганизации.Владелец = &Организация","ПодразделениеОрганизации В ИЕРАРХИИ(&Подразделение)")
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	МассивВидовРуководителей = Новый Массив();
	МассивВидовРуководителей.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	МассивВидовРуководителей.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	МассивВидовРуководителей.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидЛица",		  МассивВидовРуководителей);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.УстановитьПараметр("Организация",      Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Представление КАК ДолжностьРуководителя,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
	|	ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо
	|ИЗ
	|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|		&ДатаАктуальности,
	|		ОтветственноеЛицо В (&ВидЛица)
	|		    И СтруктурнаяЕдиница = &Организация) КАК ОтветственныеЛицаОрганизацийСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаАктуальности, ) КАК ФИОФизЛицСрезПоследних
	|		ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо";
	
	РезультатДляПодвала = Запрос.Выполнить();
	
	
	
	
	
	Макет = ПолучитьМакет("ШтатноеРасписаниеРБ");
    Если не Дописать Тогда
	    ДокументРезультат.Очистить();
    КонецЕсли;
    
	//Параметры документа
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ПолеСлева			 = 0;
	ДокументРезультат.ПолеСправа		 = 0;
	ДокументРезультат.Защита             = Ложь;
	ДокументРезультат.ТолькоПросмотр     = Истина;
	
	ФондОрганизации = 0;
	ВсегоСтавокОрганизации = 0;
	
	//Первый обход выборки: Заполняем список названий надбавок и рассчитываем итоги по организации для вывода шапки
	СписокНадбавок = Новый СписокЗначений;
	ВыборкаДетали = РезультатЗапроса.Выбрать();
	КоличествоСтрокОтчета = ВыборкаДетали.Количество();
	ПерваяДолжность = Истина;
	
	// массив подразделений для подписи
	СписокПодразделений = Новый Массив();
	
	Пока ВыборкаДетали.СледующийПоЗначениюПоля("ПодразделениеОрганизации") Цикл 
		
		СписокПодразделений.Добавить(ВыборкаДетали.ПодразделениеОрганизации);
		
		КоличествоСтавок = 0;
		Фонд = 0;
		Ставка = 0;
		ПерваяНадбавка = Истина;
		
		Пока ВыборкаДетали.Следующий() Цикл
			
			Фонд = ?(ВыборкаДетали.РасчетнаяТарифнаяСтавка <> NULL,ОбщегоНазначения.ОкруглитьПоВалюте(ВыборкаДетали.РасчетнаяТарифнаяСтавка*ВыборкаДетали.КоличествоСтавок,Константы.ВалютаРегламентированногоУчета.Получить()),0);
			КоличествоСтавок = ВыборкаДетали.КоличествоСтавок;
						
			Если ВыборкаДетали.СуммаНадбавки <> NULL Тогда
				Фонд = Фонд+ОбщегоНазначения.ОкруглитьПоВалюте(ВыборкаДетали.СуммаНадбавки*ВыборкаДетали.КоличествоСтавок,Константы.ВалютаРегламентированногоУчета.Получить()); 
			КонецЕсли;	 
 	       				
			ВсегоСтавокОрганизации = ВсегоСтавокОрганизации + КоличествоСтавок;
			ФондОрганизации =ФондОрганизации + Фонд;
		КонецЦикла;	
	КонецЦикла;
	
	//Выводим шапку
    ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаЛиста = Макет.ПолучитьОбласть("ШапкаЛиста");

	ОбластьШапка.Параметры.НазваниеОрганизации = Организация.НаименованиеПолное;
		
	
	ОбластьШапка.Параметры.ДатаДокумента =Формат(ДатаАктуальности,"ДЛФ=Д");
	ОбластьШапка.Параметры.ШтатВКоличестве = Формат(ВсегоСтавокОрганизации,"ЧГ=0")+" единиц";
	ОбластьШапка.Параметры.МесячныйФонд	 = Формат(ФондОрганизации)+" рублей";

	ВыборкаДляПодвала = РезультатДляПодвала.Выбрать();
	
	Если ВыборкаДляПодвала.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель,"ОтветственноеЛицо") Тогда
		Инициалы = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ВыборкаДляПодвала.ФИОРуководителя," ");			
		ОбластьШапка.Параметры.ФИОРуководителяОрганизации = Инициалы[0]+" "+Лев(СокрЛП(Инициалы[1]),1)+". "+Лев(СокрЛП(Инициалы[2]),1+".");
		ОбластьШапка.Параметры.ДолжностьРуководителя = ВыборкаДляПодвала.ДолжностьРуководителя;
	КонецЕсли;
	
	
	ДокументРезультат.Вывести(ОбластьШапка);
    ДокументРезультат.Вывести(ОбластьШапкаЛиста);
	
	
	КоличествоСтавокЛиста = 0;
	ФондЛиста = 0;
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвалЛиста = Макет.ПолучитьОбласть("ПодвалЛиста");
	// массив с двумя строками - для разбиения на страницы
	ВыводимыеОбласти = Новый Массив();
	ВыводимаяСтрока = Новый ТабличныйДокумент();
	ВыводимыеОбласти.Добавить(ВыводимаяСтрока);
	ВыводимыеОбласти.Добавить(Макет.ПолучитьОбласть("ПодвалЛиста"));
	
	
	//Второй обход выборки:Выводим строки штатного расписания
	ВыведеноСтрокВОтчет = 0;
	
	//ВыборкаСтрокШтРасписания =  РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ВывестиСтрокиПодразделения(РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией), ВыведеноСтрокВОтчет, КоличествоСтрокОтчета, ДокументРезультат,ОбластьСтрока, ОбластьПодвалЛиста, ОбластьШапкаЛиста,ВыводимаяСтрока,  
		КоличествоСтавокЛиста, ФондЛиста, ВыводимыеОбласти, Макет);
		
	Для Сч = 1 По ОбластьСтрока.Параметры.Количество() Цикл
		ОбластьСтрока.Параметры.Установить(Сч - 1,""); 
	КонецЦикла;

	ВыводимаяСтрока = Новый ТабличныйДокумент();
	ВыводимаяСтрока.Вывести(ОбластьСтрока);
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ВыводимаяСтрока);
	ВыводимыеОбласти.Добавить(Макет.ПолучитьОбласть("ПодвалЛиста"));
	ВыводимыеОбласти.Добавить(Макет.ПолучитьОбласть("Подвал"));
	Пока ДокументРезультат.ПроверитьВывод(ВыводимыеОбласти) Цикл
		ДокументРезультат.Вывести(ВыводимаяСтрока);
	КонецЦикла;
	//
	//Выводим подвал листа
	ОбластьПодвалЛиста.Параметры.КоличествоСтавок = КоличествоСтавокЛиста;
	ОбластьПодвалЛиста.Параметры.МесячныйФонд = ФондЛиста;
	ДокументРезультат.Вывести(ОбластьПодвалЛиста);

	//Выводим подвал документа
	НомерВерхнейСтрокиПодвала = ДокументРезультат.ВысотаТаблицы+1;
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.КоличествоСтавок = ВсегоСтавокОрганизации;
	ОбластьПодвал.Параметры.МесячныйФонд = ФондОрганизации;
	ВыборкаДляПодвала = РезультатДляПодвала.Выбрать();
	Если ВыборкаДляПодвала.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы,"ОтветственноеЛицо") Тогда
			Инициалы = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ВыборкаДляПодвала.ФИОРуководителя," ");			
			ОбластьПодвал.Параметры.ФИОРуководителяКС =  Инициалы[0]+" "+Лев(СокрЛП(Инициалы[1]),1)+". "+Лев(СокрЛП(Инициалы[2]),1+".");;
	КонецЕсли;
			
	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры


#КонецЕсли

