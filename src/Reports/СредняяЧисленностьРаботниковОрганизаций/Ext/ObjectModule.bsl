Перем СохраненнаяНастройка Экспорт;
Перем Расшифровки Экспорт;

#Если Клиент ИЛИ ВнешнееСоединение Тогда

Функция СформироватьОтчет(Результат = Неопределено, ДанныеРасшифровки = Неопределено, ВыводВФормуОтчета = Истина) Экспорт
	
	//О.Т. Аверсон 08.11.2018 № 2753
	Если РольДоступна("ЗапретИзмененияОтбораПодразделения") Тогда 
		СписокЭлементов = КомпоновщикНастроек.Настройки.Отбор.Элементы;
		Для каждого Элемент из СписокЭлементов Цикл
			Если ТипЗнч(Элемент) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				//О.Т. Аверсон 12.04.2018 № 1824
				Если Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодразделениеОрганизации") Тогда
					мПодразделениеОрганизации = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
					Если ЗначениеЗаполнено(мПодразделениеОрганизации) Тогда 
						Элемент.ПравоеЗначение = мПодразделениеОрганизации;
						Элемент.Использование = Истина;
						Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;					
					КонецЕсли;
				КонецЕсли;
				//О.Т. Аверсон 12.04.2018 № 1824
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//О.Т. Аверсон 08.11.2018 № 2753
	
	Возврат ТиповыеОтчеты.СформироватьТиповойОтчет(ЭтотОбъект, Результат, ДанныеРасшифровки, ВыводВФормуОтчета);
	
КонецФункции                                                               

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ДоработатьКомпоновщикПередВыводом() Экспорт
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ЗначениеПараметра = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеПараметра.Значение = '00010101' Тогда
		ЗначениеПараметра.Значение = КонецДня(ТекущаяДата());
		ЗначениеПараметра.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

#Если Клиент Тогда
	
// Для настройки отчета (расшифровка и др.)
Процедура Настроить(Отбор, КомпоновщикНастроекОсновногоОтчета = Неопределено) Экспорт
	
	ТиповыеОтчеты.НастроитьТиповойОтчет(ЭтотОбъект, Отбор, КомпоновщикНастроекОсновногоОтчета);
	
КонецПроцедуры

Процедура СохранитьНастройку() Экспорт
	
	СтруктураНастроек = ТиповыеОтчеты.ПолучитьСтруктуруПараметровТиповогоОтчета(ЭтотОбъект);
	СохранениеНастроек.СохранитьНастройкуОбъекта(СохраненнаяНастройка, СтруктураНастроек);
	
КонецПроцедуры

// Процедура заполняет параметры отчета по элементу справочника из переменной СохраненнаяНастройка.
Процедура ПрименитьНастройку() Экспорт
	
	Если СохраненнаяНастройка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	 
	СтруктураПараметров = СохраненнаяНастройка.ХранилищеНастроек.Получить();
	ТиповыеОтчеты.ПрименитьСтруктуруПараметровОтчета(ЭтотОбъект, СтруктураПараметров);
	
КонецПроцедуры

Процедура ИнициализацияОтчета() Экспорт
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НачалоПериода"));
	
	Если ЗначениеПараметра <> Неопределено и ЗначениеПараметра.Значение = '00010101' тогда
		
		ЗначениеПараметра.Значение = НачалоМесяца(ОбщегоНазначения.ПолучитьРабочуюДату());
		
	КонецЕсли;
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("КонецПериода"));
	
	Если ЗначениеПараметра <> Неопределено и ЗначениеПараметра.Значение = '00010101' тогда
		
		ЗначениеПараметра.Значение = КонецМесяца(ОбщегоНазначения.ПолучитьРабочуюДату());
		
	КонецЕсли;
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	
	Если ЗначениеПараметра <> Неопределено и ЗначениеПараметра.Значение = '00010101' тогда
		
		ЗначениеПараметра.Значение = КонецМесяца(ОбщегоНазначения.ПолучитьРабочуюДату());
		
	КонецЕсли;

КонецПроцедуры

/////////////////////////////////////////////////
Функция ПолучитьТекстЗапроса(РазворачиватьЧисленностьПоМесяцам = истина) ЭКспорт
	
	// получим запрос из СКД
	
	ЧисленностьПоМесяцамТекст = СхемаКомпоновкиДанных.НаборыДанных.СредняяЧисленностьРаботниковОрганизаций.Запрос;
		
	Если РазворачиватьЧисленностьПоМесяцам = истина Тогда
		
		Возврат ЧисленностьПоМесяцамТекст;
		
	Иначе
		
		СтрокаПолейВыбора = 
		"
		|	МесяцОтчета,
		|	Организация,";
	
		ЧисленностьПоМесяцамТекст = СтрЗаменить(ЧисленностьПоМесяцамТекст, СтрокаПолейВыбора, "");
		ЧисленностьПоМесяцамТекст = СтрЗаменить(ЧисленностьПоМесяцамТекст, "РАЗРЕШЕННЫЕ", "");
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СредняяЧисленностьПоМесяцам.Организация КАК Организация,
		|	ПРЕДСТАВЛЕНИЕ(СредняяЧисленностьПоМесяцам.Организация),
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленность КАК ЧИСЛО(15, 0))) КАК СредняяЧисленность,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьИнвалидов КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьИнвалидов,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьНеЕНВД КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьНеЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьЕНВД КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьМужчин КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьМужчин,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьМужчинНеЕНВД КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьМужчинНеЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьМужчинЕНВД КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьМужчинЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьЖенщин КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьЖенщин,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьЖенщинНеЕНВД КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьЖенщинНеЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьЖенщинЕНВД КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьЖенщинЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СреднеСписочнаяЧисленность КАК ЧИСЛО(15, 0))) КАК СреднеСписочнаяЧисленность,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СреднеСписочнаяЧисленностьМужчин КАК ЧИСЛО(15, 0))) КАК СреднеСписочнаяЧисленностьМужчин,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СреднеСписочнаяЧисленностьЖенщин КАК ЧИСЛО(15, 0))) КАК СреднеСписочнаяЧисленностьЖенщин,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СреднеСписочнаяЧисленностьЕНВД КАК ЧИСЛО(15, 0))) КАК СреднеСписочнаяЧисленностьЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СреднеСписочнаяЧисленностьНеЕНВД КАК ЧИСЛО(15, 0))) КАК СреднеСписочнаяЧисленностьНеЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьДоговорников КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьДоговорников,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьДоговорниковМужчин КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьДоговорниковМужчин,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьДоговорниковЖенщин КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьДоговорниковЖенщин,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьДоговорниковЕНВД КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьДоговорниковЕНВД,
		|	СРЕДНЕЕ(ВЫРАЗИТЬ(СредняяЧисленностьПоМесяцам.СредняяЧисленностьДоговорниковНеЕНВД КАК ЧИСЛО(15, 0))) КАК СредняяЧисленностьДоговорниковНеЕНВД
		|{ВЫБРАТЬ
		|	Организация,
		|	СредняяЧисленность,
		|	СредняяЧисленностьИнвалидов,
		|	СредняяЧисленностьНеЕНВД,
		|	СредняяЧисленностьЕНВД,
		|	СредняяЧисленностьМужчин,
		|	СредняяЧисленностьМужчинНеЕНВД,
		|	СредняяЧисленностьМужчинЕНВД,
		|	СредняяЧисленностьЖенщин,
		|	СредняяЧисленностьЖенщинНеЕНВД,
		|	СредняяЧисленностьЖенщинЕНВД,
		|	СреднеСписочнаяЧисленность,
		|	СреднеСписочнаяЧисленностьМужчин,
		|	СреднеСписочнаяЧисленностьЖенщин,
		|	СреднеСписочнаяЧисленностьЕНВД,
		|	СреднеСписочнаяЧисленностьНеЕНВД,
		|	СредняяЧисленностьДоговорников,
		|	СредняяЧисленностьДоговорниковМужчин,
		|	СредняяЧисленностьДоговорниковЖенщин,
		|	СредняяЧисленностьДоговорниковЕНВД,
		|	СредняяЧисленностьДоговорниковНеЕНВД
		|}
		|ИЗ     
		|	(" + ЧисленностьПоМесяцамТекст + ") КАК СредняяЧисленностьПоМесяцам
		|{ГДЕ
		|	СредняяЧисленностьПоМесяцам.Организация}
		|
		|СГРУППИРОВАТЬ ПО
		|	СредняяЧисленностьПоМесяцам.Организация";
		
		Возврат ТекстЗапроса;
		
	КонецЕсли;
	
КонецФункции

// Выполняет запрос в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	результат запроса
//
Функция ВыполнитьЗапросОтчета(Организация = Неопределено, РазворачиватьЧисленностьПоМесяцам, ДатаНач, ДатаКон) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаНач) или НЕ ЗначениеЗаполнено(ДатаКон) Тогда
		Возврат Неопределено
	КонецЕсли;
	
	Если Не ПроцедурыУправленияПерсоналом.РегламентированныйКалендарьЗаполнен(ДатаНач, ДатаКон, Истина) Тогда
		Возврат Неопределено
	КонецЕсли;
	
	ПостроительОтчета = Новый ПостроительОтчета;
	
	ПостроительОтчета.Текст = ПолучитьТекстЗапроса(РазворачиватьЧисленностьПоМесяцам);
	ПостроительОтчета.ЗаполнитьНастройки();
	
	Если Организация <> Неопределено тогда
		ПостроительОтчета.Отбор.Добавить("Организация");
		ПостроительОтчета.Отбор.Организация.Установить(Организация)
	КонецЕсли;
	
	
	ПостроительОтчета.Параметры.Вставить("НачалоПериода",     ДатаНач);
	ПостроительОтчета.Параметры.Вставить("КонецПериода",      ДатаКон);
	ПостроительОтчета.Параметры.Вставить("ДатаКонУвольнений", КонецДня(ДатаКон) + 1);
	ПостроительОтчета.Параметры.Вставить("ДатаНачУвольнений", КонецДня(ДатаНач) + 1);
	
	ПостроительОтчета.Выполнить();
	
	Результат = ПостроительОтчета.Результат;
	
    Возврат Результат;
	
КонецФункции
							 

Расшифровки = Новый СписокЗначений;

НастройкаПериода = Новый НастройкаПериода;

#КонецЕсли